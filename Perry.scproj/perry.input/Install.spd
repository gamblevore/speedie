
target false { // remove for simplicity
// "brew install speedie" would be better
extend_module perry
///// abc
	function Monthly_Name (|date| n, |string|)
		|| fs = faststring()
		fs <~ "~/Backups/perry_monthly/".PathFix
		fs.AppendIntegerAsText(n.MonthsApprox, 4)
		fs <~ "m.zip"
		return fs


	function Latest_Name (|string|)
		target debug
			return "~/Backups/perry_latest.debug.zip".PathFix
		  else
			return "~/Backups/perry_latest.zip".PathFix


	function Daily_Name (|date| n, |string|)
		|| fs = faststring() 
		fs <~ "~/Backups/perry/".PathFix
		fs.AppendIntegerAsText(n.Days, 4)
		fs <~ ".zip"
		return fs
	
	
	function Remove_Old (|file| fol, |int| allowed, |bool| keep_first=false, |file|)
		fol.MakeEntirePath
		|| items = fol.list
		while items.length > allowed
			|| i = items.pop #loop
			|| s = fol[i]!
			if keep_first
				keep_first = false
				rz = s
			  else
				s.delete


	function Backup (|file| zip, |file|)
// Daily file per week, weekly file per month.
		|| now  = gui.FrameStart - 107020500000000
		|| latest = .latest_name
		|| daily = .daily_name(now)
		|| monthly = .monthly_name(now)
		|| daily_fol = "~/Backups/perry".file
		|| monthly_fol = "~/Backups/perry_monthly".file
		|| latest_local = file.speediedir["Terminal/Backup"][latest.name]
		
		rz = latest_local.file
		zip.copyto(rz)
		zip.moveto(daily)
		
		|| it = remove_old(daily_fol, 7, true)
			it.moveto(monthly)
		remove_old(monthly_fol, 8)
				
		
	function Install_Path (|string|)
		|| p = app.exepath.parent
		expect (p contains ".app/") (beep, "Can't install/uninstall while using the app directly.")
		// we need to store this in the .app folder...
		return p.sibling("Resources/speedie_install.txt") // evade code-signing


	function DownloadData (|string| url, |int| lim, |string|)
		|| p = .install_path			#require
		return p.readfile(lim)			#expect (beep, "Can't find installation-data at:\n" + p)


	
	function InstallState (|FileArchive| Arc, |file| base, |faststring| diff, |int|)
		|| newer = FastString()
		|| older = FastString()
		
		for i in arc
			|| s = i.name
				|| curr_file = base[s]!
				if curr_File.isdir or curr_file.readall(true) != i..data.Value
					|| diskmod = curr_file.modified
					|| arcmod = i..modified.int|date|
					if diskmod <= arcmod
						rz |= arc_newer
						newer.FileDiff(i.name, "And newer ones too")
					  else
						rz |= arc_older
						older.FileDiff(i.name, "Archive contains older files")
				
		diff <~ older
		if newer.length and older.length
			diff <~ "\n"
		diff <~ newer


	function HandleState (|int| state, |file| f, |string| explain,  |file|)
		require state
		if (state & arc_older)
			|| side = f.sibling("SpeedieTest")
			|| q = faststring()
			q <~ "You don't mind losing your alterations to Speedie?\n\nInstall separately: $side\nOr overwrite:       $f\n\n$explain"
			|| it = q.getresult.ask_named("Separate/Overwrite/Cancel")
			require it != -1		// cancel
			if it == 1
				return side
		return f


	function FindSpeedieRoot (|string| In, |string|)
		|| paths = in.parent
		|| srch = "/speedie/"
		|| found = paths.find(srch, 0, int.max, true)
		expect (found > 0) ("speedie needs to be contained in a folder named 'speedie'.")
		return paths[0, found+srch.length]


	function MakeSpeedieDir (|string|)
		|| dest_path = speedie.LinksTo
		if speedie.exists and dest_path
			|| result = dest_path.ExecuteStr(["--alive"]).trim
			if result contains "seems alive"
				rz = .FindSpeedieRoot(dest_path)


	function TestShell (|string|)
		if !.makespeediedir
			return " * Link $speedie\n\n"
		
		
	function TestInstall (|string|)
		|| where = file.speediedir.file
		if !where.exists
			return " * Install Speedie (compiler and libs).\n\n"
		|| items = "GUI.scproj Image.scproj jeebox.scproj Library network.scproj Speedie.scproj Terminal" / ' '
		
		for i in items
			|| w = where[i]
			if !w.exists
				return w.path + " is missing.\n\n"
			
	
	function InBuildFolder (|bool|)
		|| paths = app.exepath.Components
		for s.backwards in paths
			if s.isproj
				return true
			// find "build" then find... ".scproj"
		
	
	function TestOlder (|&bool| not_exist, |&bool| age_bad, |bool|test, |string|)
		require platform.release or test
		if !test
			require !.InBuildFolder
		|| p = app.NeedsIdealPath$
		
		if !p.exists
			not_Exist[] = true
			return " * Move Perry to /Applications"
		  elseif p.appDate < app.Created
			age_bad[] = true
			return " * Replace old Perry (in /Applications)"
				

	function TestNewer (|string|)
		|| old = app.NeedsIdealPath$
		if old.appdate > app.Created
			return old.path


	function MoveToAppFolder (|string|) // disabled?
		|| dest = app.NeedsIdealPath$
		|| me = app.apppath.file
		|| t = file.trash(me.name)$
		
		for 10
			if dest.exists and !dest.moveto(t)
				rz = "\n\nCan't remove " + dest.path
			  elseif me.moveto(dest)
				return "" // success
			  else
				t.moveto(dest) /*undo*/ 
				rz = "\n\nFailed to move $me to destination"
		
	function TestCompiler (|string|)
		if !CppCompiler.fileexists
			return "No c++ compiler exists at '$CppCompiler' ! Please make one available from that path."



	function GenerateArchive
		expect (.GenerateArchiveSub) (beep)
			
	
	function ConfirmGen (|[file]| list, |bool|)
		|| fs = FastString()
		fs <~ "Include all these files?\n\n"
		list.sort(&File.SizeSorter)
		for f in list
			fs <~ f
			fs <~ " "
			fs <~ f.Size|int|.strsize
			fs <~ "\n"
		return fs.render.confirm
			
	function GenerateArchiveSub (|bool|)
		|| outpath = .install_path
		|| p = file.speediedir
		|| f = p.FileThatExists("generate archive")
		require f and outpath
		
		|array of file| list 
		list <~ F.Child("Terminal/Speedie")
		.GenerateallDir(list, f)
		require .ConfirmGen(list)
		|| arc = FileArchive.new(p)
		|| out = outpath.file
		for ch in list
			arc <~ ch
		arc.saveto(out)

		|| result = app.apppath.file
		require Speedie.execute(["--dosign="+result])
		|| zipped = result.ZipTmp					#require
		|| latest = .backup(zipped)

		|| out_size = out.size|int|.StrSize
		|| in_size = arc.size|int|.StrSize
		beep "Done!\n\n$out created at $out_size\n\nExpands to $in_size"
		latest.Reveal
		return true
		
		
	function CanGenerate (|file| f, |bool|)
		return .CanGeneratesub(f, f.name)
	
	function CanGenerateSub (|file| f, |string| n, |bool|)
		require !f.linksto
		require !(f.path contains "/Terminal/Debug" or "/Terminal/Backup")
		require !n[0, ".git"]
		require !(n contains "DerivedData") // fucking xcode!
		require !(n contains "(Disabled)")
		require n ~!= ".DS_Store"
		if f.isdir
			require !n[0, "Build"]
			require n ~!= "Cpp" and "Terminal" and "cpp.hidden" //
		return !(n isa "xcodeproj" or ".mz")


	function GenerateAllDir (|[file]| f, |file| dir)
		for c.files in dir
			|| n = c.name
			if .CanGenerate(c)
				.GenerateDir(f, c)


	function GenerateDir (|[file]| f, |file| dir)
		for c.files in dir
			if !.CanGenerate(c)	
				//
			  elseif c.isdir
				.GenerateDir(f, c)
			  else
				f <~ c


	function SetupLinks (|file|f, |string|)
		|| term = f["Terminal/Speedie"]!
		require term.mustexist
		|| dest = speedie.linksto
		if Dest == term.path
			//
		  elseif speedie.linkto(term)
			rz = "\n\nInstalled speedie at: " + speedie
		  else
			rz = "\n\nFailed to install speedie at: " + speedie


	function Install_Files (|file| f, |string|)
		|| URL		= app.conf("install_url")
		|| str		= .DownloadData(URL, 10MB)	#require
		|| arc		= str.GetArchive(64MB)		#expect (beep, "Can't use installation-file.")
		|| diff		= faststring()
		|| State	= .InstallState(Arc, f, diff)
		|| f2		= .HandleState(state, f, diff)
		if !f2 and state // cancelled
			beep "No files changed."
			return ""
		
		rz = "Already installed and up to date."
		if f2
			|| size = arc.InstallTo(f2, true)
			rz = "${arc.FileCount} files (${size.strsize}) written to:\n\n$f2"
			if !stderr.ok
				rz += "\n\nSome errors occurred:\n"
				rz += stderr.render


	function Install (|bool| DoInstall, |bool| DoLinks, |bool|)
		stderr.clear
		|| msg = ""
		|| f   = file.speediedir.file
		if doinstall
			msg += .install_files(f)
		if dolinks
			msg += .SetupLinks(f)
		if msg
			beep msg
		return true
		
			
	function UnInstall
		|| fs = FastString()
		|| per = app.Idealpath
		|| fol = file.speediedir
		if speedie.exists and speedie.delete
			fs <~ "Deleted Link at: $speedie"
		if per.FileExists and per.MoveToTrash
			fs <~ "\nDeleted Perry at: $per"
		fs <~ "\n\nTo fully uninstall, remove folder at:\n$Fol"
		beep fs


function ClickedInstallMenu (guimenuevent)
	perry.install(true, true)

function ClickedMakeArchiveMenu (guimenuevent)
	perry.GenerateArchive

function ClickedUninstallMenu (GUIMenuEvent)
	perry.uninstall

function faststring.FileDiff (|string| name, |string| Title)
	if !.length
		self <~ title
		self <~ ":\n"
	self <~ "\t"
	self <~ name
	self <~ "\n"

function file.AppDate (|date|)
	if self
		ifn self isa "app"
			return .anydate
		|| n = "/Contents/MacOS/" + .TrimExtAndPath
		|| c = .childexisting(n)
			return c.anydate

function app.IdealPath (|file|)
	return .apppath.idealApp.file

function app.NeedsIdealPath (|file|)
	|| f = .idealpath
	if f.path != app.apppath
		return f

}
