


class PerryIDE (Window) 
	linkage: cpp_part IDE

	|SpdProcess?|				Feedback
	|Process?|					compilr
	|Process?|					RunCompiledApp
	|string|					CompileOKString
	|[error]|					StartupErrors
	|FileViewer|				FileList

	|listview|					Stack
	|ListView|					Variables
	|stretchcontrol|			Debugger

	|textview|					AppInfo
	|Guicontrol|				DebugBtns
	|Guicontrol|				SearchArea
	|Guicontrol|				rborder
	|guicontrol| 				CompileArea
	
	|FileRow?|					NotTodo
	|message?|					CurrASMFunc
	|listviewrow?|				SharedProjectTabSource
	|message?| 					LostFunks
	|[guicontrol]|				Views
	|File|						Project
	|ivec2|						AutoPoint
	|TextView| 					ProductOut
	|TextView| 					ProductErrors
	|TextView|					SpeedieText
	|TextView|					JeeboxText
	|TextView|					MassSearchField
	|TextView|					MassReplaceField
	|TextView|					EditMe
    |listview|                	ASMView
    |StretchControl|            ASMStretch
	|GraphicsEditor| 			DrawMe
	|TextView|					EditStatus
	|TextView|					ProjectInfo
	|TextView|					LineInfo
	|TextView|					CharInfo
	|TextView|					UnNeededLabel
	|TextView|					PopupDisplay
	|message?|					LastExtraInfo
	|StretchControl|			ProductSep
	|StretchControl|			FileSep
	|listviewrow?|				LogComms
	|listviewrow?|				LogActions
	|menu|						AutoComplete
	|ListView|					ErrorList
	|listview|					CommsLogList
	|ListView|					FoundList
	|textview| 					DebugArgsText
	|Guicontrol of buttonview|	ViewButtons
	|buttonview?--|				PrevView
	|ButtonView|				CurrFileName
	|Buttonview|                ASMButton
	|ButtonView|				FunctionMenu // .funclist
	|ButtonView|				CommandsMenu
	|buttonview|				Toolbar
	|Buttonview|				ProjectTabs
	|buttonview|				CompileTabs // compilertabs
	|buttonview|				SubFindCheckBox    
	|Buttonview|				ViewErrors
	|Buttonview|				ViewFiles
	|Buttonview|				ViewSearch
	|Buttonview|				ViewDebugger
	|Buttonview|				ViewLog
	|Buttonview|				GoNext
	|Buttonview|				GoPrev
	|Buttonview|				StartScreen
	|Buttonview|				BuildButton
	|Buttonview|				RunButton
	|Process?|					Talker
	|message|					RecentFiles
	|string| 					LastCmdStr
	|date|						LastSendTime
	|date|						SectionKeyTime
//	|string|					AttachedProgram
	|ProgressBar|				Progress
	|Faststring?|				Stats
	|ivec2|						PopupDisplayCorner
	|date|						LastAutoCompleteDate
	|date|						PopupDateOn
	|uint[3]| 					LastMemory
	|int|						EditMeTabRetryThing
	|int|						ShowProductIfMoreOutput
	|byte|						DuringLoad
	|byte| 						DuringTempView
	|bool|						BytesInfoMode
	|bool|						DuringASMEvent
	|bool|						AlreadyExpandedCompileTab
	|bool| 						StillJumping
	|bool|						CanAskingAboutMissing
	|bool|						DuringMsgFB  // canloadfile, canupdate, .disable 
	|int8|						DuringNextPrev
	|bool|						IsTextMode
	|bool|						UsingSpdLib
	|bool| 						DebugIsWaitingForCompile
	|bool|						InConstructor
	|bool|						ModifiedForCompile
	|bool| 						IsGraphics
	|byte|						PopupReason

	
	destructor
		opt norefcounts
		if !gui.isquitting
			|| proj = .SharedProjectTabSource
				proj["pos"] = .editme.sel0
				Perry.save
		
				|| n = proj.parent.slowcount
				if n >= 2
					.RemoveProjFromList
				|| p = .prev
				if p isa perryide
					p.showide

	
	function UpdateStatusErrors
		.EditStatus.text = .PerryStatus


	function ExpandStr (|string| path, |string|)
		if !perry.pathdict
			perry.pathdict = [ "spd"  :  file.speediedir ]
		rz = path.Path_Expand(perry.PathDict) #expect ("Unrecognised @ in: " + path)
		rz = rz.resolve(true)
	
	
	function PerryError (|error?|) 
		opt norefcounts
		if keys.shift
			|| u = GUI.tooltip.useable
				return u
		|| e = .editme.myerror.useable(0)
			if .editme.ParsedMessage				// don't show simple parse errors
				return e
			if .editme.expectsparse >= textview.probablyjeebox
				return e
		return GUI.tooltip.useable
	
	
	function PerryStatus (|string|)
		opt norefcounts
		|| p = .PerryError
			return p.name


	function RemoveProjFromList
		|| r = .SharedProjectTabSource
			.SharedProjectTabSource = message() // for now
			|| n = r.next
			n ?= r.prev
			.ProjectTabs.selected = n
			r.remove


	behaviour Tickle
		opt norefcounts
// seems to cause blockages sometimes? why?
//		.Feedback$.Pico.Tickle
	

	behaviour CancelClose 
		GUI.SavePrefs					// seems better
		if !.filelist.AskSaveall
			return true					// failed
		if !ClickedCancel(self) or !.MakeNothingRunning(" for closing the project?")
			return true
			

	function LocateProjRoot (|string| path, |filerow|)
		opt norefcounts
		|| R = .FindPath(path)$
		|| P = .MainProjFol$
		if r.location[0, p.location]
			return r
			

	function FindPath (|string| path, |filerow|)
		opt norefcounts
		return .FileList.FindAPath(Path) 

		
	function MainProjFol (|filerow|)
		opt norefcounts
		|| it = .FileList
		|| r = it[0]
		return it.FindAPath("./input/", r)  ??  it.findapath("./*.input/", r) 


	behaviour AppForeGroundChange
		.CheckModified
		if IsForeGround and .IsFront
			.Recheck(true)
		.PopupHide
		if !.focus
			.setfocus(.editme)
	
	
	function MyStatsMode (|int|)
		rz = perryide.StatsMode|int|
		if rz > 3
			rz = 0
		if rz < 0
			rz = 0
		perryide.StatsMode = rz
	
	
	function InfoStatus (|string|)
		|| fs = .stats init FastString()
		|| i = .MyStatsMode
		|| d = .editme.Document
		if i == 0
			fs <~ "  --" // :)
		
		  elseif i == 1
			|| e = .editme
			|| t = e.text

			fs <~ "Chars ${t.charcount}   Lines ${t.linecount}"
			|| msg = .EditMe.ParsedMessage
				|| loc = msg.loc
				fs <~ "   LOC ${loc[0]}   Nodes ${loc[1]}"

			fs <~ (' ', 3)
			if t.IsASCII
				fs <~ "ASCII"
			  elseif t.FindBadUTF8
				fs <~ "binary"
			  else
				fs <~ "UTF-8"
				
			t = e.SelText
			if t.LengthUTF8 == 1
				fs <~ "   CP "
				fs <~ t.Codepoint(false	)
			
			if d
				if d.WasJbin
					fs <~ ".jbn"
				if d.WasCompressed
					fs <~ ".mz"
			
		  elseif i == 2
			|| f = .filelist.FirstSelected as filerow
			if !f
				0
			  elseif f.asdir
				fs <~ f.Name + "/"
			  else
				fs <~ "proj"
				f = (.filelist.listsource as filerow)
			
			if f
				|| s = f.lastsize
					fs <~ " ${s.StrSize} in (${f.LastFileCount} \11)"
				  else
					fs.Length = 0
					fs <~ "no source found"
		
		  elseif i == 3
			|| img = GUI.screen.size.area*4*2
			|| PS = PicoGlobalStats.Read 
			fs <~ "Obj ${app.ObjMemory.strsize}"
			fs <~ "   Str ${app.stringmemory.strsize}"
			fs <~ "   Img ${(app.OtherMemory+img).strsize}"
			fs <~ "   Sck ${PS.OpenSockets}/${PS.OpenPicos}"
		
		if d and d.ShouldntSave
			fs <~ "  Invalid Jeebox!"
		return fs


	function NextProjInfo
		.ShowLeftStats(perryide.StatsMode+1)


	function ShowLeftStats (|int| i = -1)
		if i >= 0
			perryide.StatsMode = i
		.ProjectInfo.text = .InfoStatus	


	function ShowRightStats
		if .isgraphics
			(.charinfo isnt visible)
			return
		
		(.charinfo is visible)
		(.lineinfo is visible)
		|| fs = FastString()
		|| t = .Editme.Text
		|| sel0 = .editme.sel0
		|| sel = .EditMe.SelText
		|| sel2 = sel
		if sel2.last.IsTextLine
			sel2 = sel2.Shorten(1)
		fs <~ "ln "
		fs <~ t[0, sel0].LineCount
		|| n = sel2.linecount-1			
			fs <~ "+"
			fs <~ n+1
		.LineInfo.text = fs
		
		if .BytesInfoMode
			fs <~ "byte $sel0"
			n = sel.Length
		  else
			fs <~ "ch ${t.charcount(0, sel0)}"
			n = sel.CharCount
		if n
			fs <~ "+"
			fs <~ n

		.charInfo.text = fs	

	
	function ProjName (|string|)
		disabled "Use .name"
		
	function Name (|string|) //.ProjName
		opt norefcounts
		|| m = .SharedProjectTabSource
			return m.name.name


	function Show (|bool| FromClick=false)
		disabled "use .ShowIDE"

	
	function ShowIDE (|bool| FromClick=false)
		debug_require self // what?
		|| p = .Project
		app.prefs["currproj"] = p
		|| rec = Perry.RecentProjects.Find(@tmp, p)
			rec.Parent.first = rec
		if FromClick
			if .islast
				.ClickedTodo
			  else
				.recheck
		if !.isfront
			.ProjectTabs.selected = .SharedProjectTabSource
			.editme.SetFocus // make it simple
			super.show
		.id = .name
		GUI.Title = .id


	helper UpdateSearchSwap (|textview|)
		opt norefcounts
		require .isfront
		|| E = .inline.editor
		|| M = .MassSearchField
		|| F = .realfocus
		if F == E
			return M
		if F == M
			return E
			
	
	function UpdateSearchText
		opt norefcounts
		|| other = .UpdateSearchSwap #require 
		|| rf = .RealFocus // what if they just used the same document??
			|| t = rf.text // sigh
			GUI.LastSearch = t
			if other.text != t
				other.text = t
				other.Selrange = 0


	function HideStuff 
		|| M  = .MouseXY
		|| LB = .ViewButtons.lastrect.y1
		|| LF = .filelist.lastrect.x2-2
		
		|| HideTop  = (m.y < LB)
		|| HideSide = (m.x > LF)
		|| HideTabs = HideTop or .IsTextMode
		|| VisSide  = !.filelist.decluttering
		|| VisTop   = !.StartScreen.decluttering
		|| VisTabs  = !.ProjectTabs.Decluttering

		// OK... so .. if one is visible, we don't make the other invis
		if .filelist.cmm == .openedmenu
			hideside = false 

		if VisSide and VisTop and (HideSide != HideTop) 
			HideSide = false
			HideTop = false
			HideTabs = false
		
		require !.CurrMouseDown 
		if HideTabs == VisTabs
			.ProjectTabs.declutter = HideTabs
			
		if (hidetop == vistop)
			.StartScreen.declutter = hidetop
			.ASMButton.Declutter = Hidetop
			if HideTop
				(.DebugArgsText isnt visible)			// better to declutter
			|| ShowBuilders = !HideTop and (!.IsTextMode or .path.isspd)
			|| HideBuilders = !ShowBuilders
			.runbutton.declutter		= HideBuilders
			.buildbutton.declutter		= HideBuilders
			.Progress.Declutter			= HideBuilders
		
		if (hideside == visside)
			.filelist.declutter = hideside
			|| f = .Focus
			if HideSide and (!f or f.declutterer.final)
				.editme.SetFocus
			if .viewing == .viewfiles
				.noneed
				|| fl = .filelist.ListSource as filerow
					fl.SmartName = "file".pluralize(fl.LastFileCount)


//	function CheckNotCrashed (|&Process| pp, |string| what, |bool|)
//		|| p = (pp[])|process--|
//		require p
//		|| Exit = p.Status
//		if !exit.Crashed
//			return true
//
//		|| fs = faststring()
//		fs <~ "Crashed: ${exit.name}  {${p.render}}\n"
//		fs <~ p.ErrorOutput.getresult.FindStacktrace
//		pp[] = nil
//		.Notify(fs, @emb + what)
//		.CancelCompile


	function CheckCrashed (|Process| p, |string| what, |bool|)
		require p
		|| Exit = p.Status
		if exit.Crashed
			|| fs = faststring()
			fs <~ "Crashed: ${exit.name}  {${p.render}}\n"
			fs <~ p.ErrorOutput.getresult.FindStacktrace
			.Notify(fs, @emb + what)
			return .CancelCompile


	function string.FindStacktrace (|string|)
		|| f = "\n****** CRASH"
		|| where = .find(f) #require
		where = .find("\n", where) #require
		where += f.length
		|| After = .find("\n-----------------------\n", where)
		if !after: after = int.max
		
		return self[where, after] 
		

	function SaveMemory
		if .IsFront
			return 
		require !.UsingSpdLib
		|| f = .Feedback
		if f.CommsOpen and f.lastsend.ago > (30.0+.position|float|).date
			|| msg = "Released Speedie: " + .name
			.LogOneAction(@emb + msg)
			f.Kill(0, "SaveMemory")
	
	
	behaviour FrameStep // perframe, eachframe, onframe
		.ProjectTabs.TrimWidth
		if GUIactive
			.SaveMemory
			if !.focus
				.setfocus(.editme) // shouldn't need this... why doesn't .focus setter get called?
		.UpdateDebuggedApp(false)

		if .CheckCrashed(.feedback, "feedback")
			.Feedback = nil
		if .CheckCrashed(.compilr,  "compiler")
			.Compilr = nil

		|| f = .Feedback
			.SpeedieMsgReceive(f)
		.UpdateSearchText
		.UpdateStatusErrors
		.ReadLog
		.PopupDisplayFadeIn
		
		require .IsFront
		if ._FrameNum == 1
			require !.ProjClosed
			
		if .UpdateMem 
			.ShowLeftStats
		
		if ._FrameNum == 1
			.CheckModified
		
		if GUIActive and !.DebugArgsText.HasFocus
			.HideStuff


	function UpdateMemOne (|int| i, |uint| c, |bool|)
		require .LastMemory[i] != c
		.LastMemory[i] = c
		return true
		
		
	function UpdateMem (|bool|)
		require perryide.StatsMode == 3
		rz |= .UpdateMemOne(0, app.ObjMemory)
		rz |= .UpdateMemOne(1, app.stringmemory)
		rz |= .UpdateMemOne(2, app.OtherMemory)


	function CheckModified 
		.EditMe.document.HandleExternalModified?
		

	function ProjClosed (|bool|)
		require .CanAskingAboutMissing
		|| f = .project
		require !f.exists
		.CanAskingAboutMissing = false
		
		if ("Project "+f.name+" can't be found on the file-system anymore.\n\nWould you like to close it?").confirm
			.close
			rz = true
		  elseif .IsSingleFileProject
			f.write("")
				

	function ShowFind (|bool|)
		.retextsubfind
		.ViewAsProject
		.View(.ViewSearch)
		.MassSearchField.SelectAll
		.MassSearchField.SetFocus
		return true

	
	helper CmdKey (|keys| key, |bool|)
		if key is f
			if .masssearchfield.hasfocus
				.PrevView.Click?
				return .editme.OpenSearch
			return .ShowFind
		if key is k
			if super.KeyDown(key)
				return true
			|| t = .outtext 
			if t and t.text
				t.text = ""
				return true
			LogAreaKeys(.CommsLogList, key)
			return true
		if key is i
			FileViewerCountCode(.filelist, nil, .filelist.LastSelected.cell)
			return true
		if key is comma
			return ClickedPrevNext(.goprev)
		if key is period
			return ClickedPrevNext(.gonext)
		if key is b
			Clickedcompile(.buildbutton)
			return true
		if key is r
			if keys.Shift
				PressingDebug(.runbutton)
			  else
				ClickedDebug(.runButton)
			return true
		if key is o
			if keys.shift
				clickedopenfile(self)
			  else
				clickedopenproj(self)
			return true
		if key is n
			if keys.shift
				clickedCreatefile(self)
			  else
				clickedCreateproj(self)
			return true
//		if key is m
//			return .markup()
		if key is l // l = file list
			if keys.option or keys.Shift
				RightClickNext(.goprev)
				return .goprev.searchmenu
			  else
				return .ShowQuickStuff(true)
		if key is d
			return .ShowQuickStuff(false)
		if  key is s  and  .editme is visible
			.SaveEditing(documentstate.saving)
			return true
		|| NumKey = key.IsNumber
			ifn keys.Shift or keys.option
				if NumKey == 9
					.ASMButton.Click
					return true
				if NumKey == 8
					.flip(.ProductOut)
					return true
				if NumKey == 7
					.flip(.SpeedieText)
					return true
				if NumKey == 6
					return .FlipJeeboxField			
				if NumKey == 5
					return .StrongView(.ViewLog)
				if NumKey == 4
					return .StrongView(.ViewDebugger)
				if NumKey == 3
					return .SaveAndShowErrors
				if NumKey == 2
					return .StrongView(.ViewSearch)
				if NumKey == 1
					return .StrongView(.ViewFiles)
				if NumKey == 0
					.ViewTextModeSwap
					return true
		if key is p or backslash
			.CommandsMenu.Click
			return true
			

	function Flip (|textview| s, |bool|)
		|| Item = .compiletabs.selected
		if item  and  .ProductSep.isopen  and item.obj == s
			.ProductSep.collapse
			return false
		.ShowText(s, nil, true)
		return true


	function ShowQuickStuff (|bool| FuncMenu, |bool|)
		if FuncMenu
			if (.FunctionMenu.MenuIsOpen)
				FuncMenu = false
		  else
			if (.filelist.QuickOpen is visible)
				FuncMenu = true
		if FuncMenu
			.FileList.quickopen isnt visible
			return .FunctionMenu.SearchMenu
			
		  else
			|| sub = .FunctionMenu.SubMenu
				sub.CloseMenu
			if .FileList.ShowQuickOpen
				return true
			return .FunctionMenu.SearchMenu
			
	
	behaviour KeyDown
		if keys.cmd and .cmdkey(key)
			return true
		if (key is tab) and .OpenedMenu == .autocomplete
			return .autocomplete.doreturn
		if super.keydown(key)
			return true
		if .OpenedMenu or key.IsModifier
			return false
		require !keys.cmd
		
		ifn .focus isa textview // the main listview IS considered editable!
			.Editme.SetFocus	// should I just do this anyhow?
			return .editme.__KeyDown__(key)

	
	function GotoIncludedFile (|string| path, |bool|)
		opt norefcounts
		|| f = .FileList[path]
			.FileList.Select(f)
			return true
	
	
	function EditPath2 (|string| s, |bool|)
		opt norefcounts		// using the other seems to cause issues
		if s
			return .editfile(.FileList[s] ?? .FileList.AddExtra(s))


	function EditPath (|string| path, |bool| ChangeFocus=true, |bool|)
		require path.FileExists
		if ChangeFocus
			.Editme.SetFocus
		return .GotoIncludedFile(path) or .FileList.AddExtra(path)
	
		
	function Root (|filerow|)
		opt norefcounts
		return .FileList.start as filerow


	function file.NeedsOwnWindow (|bool|)
		if .IsProj
			return true
		if .isspd
			rz = .parse.Find(@tmp, "main")


	Syntax UsingComplete (|object| idk)
		(Idk)
		.StillJumping = false
		.AddToJumpHistory

		
	Syntax Using (|PerryIDE|)
		.AddToJumpHistory
		.StillJumping = true
		return self


	module
		|byte|						StatsMode
		
		function New (|file| path, |perryide!|)
			rz = PerryIDE(path, false)
			rz.LoadProject
			rz.EditMe.SetFocus
			if !Perry.FinishedStartUp
				Perry.WindowNumber++
		
		
		function EmptyBase (|perryide|)
			opt norefcounts
			for w in window.all
				if w isa perryide
					if !w.root
						return w
	
	
		function PerryOpenDoc (GUIPasteEvent)
			|| i = .ide
			if i?
				require data.fileexists
			  else
				i = perry.ProjContaining(data)
				i ?= perry.IdeAddProj(data, nil)
			return i.OpenDoc(data)


	function OpenDoc (|string| Path, |object|)
		require self
		|| p   = path
//		|| ext = p.ext
		|| f   = p.file
		if f.isdir
			p = p.pathdir
		|| ide = self
		if .GotoIncludedFile(p)
			//
		  elseif f.NeedsOwnWindow
			ide = perry.IdeAddProj(p)							// add as project
		  else
			.filelist.addextra(p)						// add root
		startupwindow.hide
		ide.ShowIDE
		return ide

		
	function LoadErrors (|bool| jump, |bool| actual_compile) 
		opt norefcounts
		|| txt = .EditMe
		|| doc = txt.document
		|| path = (doc.location, "")(doc)
		|| lst = .errorlist
		txt.ClearErrors
		.filelist.ClearErrors

		|error| jumpto 
		|| src = lst.ListSource
		|| ActualErrors = 0
		|| sel = txt.Selectedline
		if src.hasany and src mustbe error
			for err in src
				if err.severity >= ErrorSeverity.Error
					ActualErrors++
				if err.path ~= path
					if txt.AddErrorToLine(err) == sel
						txt.TryShowLineError(sel)
					if jump and (!jumpto or err == lst.FirstSelected)
						jumpto = err
				if actual_compile
					.CommsLogList <~ err.copy
				.filelist.ErrorAt(err)
				
		if jumpto
			txt.ErrorJump(jumpto.position)
		  elseif jump and actual_compile
			if .DuringMsgFB
				.DuringMsgFB = false
				lst.select(src.first)
		(self is broken) = ActualErrors > 0
		.RelistStartupErrors
		.CommsLogList.listsource.trim(144)


	function EditRow (|listviewrow| r, |int|)
		description `
-1 means "its OK, its already loaded"
1 means "its OK, we managed to load it"
So anything not 0 means "its OK"`

		if r isa filerow
			rz = .EditFile(r)


	function CurrFileRow (|FileRow|)
		opt norefcounts
		return .FileList.FirstSelected as filerow
	

	function Path (|string|)
		opt norefcounts
		return .editme.document$.location
	

	function EditFile (|FileRow| r, |int|) // editfile, loadfile, fileload, fileedit, openfile, fileopen
		require r and r.location and !.DuringLoad
		// the drawing thing needs a document too!
		// why not the same?
		// we could make editfield have an internal draw editor...
		// but at that point we might as well make a general viewer...
		// why not a 3D SDF viewer and audioplayer too.
		// or a fileviewer or a gamepreview player
		// better to have a generalised "fileviewer"
		|| g = (r isa "voi" or "png" or "qoi")
		if .IsGraphics != g
			.IsGraphics = g
			.DuringLoad++
			.ViewAsCurr
			.DuringLoad--
		|| ok = (.EditDraw(r), .EditText(r))(g)
		if ok != 1
			return ok

		|| s = r.DirFile.name
		if s[] == '.'
			s = r.name + "/" + s
		  else
			s = r.name
		.CurrFileName.text = s
		.DuringLoad++
		.filelist.select(r)
		
		if !g
			.LoadErrors(true, false)
			if .editme.FindExpectsParse < 2
				.ProductSep.Collapse
		
		.PopupHide
		.AddToJumpHistory
		if  !g  and  .IsEditingSource
			.Recheck
			.TryReselectASM
		  else
			.HideASM
		.DuringLoad--
		
		return 1
			

	function EditDraw (|FileRow| r, |int|)
		require .drawme.LoadFile(r.location)
		.HideASM
		return 1
		

	task CheckScrollErrors (taskgui)
		run
			|| e = .upon.editme 
			if e.myerror
				e.BumpScroll
			
	
	function EditText (|FileRow| r, |int|) // edittextfile, loadfile, fileload, fileedit, openfile, fileopen
		|| E	= .editme
		|| doc	= r.document
		if e.Document == doc
			return -1
		
		if doc.location.pathiscode
			doc.ExpectJeebox = TextView.AlwaysJeebox
		  else
			doc.ExpectJeebox = TextView.AllowJeebox

		|| Loaded = E.LoadDocument(doc, true)					#require
		|| m = .FunctionMenu.SubControl as menu
			m.StopFilter
		doc.CompareTreesOnSave = doc.ExpectJeebox
		.ListAllFuncs
		.CheckScrollErrors(0, gui.framestart + 0.25s)		
		|| it = .SharedProjectTabSource
			if !.DuringLoad
				it["edit"] = r.location
			it["pos"] = doc.Position

		return 1


	function HandleAltered
		// well it depends on if we modified shared files?
		for i in perry.OpenProj
			i.ModifiedForCompile = true
		.recheck				// could be delayed, too.


	function UpdateActiveFile
		|| d = .editme.Document
			|| l = d.Location.Resolve(true)
			if l ~= app.PrefsPath.resolve
				.UpdatePrefs
			  else
				|| p = .PerryFile
				if l ~= p
					.UpdatePerry(p)
	
	
	function UpdatePerry (|file| ProjConf)
		.startuperrors.clear
		error.SourceAdd(@tmp+projconf)
		if projconf.exists
			|| conf = projconf.parse
				|| root = .root
				root.RemoveAfter
				.LoadConfSub(conf)
		.LoadStartupErrors
	
	
	function UpdatePrefs
		|| OldPrefs = app._prefs
		app._prefs = nil
		app.prefs()
		if StdErr.ok
			.notify("Reloaded Prefs.")
		  else
			.notify("Prefs are corrupted")
			app._prefs = OldPrefs
		perry.LoadRecents
		Â¥.LoadFromPrefs


	function ParseErrorDetect
		// not necessarily an error!? what if the file is not spd?
		for e.toplist in .errorlist
			if e isa error
				if e is parse
					e.remove
		
		if .IsEditingSource	
			|| err = .editme.MyError
				ifn err isa error
					log "ParseErrorDetect bug"
				  elseif err is parse
					.errorlist <~ err // right?

	
	function LoadStartupErrors
		while (|| err = stderr.pop)
			notifications <~ err
			if err.IsBad
				.StartupErrors <~ error(err)


	function SaveDrawing
		.DrawMe.Save


	function SaveEditing (|int| Purpose) // savetext, savefile, filesave
		opt norefcounts
		if .DrawMe is visible
			return .SaveDrawing

		.ParseErrorDetect
				
		|| e = .editme
		if purpose == documentstate.errorchecking
			require e is modified
		// otherwise resave anyhow.
		
		if (!keys.shift and .projIsSpeedieCode)
			.filelist.saveall(Purpose)
		  elseif e.WasEverAltered
			require e.Save
		  else
			return
		
		.UpdateActiveFile
		with .currfilerow
			.lastsize = e.Length
			if .location.pathiscode
				self.HandleAltered
		(self is modified) = (e is modified)


function string.GetSpeedieProj (|string|)
	rz = self
	while rz and !(rz isa "scproj")
		rz = rz.parent

