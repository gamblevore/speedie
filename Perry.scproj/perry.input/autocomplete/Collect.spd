

function PerryIDE.MaybeSendMessage (|string| cmd,  |message| where,  |string| name="", |string| reason="", |bool|)
	#!sendmsg, msgsend, sendcmd, cmdsend
	|| fn = where.Func
	if fn == @char or @num
		.PopupHide
		return false
	if (fn == @name) and cmd[0, "autocomplete"]
		return false
	if (fn == @thg) and (where in @decl) and !where.isfirst
		return false
	.SetAutoPoint(where.Position)
	|| isString = (fn == @str or @sstr or @back or @bin)
		ifn cmd starts "autocomplete_sel"
			return .DisplayStr(where)
	  else
		return .SendFeedbackRequest(cmd, where, name, reason)


function PerryIDE.CanSend (|string| cmd, |bool|)
	if .LastCmdStr != cmd
		return true
	if !.LastSendTime or .LastSendTime.ago > 0.66s
		return true


function PerryIDE.SendFeedbackRequest (|string| cmd,  |message| where,  |string| name,  |string| reason, |bool|)
	|| msg = .BuildCmd(cmd, where, name, reason)		#require

	|| CmdStr = msg.RenderJbin
	if .CanSend(Cmdstr)
		.SendFeedbackMsg(msg)
	.LastCmdStr = CmdStr
	.LastSendTime = date.now
	return true


function PerryIDE.SendFeedbackStr (|string| op)
	if .IsSpeedieCode
		.SendFeedbackMsg(.checkmsg(op, false))
	

function PerryIDE.DateWrap (|message| msg, |message|)
	rz = @arg + "Perry"
	|| d = date.now
	rz <~ d
	rz <~ msg
	if msg.name[0, "autocomplete"]
		.LastAutoCompleteDate = d


function PerryIDE.SendFeedbackMsg (|message| msg)
	require msg and .IsSpeedieCode
	|| fb = .FeedbackInit(.feedback)			#require
	.feedback = fb
	|| record = .DateWrap(msg)
	fb <~ record
	.SpeedieMsgReceive(fb)
	.LogOneComm(msg, "\1C ")


function PerryIDE.FeedbackInit (|SpdProcess| C, |SpdProcess|)   // InitCompiler, initfeedback
	if c
		if !c.subprocess and C.TimedOut(5s)
			c.Deactivate
		if c.IsOpen
			return c
		if c.SubProcess
			return c // can't deactivate it.
		if c.ActivateChild
			return c
		return nil // ded?

	|| p = [.project, "--perry=bin", "--nocolor"]
	|| f = .projmsg[@nil]$
	|| name = .name
	|| Noisy = false//SpdLibAlreadyInUse
	if noisy
		p <~ "--noisy"		

	p <~ "--check"
	
	// 
	|SpdProcess| spd 
	
	if !SpdLibAlreadyInUse
		SpdLibAlreadyInUse = true
		target debug {
		if !.UsingSpdLib
			.UsingSpdLib = true
			f.name = name.TrimExt + "()"
		spd = (Speedie_Main).RunThreaded(p)
		}

	if !spd?
		|| proc = speedie.resolve
		target debug
			proc = "/usr/local/speedie/Speedie.scproj/Build/Products/Debug/Speedie"
		if proc
			|| Mode = ProcessMode.CaptureAll * !noisy
			spd = SpdProcess.RunChild(Proc, p, .project.name, mode)
	if noisy and spd
		spd.pico.config.noise = -1
	return spd


function PerryIDE.ActualCompile (|string| operation)
	if .compilr: return app.beep // wah?
	
	require .filelist.SaveAll(documentstate.saving)
	|| args = [.project, "--perry=bin", "--nocolor"]
	if .DebugIsWaitingForCompile
		args <~ "--usescriptloc"

	.compilr = process.Stream(speedie, args)		#require
	.SpeedieText.text = ""
	
	(.BuildButton isnt enabled)
	(.BuildButton isnt visible)
	(.RunButton isnt enabled)
	(.Progress is visible)

	is active
	.ShowCompileTab(.speedietext, .name ~= "Speedie.scproj")
	.ViewAsCurr


target debug
	cpp_libwrapper Speedie_Main (SpdMainFn)
|| SpdLibAlreadyInUse = false
