

function PerryIDE.MaybeSendMessage (|string| cmd,  |message| where,  |string| name="", |string| reason="", |date|)
	#!sendmsg, msgsend, sendcmd, cmdsend
	|| fn = where.Func
	.SectionKeyTime = nil
	if (fn == @name) and cmd[0, "autocomplete"]
		return nil
	|| isString = fn.IsString
		if where.ShouldDisplayString
			ifn cmd starts "autocomplete_sel"
				.SetAutoPoint(where.Position)
				return .DisplayStr(where)

	if (fn == @char or @num) or fn.isstring
		.PopupHide
		return nil

	.SetAutoPoint(where.Position)
	return .SendFeedbackRequest(cmd, where, name, reason)


function PerryIDE.SendFeedbackRequest (|string| cmd,  |message| where,  |string| name,  |string| reason, |date|)
	|| msg = .BuildCmd(cmd, where, name, reason)		#require

	|| CmdStr = msg.RenderJbin
	if .CanSend(Cmdstr)
		rz = .SendFeedbackMsg(msg)
	.LastCmdStr = CmdStr
	.LastSendTime = rz


function message.ShouldDisplayString (|bool|)
	if self == @bin
		return true
	require self == @str or @char
	require .name.LengthUTF8 < 350
	return .name.Escape != .name
	

function PerryIDE.CanSend (|string| cmd, |bool|)
	if .LastCmdStr != cmd
		return true
	if !.LastSendTime or .LastSendTime.ago > 0.66s
		return true


function PerryIDE.SendFeedbackStr (|string| op)
	if .projIsSpeedieCode
		.SendFeedbackMsg(.checkmsg(op, false))
	

function PerryIDE.DateWrap (|message| msg, |date| d, |message|)
	rz = @arg + "Perry"
	rz <~ d
	rz <~ msg
	if msg.name[0, "autocomplete"]
		.LastAutoCompleteDate = d


function PerryIDE.SendFeedbackMsg (|message| msg, |date|)
	require msg and .projIsSpeedieCode
	|| fb = .FeedbackInit(.feedback)			#require
	.feedback = fb
	|| d = gui.framestart
	|| record = .DateWrap(msg, d)
	fb <~ record
	.SpeedieMsgReceive(fb)
	.LogOneComm(msg, "\1C ")
	return d


function PerryIDE.FeedbackInit (|SpdProcess| C, |SpdProcess|)   // InitCompiler, initfeedback
	if c
		if !c.subprocess and C.TimedOut(5s)
			c.kill(32) // epipe
		if c.SubProcess
			return c // can't deactivate it.
		if c.ActivateChild
			return c
		return nil // ded?

	|| f = .SharedProjectTabSource[@nil]$
	|| p = [.project, "--perry=bin", "--nocolor"]
	|| Noisy = SpdLibAlreadyInUse
		p <~ "--noisy"
	p <~ "--check"
	
	target debug and !cake and !exec
		if !SpdLibAlreadyInUse
			SpdLibAlreadyInUse = true
			if !.UsingSpdLib
				.UsingSpdLib = true
				f.name = .name.TrimExt + "()"
			|| spd = (Speedie_Main).Spawn(f.name, p)
				return spd

//	|| proc = file.speediedir.child("Speedie.scproj/Build/Products/Debug/Speedie")
	|| proc = speedie.resolve
		|| Mode = ProcessMode.CaptureAll
		if !noisy
			mode = ProcessMode.silence
		|duration| Delay = 0
		if !Perry.FinishedStartUp
			Delay = 0.125s * Perry.WindowNumber
		|| spd = SpdProcess.spawn(Proc, f.name, p, mode)
			spd.pico.name = .project.name
			return spd
	


function PerryIDE.ActualCompile (|string| operation)
	(Operation) // i guess we'll use this later. debug vs compile
	if .compilr: return app.beep // wah?
	
	require .FileList.SaveAll(documentstate.saving)
	|| IsSCProj = .name ~= "Speedie.scproj"
	|| args = [.project, "--nocolor"]
	if .DebugIsWaitingForCompile
		args <~ "--usescriptloc"
	if IsSCProj
		args <~ "--perry"
	  else
		args <~ "--perry=bin"

	.LogOneAction(@emb + speedie + " " + args.render)
/	.compilr = process.Stream(speedie, args, processmode.captureall)		#require
	.SpeedieText.text = ""
	
	(.BuildButton isnt enabled)
	(.BuildButton isnt visible)
	(.RunButton isnt enabled)
	(.Progress is visible)

	(self is active)
	.ShowCompileTab(.SpeedieText)
	.ViewAsCurr


// how should a lib-wrapper be called by a cake file?
// hmmmm. well... basically we don't have access to that
// so it shouldn't be compilable.

target debug
	target exec
		cpp_libwrapper Exec_Main (spdprocess.Thread)
	  else
		cpp_libwrapper Speedie_Main (spdprocess.Thread)
|| SpdLibAlreadyInUse = false

