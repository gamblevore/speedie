


function PerryFuncListFilter (GUIListEditEvent)
	opt norefcounts
	.ide.listallfuncs(false, value)


function MenuAllFuncs (GUIaction)
	opt norefcounts
	if self isa buttonview
		.ide.ListAllFuncs(true, "")
		.OpenMenu


function perryide.ListAllFuncs (|bool| sel=false, |string| filter="")
	|| submenu = .FunctionMenu.submenu!
	|| result = submenu.listsource
	result.clear
	.ListAllFuncsMsg(.editme.Parse, result, filter)
	if sel
		.FindSelectedMenuItem(result)
	result.obj = (.editme._Start, nil)(sel)
	result.Sort(FuncListSorter)
	(.FunctionMenu is visible) = filter or result.HasAny
	.FunctionMenu.text = "(" + result.SlowCount.Render + ")"
	.ImproveFuncList(Result)


function message.FuncAtPos (|int| n, |message|)
	opt norefcounts
	for s in self
		if s.position > n
			return s.prev
	return .last


function PerryIDE.FindSelectedMenuItem (|message| result)
	opt norefcounts
	require result.hasany
	|| fn = result.FuncAtPos(.editme.sel0 )
	.FunctionMenu.submenu!.select(result)
	
	
function PerryIDE.ImproveFuncList (|message| result)
	opt NoRefCounts
	|message| p0
	for s.fast in result
		|| p = s[@nil][@nil]
			if !p0 or !p.ShallowEquals(p0, true)
				|| d = listviewrow()
				d.name = "------<" + p.name + ">------"
				(d is disabled)
				s.prev = d
			|| n = s.first!.name
			if p != @name
				n = "." + n 
			s.name = n
			p0 = p



function PerryAutoCompleteSelected (GUIListEvent)
	opt norefcounts
	require mode isnt initialselection

	|| i = .ide
	i.DuringTempView++

	|| e = i.EditMe
	|| p = row.firstname
	if !row
		p = .ListSource.obj as string
	if p and i.EditPath(p)
		|| rl = row[2]
			e.ShowAndSel(rl.int, true)

	i.DuringTempView--
	

function PerryFuncMenuSelected (GUIListEvent)
	opt norefcounts
	|| e = .ide.EditMe
	if row
		e.ShowAndSel(row.position)
		return false
	|| obj = .ListSource.obj as TextViewLine
		e._Start = obj


function FuncListSorter (SorterComparerMsg)
	opt norefcounts
	|| aa = a[]
	|| bb = b[]
	if (!aa or !bb)
		return aa < bb
	if aa.func != bb.func
		return aa.func|int| < bb.func|int|
	
	if aa == @emb
		return bb.name.SyntaxCompare(aa.name, true) < 0
	
	|| af = aa[]
	|| bf = bb[]
	if (!af or !bf)
		return af < bf
	|| an = af.name
	|| bn = bf.name
	|| cmp = bn.SyntaxCompare(an, true)
	cmp ?= (bf.func|int| - af.func|int|)
	cmp ?= bb.name.SyntaxCompare(aa.name, true)
	return cmp < 0


function message.FuncArg (|message|)
	opt norefcounts
	|| s = .last
	if s == @arg
		return s


function perryide.ListAllFuncsMsg (|message| items, |message| result, |string| filter)
	opt norefcounts
	for line in items
		if line == @decl
			0 // result.IDEAddfunc(line, filter)
		  elseif line == @tmp
			if line ~= "target"
				|| arg = line.last
				if arg == @arg
					.ListAllFuncsMsg(arg, result, filter)
			  elseif AutoComplete.owner.valuelower(line.name)						// class/module/etc
				.ListAllFuncsMsg(line.funcarg, result, filter)
			  elseif AutoComplete.isafunc(line)
				result.IDEAddfunc(line, filter)


function message.IDEAddfunc (|message| line, |string| filter)
	opt norefcounts
	if line != @decl
		|| name = line.OwningFullName(@emb) 
		if name and filter.filter(name.name)
			|| fn = .msg(@str)
			fn.position = line.position
			fn <~ name


function string.Filter (|string| find, |bool|)
	if !find
		return true
	for s in .words
		if s
			require find contains s
	return true

