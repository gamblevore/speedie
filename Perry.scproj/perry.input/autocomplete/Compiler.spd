

extend PerryIDE
	function Recheck 
		if .ModifiedForCompile
			.ForceRecheck
		|| p = .path					#debug_require
		|| unused = @tmp + "unused_funcs"
		unused <~ p
		|| lf = .LostFunks
			unused <~ lf.copy
		.SendFeedbackMsg(unused)
		
	function ForceRecheck
		.ModifiedForCompile = false
		.Feedback = nil
		if .IsSpeedieCode and !.UsingSpdLib
			.AskCheck
	
	function AskCheck
		.SendFeedbackStr "check"

	syntax is (|ListViewState| s, |bool|)
		return .projmsg.syntaxis(s)
		

	syntax is (|ListViewState| s, assigns:|bool| mod)
		opt norefcounts
		.projmsg.syntaxis(s) = mod
		
		if (s == listviewstate.modified)
			|| sub = .CurrFileName.SubMenu
				(sub.ListSource is modified) = Mod


	function ClearErrors (|bool| Compiled)
		isnt Broken
		.CompileOKString = ("Compile OK", "Check OK")(Compiled)
		.errorlist.Clear
		.editme.ClearErrors
		.filelist.ClearErrors
		.RelistStartupErrors
		.UpdateErrorCount
		
	
	
	function CheckedOK (|string| kind, |bool|)
		.ClearErrors(kind=="compile")
		if .viewing == .ViewErrors
			.NoNeed(.CompileOKString, .viewfiles)
		return true

	function message.ActualReplaceError (|string| src)
		|| SubError = src.parse.first
			.ReplaceWith(suberror)
	
	function message.CleanErrors
		opt norefcounts
		for e in self
			|| a = e.arg
				|| f = a["file"]
				|| str = a.firstName
				if (f ~= "(string)") and (str[0, "error\n"])
					e.ActualReplaceError(str)


	function ShowCompileErrors (|message!| msg, |bool|)
		|| MaybeOK = msg.last
		if (MaybeOK == @thg and "ok")
			.ErrorList.clear
			.UpdateErrorCount
			return .CheckedOK(msg.name)
	
		// inconsistant input...
		|| arg = msg
		if arg != @arg
			arg = msg.arg
		  else
			maybeok = msg
		arg.cleanErrors
		
		is Broken
		.NoNeed
		|| newlist = error.from(MaybeOK)
		newlist := Error()
		.errorlist.listsource = newlist
		.UpdateErrorCount
		if .ErrorList isnt Visible
			beep
		.ShowErrors


	function UpdateErrorCount
		opt norefcounts
		|| h = .errorlist.headers
			|| n = .errorlist.listsource.slowcount
				h.name = n of "error"
			  else
				h.name = ""

	function Compile (|string| operation)
		if .isBuilding // they pressed it twice
			if .ProductSep.div.Collapsed
				return .ShowCompileTab(.SpeedieText, true)
			require ClickedCancel(.BuildButton)
		.ActualCompile(operation)
			
	
	function CheckMsg (|string| operation, |bool| SaveAll, |message|)
		if SaveAll
			require .filelist.SaveAll(documentstate.saving)
		|| cmd = @tmp + operation
		cmd <~ .Project.path
		return cmd


	function DebugFB (|message| msg)
		// || pp = .ProductPath
		// we actually... hmmm... can't open the debugger yet. not until it's made!
	
		
	function ErrorListFB (|message| msg, |bool|)			// errorfb, errorsfb
		|| RealErrors	= msg ~!= "check"
		.ClearErrors(RealErrors)
		|| NoErrors		= .ShowCompileErrors(msg)
		|| ErrsVis		= .isviewing(.ErrorList) 
		.errorlist.SelectFirst
		if !RealErrors
			0
		  elseif NoErrors
			if errsvis
				.view(.viewfiles)
			if msg ~= "debug"
				.debugFB(msg)
		  elseif !errsvis
			.view(.viewerrors)
			.say(" failed. Could not compile")
		.LoadErrors(RealErrors, realerrors)
		.showstats


	function LogFB (|message| msg)
		// ignore.
	
	
	function Readlog (|bool| Killed=false)
		|| c = .Compilr $
		.speedietext <~ c.ReadStdOut
		require !c.IsRunning or Killed

		|| OK = !killed and .TestCompilerAlive

		|| errs = c.ErrorOutput.GetResult
			|| p = errs.parse
				.ShowCompileErrors(p)
			// notifications will pick up the err otherwise!
		  elseif OK
			|| msg = @tmp + "compile"
			msg <~ (@thg, "ok")
			.ShowCompileErrors(msg)
			.CompiledOK
		.compilr = nil
		.RenableAfterCompile


	function Say (|string| str)
		target osx
			if !.IsFront or SDLApp.IsBackground
				.Talker = Process.stream("/usr/bin/say", [.name.trimext + str])
	
	
	function CompiledOK
		|| db = .DebugIsWaitingForCompile
		.CheckedOK("compile")
		|| msg = .speedietext.text.parse
		
		|| last = msg[@tmp, -1]
		if last.expect(@tmp, "Product")
			|| path = last[@nil]
			if path.ExpectString
				|| file = @url + "file"
				file <~ path
				|| str = "\n\nCompiled OK!"
				|| f = path.name.file
				if !f.exists
					str += " But it doesn't exist :("
				.notify(.name + str, file)
				.say(str)
				if db
					.StartDebugging(f)
					
				
	function Notify (|string| What, |message| response=nil)
		|| c = what
		if response
			c = what + ("\n" + response.render) // hmmm... seems awkward
		.LogOneComm(@emb + ("\1b " + c))
		notifications <~ (what, response)
	
	
	function CancelCompile (|string| why="CompiledOK")
		|| c = .Compilr
			c.Kill
			app.Sleep(0.025) // allow pipes to work
			.Readlog(true)
		  else
			.RenableAfterCompile
	
	function RenableAfterCompile
		.DebugIsWaitingForCompile = false
		(.buildbutton is enabled)
		(.buildbutton is visible)
		(.runbutton is enabled)
		(.progress isnt visible)
		.HideStuff
		isnt active

