

function DebugArgsKeyDown (GUIKeyEvent)
	opt norefcounts
	if key.IsCancel
		.window!.ClearFocus
		return true
	

function FoundListKeys (GuiKeyEvent)
	opt norefcounts
	if key.IsDelete
		if self isa ListView
			|| s = .FirstSelected
				.RemoveRow(s)
		return true


function DebugArgsGotFocus (guiaction)
	|| v = state == 1
	if (self is visible) != v
		(self is visible) = v
		if self isa textview
			.UpdateDebugArgs(v)


function textview.UpdateDebugArgs (|bool| Visible=false)
	|| f = .ide.DebugArgsFile
	if !.Document
		.LoadDocument(Document(f))
	if !.text
		.text = defaultfiles.DebugArgs
	if visible
		.window.SetInnerFocus(self)?
	  else
		.Save
		

function fnStillRunning (fnConfirmCanceller)
	if self isa perryide
		.UpdateDebuggedApp(false)
		if !.RunCompiledApp
			return true

	
function Window.Confirm (|string| Q, |fnConfirmCanceller| fn, |bool|)
	|| can = ConfirmCanceller(self, fn)
	if q.confirm(can)
		return true
	

function ClickedDebug (GUIAction)
	|| i = .ide!
	if .text == "\02"		// play
		i.DebugIsWaitingForCompile = true
		i.compile("debug")
		i.ShowText(i.ProductOut, "", false)
		|| itm = i.CompileTabs.items[3]
			(itm is hidden)
		i.ProductErrors.text = ""
	  elseif .text == "\07" // stop
		|| prod = i.runcompiledapp
		if prod and i.confirm("Kill running product " + i.Name + "?", fnStillRunning)
			prod.kill
			i.UpdateDebuggedApp(true)


function PressingDebug (GUIAction)
	|| d = .ide!.DebugArgsList
	DebugArgsGotFocus(d, 1)


function perryide.DebugEnvs (|message| env)
	opt norefcounts
	for rel in env
		|| ch2 = rel[2]
		if ch2 and rel[1] == "="
			app.ChildEnv(rel.first!.name) = ch2.name


function PerryIDE.DebugActualStr (|file| f, |[string]| args, |faststring|)
	rz = FastString()
	rz <~ f.name
	rz <~ ":"
	for a in args
		rz <~ ","
		start
		rz <~ " "
		rz.AppendQuotedEscape(a)


function perryide.DebugActual (|file| f, |[string]| args) // actuallyruncompiledapp, runproduct, actualdebug, debugproduct
	|| out = .DebugActualStr(F, args)
	.LogOneAction(@emb + out.Copy)
	out <~ ("\n", 2)
	|| r = Process.stream(f, args, out)
	.RunCompiledApp = r
	if r and r.PID
		.RunButton.text = "\07"


function perryide.DebugPath (|file| f, |message| conf)
	|| p = .Project
	if !p.isdir
		p = p.Parent
	app.cwd = p
	
	|| pwd = conf["pwd"]
		|| path = pwd.goodname
		if pwd != "."
			app.cwd = path


function PerryIDE.StartDebugging (|file!| f)
	require f.MustExist and StdErr.ok

	.DebugArgsList.UpdateDebugArgs
	|| debugargs = .DebugArgsFile.parse				#require
	|| ArgMsg = DebugArgs["args"]					#require
	ArgMsg = ArgMsg.Copy
	for a in ArgMsg
		if a == @arel
			expect (a == "?") (a)
			|| str = a.firstname.Ask				#require
			a.Become(@str, str)
			a.clear
				
	|| args = argmsg.AllNames
	if debugargs["flags"]["open"].yes
		check (!args) (ArgMsg, "You can't pass arguments, when using open-flags")
		args = [f]
		f = "/usr/bin/open".file
	  elseif f.IsDir and Platform.OSX
		f = f.Child("Contents/MacOS/" + f.TrimExtAndPath)!
	
	|| old = app.cwd
	.DebugPath(f, debugargs)	
	.DebugEnvs(debugargs["env"])		
	.DebugActual(f, args)

	if !StdErr.ok
		.DebugArgsList.FillErrors(StdErr)
		|| err = StdErr.LastError
			Notifications <~ err
	app.cwd = old


function perryide.CompileTab (|int| i, |listviewrow|) // for autocomplete
	return .CompileTabs.items[i]


function perryide.AddProductErr (|string| err)
	if err
		.ProductErrors <~ err
		|| t = .CompileTab(3)$
		if .ProductSep.iscollapsed or t is hidden
			.ShowText(.ProductErrors, nil, true)
		(t isnt hidden)


function perryide.UpdateDebuggedApp (|bool| Kill, |bool|)
	|| r = .RunCompiledApp						#require

	|| rout = r.Output
	if !(r.userflags&1) and rout and rout.length
		.ShowCompileTab(.productout, true)
		r.userflags |= 1
	
	r.step
	if kill or !r.IsRunning
		.RunCompiledApp = nil
		.RunButton.text = "\02"
	
	.ProductOut <~ rout
	.AddProductErr(r.ErrorOutput)
	
	if kill
		.ProductOut <~ "\n(--Process Killed--)"
	  elseif !r.IsRunning
		|| exitcode = "\n(--Exit OK--)"
		if r.exit > 0
			exitcode = "\n(--Exit Error: ${r.Exit}--)"
			.addProductErr(exitcode)
		.ProductOut <~ exitcode
	
	return !kill
		
