

function ButtonView.FillMenuPath (|string| L, |listviewrow| tab=nil)
	|| p = .SubMenu		#require
	|| orig = l
	p.listsource.clear
	while l
		|| S = filerow.icon(l, "").render + " " + l.NameOrRoot
		|| dirfile = (".nothing", "")(l != orig)
		|| row = filerow(l, s, dirfile)
		(row isnt editable)
		p <~ row
		if l.IsVolume
			exit
		l = l.parent
	.OpenMenu(tab)


function string.NameOrRoot (|string|)
	opt norefcounts
	|| s = .name
		return s
	return "Root"


function PerryIDE.EnablePrevNext
	|| m = .FindInHistory(.path)
	(.GoNext is enabled) = m.next!=nil
	(.GoPrev is enabled) = m.prev!=nil


function PerryIDE.FindInHistory (|string| p, |message|)
	opt norefcounts
	for s in .RecentFiles
		if s.firstname ~= p
			return s


function PerryIDE.AddToJumpHistory
	require !.StillJumping
	|| p = .path
	|| old = .findinhistory(p)
	if  !.duringNextPrev  or  !old
		if old
			old.remove
		.RecentFiles.msg(@str, p.name).msg(@str, p)
		.RecentFiles.trim(60, false)
	.EnablePrevNext
	

function RightClickNext (GUIaction)
	if self isa ButtonView
		|| s = .SubMenu
			s.OnTypingFilter = PerryFileListFilter
			s.ListSource = .ide.SortedPrevious
			.OpenMenu


function ChosePrevItem (GUIListEvent)
	|| i = .ide
	|| old = i.FindInHistory(row.firstname)
	i.GoToMarker(old ?? row, 1)
	return true


function ClickedPrevNext (GUIaction)
	opt norefcounts
	|| i = .ide
	|| m = i.FindInHistory(i.Path)
		|| fwd = self == i.gonext
		|| m2 = m.step(fwd)
		if m2 and m != m2
			i.GoToMarker(m2, fwd.dir)
		  else
			beep
		return true
	

function PerryFileListFilter (GUIListEditEvent)
	opt norefcounts
	.ListSource = .ide.SortedPrevious(value)


function PerryIDE.GoToMarker (|message| m, |int| dir)
	require m 
	.duringNextPrev = dir
	using self
		.EditPath(m.firstname)
	.duringNextPrev = 0


function PerryIDE.SortedPrevious (|string| StrFilter = "", |message|)
	rz = Message()
	for ch in .RecentFiles
		if StrFilter.filter(ch.name)
			|| cpy = ch.copy
			rz <~ cpy


function PressingTabs (guiaction)
	if self isa ButtonView
		|| p = .submenu|listview|
			|| ide = row.ide
				p.ListSource.clear
				p <~ ("Copy " + ide.name, MenuCopyProject, ide)
				|| file = ide.path|string|
				if file and file != ide.Project|string|
					p <~ ( "Copy " + file.name, MenuCopyFile, ide)
				p <~ ("Show In Desktop", MenuShowProject, ide)
				p <~ ("Close", MenuCloseProject, ide)
				.OpenMenu(row)


function message.IDE (|perryide|)
	opt norefcounts
	if self
		|| o = .MenuIDE
			return o
		for w in window.all
			if w isa PerryIDE
				if .name ~= w.Project
					return w


function ClickedOpenRecent (GUIaction)
	if self isa ButtonView
		|| s = .SubMenu
			s.ListSource = Perry.RecentProjects
			.OpenMenu


function list.MenuIDE (|Perryide|)
	opt norefcounts
	|| o = .Obj
	if o isa GuiControl
		if o isa PerryIDE // leak!
		return o.ide
	
	
function MenuCopyProject (guimenuevent)
	opt norefcounts
	|| ide = menu.MenuIde
		clipboard.notifiedtext = ide.project


function MenuCopyFile (guimenuevent)
	opt norefcounts
	|| ide = menu.MenuIde
		clipboard.notifiedtext = ide.Path


function MenuCloseProject (guimenuevent)
	opt norefcounts
	|| ide = menu.MenuIde
		ide.Close

function ClickedTabs (GUIAction)
	opt norefcounts
	|| i = row.ide
		i.ShowIDE(True)


function MenuShowProject (guimenuevent)
	opt norefcounts
	|| ide = menu.MenuIde
		ide.Project.Reveal
	

function RightClickedTabs (GUIAction)
	if row and self isa ButtonView
		|| i = row.ide
			.FillMenuPath(i.Project, row)

