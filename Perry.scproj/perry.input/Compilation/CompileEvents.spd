

function PerryIDE.DebugArgs (|message|)
	return .DebugArgsText.text.Parse(@arg)	

	

// RUN COMPILE
function PerryIDE.StartDebugging (|file!| f)
/	require f.MustExist and StdErr.ok

	.DebugArgsText.InitDebugArgs
	|| DebugArgs = .debugargs						#require
	|| ArgMsg = DebugArgs["args"]					#require
	
	ArgMsg = ArgMsg.Copy
	for a in ArgMsg
		if a == @arel
			expect (a == "?") (a)
			|| str = a.firstname.Ask				#require
			a.Become(@str, str)
			a.clear
				
	|| args = ArgMsg.DebugArgNames
	if DebugArgs["flags"]["open"].yes
		check (!args) (ArgMsg, "You can't pass arguments, when using open-flags")
		args = [f]
		f = "/usr/bin/open".file
	  elseif f.IsDir and Platform.OSX
		f = f.Child("Contents/MacOS/" + f.TrimExtAndPath)!
	
	|| old = app.cwd
	.DebugPath(debugargs)	
	.DebugEnvs(debugargs["env"])		
	.DebugActual(f, args)

	if !StdErr.ok
		.DebugArgsText.FillErrors(StdErr)
		|| err = StdErr.LastError
			Notifications <~ err
	app.cwd = old




// when do we call --bake? For now, I guess always? Depends...
// if I mean are we building or trying to debug?

function PerryIDE.ActualCompile (|CompileMode| Mode)
	if .compilr: return app.beep // wah?
	
	require .FileList.SaveAll(DocumentState.saving)
	|| args = ["--nocolor"]
	if .DebugIsWaitingForCompile
		args <~ "--usescriptloc"

	if .name ~= "Speedie.scproj"
		args <~ "--perry"
	  else
		args <~ "--perry=bin"

	if Mode is Debug or Cake  
		args <~ "--bake"
	args <~ .project

	|| Name = speedie + " " + args.render
	.LogOneAction(@emb + name)
/	.compilr = UnixProcess.Stream(speedie, args, processmode.captureall)		#require
	.SpeedieText.text = ""
	
	(.BuildButton isnt enabled)
	(.BuildButton isnt visible)
	(.RunButton isnt enabled)
	(.Progress is visible)

	(self is active)
	.ShowCompileTab(.SpeedieText)
	.ViewAsCurr



function ClickedCompile (GUIaction)
	|| DebugArgs = .ide.DebugArgs	#require

	// what about compiling to cake? we need it in the GUI or conf
	.ide.compile(CompileMode.compile)


function ClickedDebug (GUIAction)
	|| i = .ide
	if .text == "\02"		// play
		require i.MakeNothingRunning(" before debugging?")
		i.DebugIsWaitingForCompile = true
		i.compile(compilemode.Debug)
		i.ShowText(i.ProductOut, "", false)
		|| itm = i.CompileTabs.ButtonSource[3]
			(itm is hidden)
		i.ProductErrors.text = ""
	  elseif .text == "\07" // stop
		require i.MakeNothingRunning("?")


function PerryIDE.Compile (|int| CompileMode)
	if !.isBuilding // they pressed it twice
		.ActualCompile(CompileMode)
		return 
	if !.ExpandCompileSep					// appeared to do nothing
		ClickedCancel(.BuildButton)			// might as well just do the next thing then

