


class StartupWindow (window) // splashscreen, startupscreen, startwindow
	linkage: cpp_part ttup

	behaviour TryClose
		if perry.Projects.hasany
			return true
		Notifications <~ "Open a project or create a new one, to use Perry"
		beep
		
	module
		|StartupWindow|			_win

		function Open (guiaction)
			startupwindow.Show

		function Hide
			opt norefcounts
			|| w = ._win
				w.hide
				w.closedialog

		
		function Show
			opt norefcounts
			|| w = .create
			if GUI.startedproperly
				(w)()
			  else
				w.show


		function CreateOption (|string| name,  |string| icon,  |guiaction| fnc,  |string| tooltip)
			|| b = buttonview(._win,	fnc)
			|| t = buttonview(._win,	fnc,		nil)
			
			(t isnt editable) 
			t.text		= "\n" + name
			t.tooltip	= tooltip
			b.tooltip	= tooltip
			b.text		= icon
			b.id		= name
			t.id		= name
			t.takesfocus = 0
			b.PressMakesLighter = true
			t.PressMakesLighter = true
			b.gnormal.solid = (1.0, 1.0, 1.0, 0.15)
			t.gnormal.solid = (1.0, 1.0, 1.0, 0.15)
			
			b.size 		= 4~right for 30 by 4~after for 30 
			t.size 		= 6~after to 6~left  by  the~same
			t.align		= ListViewState.aligncenter


		function Create (|startupwindow|)
			opt norefcounts
			rz = ._win
			if rz
				return rz
			rz = StartupWindow()
			._win = rz
			
			with rz
				.id				= "startup"
				.size			= 200 by 140
				.gnormal        = Â¥.SystemStyle.copy
				.gnormal.solid = ((0.01, 0.05, 0.01, 0.9))
				.ClickOutsideCloses = true
				isnt visible

			.CreateOption("Open Project",	"\1a",	ClickedOpenProj,   "Open a Speedie project.")
			.CreateOption("Open File",		"\11",	ClickedOpenFile,   "Open a text file.")
			.CreateOption("Create Project", "\19+",	ClickedCreateProj, "Create new Speedie project!")
			.CreateOption("Create File",	"\11+",	ClickedCreateFile, "Create new text file.")



function ClickedOpenProj (GUIaction)
	|| start		= "~/".resolve
	|| path			= app.AskOpen("spd,scproj/,input/", start, "Open Project", "Open", "OpenProj")	#require
	|| better_path	= path.GetSpeedieProj
		perry.IdeAddProj(better_path)
	  else
		perry.IdeAddProj(path)
	perry.HideStartIfNeeded


function ClickedCreateProj (GUIAction)
	|| path = app.AskSave("scproj/",  "",  "New Project",  "Create", "OpenProj")
		perry.CreateProj(path)
	perry.HideStartIfNeeded


function ClickedOpenFile (GUIaction)
	|| file = app.AskOpen("",  "",  "Open File", "Open", "OpenProj")
		perry.IdeAddProj(file)
	perry.HideStartIfNeeded


function ClickedCreateFile (GUIaction)
	|| path = app.AskSave("spd",  "",  "New File",  "New", "OpenProj")
		perry.CreateProj(path)
	perry.HideStartIfNeeded


function perry.HideStartIfNeeded
	if .projects.hasany
		startupwindow.hide
		
