#!/usr/local/bin/spd
import "GUI"

// possible games:
	// 3d arkanoid with 3d balls?
	// bubbl-tank?
	// bubble-bobble? (maybe not, needs sprite artwork)
	// that fruit-merging game?
	// clouds dropping rain or fire or somthing and you have to burn people? (or tanks)


main
	gui.init(3)
	return RayScene().RunAsMain(True)



class RayScene (window)
	|date| 			LastDraw
	|raytracer|		T
	|textview| 		FPS
	|date| 			LastFPSChange

	constructor
		.t = RayTracer((1.6, 5.0, -22.0))
		.FPS = TextView(self)
		.fps.MakeInline
		
		|| L = 10.0
		for i in (-L to L)
			|| sh = (i, 0.0, i.abs)*24.0
			|| C = vec4((2.0-i)*0.5, 1, 1, 1)
			.T.shapes <~ vobject(sh+(-1.0, 1.5, 12.0), 2.0, C)
			.T.shapes <~ vobject(sh+( 1.5, 2.5, 18.0), 3, C)
			.T.shapes <~ vobject(sh+( 7.0, 8.0, 18.0), 4, C)
			.T.shapes <~ vobject(sh+( -2.0, 9.0, 24.0), 6, C)
			.T.shapes <~ vobject(sh+( 12.0, 3.0, 24.0), 4, C)


	function PrepareDraw
		|| Ago = .LastDraw.ago
		.LastDraw = date.now
		.LastFPSChange ?= .LastDraw
		if .LastFPSChange.ago > 0.5s
			|| fps = (1.0/ago.Float).Render(1) + "fps"
			.fps.text = fps
			.LastFPSChange = .LastDraw
		
		
	behaviour MouseMove
		.t.pitch -= move.y|float| * 0.0001
		.t.spin += Move.x|float| * 0.0005
	
	
	behaviour Drawbackground
		opt norefcounts
		.PrepareDraw
		.T.RayTrace(where)
		gui.Touch
	
	
	behaviour FrameStep
		|| m = keys.Movement
			m = m.keyfix((0.6, 0.25, 5.0))
			|| P = m.xyz
				.t.Camera += p.normal * .t.ViewMat
			.t.spin += m.w*0.003


function float.KeyFix (|vec3| V, |float|)
	if self
		return ((.abs * v.r + v.g) min v.b).CopySign(self)


function vec4.KeyFix (|vec3| V, |vec4|)
	for i in 4
		self[i] = self[i].KeyFix(V)
	return self
	
	