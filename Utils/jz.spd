#!/usr/local/bin/speedie

// todo: reporting jbin files as plain for some reason?
datatype Type (int)
	flags
		Compressed, Plain, XML, actions=(Box, Jbin, Report), StdOut, SelfReplace
	
	syntax is (|Type| test, |bool|)
		return self & test
	syntax isnt (|Type| test, |bool|)
		return !(self & test)
	
	render
		if self is Compressed
			fs <~ "compressed "
		if self is jbin
			fs <~ "jbin"
		  elseif self is box
			fs <~ "jeebox"
		  elseif self is xml
			fs <~ "xml"
		  else // speedie can't put this on a single line. C++ compile-error!
			fs <~ "plain"


function App.GetActions (|Type|)
	for s in app.Switches
		|| name = s.ArgName
		if name ~= "r"
			rz |= type.report
		  elseif name ~= "j": rz |= type.box
		  elseif name ~= "b": rz |= type.jbin
		  elseif name ~= "c": rz |= type.compressed
		  elseif name ~= "s": rz |= type.selfreplace
		  elseif name ~= "p"
			rz |= type.stdout
			check (app.args == 1) ("Can't write multiple files to stdout.")
		  else
			error "Unknown switch $s"

	rz |= type.plain
	check ((rz & type.actions).CountBits <= 1) ("Too many actions.")


function app.usage (|string|)
	return "Usage: ${.filename} File1 [File2, ...],  or: ${.filename} FileIn -p > FileOut
	options:
		-j		Convert to jeebox
		-b		Convert to jbin
		-c		Compress
		-s		Replace file in-place
		-p		Print to stdout
		-test	Tests if compression works using input file
		-r		Just report types, otherwise do nothing.

	examples:
		jz -j A.xml						// xml to box
		jz -c A.xml						// compress A.xml into A.xml.mz
		jz    A.mz						// decompress
		jz -b A.box						// box to jbin
		jz -b A.xml						// xml to jbin
"
		

main  

	if app.yes("test")
		return 1-MZLab.TestMz(app[0]) 
		// so we wanna run the mzlab thing
		
	|| action = .getactions$
	for r in (app.args  #expect (.Usage))
		|| Start = date.now
		|| data = r.ReadFile(int.max, false)
			.Time(start.ago, "Read", data.length)
			conv(data, r).Process(Action)
	
	if stderr.ok
		check (conv.done) "Nothing to do. All files already ${action.render}."


function App.Time (|date| ago, |string| Name, |int| Size)
	|float| scale = 1MB
	|| unit = "MB"
	if size < 10MB
		scale = 1KB
		unit = "KB"
	|| s = ago.seconds
	"$Name: ${s}s  (${Size/(scale*s)}$unit/s)"


class Conv
	|string|	Data
	|string|	In
	module: |int| Done

	function Process (|Type| Dest, |bool|)
		|type| Detected = .data.IsCompressed * type.compressed
		ifn Dest&type.Actions // (de)compress only
			require (detected is Compressed) == (dest isnt compressed)
		if Detected and .data.IsCompressed
			|| start = date.now
			.data = .data.Decompress(int.max)
			app.time(start.ago, "Decompress", .data.length)

		++done
		|| ext = ""
		|| Do = Dest&type.Actions
			|| start = date.now
			|| parse = .data.parse
			|| Duration = start.ago
			expect (parse or dest is report) ("File isn't parseable.")
			if parse
				app.time(duration, "Parse", .data.length)
			if !parse
				detected |= type.plain
			  elseif .data.IsJbin
				detected |= type.jbin
			  elseif parse.Find(@xml)
				detected |= type.xml
				if !(dest is report)
					require parse.XMLToJeebox
			  else
				detected |= type.box

			if Dest is report
				"$.in is: ${detected.render}"
				StdErr.clear
				return true

			require Detected&type.actions != Do
	
			if dest is box
				.data = parse.Render
				ext = "box"
			  else
				.data = parse.render_jbin
				ext = "jbin"
	
		if dest is compressed
			.data = .data.Compress
		if dest is stdout
			print .data
		  else
			|| Out = .in
			if dest isnt selfreplace
				out = .in.RemoveExt("mz")
				if ext
					out = out.SetExt(ext) // killing good exts like "spd" <-- "". sigh
				if dest is compressed
					out = out.AddExt("mz")
			if Out <~ .data
				printline out
		
		return true
