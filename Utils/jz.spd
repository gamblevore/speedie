#!/usr/local/bin/speedie

// todo: reporting jbin files as plain for some reason?
datatype Type (int)
	flags
		Compressed, Plain, XML, actions=(Box, Jbin, Report), StdOut, SelfReplace
	
	syntax is (|Type| test, |bool|)
		return self & test
	syntax isnt (|Type| test, |bool|)
		return !(self & test)
	
	render
		if self is Compressed: fs <~ "compressed "
		if self is jbin
			fs <~ "jbin"
		  elseif self is box:  fs <~ "jeebox"
		  elseif self is xml:  fs <~ "xml"
		  else:                fs <~ "plain"


function App.GetActions (|Type|)
	for s in app.Switches
		|| name = s.ArgName
		if name ~= "r"
			rz |= type.report
		  elseif name ~= "j": rz |= type.box
		  elseif name ~= "b": rz |= type.jbin
		  elseif name ~= "c": rz |= type.compressed
		  elseif name ~= "s": rz |= type.selfreplace
		  elseif name ~= "p"
			rz |= type.stdout
			check (app.args == 1) ("Can't write multiple files to stdout.")
		  else
			error "Unknown switch $s"

	rz |= type.plain
	check ((rz & type.actions).CountBits <= 1) ("Too many actions.")


function app.usage (|string|)
	return "Usage: ${.filename} File1 [File2, ...],  or: ${.filename} FileIn -p > FileOut
	options:
		-j		Convert to jeebox
		-b		Convert to jbin
		-c		Compress
		-s		Replace file in-place
		-p		Print to stdout
		-test	Tests if compression works using input file
		-r		Just report types. Otherwise do nothing."
		

main  
	if app.yes("test")
		return 1-MZLab.TestMz(app[0])
		// so we wanna run the mzlab thing
		
	|| action = .getactions$
	for r in (app.args  #expect (.Usage))
		|| data = r.ReadFile(0.25GB, false)
			conv(data, r).Process(Action)
	
	if stderr.ok
		check (conv.done) "Nothing to do. All files already ${action.render}."


class Conv
	|string|	Data
	|string|	In
	module: |int| Done

	function Process (|Type| Dest, |bool|)
		|type| Detected = .data.IsCompressed * type.compressed
		ifn Dest&type.Actions // (de)compress only
			require (detected is Compressed) == (dest isnt compressed)
		if Detected
			.data = .data.Decompress 
		
		++done
		|| ext = ""
		|| Do = Dest&type.Actions
			|| parse = .data.parse
			expect (parse or dest is report) ("File isn't parseable.")
			if !parse
				detected |= type.plain
			  elseif .data.IsJbin
				detected |= type.jbin
			  elseif parse.Find(@xml)
				detected |= type.xml
				require parse.XMLToJeebox
			  else
				detected |= type.box

			if Dest is report
				"$.in is: ${detected.render}"
				StdErr.clear
				return true

			require Detected&type.actions != Do
	
			if dest is box
				.data = parse.Render
				ext = "box"
			  else
				.data = parse.render_jbin
				ext = "jbin"
	
		if dest is compressed
			.data = .data.Compress
		if dest is stdout
			print .data
		  else
			|| Out = .in
			if dest isnt selfreplace
				out = .in.RemoveExt("mz")
				if ext:  out = out.SetExt(ext) // killing good exts like "spd" <-- "". sigh
				if dest is compressed: out = out.AddExt("mz")
			if Out <~ .data
				printline out
		
		return true

/// jz -j A.xml						// xml to box
/// jz -c A.xml -out=B.xml.mz		// compress A.xml a.xml.mz ?
/// jz -c A.xml.mz					// do nothing
/// jz    A.whatever				// decompress (Assuming it is compressed)
/// jz -b A.box						// a.box.jbin
