#!/usr/local/bin/spd

// todo
	// fix room connection...no need connect to the same room
	// also delete rooms that can't connect
	// also some rooms should connect but aren't
	// fill genplayer and genmonster


module Info
	|config| 		GameBank
	|config| 		RoomTypes
	|config| 		CellTypes


role RogueObject (message)
class RogueCreature (RogueObject)
    render
		fs <~ "${.name} has ${..strength} STR and ${..health} HP. "


class RogueRoom // this class is only to be used during generation! Doesn't exist during run/reload
	|ivec4|		Size
	|message| 	Purpose
	|int| 		MonsterCount
	constructor (|ivec4| size, |message| purpose)
		.size = size, .purpose = purpose
	function IsSpawn (|bool|)
		return .purpose.IsFirst


datatype RogueCellState (byte)
	flags
		Visited
		Seen
		Glimpsed


role RogueCell	(message)
	contains RogueObject
	
	constructor (|message| parent, |int| x,  |message| Type)
		super.Constructor(parent, @tmp, x, type.name)
		.msg(@arg)
	
	function Room (|RogueRoom|)
		|| o = .obj
		if o mustbe rogueroom
			return o // actually don't do this
	function TermColor (|int|)
		return .first.Indent
	function Type (|message|)
		return map.types[.name]	// The compiler can opt this? An ideal compiler would
	setter Type (|message|)
		.Name = Value.name	
	function Pos (|ivec2|) 
		return (.position, .parent.position)
	function DrawOne (|message| t, |bool|)
		|| f = t.first #expect (t, "no name msg?")
		|| name = f.name #expect (f, "no name str?")
		Terminal <~ (f.name, .pos, f.indent)
		return f != @str
	function Draw 
		opt norefcounts
		for obj in .arg
			require .drawone(obj)
		.drawone(.type)

module Map {
|dictionary of message| types
|[RogueCell]|	Data
|ivec2|			Size
|int64| 		Seed
|message|		MapState
|message| 		Level
|message|		Player
|message| door, |message| floor, |message| corriwall
|message| rock, |message| corridor, |message| wall

		

syntax access (|ivec2| s, |RogueCell|)
	if s inrange .size
		return .data[s.y * .size.x  +  s.x]

function Init (|ivec2| size)
	.size = size
	.MapState = Message()
	|| maplist = .MapState.msg(@tmp, "maps").msg(@arg)
	for i in 6
		|| map = maplist.msg(@tmp, "map")
		map <~ i+1
		for y in size.y
			|| row = map.GiveArg.msg(@tmp, "row").msg(@arg)
			row.Position = y
			for x in size.x
				RogueCell(row, x, rock)
	.LoadLevel(1)


function LoadLevel (|int| i)
	.Level = (.mapstate..maps)[i-1]
	.data.clear
	for row in .level.arg
		for cell in row.arg
			.data <~ cell
	// thats the level. Now what?
	

function IsRoomOK (|ivec4| i, |bool|)
	for p.rect in i
		require self[p].Type != Floor
	return true

function OKSize (|ivec4|)
	for attempt in 1000
		|| RoomSize = .rndsize
		if .IsRoomOK(RoomSize)
			return RoomSize

function RndSize (|ivec4|)
	|| c1 = .size.rnd
	|int| w = random[5, 25, 1]+2
	|int| h = random[4, 15, 1]+2
	|| c2 = c1 + (w,h)
	if c2.x >= .size.x // reflect room size back inwards if too far
		c2.x = c1.x
		c1.x -= w
	if c2.y >= .size.y
		c2.y = c1.y
		c1.y -= h
	return (c1, c2)

function ivec4.EdgeLength (|int|)
	return .area - (self Â± 2).area

function ivec4.Edge (|int| where, |ivec4|)
	// no corners!
	|| W = .Width-2
	|| H = .Height-2
	while where >= 0 and w > 0 and h > 0
		if (where < W)		// bottom
			return (.x + where+1, .Y, 0, -1)
		where -= w

		if (where < H)		// right
			return (.X2-1, .y+where+1, 1, 0)
		where -= h

		if (where < W)		// top
			return (.x + where+1, .Y2-1, 0, -1)
		where -= w

		if (where < H)		// left
			return (.X, .y + where+1, 1, 0)
		where -= h
		
	
function Connect (|ivec2| p, |ivec2| d)
	|| first = self[p] #require
	|| Length = 1
	|roguecell| last
	|| d2 = d.yx
	while Length < 30
		p += d
		|| c = self[p] #require
		|| ctype = c.type
		ifn cType == rock or wall or Corridor
			exit
		last = c
		Length++
		c.Type = Corridor
		with self[p+d2]
			if .type == Rock
				.type = corriwall
		with self[p-d2]
			if .type == Rock
				.type = corriwall
	
	if Length >= 2
		first.Type = door
	if length > 2
		last.Type = door
	  elseif length == 2
		last.Type = Floor


function CanConnect (|rogueroom| r, |ivec2| p, |ivec2| d, |bool| Coridoors=false,  |bool|)
	|| s = r.size
	|| hitwall = 1
	
	for length in 30
		p += d
		|| c = self[p]$
		|| r2 = c.obj
		|| ct = c.type
		if ct == wall
			require hitwall++ <= 1
		if r2 and (r2 != r) and (ct == Floor)
			return true
		require (ct == rock or wall) or (coridoors and ct == corridor)


function ConnectRoom (|rogueroom| r)
	|| n = 5 * r.size.area/(25*15)
	|| needed = random[1, n, 1]|int|
	|| ToTest = r.size.EdgeLength
	for 1000
		|| Cell = r.size.Edge(Random[0, ToTest])
		if .CanConnect(r, cell.xy, cell.x2y2)
			.Connect(cell.xy, cell.x2y2)
			needed--
			require needed > 0
//		else // seems that some good positiosn ARE being tested, but are failing. Can't see why?
// everything seems OK?'
//			self[cell.xy].Type = roguetype.gate


function ivec2.isCornerOf (|ivec4| R, |bool|)
	r-=(0,0,1,1)
	return self == R.x1y1 or R.x1y2 or R.x2y1 or R.x2y2 // fix later


function RoomPurpose (|[rogueroom]| rooms, |message|)
	if !rooms
		return info.RoomTypes.first
	
	|| Types = .AllowedRoom #require
	|| Count = 0.0
	for r in types
		Count += r.first.Float
	
	|| rnd = random[0,count]
	for r in types
		count -= r.first.Float
		if count <= 0
			return r
	return types.Last
	
	
function GenRoom (|[rogueroom]| rooms)
	|| RoomSize = .oksize #require
	|| room = RogueRoom(roomsize, .roompurpose(rooms))
	rooms <~ room
	for P.rect in RoomSize
		|| cell = self[p]
		cell.obj = room
		|| type = Floor
		if !RoomSize.contains(P, 1)
			if cell.type != wall
				Type = wall
			  elseif p.IsCornerOf(Roomsize)
				0
			  else
				type = floor
		cell.Type = type

	require room.isspawn
	for 100
		for P.rect in RoomSize
			|| cell = self[p]
			if (cell.type == floor) and 100th[]
				cell.arg <~ .Player
				return 
				
		
function GenPlayer
	|| P = info.GameBank..classes
	require p and p.hasany
	.player = p[].Copy

		
	
	
function GenMap
	|[rogueroom]| 	Rooms
	for i in 10
		.GenRoom(rooms)
	for r in Rooms
		.ConnectRoom(r)


	
function GenMonster
	|| lim = random[18,30,1]
	|| Mon = .AllowedMonsters				#expect ("no monsters?")
	for 1000
		for c in .data // all cells
			if !c.room.isspawn and c.type..walkable and 100th[]
				c.arg <~ Mon[].copy
				require --lim> 0


function AllowedMonsters (|int| Level_Num = 0, |[roguecreature]|)
	return (info.GameBank..monsters).LevelFilter(level_num, true)
		
function AllowedRoom (|int| Level_Num = 0, |[roguecreature]|)
	return (info.RoomTypes).LevelFilter(level_num, false)


function message.LevelFilter (|int| L, |bool| IsMonster,  |[roguecreature]|)
	L := map.level[0].int
	opt norefcounts
	for Mon in self
		if ismonster
			|| str = mon[@str] // expect a name!
			expect (str and str.name) (Mon, "Monster has no name!")
		|| allowed = mon[@bra,1][@rel]
		allowed[@opp, 1].Expect(nil, "-")
		|| f = allowed[@num].int
		|| t = allowed[@num, -1].int
		if L >= f and L <= t
			rz <~ mon


function info.LoadColors (|Message| msg, |TerminalColor| Default=0)
	for s in msg
		|| col = s..color
		|| f = s.first
		|| c = default
		if col ~= "white"
			c = Terminal.white
		if col ~= "red"
			c = Terminal.red
		if col ~= "blue"
			c = Terminal.blue
		if col ~= "yellow"
			c = Terminal.yellow
		f.Indent = c


function info.Load (|int64| Seed, |bool|)
	seed := date.now & 0xffff
	Random.shared.seed = seed
	map.seed = seed
	app.CWD = app.Path.parent
	.GameBank = "Creatures.txt".file.parse|config|
	.roomtypes = "Rooms.txt".file.parse|config|
	.CellTypes = "Cells.txt".file.parse|config|

	|| c = map.types init .CellTypes.Dict
	map.door = c["door"]
	map.wall = c["wall"]
	map.floor = c["floor"]
	map.rock = c["rock"]
	map.corriwall = c["corriwall"]
	map.corridor = c["corridor"]

	.LoadColors(.celltypes)
	.LoadColors(.gamebank..classes, terminal.white)
	.LoadColors(.gamebank..monsters, terminal.yellow)
	
	return StdErr.ok


function Run (|bool| Splash)
	.init(terminal.size)
	.GenPlayer
	.GenMap
	.GenMonster
	if StdErr.ok
		"monsters+map+player loaded"
	if Splash
		.splash
	while .Running
		.DetectVisibility
		.DrawMap
		Terminal.Display


function Running (|bool|)
	return true


function DetectVisibility
	
	
function DrawMap
	for i in .size
		self[i].draw
	terminal <~ ("Map: " + .Seed.Render, 30, 0)


function Splash
	|| scale = Terminal.w|float| / Terminal.h|float|
	|| h = 24

	for secs in 10s
		Terminal.DrawRect((0, 0, terminal.w, h))
		for i in 5.0
			|| Title = " Mini Rogue ".Rotate(i + secs*3)
			|| f = i*1.234 + secs*(0.33+i*0.05)
			|| x = 1+((1.5*f/scale).bounce*(Terminal.w-(title.length+2)))
			|| y = 1+f.bounce*(h-2)
			Terminal <~ (Title, x, y, terminal.Red+i)

		|| title2 = "<Mini-Rogue>"
		|| x2 = (terminal.w - title2.length) / 2
		Terminal <~ (title2, x2, 0, Terminal.Red)
		Terminal <~ (file://HeartLogo.txt , 32, 10, terminal.red)
		Terminal.Display
}
	
main
	|| a = app[0]
	if info.load(a.int)
		map.run(!a)

