#!/usr/local/bin/spd


app.usage = "Usage: cloc path [...]
--types=cpp/c/h               Only report these types
--quiet=true/false            Makes less output
-k                            Keeps errors
-j							  The size if the data was converted to jbin (usually larger)
-s                            Report parse speed
-p                            Print parse tree
-m                            Report Memory Usage
-h                            Histogram
" // set this before getting into main



main  (|[file]| files)
	if !files
		Quit app.Usage
	counts.Init(app["types"])
	for dir in files
		dir.CountAll(true)
	counts.Report



module Counts 
	|int64|		Lines
	|int64|		Names
	|int64|		Nodes
	|int64|		Parsed
	|int64|		Files
	|int64|		Dirs
	|int64| 	TotalSize
	|int64| 	TotalJBin
	|int64| 	ObjMem
	|int64| 	StrMem
	|int|		MaxNodeLength
	|uint[65]|	Histogram
	
	||			exts = dictionary.new
	||			loud = !(app.yes("quiet") or app.yes("q"))
	||			PrintTree = false
	||			PrintedTree = false
	|| 			JB = app.yes("j")
	||			ParseSpeed = app.yes("s")
	|string| 	DefaultExt = "spd/box/jbin/jb/xml"
	
	function Init (|string| ss)
		|| pt = app["p"]
			PrintTree = true
			if pt != "true"
				MaxNodeLength = pt.int
		ss = ss.trim 
		ss ?= DefaultExt
		for s in (ss / '/') 
			exts[s] = s

	function Count (|message| Msg)
		opt norefcounts
		Counts.parsed++
		|| names = 	0 
		|| nodes = 0
		for s.flat in msg
			|| len = s.name.Length
			names += (len>0)
			|| n = len min 64
			Histogram[n]++
			nodes++
		.Names += names
		.Nodes += Nodes

	function MemStart
		.objmem = app.ObjMemory
		.strmem = app.stringmemory

	function PrintMem
		"Obj: ${.objmem.strsize}  Str: ${.strmem.strsize}"
		
	function MemEnd
		require app["m"]
		print "(Original) "
		.PrintMem
		.objmem = app.ObjMemory - .objmem
		.strmem = app.stringmemory - .strmem
		print "(Extra) "
		.PrintMem
		


function string.LOC (|int|)
	for s.lines in self
		rz += s.OutWhite >= 0


function file.CLOC
	if counts.PrintTree
		require StdErr.OK
		expect (!counts.PrintedTree) "Can't have multiple files, with printtree."
		counts.PrintedTree = true
	  elseif counts.loud
		printline .path 

	|| data = .ReadAll(int.max, true) #require
	if data.IsCompressed
		data = data.Decompress(int.max) // might as well...
	
	Counts.files++
	
	counts.MemStart
	if counts.ParseSpeed
		"a=b+c".parse // warmup
	|| Start = date.now
	|| msg = data.parse
	|| Durr = start.ago
	counts.MemEnd
	if msg
		counts.Lines += msg.loc[0]
	  else
		Counts.Lines += data.loc
	counts.TotalSize += data.Length
	require msg
	if counts.jb
		counts.TotalJBin += msg.RenderJbin.Length
	counts.Count(msg) 
	if counts.PrintTree
		if counts.maxnodelength
			for s.flat in msg 
				s.name = s.name.Preview(counts.MaxNodeLength)
		msg.RenderAST(app.stdout.outputstream)
		printline
	  elseif counts.ParseSpeed
		app.stdout.outputstream!.RenderSpeed("Parsed", data.length, durr)
		printline
	(msg)
	


function file.CountAll (|bool| force=false)
	require .MustExist
	if .isdir
		counts.dirs++
		for c.files in self
			c.CountAll
		return
	if force or counts.exts[.ext]
		.cloc


function Counts.ReportHisto
	require app.Yes("h")
	|| h_done = false
	|| curr = 0
	|| total = .names + Histogram[0]
	for i in 65
		if histogram[i]
			if !h_done
				h_done = true
				"histogram:"
			curr += histogram[i]
			|| pc = (curr div total).Percent
			"    $i: $pc"
	if h_done
		""


function Counts.Report
	if !app.yes("k")
		stderr.clear
	require !counts.PrintTree
	|| f = .files
	|| l = .lines
	|| p = .parsed
	|| d = .nodes
	|| kb = .TotalSize.strsize
	"$l lines"
	|| n = .names
		"$n names, in $d nodes"
		.ReportHisto
	if (p > 1) or (f > 1) or app.yes("t")
		"Total files: $f,  total jeebox files: $p,  total filesize: $kb"
	if .JB
		"Total jbin: ${.totaljbin.strsize}"
	
	if CompressionStats.All.Out
		printline CompressionStats.all.report
		
