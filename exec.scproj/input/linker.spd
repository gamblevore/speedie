

module SpdTable 
	constants	
		FnLim = 10K // its about corrupted files, not "we can't handle more"!
	
	|int|		LinksOK
	|[SpdFunc]|	Funcs
	|spdfunc--|	CurrFunc
	|&uint16|	GlobLink = cakerun.GlobTable
	|&&nil|		RawFuncs = cakerun.LibFuncs

	syntax access (|int| i, |SpdFunc|)
		return .Funcs[i]
	
	
	function RunMain (|int|)
		opt norefcounts
		|| main = .funcs[1]					#expect ("Missing main")
		rz  = .funcs[0]!.Run|&int64|[]
		rz ?= main.Run|&int64|[]


	syntax Append (|message| spd)
		opt norefcounts
		expect (.Funcs < .FnLim)					(spd, "Too many functions")
		if spd == @num
			for spd.int
				.Funcs <~ SpdFunc() // placeholder
		  else
			.Funcs <~ SpdFunc(spd)
	
	
	function LoadFuncs (|bool|)
		opt norefcounts
		.funcs <~ SpdFunc() // null func
		for spd in CakeRun.code
			self <~ spd
		
		check  (.Funcs >= 2)						(CakeRun.code, "Missing 'main'")
		.Funcs.Shrink
		return stderr.ok
	
	
	function Link
		opt norefcounts
		for f in .funcs
			.currFunc = f
			f.Link❗

	
	function SPDFunc.DebugSuccess (|int| id)
		SpdTable.LinksOK++
		printline "${spdtable.LinksOK}: ${SpdTable.CurrFunc!.Name} linked to $.name ($id)"
	
	
	function asm.PutStickersOnCake (|&asm| self, |asm| Old) // SPDLink, LinkFunction, 
		opt norefcounts
		|| ID = ((Old << 13) >> 13) - 31
		require ID > 0 // not a register!
		
		|| fn = SpdTable.Funcs[id]
			|| Pos = fn.SpeedieFunc						// Fast-branch for speed.
				|| Jump = self - &Pos.ASM[0]
				// for now, do it as 19-bit. later we can upgrade to 21-bit
				if  Jump  ==  (Jump << 13) >> 13
					|| Final = (Jump|uint| << 13) >> 13
					Final |= (Old>>24) << 24
					self[] = Final
//					fn.DebugSuccess(id)
					return
				expect (false) (spdtable.Currfunc, "Executable too large to link: " + fn.name )
			expect (false) (spdtable.Currfunc, "Function was trimmed: " + fn.name )
		error (spdtable.Currfunc, "Bad function ID: "  + id )


	function asm.Size (|int|)
		self >>= 24
		if self >= 32
			return 1 
		return 2+(self&1)
	
	
	function spdfunc.Link❗
		// this could easily be multi-threaded... but no need for now. doesn't allocate / alter ptrs
		|| Blob = .SpeedieFunc$
		|| Base = &Blob.ASM[0]
		|| after = base + .Length
//		"-----$.name-----"
		while base < after
			|| asm = base[]
//			asm.PrintType
			if asm.IsSPDFunc // seems everything else is prelinked
				base.PutStickersOnCake(asm)
			base += asm.size
	
	
