

module SpdTable 
	constants	
		FnLim = 10K // its about corrupted files, not "we can't handle more"!
	
	|[SpdFunc]|	Funcs
	|&uint16|	GlobLink = cakerun.GlobTable
	|&&nil|		RawFuncs = cakerun.LibFuncs


	syntax access (|int| i, |SpdFunc|)
		return .Funcs[i]
	
	
	function RunMain (|int|)
		opt norefcounts
		|| main = .funcs[1]					#expect ("Missing main")
		rz  = .funcs[0]!.Run|&int64|[]
		rz ?= main.Run|&int64|[]


	syntax Append (|message| spd)
		opt norefcounts
		expect (.Funcs < .FnLim)					(spd, "Too many functions")
		if spd == @num
			for spd.int
				.Funcs <~ SpdFunc() // placeholder
		  else
			.Funcs <~ SpdFunc(spd)
	
	
	function LoadFuncs (|bool|)
		opt norefcounts
		.funcs <~ SpdFunc() // null func
		for spd in CakeRun.code
			self <~ spd
		
		check  (.Funcs >= 2)						(cakerun.code, "Missing 'main'")
		.Funcs.Shrink
		return stderr.ok
	
	
	function Link
		opt norefcounts
		for (f in .funcs) (i)
			FuncID.curr = i + 1
			f.Link❗

	
	function asm.SPDLink (|&asm| self, |asm| Old)
/		|| ID = (Old << 8) >> 8
		|| fn = SpdTable.Funcs[id]					#expect (FuncID.curr, "Bad function ID")
		|| Pos = fn.SpeedieFunc						#expect (FuncID.Curr, "Function was trimmed")
		|| Jump = self - &Pos.ASM[0]

		// for now, do it as 19-bit. later we can upgrade to 21-bit
		expect (jump == ((Jump << 13) >> 13))				(FuncID.Curr, "Executable too large to link")
		|| Final = (Jump|uint| << 13) >> 13
		Final |= (Old>>24)<<24
		self[] = Final		


	function asm.Size (|int|)
		self >>= 24
		if self >= 32
			return 1 
		return 1+(self&1)
	
	
	function spdfunc.Link❗
		// this could easily be multi-threaded... but no need for now.
		// it doesn't allocate anything... or alter pointers of any kind.
		|| Blob = .SpeedieFunc$
		|| Base = &Blob.ASM[0]
		|| after = base + .Length
		while base < after
			|| asm = base[]
			if asm.IsSPDFunc // seems everything else is prelinked
				base.SPDLink(asm)
			base += asm.size
	
	

datatype FuncID (int)
	syntax expect (|string| str)
		opt norefcounts
		|| msg = CakeRun.code[Curr]
			msg.SyntaxExpect(str)

	module
		|FuncID| Curr



