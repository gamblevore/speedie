
 
module package
	linkage: cpp_part Pack
	|memory|		globals
	|config|		funcs
	|config|		linkage
	|config|		ProjClasses
	|config|		strings
	
	
	function LibGlobs (|&&nil|)
		cpp_wrapper JB_App__LibGlobs

//	function LibInfo (|string|)
//		cpp_wrapper JB_App__LibInfo
	
	function LibFuncs (|&&nil|)
		cpp_wrapper JB_App__LibFuncs

	function LibClassLayout (|string|)
		cpp_wrapper JB_App__LibClasses

	function message.Arg (|string| name, |bool| Err = false, |message|)
		opt norefcounts
		|| Ch = self[name]
			return ch
		if !err
			return message()
		.ReportFailiure(@tmp, name)
	
	
	function LoadPack (|bool|)
		if .getstructure and .Loadfuncs
			if xclass.LoadPackClasses
				return true
	
	function GetStructure (|bool|)
		opt norefcounts
		|| pk			= .InputPack
		|| app			= pk.arg("app", true)

		debugger.root	= pk.arg("debug")
		debugger.meta	= pk.arg("meta")
		
		.strings		= app.arg("strs")
		.projclasses	= app.arg("clss")
		.funcs			= app.arg("fncs", true)
		.linkage		= app.arg("link", true)

		|| g = app.int
		check (G > 0 and g <= 256MB) ("Globals must be between 1 byte and 256MB")
		.glob(g)
		
		return stderr.ok
		

	function InputPack (|messageroot|)
		opt norefcounts
		|| x = app.args[0]
		x ?= "~/Speedie/exec.scproj/demo.xpd"
		|| fl = x.FileThatExists("load executable")
			return fl.parse[@tmp, "pack"]


	function LoadFuncs (|bool|)
		opt norefcounts
		expect (package.funcs.hasany)      ("No functions found!")

		for spd in package.funcs
			|| fn = xfunction(spd)
//			linker <~ fn
			if apptable <~ fn
				fn.LinkCpp
				fn.LinkSpd

		check  (AppTable.Funcs >= 2) ("Missing 'main'")
		return stderr.ok
		
