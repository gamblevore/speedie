

module CakeRun
	linkage: cpp_part Pack
	|message|				Code
	|message|				Clss
	|String|				Strings
	|String|				FileSrc
	|dictionary of &classdata| ClassTable = dictionary()
	
	constants
		FuncIsLib = 1
	
	function LibFuncs (|&&nil|)
		cpp_wrapper JB_App__LibFuncs
		
	syntax access (|int| i, |SpdFunc|)
		return SpdTable.Funcs[i]

	function message.Arg (|string| name, |bool| Err = false, |message|)
		opt norefcounts
		|| Ch = self[name]
			return ch
		if !err
			return message()
		.ReportFailiure(@tmp, name)
	
	
	function LoadPack (|bool|)
		return  globals.CheckLib  and  .GetStructure  and  SpdTable.LoadFuncs  and  .LoadClasses
		
	
	function GetStructure (|bool|)
		opt norefcounts
		|| pk = .InputPack$
		|| app = pk.last(@arg)[@tmp, "app"]
		|| sz = app[@num]
		|| arg = sz.next(@arg)
			arg.ExpectLast
			.code = arg[@tmp, "code"][@arg]
			check (.code.hasany)      ("No functions found!")
			.strings = arg["strs"].name
			.clss = arg["clss"]
			debugger.root = arg["debug"]
		return globals.collectpackglobs(sz)
		

	function file.Compile (|file|)
		|| args = ["--bake", "--quiet", self]
		|| Answer = speedie.ExecuteStr(args)		// now what? now what? now what
			rz = Answer.file
			if !rz.Exists
				rz = nil
		
		
	function InputPackFile (|File|)
		opt norefcounts
		|| path = app[0] #expect ("No path specified")
		|| fl = path.FileThatExists("load executable")
			if fl isa "spd" or "scproj" or "uwu" or "uwuz"
				fl = fl.Compile
		return fl

	function InputPack (|messageroot|)
		|| fi = .InputPackFile
			rz = fi.Parse
			if rz
				.FileSrc = rz.OriginalParseData
				if !rz.expect(@tmp, "cake")
					rz = nil

