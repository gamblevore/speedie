

/*
	API to check if something different happened to the flow control from one platform to another
	only call: flowcontrol.Cond
*/


datatype FlowControlStopper (uint)
	syntax usingcomplete
		flowcontrol.stop
		
class FlowControl
	linkage:cpp_part Flow
	|FastBuff|				Buff
	|stringstream|			Read
	|faststring| 			Write
	|bool| 					OK

	constructor (|string| Path)
		flow off
		.ok = .buff.Alloc(1MB)
		.init(path)
		print "Capturing flow: "
		printline path

	helper init (|string| path)
		|| prev = path.AddExt(".expected")
		if !prev.fileexists and path.fileexists
			require path.file.moveto(prev)
		.write = path.out
		if prev.fileexists 
			.read = prev.readFile.decompress.stream

	helper Flush
		flow off
		.buff.tmpstr.CompressInto(.write, stats)
		.buff.Curr = .buff.start
		.write.Flush
	
	helper Test (|uint| Expected, |int| Size)
		flow off
		opt norefcounts
		|| r = .read
		require r != nil
		|| Found = r.uint(Size)
		if Found != Expected
			"Flow differs here: "
			printline app.StackTrace("JB_Flow__Cond")
			.read = nil
			 

	destructor
		FlowControl.Disabled = int.max
		.flush
		printline stats.render
		stats.clear

	helper Cond (|bool| value)	
		flow off
		visible
		if .buff <~ value|byte|
			.Flush
		.Test(value|uint|, 1)
	
	module
		|uint| 					Disabled = int.max
		|flowcontrol|			Flow 
		|CompressionStats| 		Stats
		
		function Activate
			.Disabled = int.max // in case its set badly
			.flow = FlowControl(app.ExePath.AddExt(".flow"))
			.Disabled = 0
		
		function Stop
			.Disabled = int.max
			.Flow = nil

		syntax Using (|FlowControlStopper|)
			.Activate



