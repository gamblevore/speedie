

/*
	API to check if something different happened to the flow control from one platform to another
*/

		
class FlowControl 
	linkage:cpp_part Flow
	|FastBuff|				Buff
	|StringReader?|			ReadInput
	|faststring?| 			Write
	|Faststring?| 			Excuse

	constructor (|string| path)
		flow off
		.buff.needAlloc(1MB)
		.Excuse = FastString()
		.write = nil
		if .LoadPath(path)
			printline path
			flowcontrol.Disabled = 0


	helper LoadPath	(|string| path, |bool|)
		flow off
		require (FlowControl.FlowMode >= FlowControl.Validate) and (path.FileSize)
		.write = path.out
		|| S = path.ReadFile
			.ReadInput = S.Decompress.stream
			rz = .ReadInput.HasAny	
	
	
	helper Flush
		flow off
		if .write
			.buff.tmpstr.CompressInto(.write, stats)
		.buff.Curr = .buff.start
			 

	destructor
		flow off
		FlowControl.Disabled = int.max
		
		.flush
		if .ReadInput!=nil
			"Flow: Occurred equally."
		// stats.Print(true) // wiat is this compression or decomp?
		// stats.clear

	
	helper AddByte (|byte| value)
		flow off
		if .buff <~ value
			.Flush
		
	helper Cond (|byte| value, |bool|)
		flow off, opt norefcounts, visible

		.AddByte(value)
		|| r = .ReadInput
		if r == nil
			return true
		|| b = r.byte
		if b == value
			return true
		.fail(string.byte(value), string.byte(b), "")


	function Fail (|string| found, |string| expected, |string| InputName)
		opt norefcounts, flow off
		|| fs = .Excuse
		if !fs
			debugger 
			return nil
		if InputName 
			fs <~ "Different input for: ${InputName}. "			
		if !.readinput.HasAny
			fs <~ "New-stream longer than old-stream."
		  else
			fs <~ "Expected: "
			fs.printnicely(expected)
			fs <~ " but found: "
			fs.printnicely(found)
		fs <~ "\n"
		
		if !InputName
			app.StackTrace(3, fs)
			printline fs.GetResult
			.Excuse = nil
			.ReadInput = nil
		if flowcontrol.BreakOnFail
			debugger


	function faststring.PrintNicely (|string| s)
		|| Cause = s.UnPrintable
		if cause < 0
			self <~ "#(${s.hex})#"
		  elseif cause == '\n'
			s.ReplaceAll("\n", `\n`, self)
		  else
			self <~ s
	
	
	module
		|flowcontrol|			Flow			// DONT directly alter this 

		|CompressionStats| 		Stats
		|int| 					Disabled		= int.max
		|byte|					FlowMode		= FlowControl.Validate
		|bool|					AlwaysMove		= true
		|bool|					DoSuggest		= true
		|bool|					BreakOnFail
		
		constants
			Validate			= 2
			Log					= 1
			Off					= 0
			
		target flow
			constants
				FlowEnabled = true
		  else
			constants
				FlowEnabled = false
				
		syntax append (|bool| value, |bool|)
			visible,  flow off,  cpp_name JB_Flow__Cond
			if .disabled
				return value
			.disabled = 1
			.flow!.cond(('T', 'F')(value))
			.disabled = 0
			return value

		function Input (|[string]| lines, |string| name,  |bool|)
			cpp_part InputStrings
			flow off
			.Input(lines.join("\n"), name)

		function Input (|string| data, |string| name)
			visible
			flow off
			require !.disabled

			.disabled = 1
			|flowcontrol--| f = .flow!
			|StringReader--| r = f.ReadInput
			if r != nil
				|| str = r.str(data.length)
				if str != data
					f.Fail(str, data, name)
				
			for c in data
				f.addbyte(c)
			.disabled = 0
		
		function Stop
			flow off
			.Disabled = int.max
			.Flow = nil

		function Activate (|string| name,  |FlowControlStopper|)
			cpp_part FlowAllow
			flow off
			|| path = "/tmp/Speedie/FlowControl/".child( name + ".txt.mz" )
			if !.flowmode
				print "#!FlowControl would have been at: "
				printline path
				if .DoSuggest
					"#!Pass --flowmode=1 (log) or --flowmode=2 (log+validate)."
				return nil

			.Disabled = int.max			// in case its set badly
			.flow = FlowControl(path)


		function Attempt (|string| name,  |FlowControlStopper|)
			flow off
			target flow
				return .Activate(name)



datatype FlowControlStopper (int)
	syntax UsingComplete (|object| idk=nil)
		flow off
		flowcontrol.stop
	
	syntax Using (|FlowControlStopper|)
		flow off

