

/*
	API to check if something different happened to the flow control from one platform to another
	only call: flowcontrol.Cond
*/

		
class FlowControl
	linkage:cpp_part Flow
	|FastBuff|				Buff
	|stringstream|			ReadInput
	|faststring| 			Write
	|bool| 					OK

	constructor
		flow off
		.ok = .buff.Alloc(1MB)

	helper Init (|string| path)
		flow off
		if FlowControl.FlowMode >= FlowControl.Validate
			|| prev = path.AddExt(".expected")
			if AlwaysMove or (!prev.filesize and path.filesize)
				|| f = path.file
				if f.exists
					require f.MoveTo(prev)
			if prev.FileSize
				|| S = prev.ReadFile
					.ReadInput = S.decompress.stream
		.write = path.out
	
	helper Flush
		flow off
		if .write
			.buff.tmpstr.CompressInto(.write, stats)
		.buff.Curr = .buff.start
			 

	destructor
		flow off
		FlowControl.Disabled = int.max
		.flush
		printline stats.render
		stats.clear

	
	helper AddByte (|byte| value)
		if .buff <~ value
			.Flush
		
	helper Cond (|byte| value, |bool|)
		flow off, opt norefcounts, visible

		.AddByte(value)
		|| r = .ReadInput
		if r == nil
			return true
		|| b = r.byte
		if b == value
			return true
		.fail(string.byte(value), string.byte(b))


	function Fail (|string| found, |string| expected)
		opt norefcounts, flow off
		if !.readinput.HasAny
			"New-stream longer than old-stream."
		  else
			|| Sep =  ("\n", " ")(found contains '\n'  or  expected contains '\n')
			print "Expected:"
			print sep
			print expected
			print sep
			print "but found:"
			print sep
			printline found
		printline app.StackTrace("JB_Flow_")
		.ReadInput = nil
		if BreakOnFail
			debugger

	
	module
		|flowcontrol|			Flow // DONT directly alter this 
		||						FlowDiff		= "Flow differs here:"

		|CompressionStats| 		Stats
		|uint| 					Disabled		= int.max
		|byte|					FlowMode		= FlowControl.Validate
		|bool|					AlwaysMove
		|bool|					BreakOnFail
		
		constants
			Validate			= 2
			Log					= 1
			Off					= 0
			
		syntax append (|bool| value, |bool|)
			visible, flow off, cpp_name JB_Flow__Cond
			if .disabled
				return value
			.disabled = 1
			.flow.cond(('T', 'F')(value))
			.disabled = 0
			return value
		
		syntax append (|string| name, |bool| Print=true,  |bool|)
			opt norefcounts, flow off, cpp_part AddStr
			
			if .disabled
				return true
			.disabled = 1
			rz = true
			|| f = .flow
			|| r = f.ReadInput
			if r != nil
				|| str = r.str(name.length)
				rz = str == name
				if !rz
					f.Fail(name, str)
				
			for c in name
				f.addbyte(c)
			.disabled = 0
		
		function Stop
			flow off
			.Disabled = int.max
			.Flow = nil
		
		function New (|string| name, |FlowControlStopper|)
			cpp_part FlowStarter
			flow off
			require FlowMode
			.Disabled = int.max			// in case its set badly
			.flow = FlowControl()
			.flow.init("/tmp/FlowControl/" + name + ".txt.mz")



datatype FlowControlStopper (uint)
	syntax UsingComplete
		flowcontrol.stop

	syntax Using (|FlowControlStopper|)
		flow off
		opt norefcounts
		require FlowControl.FlowMode
		print "Capturing flow: "
		printline flowcontrol.flow.write.file.path
		flowcontrol.Disabled = 0

