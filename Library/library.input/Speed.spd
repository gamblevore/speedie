

function string.RenderSpeed (|object| fs_in=nil, |int64| BytesIn, |duration| Duration, |int64| BytesOut=0, |string|)
	opt norefcounts
	|| fs = FastString.useasoutput(fs_in)$
	if self
		fs <~ self
		fs <~ ": "
		
	if bytesin
		BytesIn.StrSize(fs)

	if BytesOut 
		fs <~ " to "
		Bytesout.strsize(fs)
		fs <~ " ("
		|| ratio = ((Bytesout div Bytesin)|float|*100.0)
		fs.appendfloat(ratio)
		fs <~ "%)"

	if bytesin or bytesout
		fs <~ " in "

	|| seconds = duration.float.abs
	if seconds < 1.0
		fs.AppendFloat(seconds*1000.0, 2)
		fs <~ 'm'
	  else
		fs.AppendFloat(seconds, 2 + (seconds < 10))
	fs <~ 's'
	
	// could move unit-conversion out into its own thingy. like fs.AppendSize
	if seconds and bytesin
		|| scale = 1.0MB
		|| unit = 'M'
		|| rate = BytesIn|float| / (scale*seconds)
		if rate >= 1.0K
			rate /= 1.0K
			unit = 'G'
		  elseif rate <= 1.0
			rate *= 1.0K
			unit = 'K'
		|| dp = (5,2)(rate <= 0.01)
		fs <~ "  ("
		fs.appendfloat(rate, dp)
		fs <~ unit
		fs <~ 'B'
		fs <~ "/s)"
	return fs.GetResult(fs_in)


inline Date.Report (|string| name, |int| Size=0,  |Date| After=date.now, |date|)
	printline name.RenderSpeed(size, after-self)
	return date.now
	

struct SpeedTester
	|Date|				Start
	|string|			Name
		
	autogen SyntaxUsingComplete (|object| idk=nil)
		|| µs = date.now - .start
		printline .Name.RenderSpeed(0, µs)


function string.SyntaxUsing (|SpeedTester|)
	print "Starting: "
	printline self
	rz.name = self
	rz.start = date.now
