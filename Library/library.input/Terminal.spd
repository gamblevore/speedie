
target !minilib {

datatype TerminalColor (int)

// cleanup:
	// would be nice if	we can remove this class...
	// perhaps do it differently. Also remove Dictionry and just have FixedDict
	// don't dump ALL the string classes, just the base
	// Saveable stuff needs to go. Or just be internal.
	// TokHan should be internal.
	// validator should be embedded (if used)
	// Charset should be replaced with charprops
	// flowcontrol can be deleted... i don't know if we need it. Perhaps after getting speedie working.
	// macro can be embedded.
	// perhaps terminalcell can just be a struct, IDK.
	// process should be renamed to UnixProcess (haha)
// a lot of things to cleanup!

class TerminalCell
	|TerminalColor| 		Color
	|string| 				Text


module Terminal
	visible false
	|array of array of TerminalCell|	TermScreen
	|faststring| 		fs
	|date| 				LastDisplay
	visible true
	
	constants
		w = 80
		h = 35 // too many lines will break the clearscreen/reset function!
		|TerminalColor|
		Black = 30, Red,Green,Yellow,Blue,Magenta,Cyan,White
	
	
	helper init
		require !.TermScreen
		for h
			|[TerminalCell]| s
			.TermScreen <~ s
			for w
				s <~ TerminalCell(0, " ")
	
	
	syntax append (|string| text, |ivec2| V, |TerminalColor| Colors = 0)
		.init
		|| x0 = v.x
		for c.codepoints in text
			if c == 10 or 13
				++v.y
				v.x = x0
			  elseif c == ' '
				++v.x
			  elseif v inrange .size  and  c != ' '
				if c == 127
					c = ' ' // lol
				|| row = .TermScreen[v.y]
					row[v.x] = TerminalCell(colors, c)
				++v.x
	
	
	function DrawRect (|ivec4| Rect, |string| inside="", |terminalcolor| colors=0)
		|| r2 = rect + (1, 1, -2, -2)
		for s.rect in rect
			|| c = inside
			|| X = r2.x1x2 contains s.x
			|| Y = r2.y1y2 contains s.y
			if !x and !y
				c = "+"
			  elseif !x and y
				c = "|"
			  elseif !y and x
				c = "-"
			self <~ (c, s)


	function Flat (|string|)
		opt norefcounts
		|| f = .fs init FastString()
		|| PrevCol = 0
		for line in .TermScreen
			for cell in line|array of terminalcell|
				|| CCol = cell.color
				if PrevCol != CCol
					PrevCol = CCol
					f <~ "\1b[1;${CCol}m"
				f <~ cell.Text
				cell.text = " "
			f <~ '\n'
		return f.GetResult


	function Display
		|| now = date.now
		if .LastDisplay
			app.Sleep((.LastDisplay + 1s/60) - now)
		  else
			"\1b[2J"			// clear
		.LastDisplay = now
		"\1b[1;1H"				// reset
		print .flat
	
	
	function FrameText (|string| s, |ivec4| Frame, |terminalcolor| FrameCol=terminal.white, |terminalcolor| TextCol=0)
		.init
		s = s.wrap(frame.width-2)
		|| p = frame.xy+1
		for i.rect in frame Â± 1
			|| row = .TermScreen[i.y]
				row[i.x] = terminalcell(0," ")
		for l.lines in s
			self <~ (l, p, textcol)
			++p.y
		.DrawRect(frame, FrameCol)
	

	function Size (|ivec2|)
		return (w, h)
	
	syntax Expect (|string| msg="")
		cpp_part Fail
		"\07"
		if msg
			printline msg

	syntax access (|ind|)
		cpp_wrapper JB_App__GetChar
}

