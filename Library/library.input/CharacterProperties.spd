

// remove charset...?
// could just try replacing one or two calls with this... see how we get on
// if it works... then replace more.
datatype CharPropList (uint)
	linkage
		numeric false
	operator contains (|byte| b, |bool|)
		inline
		return (1<<b.Property|uint|) & self|uint|
	operator contains (|charprop| b, |bool|)
		inline
		cpp_part ContainsProp
		return (1<<b|uint|) & self|uint|
		
	operator Plus (|charproplist| p, |charproplist|)
		inline
		return (self|uint| ||| p|uint|)|charproplist|

	operator Plus (|charprop| p, |charproplist|)
		inline
		cpp_Part Add1
		return (self|uint| ||| (1<<p|uint|))|charproplist|

	syntax Access (|byte| b, |bool|)
		inline
		return self contains b


function byte.SyntaxIs (|CharProp| s, |bool|)
	inline
	return .Property == s


function Byte.Property (|CharProp|)
	inline
	return CharProp.Props[self]

	
function string.Find (|CharProp| Find, |int| From=0, |int| After=int.max, |ind|)
	cpp_part FindProp
	return .find(find.list, from, after)
	
function string.Find (|CharPropList| Find, |int| From=0, |int| After=int.max, |ind|)
	cpp_part FindPropList
	|| p = &CharProp.Props[0]
	from = from max 0
	After = After min .Length
	|| addr = .addr
	while From < After
		if find contains p[addr[from++]]
			return from-1


function string.OutCharSet (|CharPropList| Find, |int| From=0, |int| After=int.max, |ind|)
	cpp_part OutPropList
	|| p = &CharProp.Props[0]
	from = from max 0
	After = After min .Length
	|| addr = .addr
	while From < After
		ifn find contains p[addr[from++]]
			return from-1
	

datatype CharProp (uint16)
	linkage
		numeric false
	constants
		|CharProp| 
		Invalid
		Null				// 0
		Unprintable 		// 1-8, 11, 12, 14-30
		Line				// nl, rt
		HSpace				// space, tab
		Punct1				// +/*&%|~^$=@!()[]{}<>?/\`"';
		Punct2				// .-:
		Number				// 0123456789
		Underscore			// _
		Upper				// A-Z
		Lower				// a-z
		UnicodeMost			// 128+
		UnicodeCombining	// 128+ also

		|CharPropList| 
		NumMid			= (1<<Upper) + (1<<Lower) + (1<<UnderScore) + (1<<Number)
		Unicode			= (1<<UnicodeMost) + (1<<UnicodeCombining)
		Letters 		= (1<<Underscore) + (1<<Upper) + (1<<Lower) + Unicode
		XMLName			= Letters + (1<<Punct2)
		NameMid			= Letters + (1<<Number)
		White			= (1<<Line) + (1<<HSpace)
		Punct			= (1<<Punct1) + (1<<Punct2)

	operator Plus (|charprop| p, |charproplist|)
		inline
		cpp_part Add1
		return ((1<<self|uint|) ||| (1<<p|uint|))|charproplist|

	operator Plus (|charproplist| p, |charproplist|)
		inline
		return ((1<<self|uint|) ||| p|uint|)|charproplist|
		
	inline List (|charproplist|)
		return (1<<self|uint|)|charproplist|
	
	syntax cast (|charproplist|)
		inline
		return (1<<self|uint|)|charproplist|
		
	module
		|CharProp[256]| Props
		charprop.MakeDefault
//		charprop.test
//		
//	
//		inline Test
//			expect 'a' is lower and 'b' is lower and 'C' is upper and 'D' is upper and '5' is number
//			expect Letters contains 'C' and 'c'
//			|| fs = FastString()
//			for c in "We ate some cheese for 5Â£."
//				|| p = lower+Number
//				if p contains c 
//					fs <~ c
//			printline fs

	
		function MakeDefault
			|| item = &.Props[0]
			for s in 32
				item[s] = Unprintable
			item[0] = null
			item[9] = HSpace
			item[10] = line
			item[13] = line
			item[32] = HSpace
			for s in 33 to 126
				item[s] = punct1

			item['-'] = punct2
			item[':'] = punct2
			item['.'] = punct2

			item[0x7f] = Invalid
			item[0xC0] = Invalid
			item[0xFE] = Invalid
			item[0xFF] = Invalid
			
			for s in 'A'|int| to 'Z'|int|
				item[s] = charprop.upper
				item[s+32] = charprop.lower
			item['_'] = charprop.Underscore
			for s in '0'|int| to '9'|int|
				item[s] = charprop.number
	

