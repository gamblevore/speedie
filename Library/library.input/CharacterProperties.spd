

datatype CharPropList (uint)
	linkage
		numeric false
			
	operator contains (|charprop| b, |bool|)
		inline
		cpp_part ContainsProp
		return (1<<b|uint|) & self|uint|
		
	operator contains (|string| s, |bool|)
		cpp_Part ContainsStr
		return s.OutCharSet(self) == -1
		
	syntax Access (|uint| b, |bool|)
		if b <= 255
			|| cp = CharProp.Props[b]|uint|
			return (1<<cp) & self|uint|

	
function string.Find (|CharProp| Find, |int| From=0, |int| After=int.max, |ind|)
	cpp_part FindProp
	return .find(find.list, from, after)


// needs to be able to go backwards... to replace charset
function string.Find (|CharPropList| Find, |int| From=0, |int| After=int.max, |ind|)
	cpp_part FindPropList
	if from < 0 or after < 0 // i don't think its such a good API to take negative numbers...
							 // makes for confusing code. it WOULD have been good... if this were 1-based
							 // because -1 is also the return value of a missing value.
							 // nicer to make an explicit function that to handle end-relative searches
	|| p = &CharProp.Props[0]
	|| addr = .addr
	if from < After
		from = from max 0
		After = After min .Length
		while From < After
			|| b = addr[from++]
			if find contains p[b]
				return from-1
	  else // oof
		from = from min .length
		After = After max 0
		while From > After
			|| b = addr[--from]
			if find contains p[b]
				return from


// we could... define this as ~Find? like... its awesome.
function string.OutCharSet (|CharPropList| Find, |int| From=0, |int| After=int.max, |ind|)
	cpp_part OutPropList
	if from < 0 or after < 0 // same issue as in .find
	|| p = &CharProp.Props[0]
	|| addr = .addr
	if from < After
		from = from max 0
		After = After min .Length
		while From < After
			ifn find contains p[addr[from++]]
				return from-1
	  else // oof
		from = from min .length
		After = After max 0
		while From > After
			ifn find contains p[addr[--from]]
				return from
	


datatype CharProp (uint16)
	linkage
		numeric false
	constants
		|CharProp| 
		Invalid
		Null						// 0
		Unprintable 				// 1-8, 11, 12, 14-30
		Line						// nl, rt
		HSpace						// space, tab
		Underscore					// _
		Colon						// :
		DotDash						// .-
		CommaSlash					// \,
		RemainingPunct				// &|~@!()[]}?
		AfterTmp					// '`"{#$%
		EditorSeparators			// -^+/*;<>=
		Number						// 0123456789
		Upper						// A-Z
		Lower						// a-z
		UnicodeByte					// 128+

		|CharPropList| 
		SafeInC			= (1<<Upper) + (1<<Lower) + (1<<UnderScore) + (1<<Number)
		Unicode			= (1<<UnicodeByte)
		LettersOnly 	= (1<<Upper) + (1<<Lower) + Unicode
		LettersUnderScore= LettersOnly+ (1<<Underscore) 
		NameMid			= LettersUnderScore + (1<<Number)
		XMLNameMid		= NameMid + (1<<DotDash) + (1<<Colon)
		White			= (1<<Line) + (1<<HSpace)
		AfterDot		= White + (1<<Colon) + (1<<CommaSlash)
		AfterStatement	= White + (1<<Colon) + (1<<CommaSlash) + (1<<AfterTmp)
		EditorSplitter  = White + (1<<Colon) + (1<<CommaSlash) + (1<<EditorSeparators)
		Trim			= White + (1<<Null)
	
	
	inline List (|CharPropList|)
		return (1<<self|uint|)|charproplist|
	
	syntax cast (|CharPropList|)
		inline
		return (1<<self|uint|)|charproplist|
		
	module
		|CharProp[256]| Props
		charprop.MakeDefault
	
		function MakeDefault
			|| item = &.Props[0]
			for s in 32
				item[s] = Unprintable
			for s in 33 to 126
				item[s] = RemainingPunct
			for s in 128 to 253
				item[s] = UnicodeByte
			for s in '0'|int| to '9'|int|
				item[s] = charprop.number
			for s in 'A'|int| to 'Z'|int|
				item[s] = charprop.upper
				item[s+32] = charprop.lower
			for s in "'`\"{#$%"
				item[s] = CharProp.AfterTmp
			for s in "-^+/*;<>="
				item[s] = charprop.EditorSeparators

			item[0] = null
			item[9] = HSpace
			item[10] = line
			item[13] = line
			item[32] = HSpace

			item['_'] = charprop.Underscore
			item['-'] = DotDash
			item[':'] = Colon
			item['.'] = DotDash

			item[','] = CommaSlash
			item['\\'] = CommaSlash

///			item[0x7f] = Invalid // not set above
			item[0xC0] = Invalid
//			item[0xFE] = Invalid // also
//			item[0xFF] = Invalid
			
	

