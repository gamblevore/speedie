

function string.Find (|CharSet| Find, |int| From=0, |int| After=int.max, |ind|)
	cpp_part FindCharset
	nil checker
	if from < 0 or after < 0 // i don't think its such a good API to take negative numbers...
							 // makes for confusing code. it WOULD have been good... if this were 1-based
							 // because -1 is also the return value of a missing value.
							 // nicer to make an explicit function that to handle end-relative searches
	|| p = &CharSet.Props[0]
	|| addr = .addr
	if self!=nil
		if from < After
			from = from max 0
			After = After min .Length
			while From < After
				|| pb = p[addr[from++]]|uint|
				if (1<<pb) & Find|uint|
					return from-1
		  else // oof
			from = from min .length
			After = After max 0
			while From > After
				|| pb = p[addr[--from]]|uint|
				if (1<<pb) & Find|uint|
					return from


inline string.OutCharSet (|CharSet| Find, |int| From=0, |int| After=int.max, |ind|)
	return .find(~find, from, after)



datatype CharSet (uint)
	linkage
		numeric false
		
	operator contains (|string| s, |bool|)
		cpp_Part ContainsStr
		inline
		return s.OutCharSet(self) < 0
		
	syntax Access (|uint| b, |bool|)
		if b <= 255
			|| cp = CharSet.Props[b]|uint|
			return (1<<cp) & self|uint|
	
	operator Plus (|Charset| C, |charset|)
		inline // selftool uses this
		return (self|uint| ||| c|uint|)|charset|

	
	constants
		|int| 
		fInvalid
		fNull					// 0
		fUnprintable 			// 1-8, 11, 12, 14-30
		fLine					// nl, rt
		fHSpace					// space, tab
		fUnderscore				// _
		fColon					// :
		fDotDash				// .-
		fCommaSlash				// \,
		fRemainingPunct			// &|~@!()[]}?
		fAfterTmp				// '`"{#$%
		fEditorSeparators		// -^+/*;<>=
		fNumber					// 0123456789
		fUpper					// A-Z
		fLower					// a-z
		fUnicode				// 128+

	flags
		|CharSet| 
		Invalid
		Null
		Unprintable
		Line
		HSpace
		Underscore
		Colon
		DotDash
		CommaSlash
		RemainingPunct
		AfterTmp
		EditorSeparators
		Number
		Upper
		Lower

	constants
		|CharSet| 
		SafeInC			= (1<<fUpper) + (1<<fLower) + (1<<fUnderScore) + (1<<fNumber)
		Unicode			= (1<<fUnicode)
		LettersOnly 	= (1<<fUpper) + (1<<fLower) + Unicode
		LettersUnderScore= LettersOnly+ (1<<fUnderscore) 
		NameMid			= LettersUnderScore + (1<<fNumber)
		XMLNameMid		= NameMid + (1<<fDotDash) + (1<<fColon)
		White			= (1<<fLine) + (1<<fHSpace)
		AfterDot		= White + (1<<fColon) + (1<<fCommaSlash)
		AfterStatement	= White + (1<<fColon) + (1<<fCommaSlash) + (1<<fAfterTmp)
		EditorSplitter  = White + (1<<fColon) + (1<<fCommaSlash) + (1<<fEditorSeparators)
		Trim			= White + (1<<fNull)
	
	module
		|byte[256]| Props
		CharSet.MakeDefault
	
		libinternal MakeDefault
			|| item = &.Props[0]
			for s in 32
				item[s] = fUnprintable
			for s in 33 to 126
				item[s] = fRemainingPunct
			for s in 128 to 253
				item[s] = fUnicode
			for s in '0'|int| to '9'|int|
				item[s] = fnumber
			for s in 'A'|int| to 'Z'|int|
				item[s] = fupper
				item[s+32] = flower
			for s in "'`\"{#$%"
				item[s] = fAfterTmp
			for s in "-^+/*;<>="
				item[s] = fEditorSeparators

			item[0] = fnull
			item[9] = fHSpace
			item[10] = fline
			item[13] = fline
			item[32] = fHSpace

			item['_'] = fUnderscore
			item['-'] = fDotDash
			item[':'] = fColon
			item['.'] = fDotDash

			item[','] = fCommaSlash
			item['\\'] = fCommaSlash

///			item[0x7f] = Invalid // not set above
			item[0xC0] = fInvalid
//			item[0xFE] = Invalid // also
//			item[0xFF] = Invalid

