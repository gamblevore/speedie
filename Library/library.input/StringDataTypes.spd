

/*
	Jeebox internals. By Theodore H. Smith...
*/



datatype byte
	default 0

	constants
		|Byte|
		max = 255
		min = 0
		
	function Equals (|string| B, |bool|)
		cpp_part EqualsByte
		return b.length == 1 and b[] == self
	
	render
		if (!fs_in)
			return string.byte(self)
		fs_in <~ self|Byte|
		return ""

	function UTF8Size (|int|)
		cpp_wrapper JB_u8_Size
	
	function LowerCase (|Byte|)
		if .isupper
			return self + 32
		return self

	function UpperCase (|Byte|)
		if .islower
			return self - 32
		return self

	function IsHex (|bool|)
		return .IsInt  or  .in('a', 'f')  or  .in('A', 'F')

	function IsInt (|bool|)
		return (self|int| - '0')|uint| <= 9
		
	operator eq (|byte| b, |bool|)
		self = .lowercase
		b = b.lowercase
		return self == b
		
	function IsLetter (|bool|)
		return self|codepoint|.isletter
	function IsAlphaNum (|bool|)
		return self|codepoint|.isalphanum
	function IsWhite (|bool|)
		return self|codepoint|.iswhite 
	function IsUpper (|bool|)
		return self|codepoint|.isupper
	function IsLower (|bool|)
		return self|codepoint|.islower
	
	operator in (|charset| cs, |bool|)
		return cs[self]
	operator out (|charset| cs, |bool|)
		return !(cs[self])
	function In (|uint| a, |uint| b, |bool|)
		return  self >= a  and  self <= b
	
	iterator
		|| i = 0
		while i < self
			yield i
			++i
	


datatype int8 // aka char
	default 0
	
	constants
		|int8|
		max = 127
		min = -128
	


datatype cstring
	size 8
	linkage: cpp_class _cstring
	function Length (|int|)
		cpp_wrapper strlen
	
	function Copy (|string|)
		return string.copy(self|&byte|, .length)
	
	function Puts
		cpp_wrapper puts // hehe
	
	function Str (|string|)
		return string.Wrap(self)

	function Name (|cstring|)
		|| c = self|&byte|
		|| b = c
		
		while c[]
			if c++[] == '/'
				b = c
		return b|cstring|
	
	syntax cast (|&byte|)
		return self|&byte|
	
	render
		if fs_in
			fs_in <~ self
		  else
			return .copy
