
/*
	Jeebox internals. By Theodore H. Smith...
*/


class File (String)
	linkage
		cpp_class JB_File
		wrapper
	default			nil
	false			nil
	|filedes|		Descriptor

	constants
		IgnoreErrors = true
		O_CREAT	 = 0x200			/* create if nonexistant */
		O_TRUNC	 = 0x400			/* truncate to zero length */
		O_EXCL   = 0x800			/* error if already exists */
		O_APPEND = 0x008			/* set append mode */
		O_RDONLY = 0x000			/* open for reading only */
		O_WRONLY = 0x001			/* open for writing only */
		O_RDWR   = 0x002			/* open for reading and writing */

////////////////////////////////////////////////////////////////////////////

	constructor ( |string| path = "" )
		cpp_wrapper JB_File_Constructor
	
	destructor
		cpp_wrapper JB_File_Destructor

	function Mode (|FileMode|)
		cpp_Wrapper JB_File_Mode
	
	function Path (|string|)
		nil checker
		cpp_Wrapper JB_File_Path

	function Mode (assigns:|FileMode|)
		cpp_Wrapper JB_File_ModeSet

	function LinksTo (|string|)
		cpp_wrapper JB_File_LinkToGet

	function StringZeroTerminated.SymLink (assigns:|file|, |errorint|)
		cpp_wrapper JB_Str_SymLink

	function LinkTo (assigns:|string|, |errorint|)
		// This API confuses me... not sure if it's me or unix right now. 
		return Value.cstr.SymLink(self)

	function HardLinkTo (assigns:|stringzeroterminated|, |bool|)
		cpp_wrapper JB_File_HardLinkTo 

	function IsOpen (|bool|)
		disabled "use .opened"
	
	function Opened (|bool|) // isopen
		return .descriptor >= 0

	function Name (assigns:|string|Name, |errorint|)
		return .MoveTo( super.Parent[Name] )
	
	syntax cast (|bool|)
		opt norefcounts // what? why is this necessary?
		RETURN self|bool|

//	syntax cast (|faststring|)
//		cpp_part CastToStream
//		return .out

	syntax equals (|string?| s, |bool| aware, |bool|)
		return super.syntaxequals(s, aware)
			
	function Length (|int|)
		disabled "Do you mean the file's size or the file's path length?" 
	
	function Size (|int64|) // actually ints are less buggy. Like "|| NewSize = oldsize-1" with uint is bad.
		description "Return the file-size."
		cpp_wrapper JB_File_Size

	function Length (assigns:|int|)	
		disabled "Use .size"

	function Size (assigns:|int|N)
		cpp_wrapper JB_File_SizeSet

	function Created (|Date|)
		cpp_wrapper JB_File_Created

	function Close
		cpp_wrapper JB_File_Close
	
	Syntax Append (|string| data)
		.write(Data)
	
	Syntax Append (|messageroot| msg)
		cpp_Part WriteMsg
		.msg.render(.out(false))
	
	function string.Visible (|bool|)
		return .name[] != '.'
		
	function Hidden (|bool|)
		return !.visible

	function SmartData (assigns:|String| nieu, |bool|)
		if (.size != nieu.length) or (.readall(true) != nieu)
			return .data(Nieu)
	
	operator plus (|string| s, |file|)
		return (super+S).file
	
	function File (|file|)
		disabled "This is already a file."
	
	function Data (assigns:|String|Data, |bool|)
		cpp_wrapper JB_File_DataSet

	function Write (|string| Data, |ind|)
		cpp_wrapper JB_File_Write	
	
	function Parse (|int| lim = 16MB, |bool| AllowMissing=false, |messageroot|)
		// normally allowmissing is good, but here it is bad.
		// file.parse keeps track of the source+path
		|| f = .Resolve(allowmissing).file
		|| S = f.readall(lim, allowmissing)
		if s.IsCompressed
			s = s.Decompress(lim, nil)
		if s
			|memorylayer--| L = (Message).CreateUseLayer(f, s) 
			rz = S.parse(@arg, false) // already decompressed.
			(message).restore

	
	function Accessed (|Date|)
		nil checker
		cpp_wrapper JB_File_Accessed

	function Modified (|Date|)
		nil checker
		cpp_wrapper JB_File_Modified
	
	function Modified (assigns:|Date|)
		cpp_wrapper JB_File_ModifiedSet
			
	function IsPipe (|bool|)
		cpp_wrapper JB_File_IsPipe

	function Peek (|int| n, |string|)
		|| i = .offset
		rz = .read(n)
		.offset = i
	
	function Section (|int| a, |int| b, |string|)
		cpp_part AccessPeek
		|| WasClosed = .descriptor < 0
		if .open(File.O_RDONLY)
			.offset = a
			rz = .read(b-a)
			if WasClosed
				.close
		
	function IsCompressed (|bool|)
		return .section(0,16).iscompressed


	function RelFile (|file|)
		|| cd = app.cwd.pathdir
		return .trimstart(cd).File

	render 
		cpp_wrapper


	function Child (|string| name, |File!|)
		check (name) (Self, "No name specified for sub-file of directory: " + self)
		return File( self|string|.child(name) )
	
	function ChildExisting (|string| name, |File|)
		rz = .child(name)
		expect (rz.exists) (Self, "Can't find file/folder: " + rz)
	
	
	function Parent (|File|)
		return File.new( super.Parent )
	
	target !minilib {
	function Touch (|ExitCode|)
		return "/usr/bin/touch".SilentExecute([self])
	}
	
	function CompareFile (|File| A, |string| Error="", |bool|)
		cpp_part FileCompare
		FlowControl.DisabledIncr()
		|| code = .CompareSub(a)
		.Close
		A.Close
		rz = .CompareMsg(code, Error)
		FlowControl.DisabledIncr(-1)
	
	
	helper CompareMsg (|errorint| code, |string| Error, |bool|)
		flow off
		if (code==0) or !error
			return code == 0

		|string| msg = self
		if code == 1
			msg += " has different length to "
		  else
			msg += " has different contents to "
		msg += Error
		error msg
		
		
	helper CompareSub (|File| A, |ErrorInt|)
		cpp_part FileCompareSub
		flow off
		if (.Size != A.Size)
			return 1
		A.close
		.close
		.Open(O_RDONLY)
		A.Open(O_RDONLY)
		
		while {
			|| S_ = .Read(64K)
			|| SA = A.Read(64K)
			if S_ != SA
				rz = 2
				exit
		} (S_)

	
	function CompareData (|String| A, |string| Error="", |bool|)
		flow off
		|| code = 0
		if (.Size != A.length)
			code = 1
		  elseif (.ReadAll(true) != A)
			code = 2
		return .CompareMsg(code, error)

	
	function ReadAll (|int| lim = 128MB, |bool| AllowMissing = true, |stringthatwasreadsafely|)
		cpp_wrapper JB_File_ReadAll
	
	
	function MustExist (|string| operation = "", |bool|)
		if .exists
			return true
		|| fs = faststring()
		fs <~ "File does not exist"
		if super[] != '/'
			fs <~ " at "
			fs <~ app.cwd
		if operation
			fs <~ ", trying to "
			fs <~ operation
		fs <~ " "
		fs <~ super
			
		error (self, fs)

	
	function Exists (|bool|)
		nil checker
		cpp_wrapper JB_File_Exists
	
	syntax access (|string| name, |File!|)
		return .Child(name)

	function ListStart (|bool|)
		cpp_wrapper

	function ListActive (|bool|)
		cpp_wrapper

	function ListEnd
		cpp_wrapper

	helper MoveNext (|bool|)
		cpp_wrapper JB_File_MoveNext

	helper CurrChild (|String|)
		cpp_wrapper
	
	function Open (|int| Flags = File.O_RDWR ||| File.O_CREAT, |bool|AllowMissing=false, |filedes|)
		cpp_wrapper

	function OpenForRead (|bool| AllowMissing = false, |filedes|)
		return .open(0, AllowMissing)
	
	function OpenDes (|filedes|)
		.openempty
		return .descriptor
	
	
	function OpenBlank (|filedes|)
		disabled "Use OpenEmpty instead"

	function OpenEmpty (|bool|)
		nil checker
		cpp_wrapper JB_File_OpenBlank
	
	function EOF (|bool|)
		cpp_wrapper JB_File_EOF
	
	function IsDir (|bool|)
		cpp_wrapper JB_File_IsDir

	function IsLink (|bool|)
		cpp_wrapper JB_File_IsLink

	function IsTerminal (|bool|)
		return .descriptor.IsTerminal
		
	function Offset (|int64|)
		cpp_wrapper JB_File_Offset
	
	function Offset (assigns:|int64|)
		cpp_wrapper JB_File_OffsetSet
	
	function Delete (|ErrorInt|)
		cpp_wrapper JB_File_Delete
		
	function MoveToTrash (|ErrorInt|)
		|| tttt = (file)
		|| ch = file.trash(.name)
			.moveto(ch)
	
	function string.UniqueFile (|file|)
		|| f = .file
		if !f.exists
			return f
		return f.Parent.UniqueChild(.name)
			
		
	function UniqueChild (|string| name, |file|)
		|| test = name
		for 1000
			|| ch = .child(test)
			if !ch.exists
				return ch
			test = name + (0xFFFFFF()).render
		error (self, "Unable to find unique child name.")
			
	function Relax (|bool| NeedsMode=true, |errorint|)
		description "Does something with file permissions. If we are sudo, it makes this file not need sudo" 
		cpp_Wrapper JB_File_RelaxPath
	
	function DeleteAll (|bool| KeepSelf=false, |ErrorInt|)
		if super == "/" or ""
			return -1
		if .IsDir
			for C in self.ListFiles(true)
				|| tmp = C.DeleteAll
				if rz == 0
					rz = tmp
		if !KeepSelf and rz == 0
			rz = .Delete


	operator isfile (|string| ext, |bool|)
		return !.isdir and self isa ext

	operator isdir (|string| ext, |bool|)
		return .isdir and self isa ext
		
	function DirectoryContains (|string| path, |bool|)
		return path.pathfix starts .PathDir
	
	operator IsNewerThan (|File| F, |bool|)
		cpp_Part NewerThanFile
		return self isnewerthan f.modified
	
	operator IsNewerThan (|date| F, |bool|)
		return (.Modified > F)
	
	operator IsOlderThan (|File| F, |bool|)
		return (.Modified < F.Modified)
	
	function Writer (|FastString|) // streamout
		disabled "Use .out"

	function string.Out (|bool| Clear=true, |Faststring|)
		description "Lets you write to a file, via a faststring object." 
		|| f = .file
		if Clear and !f.ispipe // we don't close pipes!
			f.close
			require f.openempty
				
		rz = FastString()
		rz.file = f

	function string.In (|string| Header="", |int| ChunkSize=4MB, |StringReader|)
		description "Reads from a file, via a StringReader object. Useful because StringReader can read from files, strings, and more. Write code once and use it on multiple input-sources." 
		|| f = .file
		if f.mustexist("stream")
			rz = StringReader(f, chunkSize)
			if !rz.HasHeader(Header)
				rz = nil
	
	function Copy (|File| Dest, |bool| AttrOnly=false, |ErrorInt|)
		disabled "Use .Copyto"
	
	function CopyTo (|File| Dest, |bool| AttrOnly=false, |ErrorInt|)
		cpp_wrapper JB_File_Copy
	
	function CopyAll (|string| Dest, |bool| AttrOnly=false, |ErrorInt|)
		if !.isdir
			return .copyto(dest.file, AttrOnly)

		for f in .list
			rz = .child(f).copyall(dest[f]) 
			if !rz
				exit
	
	function List (|bool| invis = false, |bool| AsFile=false, |[string]|)
		flow off
		for name in self
			if (invis) or (name[] != '.')
				if AsFile
					rz <~ .child(name)
				  else
					rz <~ name
		rz.sort(&string.sorter)
		if !flowcontrol.isdisabled
			flowcontrol.input(rz, self)
		
	function ListFiles (|bool| invis = false, |[file]|)
		return .list(invis, true)|[file]|		
	
	function MoveTo (|string| Path, |ErrorInt|)
		cpp_wrapper JB_File_MoveTo
		
	operator inside (|file|f, |bool|)
		|| fp = f.pathdir
		return super[0, fp.length] == fp
			
	function Read (|int| Length, |bool| AllowMissing=true, |string|)
		cpp_wrapper JB_File_Read

	function Skip (|int| n=1)
		.offset += n

	iterator
		.ListStart
		while ( .MoveNext )
			yield .CurrChild
		.listend

	iterator Files
		.ListStart
		while ( .MoveNext )
			yield self.child(.CurrChild)
		.listend
	

	function SyntaxExpect (|string| Error)
		opt norefcounts
		cpp_part Fail
		errorreceiver.NewError( nil, Error, (.path, "")(self) )

	module
		|| DebugExecute = false
		|string| _Speedie
				
		function SpeedieDir (|string|)
			if !_speedie
				|| paths = app.exepath.resolve(false).parent
				|| srch = "/speedie/"
				|| found = paths.find(srch, 0, int.max, true)
					_speedie = paths[0, found + srch.length]
				  else
					_speedie = "/usr/local/speedie/".Resolve(False)
			return _speedie
		
		function SizeSorter (SorterComparer of file)
			|| aa = .size
			|| bb = b.size
			return bb - aa
			
		function string.NeatenPath (|string|)
			if self starts file.HomePath
				return "~" + self[file.HomePath.length ...]

		function string.IdealApp (|string|)
			target OSX
				return ("/Applications/" + .name)

		function file.IdealApp (|file|)
			return .path.idealapp.file


		function NewPipe (|filedes| Pipe, |file|)
			cpp_wrapper JB_File__NewPipe
		
// can rename these from path into files :)
		function TrashPath (|string|)
			target OSX
				return "~/.Trash/"
			target win
				return `C:\$Recycle.Bin`
			target lin
				return "~/.local/share/Trash"
		
		function SystemDrive (|string|)
			target win
				return getenv("%ProgramFiles%")
			  else
				return "/"
		
		function Applications (|string|)
			target OSX
				return "/Applications/"
			target win
				return `C:\$Recycle.Bin`
			target lin
				return "~/.local/share/Trash"

		function HomePath (|string|)
			cpp_wrapper JB_File__Home

		function Trash (|string|name="", |file|)
			rz = .TrashPath.Resolve.file
			if name
				rz = rz.uniquechild(name)

		function ReadCPath (|cstring| path, |bool| AllowMissingFile, |int| MaxSize = 100000000, |string|)
			cpp_wrapper JB_CPath_ReadAll

		function Documents (|file|)
			return "~/Documents/".file			// .file calls pathfix already
 
		function Desktop (|file|)
			return "~/Desktop/".file

		function Logs (|file|)
			return "/tmp/logs/".file

		function Prefs (|string|name, |file|)
			return .prefpath.child(name).file

		function Preferences (|file|)
			return .PrefPath.file 
		
		helper PrefPath (|string|)
			target osx
				return "~/Library/Preferences"
			target win
				return "~\AppData\Roaming" // whatever
			target lin
				return "~/.config"

	function AnyDate (|date|)
		return .created ?? .modified




datatype FileMode (int)
	constants
		|FileMode|
		Owner   = 7<<6
		Group   = 7<<3
		Other   = 7<<0
		CanExec = 1<<6  + 1<<3 + 1<<0
		Process = Owner + 5<<3 + 5<<0
		Data    = 6<<6  + 4<<2 + 4<<2


