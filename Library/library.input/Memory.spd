

/*
	Jeebox internals. By Theodore H. Smith...
*/

// probably the capcity should just be raw byte capacity.
// seems to make more sense... right?
// I think we could replace this with a "byte-slice" type!
// basically it would be a pointer, that has a Size/Length stored before the data.
// It must be refcounted, to replace the array class. So thats 3 ints
// We'll also need a death-action, but that can be stored within the refcount
// what about "locking" the data, so we can safely iterate over it quickly?
// won't that need another int? Perhaps...
// In fact... its not possible to reallocate ANYHOW... because if it moves
// we've fückéd it. Anything that has a ref to it, no longer does.
// Perhaps reallocation has to come via an array-wrapper object...
// So the array-wrapper object can refer to a byte-slice. I guess the slice
// can do locking, though.

class Memory
	linkage
		cpp_part Mrap
		cpp_class MWrap
		pragma noearlyfree
	
	|int|				ItemCount
	|int|				Length
	|&byte|				_Ptr
	|uint16|			ItemSize
	|byte|				DeathAction
	contains byte // should be byte?
	

	constructor (|int| ItemCount, |int| ItemSize, |&byte| ptr, |byte| DeathAction=DataObject.kfree)
		cpp_part ConstructorPtr
		.ItemSize = ItemSize
		.ItemCount = itemcount
		.deathaction = deathaction
		.length = 0
		._ptr = ptr
	
	
	function UseExternalPointer (|&nil| P, |int| ItemCount, |int| ItemSize)
		.ItemSize = ItemSize
		.ItemCount = itemcount
		if .deathaction == dataobject.kfree
			Memory.free(._ptr)
		.deathaction = 0
		._ptr = P|&byte|
	
	
	function Ptr (|&...|)
		return ._ptr|&...|
		
	function Next (|&...|)
		return (._ptr + .BytesUsed)|&...|
		
	setter Length (|int|)
		if Value > .itemCount
			value = .ItemCount
		  elseif Value < 0
			value = 0
		.length = Value
	
	operator plus (|int|n, |&...|)
		return (._ptr + .itemsize*n)|&...|
	
	function Copy (|string| Src, |int|)
		|| cap = .ByteCapacity min src.Length
			memory.Copy(.ptr, src.Addr, cap)
			return cap


	function DiffAt (|memory| other, |ind|) // seems unused
		disabled  "Unused? Also seems bad design."
		|| n = other.length
		if .length != n
			return .length min n
		|| p1 = ._ptr
		|| pz = p1 + n
		|| pc = other._ptr
		
		while p1 < pz
			if pc++[] != p1++[]
				return (p1-1) - ._ptr
		
	
	function CopyTo (|memory| other, |bool|)
		|| A1 = .ByteCapacity
		|| A2 = other.ByteCapacity
		|| A3 = A1 min A2
			memory.copy(other.ptr, .ptr, A3)
		return A3 == A1
		
		
	function AppendPlace (|&...|)
		|| L = .length
		if (L >= .itemCount)
			// try grow
			|| NewSize = .itemsize*L*2
			|| P2 = Memory.realloc(._ptr, NewSize)
			if (!P2) // fail
				return (&Memory.MDummy_)|&...|
			|| OldSize = .ByteCapacity
			Memory.zero((p2 + OldSize), NewSize - OldSize) 
			._ptr = p2
	
		.length = l + 1
		return (._ptr + (.itemsize*L))|&...|
	
	
	function Temporal
		random.shared.temporal(self)
	
	function Count (|int|)
		debugger "Use .ItemCount"
	
	setter ItemCount (|int|)
		.SetCount(value)
	
	helper SetCount (|int| ItemCount, |bool|)
		require .deathaction == DataObject.kfree
		|| S = .itemsize|int|
		|| old = S * .itemCount
		|| New = S * ItemCount
		if new == old
			return true // right?
		|| Mem = Memory.realloc(._ptr, New)
			._ptr = mem
			.itemCount = ItemCount
			if ItemCount < .length
				.length = ItemCount
			if New > Old
				memory.Zero((mem+old), New-Old)
			return true
		error (self, "Can't allocate enough bytes: " + new.render)

	function NeedSpare (|int| ReqItems, |int| Extra = 0,  |&...|)
		|| Q = .length + reqitems
		if .ItemCount < Q
			require .SetCount(Q+Extra)
		return (.ptr + .ItemCount*.itemsize)|&...|


	function HasSpare (|bool|)
		return .Length < .itemCount
	
	function ByteCapacity (|int|)
		return .ItemSize|int|*.ItemCount|int|
	
	function BytesUsed (|int|)
		return .ItemSize|int|*.Length|int|
	
	
	function Clear
		disabled "Do you mean .Free or .Zero?"
	
	
	function Zero
		zero(._ptr, .length * .itemsize)
	

	function DebugPrint
		|| fs = FastString()
		.dump(fs)
		printline fs
	
	render Dump (|int| BytesOnRow=16, |int| TabsAfterBytes=4)
		|| p = ._ptr
		|| p2 = p + .ByteCapacity 
		|| i = 0
		|| com = 0
		fs <~ "dumping memory:\n"
		
		while (p < p2)
			|| c = p++[]
			fs.appendhex(c, 2)
			if (++i >= BytesOnRow)
				i = 0
				com = 0
				fs <~ "\n"
			  elseif ++com >= TabsAfterBytes
				com = 0
				fs <~ ", "

	
	destructor
		if (.deathaction == DataObject.kfree)
			Memory.free(._ptr)
		

	iterator safe
		|| i = 0
		while (i < .length)
			yield self[i] (i)
			++i


	iterator
		|| FFFirst = .ptr 
		|| Curr = FFFirst  
		|| After = curr + .length
		while (Curr < after)
			yield Curr[0] (curr - FFFirst)
			++Curr

	
	function NextIndex (|int|)
		rz = .Length
		.Length = rz+1

	macro Next
		(_0.ptr|&...|)[_0.NextIndex]
	
	macro SyntaxAccess
		(_0.ptr|&...|)[_1]
	
	macro SyntaxAppend
		(_0.appendplace|&...|)[0] = _1

	syntax cast (|&...|)
		return .ptr

	module
		|uint64[2]| MDummy_
		function Object (|int| Count, |int| ItemSize, |Memory|)
			visible 
			|| p = byte.zalloc(Count * ItemSize)
				return .new(Count, ItemSize, p)
			description "Returns a memory of object size requested. Creates an error() if fails."

		function Random  (|int| n, |memory|)
			rz = byte[n]
			if rz
				random.shared.temporal(rz)
		
		helper _memset	 (|&byte| p, |byte| B, |int| n):				cpp_wrapper memset
		function Fill	 (|&byte| p, |int| n, |byte| B):				._memset(p, b, n)
		function FillInt (|&int| p, |int| n, |int| Pattern):			cpp_wrapper JB_FillInts
		function Unmap	 (|&nil| p, |int| n):							cpp_wrapper JB_munmap
		function Zalloc  (|int| n, |?&byte|)
			return byte.zalloc(n)
		function Realloc (|?&nil| b, |int| n, |?&byte|):				cpp_wrapper JB_realloc
		function Free	 (|?&nil| b):									cpp_wrapper JB_free
		function Zero	 (|&nil| addr, |int| n):						cpp_wrapper memzero
		function Copy	 (|&nil| dest, |&nil| src, |int| N):			cpp_wrapper memcpy

