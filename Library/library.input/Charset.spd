
/*
	Jeebox internals. By Theodore H. Smith...
*/
// need to upgrade dis...
// so we can have multiple classes
// ' \n\r\t' should be whitespace class
// abcd... should be letterclass
// 0-9 number class
// abcd+numbers = continuation class :)

class Charset
	size 260
	linkage
		wrapper
		cpp_class CharSet
		cpp_part CS

	constructor (|string| Chars, |bool| UseRange = false)
		cpp_wrapper JB_CS_Constructor
	
	function Invert (|charset|)
		cpp_wrapper JB_CS_Invert
	
	function Reverse
		disabled "use invert"
	
	function Bytes (|[string]|)
		opt norefcounts
		for b in self
			rz <~ b.render

	operator contains (|string| Data, |bool|)
		return !Data.OutCharSet(self)
		
	syntax Access (|int| c, assigns:|bool|)
		cpp_wrapper JB_CS_HasCharSet
	
	syntax Access (|int| C, |bool|)
		cpp_wrapper JB_CS_HasChar
		
	helper NextInCharset (|&int| p, |bool|)
		|| i = *p
		while ++i < 256
			if self[i]
				*p = i
				return true
		
	iterator
		|| i = -1
		while .NextInCharset(i)
			yield i|Byte|
