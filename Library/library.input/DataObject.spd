

class DataObject 
	linkage
		cpp_part Wrap
		cpp_class DTWrap
	contains int64
	constants
		kNothing = 0
		kFree    = 1
	
	|DataTypeCode|		Kind
	|byte|				DeathAction
	|int64|				PrivValue
	
	destructor
		if .deathaction == DataObject.kfree
			Memory.free(.privvalue|&nil|)

	
	constructor (|int64| v)
		cpp_part ConstructorInt
		.Kind = datatypecode.s64
		.PrivValue = v

	
	constructor (|bool| b)  // could be an issue as most things convert to a bool...
		cpp_part ConstructorBool
		.Kind = datatypecode.bool
		.PrivValue = b|int|

	constructor (|float64| v)
		cpp_part ConstructorF64
		.Kind = datatypecode.f64
		.PrivValue = v.AsInt

	constructor (|float| v)
		cpp_part ConstructorF32
		.Kind = datatypecode.float
		.PrivValue = v.AsInt
	
	constructor (|?&nil| p)
		cpp_part ConstructorVoidPtr
		.Kind = datatypecode._voidptr
		.PrivValue = p|int64|


	syntax access (||) // for floats... perhaps use this?    macro first: .privvalue|...(samebits)|
		inline
		if self
			return .PrivValue

	
	function Value (|int64|)
		inline
		if self
			return .privvalue
	
	
	function FloatValue (|Float64|)
		if self
			return .privvalue.AsFloat


	function Value (assigns:|int64|)
		disabled "This property is immutable"

	render
		visible // hmmm
		|| k = .kind
		if k.isfloat
			fs <~ .privvalue.AsFloat
		  else
			|| v = .PrivValue
			if k == datatypecode.bool	
				v|bool|.render(fs)
			  elseif k isnt numeric // oop
				fs <~ "0x"
				fs.appendhex(v)
			  else
				fs <~ .privvalue.AsFloat

	module
		inline NewFloat (|Float64| v, |DataobjectFloat64|)
			cpp_Part F64
			return DataObject(v) 
		inline NewFloat (|Float| v, |DataObjectFloat32|)
			cpp_Part F32
			return DataObject(v) 



role DataObjectFloat64 (DataObject)
	syntax access (|float64|)
		if self
			return .PrivValue.AsFloat
	
	inline Value (|float64|)
		return self[]



role DataObjectFloat32 (DataObject)
	syntax access (|float|)
		if self
			return .PrivValue|int|.AsFloat
	
	inline Value (|float|)
		return self[]


