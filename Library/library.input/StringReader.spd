

class StringReader 
	|int|						Length
	|FastBuff|					Data
	|file?|						File
	|ParserCallBack?|			CallBack
	|object--?|					_Object
	|int|						ChunkSize
	|int| 						StartFrom
	|bool| 						_NoMoreChunks

	linkage:cpp_Part SS
	
	constructor (|string| Data)
		.reset(Data)
	
	constructor (|file!| file, |int| ChunkSize=4MB)
		cpp_part ConstructorFile
		file.close
		.file = file
		.ChunkSize = chunksize
		.length = file.size
		.ReadChunk(file)
	
	function Reset (|string| Data)
		.data.readfrom = Data
		.length = Data.length
		._NoMoreChunks = true

	function ReadAll (|string|)
		return .str(.length - .startfrom)


	function Parse (|syntax| owner = @arg, |bool| AllowDecomp=true, |message|)
		return .ReadAll.parse(owner, allowdecomp)

	
	helper ReadChunk (|file| f, |bool|)
		.data.readfrom = f.read(.chunksize)		// for now
		if .data.readfrom.length < .chunksize
			._NoMoreChunks = true // not technically true for pipes... but this will do for now!
		return .data.readfrom
	
	function Skip (|int| p = 1)
		if p < 0 // end
			.position = .length
		  else
			.position += p
	
		
	helper Remaining (|int|)
		return .length - .position


	function HasAny (|bool|)
		nil checker
		require (self != nil)
		if .data.hasany
			return true
		return .NextChunk


	helper NextChunk (|bool|)
		opt norefcounts
		|| f = .file
			.startfrom += .data.size
			if .ReadChunk(f)
				return true
			.file = nil // why not
		
		
	function NonZeroByte (|int|)
		while 
			|| b = .byte
				return b
	
	function Byte (|int|)
		if .data.HasAny()
			return .data.byte
		if .NextChunk
			return .data.byte
		return -1
		
	syntax Compare (|int| n, |int|)
		return .length - n
	
	function Position (|int64|)
		return .data.position + .StartFrom
	setter Position (|int64|)
		.data.position = value - .StartFrom

	function StrLength (|int|)
		|uint| v = .byte
		if v <= 127
			return v
		v = v & 127
		|| sh = 7
		while
			|uint| b = .byte
			v |= (b&127) << sh
			if b <= 127
				return v
			// should really cap this to 31-bits.
			sh += 7
			expect (sh <= 28) (self, "Bad Length")

	
	function DelimitedString (|byte| split, |string|)
		// this function doesn't work with streaming...
		// its not ideal. but none of my code needs this streamed!
		// they just read the entire file in. So I don't need it!
		|| p = .position
		|| zpos = .data.find(split, p, int.max)
			.position = zpos+1
			return .data[p, zpos] // don't call .section, seeing as its not streaming.

	function CString (|string|)
		return .DelimitedString(0)
	
	function UpTo (|int| end, |int|)
		return end - .position
	
	function Int32BE (|int|)
		return .uintbe(4)

	function UIntBE (|int| Size, |uint64|)
		for Size
			rz <<= 8
			rz += .byte max 0

	function UInt (|int| size, |uint64|)
		for i in size
			|| b = .byte max 0
			rz += b << (i<<3)
	
	function Trim (|int| n, |string|)
		return .str(n).Trim
		
	function StrNoAdvance (|int| n, |string|)
		|| p = .Position
		rz = .str(n)
		.Position = p
	
	function NoMoreChunksToRead (|bool|)
		if ._NoMoreChunks
			return true
		if .data.Remaining <= 0
			if !.file
				return true
			if .length <= .startfrom
				return true
				
	function Str (|int| n, |int| skip = 0, |string|)
		ifn n > 0 and skip < n
			return ""
		if (.data.has(n)) or (.NoMoreChunksToRead)
			(n min= .remaining)
			|| pos = .data.position		// no need use the .startfrom
			.data.position = n + pos
			return .data.syntaxaccess(pos + skip, .data.position)

		|| Remaining = n - skip
		|| r = string(Remaining) #require
		|| Dest = r.addr
		while
			|| Copied = .data.CopyTo(dest, .data.Remaining min Remaining)
			Remaining -= Copied
			dest += copied
			loop Remaining > 0
			if !.NextChunk
				r = r[0, r.length - remaining].copy
				exit
		return r
	
	function HasHeader (|string| Header, |bool|)
		if !Header
			return true
		return Header == .str(Header.length)
	

	iterator
        while
		    || _b = .byte
			if (_b < 0): exit
			yield (_b) (.Position)
		
	syntax expect (|string| Error)
		opt norefcounts
		cpp_part Fail
		._Object = nil
		require !.data.WentBad
		.data.WentBad = true
		
		if !error
			if (.file)	
				error = "Error decoding file: "
			  else
				error = "Error decoding data."
				
		errorreceiver.NewError(nil, error, .file)


	task ParserCallBack (|stringreader| Upon)
		run (|message| msg)



	
function string.Stream (|string| Header, |stringreader|)
	cpp_part StreamWithHeader
	rz = StringReader(self)
	if !rz.HasHeader(Header)
		rz = nil

	
function string.Stream (|StringReader|)
	return StringReader(self)

