

function string.ArgName (|string|)
	// "--Name=Value" --> "Name"
	// "--Name" --> "Name"
	if (self[] == '-') 
		|| i = .find( '=' ) ?? int.max
		|| S = self[ 0, i ]
		S = S.TrimFirst('-')
		return S.lowercase


function string.ArgValue (|string|)
	// "--Name=Value" --> "Value"
	// "--Name" --> ""
	// "Value" --> "Value"
	if (self[0] != '-')
		return self

	|| it = .find( '=' )
		return self[it + 1...]
	return "true"



role AppArgument (object)
	function Value (|String|)
		if (self isa string):  return .argValue
		if (self isa message): return .argValue
	
	function Name (|string|)
		if (self isa string):  return .argName
		if (self isa message): return .argName


// The idea is... we want to have an array of arguments... that can be messages or strings:
	// * (name:)	  for switches:  --nocolor
	// * (name:value) for switches:  --level=3
	// * (value)	  for paths:	 "/usr/dir/filexx"


function Message.ArgName (|string|)
	opt norefcounts
	require self == @item
	return .firstname.lowercase


function Message.ArgValue (|string|)
	opt norefcounts
	if (self == @item)
		return .second.name ?? "true"
	return .name



helper @int.MainArg         (|int| Pos, |string| name,  |int|)
	return string.MainArgSub(Pos, name, "integer")$.int

helper @bool.MainArg        (|int| Pos, |string| name,  |bool|)
	return string.MainArgSub(Pos, name, "boolean")$.Yes

helper @string.MainArg      (|int| Pos, |string| name, |string|)
	return string.MainArgSub(Pos, name, "string")

helper @message.MainArg     (|int| Pos, |string| name, |message|)
	return string.MainArgSub(Pos, name, "jeebox")$.parse

helper @float.MainArg       (|int| Pos, |string| name, |f64|)
	return string.MainArgSub(Pos, name, "float")$.float

helper @file.MainArg		(|int| Pos, |string| name, |file|)
	return string.MainArgSub(Pos, name, "file-path")$.file

helper @ExistingFile.MainArg (|int| Pos, |string| name, |file|)
	return string.MainArgSub(Pos, name, "file-path")$.FileThatExists

helper @JeeboxFile.MainArg   (|int| Pos, |string| name, |messageroot|)
	return ExistingFile.MainArg(pos, name)$.parse

helper @OutputFile.MainArg   (|int| Pos, |string| name, |faststring|)
	return file.MainArg(pos, name)$.Out


// these roles really should be submodules on the file-module.
// maybe we can do this now? we do have some kinda name spaces now...
// like these:
//			module file.Jeebox: helper MainArg (...)
// 			module file.Output: helper MainArg (...)
//			module file.Existing: helper MainArg (...)
//			module file.Data: helper MainArg (...)

role ExistingFile (file)
role JeeboxFile   (file)
role OutputFile   (faststring)


helper @string.MainArgSub (|int| Pos,  |string| name, |string| type,   |stringthatwasreadsafely|)
	// could remove "pos", and use array.mark?
	opt norefcounts
	|| str = app.args[pos]
	if str != nil
		return str
	if StdErr.ok
		|| usage = app.Usage
		error "Missing $name ($type).\n\n$usage"
	return string.Error


helper @FileSizeInt.MainArg (|int| Pos, |string| name,  |FileSizeInt|)
	|| size = string.MainArgSub(Pos, name, "file-size")$
	|f64| Mul = 1.0
	|| T = size.length-2
	|| x = 1.0KB
	|| x2 = 1KB
	if size endswith "kb"
		mul = 1.0KB
	  elseif size endswith "mb"
		mul = 1.0MB
	  elseif size endswith "gb"
		mul = 1.0GB
	  else
		T+=2
	
	|| f = size[0, T].float
	return (f*mul)|FileSizeInt|
