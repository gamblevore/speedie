

class StringShared (string)
	linkage
		wrapper
		cpp_class JB_StringShared
		cpp_part Str

	cpp_refs
		Disturbs		nil
	
	constructor (|string| data)
		cpp_wrapper JB_Str_Clone

	default			""



class MessageID (stringshared)				// now is usefully used! :D
	linkage
		cpp_part ID
		
	contains object
	||					Obj
	|uint64| 			Tag
	|Syntax|			Func
	
	render
		fs <~ .func.name
		fs <~ "@"
		if .length
			fs.AppendQuotedEscape(self)
	
	constructor (|string| Name, |Syntax| Fn, |uint64| Tag)
		super.constructor(name)
		.Func = fn
		.obj = nil               // c++ generator fails otherwise
		.tag = tag
		
	function Msg (|message| parent, |message|)
		return Message(parent, .func, -1, .name, .name.length)



struct MessageDecompressor
	linkage:cpp_part Mxpd
	|&Message--|					End
	|&Message--|					Curr
	|&Message--|					Table

	helper Allocate (|&message|)
		require !StdErr.ok
		
		|| pos = .Curr - .Table
		|| Amount = (pos*2) max 4K
		rz = Memory.realloc(.table, Amount * platform.PointerBytes)|&message|
			
		expect rz ("jbin decompressor can't allocate")
		.Table = rz
		.Curr = rz + pos
		.End = rz + Amount
	
	syntax append (|Message| New, |bool|)
		|| c = .Curr
		if c >= .End
			c = .Allocate #require
		*c++ = New
		.Curr = c
		return true
	
	destructor
		memory.Free(.Table)
		.End = nil
		.Table = nil
		.Curr = nil
	


struct MessageCompressor (MessageDecompressor)
	linkage:cpp_part MCmp
	|(dictionary of messageID)[64]| D
	
	function Position (|int|)
		return .curr - .table
		
	function MakePlace (|message| m, |&byte|)
		opt norefcounts
		|| fn = m.func
		if fn|int| < 64
			|| pl = &.d[fn]
			|| v = (*pl) init dictionary()
			return v.MakePlace(m.name)|&byte|

	function Find (|message| m, |MessageID|)
		opt norefcounts
		|| where = .MakePlace(m)|&messageID|
			return *where init MessageID(m.name, m.func, .Position)
		return nil
		
	destructor

