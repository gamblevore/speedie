

class Validator
	description "This is a replacement for XML-DTD. You can validate a Jeebox document using the validator, as if you were using a DTD to validate XML. Instead of writing a DTD to validate Jeebox, you write validator code to validate Jeebox. The validator class gives lots of useful functions to make this expressive, and simple to write. It probably runs faster than a DTD anyhow, as its more direct. And you can do far more... Look in /usr/local/speedie/Examples/xml_dtd/tv.val.spd for an example"
	// linkage: inline

	// can we make this class "cake-only?"
	// like as if it were created within the project
				
	|Message|	Msg_
	|Syntax|	TestFunc = @nil
	|String?|	TestName = nil
	|Message?|	Curr = nil
	
	iterator
		while (|| Curr = .Allow)
			yield Curr
	
	render
		if self
			return .msg_.Render(fs_in)
	
	function Msg (|Message?|)
		if self
			return .msg_
		
	function Int (|int64| Low = int64.min, |int64| High = int64.max, |int64|)
		if self
			return .msg_.Int(0, low, high)
	
	function Str (|validator|)
		return self[@str].end
	
	function Name (|string|)
		if self
			return .msg_.name
	
	function All (|syntax| Fn=@tmp, |string?| name=nil, |ValidatorAll|)
		description "At least one must be found, and none after us."
		return .any(fn, name)
	
	function Some (|syntax| Fn=@tmp, |string?| name=nil, |ValidatorSome|)
		description "At least one must be found"
		return .any(fn, name)
		
	function Any (|syntax| Fn=@tmp, |string?| name=nil, |Validator|)
		description "Zero or more may be found"
		nil checker
		if self
			.TestFunc = fn
			.TestName = name
			return self

	function Upon (|message|)
		opt norefcounts
		|| upon = .curr
			return upon.next
		return .msg_.first
	
	
	function Allow (|Validator|)
		cpp_part Allow0
		nil checker
		if self
			return .allow(.Testfunc, .Testname)

	function Allow (|syntax| Fn=@tmp, |string?| name=nil,  |Validator|)
		opt norefcounts
		|| after = .Upon
		if (After == fn) and (name == nil or After.name ~= name)
			.curr = after
			return validator(After)

	syntax Access (|syntax| Fn=@tmp, |string?| name=nil,  |Validator|)
		nil checker
		opt norefcounts
		if self
			|| n = .allow(Fn, name)
				return n
			.msg_.ReportFailiure(Fn, name, .upon)
	
	function Arg (|validator|)
		return self[@arg].end
	
	function ChildEnd
		if self
			.curr.ExpectLast
		
	function End (|Validator|)
		nil checker
		if self
			.msg_.ExpectLast
		return self

	autogen ReportMissing
		if self
			"!!!!"
			printline .msg_
			"----"
			printline .curr
			"????"
			.msg_.ReportFailiure(.testfunc, .testname, .upon)
	
	syntax Equals (|string?| s, |bool| Aware=false, |bool|)
		cpp_part AllowStr
		return .msg_.SyntaxEquals(s, aware)
			
	syntax Equals (|syntax| fn, |bool|)
		return .msg_.func == fn
			
	syntax Expect (|string?| Description=nil)
		inline
		.msg.SyntaxExpect(description)


role ValidatorSome (Validator)
	iterator
		|| Count = 0
		while (|| Curr = .Allow)
			Count++
			yield Curr
		if !Count
			.ReportMissing


role ValidatorAll (Validator)
	iterator
		while (|| Curr = .Allow)
			yield Curr
		.childend



function Message.Validate (|syntax| func=nil, |string?| name=nil, |Validator|)
	real self
	opt norefcounts
	if self == @arg and func != @arg
		|| inner = .first
		if inner == @sheb
			inner++
		if inner
			inner.ExpectLast
			self = inner
	if .Expect(func, name)
		return Validator(self)

