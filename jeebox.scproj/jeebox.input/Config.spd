

// Use message... as a config-file!

role config (message)
	linkage
		pragma noearlyfree
	iterator
		|| C = .conffirst
		while (C)
			yield C
			C++

	syntax cast (|string|)
		cpp_part AsString
		inline
		if self
			return .Value
			
	syntax cast (|int64|)
		cpp_part AsInt
		inline
		return .int
			
	syntax cast (|f64|)
		cpp_part AsFloat
		inline
		return .float
	
	syntax cast (|MessageTable|)
		cpp_part AsTable
		inline
		return .Dict(true)
	
	function Save (|bool|)
		|file--| f = .path
			|| r = .render
			|| p = r.parse
				rz = f.smartdata(r)
	
	function Path (|file|)
		return .obj as file
	
	function ConfFirst (||)
		opt norefcounts
		|| l = .confarg
			return l|message|.first
			
	module
		function Create (|string| path,  |config!|)
			opt norefcounts
			return path.Resolve(true).file.Config



function file.Config (|int| lim = 1MB, |config|)
// why would a conf-file be so big?
	opt NoRefCounts
	if .Exists
		rz = .Parse(lim, @arg)|config|
	rz ?= message()
	rz.obj = self


extend message
	function ConfArg (|config|)
		opt norefcounts
		if self == @arg or nil
			return self
		|| w = (.last, .next)(self == @tmp)
		if w == @arg
			return w

	function SyntaxAccess (|string| key, |syntax| Needed, |string| DefaultName="", |config|)
		opt norefcounts
		description "Creates the correct child type if not found" 
		|| found = .SyntaxAccess(key)
		if found == Needed
			return found
		|| arg = .confarg
		if found
			error (found)
		found = arg.msg(@tmp, key)
		return found.msg(Needed, DefaultName)

	function SyntaxAccess (|string| key, |bool| Err=false, |config|)
		cpp_part GetConf
		opt norefcounts
		|| arg = .confarg
		for item in arg
			if item ~= key
				|| f = item.first
					return f
				if (Err)
					error (item, "Empty value for row: " + key)
				// don't remove the item to cleanup. Its possible they are using it for a menu or something.
				return nil
		if err
			error (self, key + " not found.")
	
	function Value (|string|)
		if self
			if self == @tmp and self in @arg
				return .firstname
			if self != @arg
				return .name
			return .FirstName
