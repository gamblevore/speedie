

Module MZLab 
	constants
		None      = 0			// none
		Fast	  = 10			// old mz?
		Strong    = 20			// 
		Strongest = 30			// 
		Default   = strong		// its fast enough!
	
	
	function FreeNow
		cpp_wrapper JB_CompFreeNow
		
	
	inline string.CompressTest_ (|bool| report, |int| which=3, |bool|)
		if which & 1
			require .CompressTestSub_(mzlab.strong, report)
		if which & 2
			require .CompressTestSub_(mzlab.strongest, report)
		return true
	
	
	inline string.CompressTestSub_ (|int| Strength, |bool| report, |bool|)
		|compressionstats| stats
		|| c = .compress(Strength, stats)
		if report // remove? compression is good for now.
			stats.Print
		|| decomp = c.Decompress(int.max, nil)
		rz = self == decomp
		if !rz
			error "Decompression failed on: " + self
			decomp = c.Decompress(int.max)
	
	
	target CompressionTest
		function TestCompression
			|| f = file.speediedir.child("Speedie.scproj/speedie.input/ASMBuilder/").file
			require f.exists
			for ch in f.ListFiles
				if ch.visible
					|| s = ch.ReadAll
					|| c = s.compress
					|| d = c.Decompress
					expect (s == d) ("Compression fail")
					(c)
	
			"TestCompression passed."
		
	
		function TestChunkedCompression
			// useful for jbin and exec.
			|| fs = FastString()
			"abcabc".compress(fs)
			"defdef".compress(fs)
			"ghighi".compress(fs)
			|| y = fs.GetResult.Stream()
			// so... now we have a stream. lets read from it.
			expect y.Decompress == "abcabc"
			expect y.Decompress == "defdef"
			expect y.Decompress == "ghighi"
		



struct CompressionStats 
	|duration| Duration
	|int|  In
	|int|  Out
	|byte| Purpose
	|bool| Live
	linkage:cpp_part MzSt	
	flags
		Compression
		Decompression


	function Start (|int| Purpose, |&CompressionStats|)
		nil self
		self ?= CompressionStats.all
		.purpose|=purpose
		.Duration -= date.now
		return self

	module
		|CompressionStats| All
		inline New (|compressionstats|)
		
	target minilib {
	
	inline End
	inline Len (|int| n)
	inline LiveUpdate (|int| s, |int| outt, |bool| compress=true)
	inline Durr (|float|)
	render Report (|bool| Compression)
		inline
	inline Clear
	inline Print (|bool| Compress=true)
	
	} else {

	function Clear
		.Duration = nil
		.in = nil
		.out = nil
		.Purpose = nil
	
	inline End
		.Duration += date.now

	inline Print
		printline .Report
		
	function LiveUpdate (|int| in, |int| out)
		.in += in
		.out += out
		if .live
			.print
	
	function Durr (|duration|)
		rz = .duration
		if rz < 0
			rz += date.now
	
	function Report (|faststring| fs_in=nil, |string|)
		|| a = .in
		|| b = .out
		if .Purpose & Decompression
			swap (a) (b)
		return "".RenderSpeed(fs_in, A, .durr, B)

}


