

prototype fn_OpASM          (|&ASMState| self, |asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asmreg|)
prototype fn_ASMConstifier  (|asmreg| L, |asmreg| R, |int64|)


extend asmreg
	function CondAnswer (|asmreg|)
		.µtype = DataTypeCode.bool
		is AlreadyNegated
		is CondAnswer
		isnt negate
		return self

	function BoolAnswer (|asmreg|)
		.µtype = DataTypeCode.bool
		
		is AlreadyNegated
		isnt negate
		if self is CondRequest
			isnt condrequest
			is CondAnswer
		return self 


extend ASMState
	function EqualsInt (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asmreg|)
		|| negate = dest is negate
		|| Res = dest.BoolAnswer						// Hmmm... so what should it be?
		if  L is constoutput  and  R is constoutput
			.Nop2Consts(r, l)
			|| num = ((l.const==r.const)!=negate)|int|
			return .NumToReg(exp, num, res, datatypecode.bool)
		if dest isnt CondRequest
			return exp.CMPB(dest, L, R, negate|int| xor 1) * res

		if l.reg
			if negate
				return exp.JMPN(r,l,0) * res
			return exp.JMPE(r,l,0) * res
		if negate
			return exp.JBAN(r,0) * res
		return exp.JBOR(r,0) * res

	
	function Exists (|asmreg| dest, |asmreg| L, |message| exp, |asmreg|)
		opt inline
		return .Equals(dest.negate, l, asmreg(), exp)


	function EqualsSame (fn_opasm)
		|| num = (dest isnt negate)|int|
		return .NumToReg(exp, num, dest, datatypecode.bool).BoolAnswer
	
	
	function Equals (fn_opasm)
		|| rr = r.reg,  || ll = l.reg
		if !rr  and  ll
			swap (l) (r) // put reg 0 into the left... more convenient.
		(Dest  as=  l & r & asmreg.RealConst)
		
		if rr == ll
			return .EqualsSame(dest, l, r, exp)
		
		if dest.IsInt
			rz = .EqualsInt(dest, l, r, exp)
		  else
			rz = .CompareFloat(dest, l, r, exp, 2)						
			
		rz = rz.boolanswer
	
		
	function NotEq  (fn_opasm)
		return .Equals(dest.negate, l, r, exp)


	function CompareInt (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		mode |= !r.signed<<2
		//if dest is negate
		mode ^= dest is Negate
		#!do this AFTER? We lose condanswer otherwise
		dest = dest.BoolAnswer
		if l is constoutput and r is constoutput
			return .ConstCompareInt(dest, L, R, exp, Mode)
		if Dest isnt CondAnswer
			return exp.CMPI(dest, L, R, mode) * dest
		return exp.JMPI(L, R, mode, 0) * dest


	function ConstCompareFloat (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		.Nop2Consts(r, l)
		|| Result = .ConstCompareFloatSub(l, r, mode)
		return .NumToReg(exp, result|int|, dest.BoolAnswer, datatypecode.bool)
		
		
	function ConstCompareFloatSub (|asmreg| L, |asmreg| R, |int| Mode, |bool|)
		opt inline
		ifn mode & 4
			|| A = L.f32
			|| B = R.f32
			if mode <= 1
				if mode == 0
					return A >  B
				return A <= B
			if mode == 2
				return A == B
			return A != B

		|| A = L.f64
		|| B = R.f64
		mode -= 4
		if mode <= 1
			if mode == 0
				return A >  B
			return A <= B
		if mode == 2
			return A == B
		return A != B


	function ConstCompareInt (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		.Nop2Consts(r, l)
		|| Result = .ConstCompareIntSub(l, r, mode)
		return .NumToReg(exp, result|int|, dest.BoolAnswer, datatypecode.bool)
		
		
	function ConstCompareIntSub (|asmreg| L, |asmreg| R, |int| Mode, |bool|)
		opt inline
		|| A = L.Const
		|| B = R.Const
		if mode <= 1
			if mode == 0
				return A >  B
			return A <= B
		if mode == 2
			return A == B
		return A != B


	function CompareFloat (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		mode |= r.IsBig<<2
		if dest is negate
		mode ^= dest is Negate
		dest = dest.BoolAnswer
		if (l is constoutput and r is constoutput) // needs to handle this!
			return .ConstCompareFloat(dest, L, R, exp, Mode)
		if dest isnt Condanswer
			return exp.CMPF(dest, L, R, Mode) * dest
		return exp.JMPF(L, R, Mode, 0) * dest


	function Compare (|asmreg| dest,  |asmreg| L,  |asmreg| R,  |message| exp,  |int| Mode,  |asmreg|)
/*	 (A >  B)    (A <= B)    (A == B)    (A != B)	*/
		if dest.IsInt
			return .CompareInt(dest, l, r, exp, mode)
		return .CompareFloat(dest, l, r, exp, mode)
	
	function More   (fn_opasm)
		return .compare(dest, l, r, exp, 0)

	function LessEq (fn_opasm)
		return .compare(dest, l, r, exp, 1)

	function Less   (fn_opasm)
		return .compare(dest, r, l, exp, 0)

	function MoreEq (fn_opasm)
		return .compare(dest, r, l, exp, 1)



	// MATH 
	function QuickFloat32Plus    (fn_OpASM)
		if  r is constoutput // a + 1,   a - 1
			|| QQ = .QuickFloatPlusConstSub(dest, l, r, exp)
				return qq
		if  l is constoutput  and  l isnt Alternate   // 1 + a --> a + 1
			return .QuickFloatPlusConstSub(dest, r asnt asmreg.alternate, l, exp)
	
			
	function QuickFloatPlusConstSub    (fn_OpASM)
		|| k = r.Const|uint64|
		k ^= (l is Alternate)<<31
		require (k >> 18) << 18 == k
		.nopconst(r)
		return exp.FADK(dest, l, k) * Dest


	function message.DivByZero
		problem (self, "Divide by zero???")	


	function QuickFloatDiv    (fn_OpASM)
		require r is ConstOutput
		|| v = r.float
		|| v2 = v.abs
		// could use exp.FEXK to do powers of 2
		if v2 == 1.0 or 0.0
			if v2 == 0.0
				Exp.DivByZero
			.nopconst(r)
			return .Quick1Or1Sub(dest, l, v|int|, exp)


	// from 1 to -1
	function Quick1Or1Sub (|&ASMState| self, |asmreg| dest, |asmreg| L, |int| ptoi, |message| exp, |asmreg|)
		if ptoi == -1										//   x * -1  -->  0-x 
			return .subtract(dest, asmreg(), l, exp)
		if ptoi == 1										//   x * 1   -->  x 
			return l
		return dest.µtype.zero								//   x * 0   -->  0


	function QuickIntMul    (fn_OpASM)
		|| Pow2 = .IntPowerOfTwo(r)
			if Pow2 <= 1
				return .Quick1Or1Sub(dest, l, Pow2, exp)
			return exp.BFLG(dest, l,  Pow2-1, 0, 0) * Dest	// y = x * 4  -->  y = x << 2

		|| five = .IntPowerOfTwo(r, 1) // 5 or 9 or 17 or 33, etc
			return exp.ADD(dest, l, l, five-1) * dest


	function QuickFloatMul    (fn_OpASM)
		require r is constoutput	
		|| v = r.float
		if  v == 1.0 or 0.0 or -1.0
			.nopconst(r)
			return .Quick1Or1Sub(dest, l, v|int|, exp)
		if  v == 2.0
			.nopconst(r)
			return .plus(dest, l, l, exp)
		
		if r.Fourbytes
			|| x = r.const
			|| y = x >> 18
			if x == y << 18
				.NopReg(r)
				return exp.FMLK(dest, l, x) * Dest

	
	function ASMReg.SomePointer (|bool|)
		return (self is containsaddr) or .µtype.ispointer


	function ASMReg.LeftScore (|int|) // we want pointers on the left... ints on the left, and consts on the right
		return (.somepointer<<1) + (self isnt RealConst)
	
	
	function asmreg.PointerMul (|message| exp, |int| swapped, |int|)
		opt norefcounts
		|| fn = exp.Func
		if fn == @opp
/			if swapped
				exp = exp.next!
			  else
				exp = exp.prev!
			
		  elseif fn == @arel or @brel
/			exp = exp.first! // merge after testing this
		
		  elseif fn == @acc
			exp = exp.first!
			
		  elseif fn == @dot // obj.Prop // requires pointermul on .prop cos of prev props
			// leave it?
			debugat
		  else
			error "aaargh"
			debugger 
		|| d = exp.ASMDecl.Internal
			return d.CArraySize
		debugger // hmmmm. its not a pointer after all?


	function InlineAddK (|asmreg| L, |int64| R, |asmreg| Dest, |bool|)
		// this may be over-optimisation:   (dest.reg == l.reg) + .SmartSecretFat
		// this allows for optimising x++, x++ --> x+=2
		// but thats all, and it does a lot of effort to achieve this.
		// we can't make the same effort all over for every opt we wanna make
		// Allow as a once-off, to prove a point. Its possible but not the right way.
		
		if (l is temp) or (dest.reg == l.reg)
			|| fat = .SmartSecretFat(l)
			if fat isa asm.addk
				|| CC = fat.r2 + R
				if CC.CanStoreAsIntImmediate
					fat.r2 = CC
					fat.r0 = dest
					return true


	function IntPlus     (fn_opasm)
		//		a - 1,		a + 1		//
		//		1 - a,		1 + a		//
		if dest is constoutput // perhaps we can do this in domathsub?
			return .MakeConst2(dest, l, r, exp, ConstifyIntPlus)

		|| swapped = 0
		if  l.LeftScore < r.LeftScore // put consts into R
			if l is alternate
				target debug
					if r.µtype.IsPointer				// 1 - ptr??? we blocked this already.
			  else
				swapped = 1
				swap (l) (r) // a+b == b+a
	
		if !r.reg // add or subtract zero
			return .Assign(dest, l, exp)
		
		|| lmul = 1
		if (l isnt noscale) and l.SomePointer
			lmul = l.PointerMul(exp, swapped)
		if r is ConstOutput
			|| RR = r.const
			if l is alternate
				RR = -RR
			rr *= lmul
			if r.CanAddK(RR)								// a - 1,  a + 1
				.nopconst(r)
				if .InlineAddK(l, rr, dest)
					return dest
				return exp.ADDK(dest, L, RR) * dest

		|&fatasm| fat
		if lmul.ispow2		  								// x = int1 + int2
			fat = exp.ADD(dest, L, R, lmul.log2)			// x = lptr + r<<3
			if l is alternate
				fat.op ++
		  else
			|| v = .NumToReg(exp, lmul, asmreg(), datatypecode.uint)
			if l isnt alternate								// x = lptr + r*12
				fat = exp.MULT(dest, R, V, L)
			  else											// x = (lptr - rptr)/12
				exp.SUB(dest, L, R, ASMReg())
				|| uinttype = 0, debugger
				fat = exp.DIVV(dest, ASMReg(), dest, V, uinttype)  
			  
		return fat * Dest
	

	function FloatPlus (fn_opasm)
		if dest is ConstOutput
			return .makeconst2(dest, l, r, exp, ConstifyFloatPlus)

		|| IsF32 = dest.FourBytes
			|| qq = .QuickFloat32Plus(dest, l, r, exp)
				return qq

		|| ss = (l is alternate)
		|| rr = r.reg
		return exp.fADD(dest, l, r*!ss, r*ss, 1-IsF32) * Dest


	function Plus (fn_opasm) // add (
		|| rr = r.reg, || ll = l.reg
		if !rr
			return l
		if l is alternate
			if rr == ll
				return .zeros(dest)
		  else
			if !ll
				return r
		if l.isint
			return .IntPlus(dest, l, r, exp)
		return .FloatPlus(dest, l, r, exp)


	function Subtract (fn_opasm)				// x = a - b
		(l is alternate)						// the logic ismostly shared...
		return .plus(dest, l, r, exp)


	function Minus (fn_opasm)					// x = -b
		// not the same as negative, which is an fn_asm... that means it works on the exp
		// where as fn_opasms work on the op.
		(l is alternate)
		return .plus(dest, l, r, exp)


	function FloatMul (fn_opasm)
		if dest is ConstOutput
			return .makeconst2(dest, l, r, exp, ConstifyFloatMul)
		rz = .QuickFloatMul(dest, R, L, exp)
		if rz == nil
			rz = .QuickFloatMul(dest, L, R, exp)
			if rz == nil
				rz = exp.FMUL(dest, L, R, asmreg(), l.IsBig|int|) * Dest


	function IntMul (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, l, r, exp, ConstifyIntMul)
		rz = .QuickIntMul(dest, R, L, exp)
		if rz == nil
			rz = .QuickIntMul(dest, L, R, exp)
			if rz == nil
				rz = exp.MULT(dest, L, R, asmreg()) * Dest


	function BoolMul (|asmreg| dest, |asmreg| Bule, |asmreg| V, |message| exp, |asmreg|)		
		if dest is ConstOutput
			return .MakeConst2(dest, Bule, V, exp, ConstifyBoolMul)
		rz = .QuickIntMul(dest, Bule, V, exp)
		if !rz
/			rz = exp.TERN(dest, Bule, V, asmreg()) * Dest				// TERN


	function Multiply (fn_opasm)
		if l isa DataTypeCode.bool
			return .BoolMul(dest, l, r, exp)
		if r isa DataTypeCode.bool
			return .BoolMul(dest, r, l, exp)

		if l is ConstOutput
			swap (l) (r)
		if l.isint
			return .IntMul(dest, r, l, exp)
		return .floatmul(Dest, l, r, exp)
	

	function Divide (fn_opasm)
		if r.reg == l.reg											// return 1
			return .SelfDivide(dest, exp)
		if dest.isint
			return .DivInt(dest, l, r, exp)
		return .DivFloat(dest, l, r, exp)


	function DivFloat    (fn_opasm)
		if (dest is ConstOutput) and (l isnt alternate)
			return .MakeConst2(dest, l, r, exp, ConstifyFloatDiv)
		rz = .QuickFloatDiv(dest, L, R, exp)
		if rz == nil
			rz = exp.FDIV(dest,  L,  R,  asmreg(), dest.isbig|int|) * dest


	function SelfDivide (|asmreg| dest, |message| exp, |asmreg|)
		if dest.IsInt
			return .NumToReg(exp, 1, dest, datatypecode.int) // x/x=1
		if !dest.Isbig
			return .NumToReg(exp, 0x3f800000 /*1.0*/, dest, datatypecode.float)
		return .NumToReg(exp, 0x3ff0000000000000 /*1.0 double*/, dest, datatypecode.f64)
		

	function QuickIntDiv    (fn_opasm)
		|| PToi = .IntPowerOftwo(r)
			if ptoi <= 1											//   x/1  x/0  x/-1
				if ptoi == 0
					exp.DivByZero
				return .Quick1Or1Sub(dest, l, ptoi, exp)
			if !l.Signed
				return exp.BFLG(dest, l, 0, ptoi-1, 0) * Dest		// u / 2 --> u >> 1
			return exp.DIV2(dest, l, ptoi-1, ptoi-1) * Dest			// nice
	
	
	function DivInt    (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, L, R, exp, ConstifyIntDiv)
		|| q = .QuickIntDiv(dest, l, r, exp)
			return q
		return exp.DIVV(dest, ASMReg(), l, r, dest.IntDivType) * dest
	
	
	function ModFloat    (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, L, R, exp, Constifyfloatmod)
		|| big = dest.IsBig|int|
		exp.fdiv(dest, L, R, asmreg(), big)
		return exp.FFRC(dest, asmreg(), dest, R, big) * dest
	
	
	function ModInt    (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, L, R, exp, ConstifyIntMod)
		return exp.DIVV(asmreg(), dest, L, R, dest.IntDivType) * Dest
	
	
	function Mod    (fn_opasm)
		if dest.isint
			return .ModInt(dest, l, r, exp)
		return .ModFloat(dest, l, r, exp)


	// BITS
	function BitNot (fn_opasm)
		if r.reg // what?
		if dest is ConstOutput
			return .makeconst2(dest, l, asmreg(), exp, ConstifyBitNot)
		return .BitMaker(dest, l, asmreg(), exp, asm.bnot)


	function BitXor (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, l, r, exp, ConstifyBitXor)
		if (l.reg == r.reg)
			return .Zeros(dest)
		if !l.reg
			return r
		if !r.reg
			return l
		return .BitMakerSwap(dest, l, r, exp, asm.bxor)


	function BitOr (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, l, r, exp, ConstifyBitOr)
		|| ll = l.reg
		|| rr = r.reg
		// we should use the type... of dest?
		// that or domath should call us?
		if (ll == rr)
			return l
		if l is constoutput
			if !l.reg
				return r
			if l.isconst(-1)
				return l
		if r is constoutput
			if !r.reg
				return l
			if r.isconst(-1)
				return r
		return .BitMakerSwap(dest, l, r, exp, asm.bor)

			
	function BitAnd (fn_opasm)		
		if dest is ConstOutput
			return .MakeConst2(dest, l, r, exp, ConstifyBitAnd)
		|| ll = l.reg
		|| rr = r.reg
		if (ll == rr)
			return l
		if !ll or !rr
			return .Zeros(dest)
		if .nopminusone(l)
			return r
		if .nopminusone(r)
			return l
		return .BitMakerSwap(dest, l, r, exp, asm.band)


	function BitMakerSwap (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asm| Op, |asmreg|)
		if (l is ConstOutput)
			swap (l) (r)
		return .BitMaker(Dest, l, r, exp, op)
	
	
	function BitMaker (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asm| Op, |asmreg|)
		|| k = .const(R, 9, op!=asm.bor)
			r = asmreg()
		  else
			k = 0
		|| fat = exp.BAND(dest, L, R, k) 
		fat.op = op
		return fat * Dest


	function SHR  (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, l, r, exp, (ConstifyBitSHS, ConstifyBitSHR)(l.signed))
		|| K = .const(R, 6, false)
			return .BFLG_Const(exp, dest, l, 0, k)
		
		|| op = (asm.BSHS, asm.BSHR)(l.signed)
		return .BitMaker(dest, l, r, exp, op)
	

	function SHL  (fn_opasm)
		if dest is ConstOutput
			return .MakeConst2(dest, l, r, exp, ConstifyBitSHL)
		|| K = .const(R, 6, false)
			return .BFLG_Const(exp, dest, l,  K, 0)
		
		return .BitMaker(dest, l, r, exp, asm.bshl)


	function BFLG_Const  (|message| exp, |asmreg| dest, |asmreg| Src, |uint| up, |uint| down, |asmreg|)
		|uint| srcbits = src.BitCount
		if up > srcbits or down > srcbits			// wierd?
			problem (exp, "Shift too far")
		if !up and !down
			return .assign(dest, src, exp)
		
		|| extra = 64 - srcBits // handle extra bits.
		up += extra
		down += extra
		if  !src.reg  or  (up >= 64)  or  (!src.signed and down >= 64)
			return .zeros(dest)
	
		return exp.BFLG(dest,  src,  up,  down,  src.signed|int|) * dest


	function Zeros (|asmreg| dest, |asmreg|)
		rz = asmreg()
		(rz is constinput) = (dest is ConstInput)
		rz.µtype = dest.µtype
	
	
	function CanImproveAssign (|asmreg| dest, |asmreg| src, |&fatasm|)
		opt inline
		if  src is temp
			return src.fat
		if  src.reg > .vdecls
			|| f = src.fat
			if f == .last
				return f
	
	function Assign (|asmreg| dest, |asmreg| src, |message| exp, |asmreg|)  // assigns (
		|| sr = src.reg
		|| dr = dest.reg
		if sr == dr
			return dest								// hmmm... we are already there
		if !sr
			return ._num(exp, 0, dest)	// force zero. More readable

		if dest isnt temp
			|| improve = .CanImproveAssign(dest, src)
				if improve.ResetOutput(dr)
					return dest
		return .BitMakerSwap(dest, src, asmreg(), exp, asm.bor) //


	function NopMinusOne (|asmreg| r, |bool|)
		if r.IsConst(-1)
			.nopconst(r)
			return true
	
	
function int64.Fits (|int| amount, |bool| signed=true,  |bool|)
	|| sh = 64 - amount
	|| x = self << sh
	if signed
		x >>= sh
	  else
		x = self|uint64| >> sh
	return x == self

