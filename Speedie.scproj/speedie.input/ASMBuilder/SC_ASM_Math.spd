

prototype fn_OpASM  (|&ASMState| self, |asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asmreg|)
prototype fn_OpASM2 (|&ASMState| self, |asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |&fatasm|)


extend asmreg
	function BoolASM (|asmreg|)
		.µtype = DataTypeCode.bool
		
		is AlreadyNegated			// speedie crashing somehow but it is used OK elsewhere?
		if self is CondRequest
			isnt condrequest
			is cond
		return self 
		
		
	function BitsAreCorrect (|int| gap, |bool|)
		return  gap == 0  or   self is bitcorrect  or   .reg == 0  or  .µtype is bool


	function BadBits (|asmreg| r, |errorint|)
		|| gl = .GapBits
		|| gr = r.GapBits
		|| cl =  .BitsAreCorrect(gl)
		|| cr = r.BitsAreCorrect(gr)
		if (cl and cr)
			return 0
		if (gl == gr)
			return gl
		if (gl < gr) and cr
			return gl
		if (gl > gr) and cl
			return gr
		
		return -1
			
		// if the smaller one is bit-correct... its ok? lets see:
		// byte GB vs int  OK	// needs correct
		// int  OK vs byte GB 	// needs correct
		// byte OK vs int  GB 	// can CMPI
		// int  GB vs byte OK	// can CMPI



extend ASMState
	function EqualsInt (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asmreg|)
		|| negate = dest is negate
		|| badness = l.BadBits(r) 

		if (dest is condrequest) and (badness >= 0)
			if badness > 0
				dest = .TempMe(exp, dest)
				.AddASM(asm.CMPB, exp, dest, L, R, badness, Negate|int|)
				r = dest
				l = 0.asm
			if !l.reg
				return .AddASM(asm.JBRA • negate, exp, R, 1) * Dest
			return .AddASM(asm.JMPE • Negate, exp, L, R) * Dest

		
		if badness > 0
			return .AddASM(asm.CMPB, exp, dest, L, R, badness, negate|int|) * Dest

		if badness == -1
			|| gl = l.GapBits
			|| gr = r.GapBits
			.AddASM(asm.BCLR, exp, L, R, gl, gr, (l.signed|int|) + (r.signed|int|<<1))
		return .AddASM(asm.JMPE • Negate, exp, L, R, dest) * Dest
				
	
	
	function Equals (fn_opasm)
		if r.reg == 0  and  l.reg != 0
			swap (l) (r) // put reg 0 into the left... more convenient.

		if dest.IsInt
			rz = .equalsint(dest, l, r, exp)
			if dest is ConstAny
				rz.const = (l.const == r.const)|int64|
		  else
			|| Mode = 2+(dest is negate)
			rz = .Compare(dest, l, r, exp, mode) // can't really be bothered to do this const comparison
		rz = rz.boolasm
	
		
	function NotEq  (fn_opasm)
		return .Equals(dest.negate, l, r, exp)


	function CompareInt (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		|| bb = l.bitcount max r.bitcount
		if !l.signed
			mode += 8
			if r.signed and r.bitcount >= l.bitcount // bad
		  elseif !r.signed
			if l.signed and l.bitcount >= r.bitcount // bad

		|| bigness = (bb.log2-3)<<1
		mode += bigness
		
		if l.BadBits(r) 
			|| gl = l.GapBits
			|| gr = r.GapBits
			.AddASM(asm.BCLR, exp, L, R, gl, gr, (l.signed|int|) + (r.signed|int|<<1))

		|| OPP = (asm.JMPI, asm.CMPI)(dest is condrequest)
		return .AddASM(OPP, exp, L, R, mode, dest) * Dest


	function CompareFloat (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		mode += 8 * l.IsBig
		mode += 4 * r.IsBig
		|| OPP = (asm.JMPF, asm.CMPF)(dest is condrequest)
		return .AddASM(OPP, exp, L, R, Mode, dest) * Dest


	function Compare (|asmreg| dest,  |asmreg| L,  |asmreg| R,  |message| exp,  |int| Mode,  |asmreg|)
		if dest.IsInt
			rz = .compareint(dest, l, r, exp, mode)
		  else
			rz = .comparefloat(dest, l, r, exp, mode)
		rz = rz.BoolASM
//		if rz.Reg.µType != des
	
	function Less   (fn_opasm)
		return .compare(dest, l, r, exp, 1)

	function MoreEq (fn_opasm)
		return .compare(Dest, r, l, exp, 1)

	function More   (fn_opasm)
		return .compare(dest, l, r, exp, 0)

	function LessEq (fn_opasm)
		return .compare(Dest, r, l, exp, 0)
	


	// MATH 
	function QuickFloatPlus    (fn_OpASM2)
		require r is ConstAny
		|| v = .f(r)
		if v == 0.0
			return l.fat
		
		if r.fourbytes
			|| k = .GetConst(r)[]
			if (k >> 18) << 18 != k
				debugger, #!hmmm. const-optimiser/renderer will handle it, but lets check to make sure
			rz = .AddASM(asm.fadk, exp, dest, l, k)
			rz.info = Dest


	function QuickFloatDiv    (fn_OpASM)
		require r is ConstAny
		|| v = .f(r)
		if v == 0.0
			problem (Exp, "Divide by zero???")
		if !v or v.abs == 1.0
			return .Quick1Or1Sub(dest, l, v|int|, exp)

		|| p = .GetConst(r)
		|ivec2| ex
		if l.IsBig
			|| f8 = p|&f64|[]
			require f8 > 0
			ex = f8.exponent
		  else
			|| f4 = p|&float|[]
			require f4 > 0
			ex = f4.exponent
		
		if ex.y == 1
			return .AddASM(asm.FEXK, exp, dest, L, 0, ex.x, l.IsBig|int|) * Dest


	// from 1 to -1
	function Quick1Or1Sub (|&ASMState| self, |asmreg| dest, |asmreg| L, |int| ptoi, |message| exp, |asmreg|)
		if ptoi == -1										//   x * -1  -->  0-x 
			return .subtract(dest, asmreg(), l, exp)
		if ptoi == 1										//   x * 1   -->  x 
			return l
		return asmreg.zero									//   x * 0   -->  0
		

	function QuickIntMul    (fn_OpASM)
		|| PToi = .IntPowerOftwo(r)$

		if ptoi <= 1
			return .Quick1Or1Sub(dest, l, ptoi, exp)
	
		return .BFLG_Const(exp, dest, l,  ptoi-1, 0)	// y = x * 4 --> y = x << 2


	function QuickFloatMul    (fn_OpASM)
		require r is ConstAny	
		|| v = .f(r)		
		if  v == 1.0 or 0.0 or -1.0
			return .Quick1Or1Sub(dest, l, v|int|, exp)
		if  v == 2.0
			return .plus(dest, l, l, exp)
		
		if r.Fourbytes
			|| x = .GetConst(r)[]
			|| y = x >> 18
			if x == y << 18
				return .AddASM(asm.FMLK, exp, dest, l, x) * Dest

	
	function PlusInt     (fn_opasm)
		|| LL = .GetConst(L)[],		|| RR = .GetConst(R)[]
		//		a - 1,		a + 1
		//		1 - a,		1 + a
		|| subtract = l is alternate
			RR = -RR

		|&fatasm| fat
		if !subtract and l.CanAddK(LL)					// 1 + a
			fat = .AddASM(asm.ADDK, exp, dest, R, LL)
//			exp.addk(dest, R, LL)

		  elseif r.CanAddK(RR)							// a - 1,  a + 1
			fat = .AddASM(asm.ADDK, exp, dest, L, RR)

		  elseif subtract
			fat = .AddASM(asm.SUBB, exp, dest, L, R)

		  else
			fat = .AddASM(asm.ADD, exp, dest, L, R)

		if dest is constany
			fat.const = ll + rr
		return fat*Dest
	
	
	function PlusFloat   (fn_opasm)
		|| IsF32 = dest.FourBytes
			if  .QuickFloatPlus(dest, r, l, exp)  or  .QuickFloatPlus(dest, l, r, exp)
				return dest

		|| ss = (l is alternate)
		|| rr = r.reg
		|| fat = .AddASM(asm.fADD, exp, dest, l, rr*!ss, rr*ss, 1-IsF32)
		ifn dest is ConstAny
			0 //
		  elseif IsF32
			|| rf = r.F32
			|| lf = l.F32
			if ss
				lf += rf
			  else
				lf -= rf
			fat.F32 = lf
		  else
			|| rf = r.F64
			|| lf = l.F64
			if ss
				lf += rf
			  else
				lf -= rf
			fat.F64 = lf
		return fat * Dest


// do this transform here:
// p1 = p2 * k3,  f1 = f2 + p1  -->  f1 = f2 + (p2 << log2(k3))	
// we need the info on double/float...
//	if p isa ASM.FMLK
//		|| k = p.a3|int|.asfloat.Log2
//		require !k.Fract and !.match3_2(out)
//		.op = asm.fexk
//		.a3 = p.a2
//		.a4 = k|int|
//		return true
// FEXK  (FloatAddExp)
//		f1 = FloatSh1(u2, r4) + FloatSh1(u3, r5)


	function BoolMul (|asmreg| dest, |asmreg| Boo, |asmreg| V, |message| exp, |asmreg|)
		|| fat = .addasm(asm.TERN, exp, dest, boo, V)
		if dest is ConstAny
			fat.const = boo.const * v.const
		return fat * Dest

		
	function Plus   (fn_opasm) // add (
		if !r.reg
			return l
		if !l.reg and (l isnt alternate)
			return r
		if l.isint
			return .PlusInt(dest, l, r, exp)
		return .PlusFloat(dest, l, r, exp)


	function Subtract  (fn_opasm)
		(l is alternate) // the logic ismostly shared...
		return .plus(dest, l, r, exp)


	function IntMul (fn_opasm)
		rz = .QuickIntMul(dest, R, L, exp)
		rz := .QuickIntMul(dest, L, R, exp)
		rz := exp.MULT(dest, L, R, asmreg()) * Dest
		if dest is ConstAny
			rz.needfat.const = l.const * r.const


	function FloatMul (fn_opasm)
		rz = .QuickFloatMul(dest, R, L, exp)
		rz := .QuickFloatMul(dest, L, R, exp)
		rz := exp.FMUL(dest, L, R, asmreg(), l.IsBig|int|) * Dest
		if dest is ConstAny
			|| fat = rz.needfat
			if dest.FourBytes
				fat.F32 = l.F32 * r.F32
			  else
				fat.F64 = l.F64 * r.F64


	function Mul    (fn_opasm)
		if l isa DataTypeCode.bool
			return .BoolMul(dest, l, r, exp)
		if r isa DataTypeCode.bool
			return .BoolMul(dest, r, l, exp)
			
		if l is ConstAny
			swap (l) (r)
		if l.isint
			return .IntMul(dest, r, l, exp)
		return .floatmul(Dest, l, r, exp)
	

	function Div    (fn_opasm)
		if dest.isint
			return .DivInt(dest, l, r, exp)
		return .DivFloat(dest, l, r, exp)
	
	
	function DivFloat    (fn_opasm)
		rz = .QuickFloatDiv(dest, R, L, exp)
		rz := .QuickFloatDiv(dest, L, R, exp)
		rz := .AddASM(asm.FDIV,  exp,  dest,  dest.isbig|int|,  L,  R) * dest
		if dest is ConstAny
			|| fat = rz.needfat
			if dest.FourBytes
				fat.f32 = l.f32 / r.f32
			  else
				fat.f64 = l.f64 / r.f64


	function DivInt    (fn_opasm)
		|| PToi = .IntPowerOftwo(r)
			if ptoi <= 1   //   x/1  x/0  x/-1
				return .Quick1Or1Sub(dest, l, ptoi, exp)
			if !l.Signed
				return .BFLG_Const(exp, dest, l, 0, ptoi) // u / 2 --> u >> 1
		
		|| fat = exp.DIVV(dest, asmreg(), l, r, dest.IntDivType)
		if dest is ConstAny
			|int64| C
			if dest.FourBytes
				if dest.Signed
					C = l.const|int| / r.const|int|
				  else
					C = l.const|uint| / r.const|uint|
			  else
				if dest.Signed
					C = l.const|int64| / r.const|int64|
				  else
					C = l.const|uint64| / r.const|uint64|
			fat.Const = C
		return fat * Dest
	
	
	function ModFloat    (fn_opasm)
		|| d = dest.Reg #check (Exp)
		|| big = dest.IsBig|int|
		exp.fdiv(     dest, asmreg(), L,    R, big)
		|| fat = exp.FFRC(dest, asmreg(), dest, R, big)
		if dest is ConstAny
			if dest.FourBytes
				fat.f32 = l.f32 mod r.f32
			  else
				fat.f64 = l.f64 mod r.f64
		return fat * dest
	
	
	function ModInt    (fn_opasm)
		|| fat = exp.DIVV(asmreg(), dest, L, R, dest.IntDivType)
		if dest is ConstAny
			|int64| C
			if dest.FourBytes
				if dest.Signed
					C = l.const|int| mod r.const|int|
				  else
					C = l.const|uint| mod r.const|uint|
			  else
				if dest.Signed
					C = l.const|int64| mod r.const|int64|
				  else
					C = l.const|uint64| mod r.const|uint64|
			fat.Const = C
		return fat*Dest
	
	
	function Mod    (fn_opasm)
		if dest.isint
			return .ModInt(dest, l, r, exp)
		return .ModFloat(dest, l, r, exp)
		


	// BITS
	function BitNot (fn_opasm)
		if r.reg // what?
		|| fat = .AddASM(asm.BNOT, exp, dest, l)
		if l is ConstAny
			fat.const = ~l.const
		return fat*Dest

	
	function BitXor (fn_opasm)
		if (l.reg == r.reg)
			return .BitOr(dest, asmreg(), asmreg(), exp)
		|| fat = .AddASM(asm.BXOR, exp, dest, L, R)
		if dest is ConstAny
			fat.const = l.Const xor r.const
		return fat*Dest


	function BitOr  (fn_opasm)
		if l is ConstAny
			swap (l) (r)
		dest.Expect(exp)
		|| fat = exp.BOAR(dest, L, R, asmreg())
		if dest is ConstAny
			fat.Const = l.const ||| r.const
		return fat * Dest
	
	
	function BitAnd (fn_opasm)		
		|| fat = .AddASM(asm.BAND, exp, dest, L, R)
		if dest is ConstAny
			fat.const = l.const & r.const
		return fat*Dest
	
	
	function ConstForShift (|asmreg| r, |int| btc, |message| exp,  |int64|)
		// shl/shl used to use this
		rz = .GetConst(r)[] #debug_require
		if rz < 0
			error (exp, "Can't negative shift")
		  elseif rz >= btc
			error (exp, "Shift too large for type")
		

	function BFLG_Const  (|message| exp, |asmreg| dest, |asmreg| l, |int| up, |int| down, |asmreg|)
		|| fat = exp.BFLG(dest,  l,  up,  down,  l.signed|int|)
		debugger
		|| lll = l.Const
		if l.Signed
			fat.Const = (LLL << up)>>down
		  else
			fat.Const = (LLL|uint64| << up) >> down
		return fat*dest
	
		
	function SHR  (fn_opasm)
		|| up = 64 - l.bitcount
		if r is ConstAny
			if !l.signed
				up = 0
			return .BFLG_Const(exp, dest, l, up, r.const+up)
// fix this later
//		if l.Signed
//			return exp.brus(dest, l, r, 0)
//		else
//			return exp.bruu(dest, l, r, 0)
		
		return .AddASM(asm.BRUS • !l.signed, exp, dest, L, R, up*l.signed)*dest

	
	function SHL    (fn_opasm)
		if r is ConstAny
			return .BFLG_Const(exp, dest, l,  r.const, 0)
		return .AddASM(asm.BLUE, exp, dest, L, R)*Dest
	
	
	function Assign (fn_opasm)
		return .bitor(dest, l, asmreg(), exp)

