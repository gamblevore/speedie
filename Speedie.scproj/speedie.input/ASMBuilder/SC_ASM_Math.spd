

prototype fn_OpASM          (|&ASMState| self, |asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asmreg|)
prototype fn_ASMConstifier  (|&fatasm| self, |asmreg| L, |asmreg| R)


extend asmreg
	function BoolAnswer (|asmreg|)
		.µtype = DataTypeCode.bool
		
		is AlreadyNegated
		if self is CondRequest
			isnt condrequest
			is CondAnswer
		return self 


extend ASMState
	function EqualsInt (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asmreg|)
		|| negate = (dest is negate)|int|
		if dest isnt CondRequest
			if dest is constany
				l.nop
				r.nop
				return .numtoreg(exp, ((l.const==r.const)==!negate)|int|, dest)
			return exp.CMPB(dest, L, R, negate) * Dest

		|&fatasm| fat 
		if l.reg
			fat = exp.JMPE(r,l,0)
		  else
			fat = exp.JBRA(r,0)
		fat.op += negate
		return fat * dest.boolanswer

	
	function Exists (|asmreg| dest, |asmreg| L, |message| exp, |asmreg|)
		opt inline
		return .Equals(dest, l, asmreg(), exp)

	function Equals (fn_opasm)
		if r.reg == 0  and  l.reg != 0
			swap (l) (r) // put reg 0 into the left... more convenient.

		if dest.Isfloat
			return .CompareFloat(dest, l, r, exp, 2)

		return .equalsint(dest, l, r, exp)
	
		
	function NotEq  (fn_opasm)
		return .Equals(dest.negate, l, r, exp)


	function CompareInt (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		mode |= !r.signed<<2
		mode ^= dest isnt Negate
		if dest isnt CondRequest
			return exp.CMPI(dest, L, R, mode) * dest

		return exp.JMPI(L, R, mode, 0) * dest.BoolAnswer


	function CompareFloat (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |int| Mode, |asmreg|)
		mode |= l.IsBig<<3
		mode |= r.IsBig<<2
		mode ^= dest isnt Negate
		if dest isnt CondRequest
			return exp.CMPF(dest, L, R, Mode) * dest
		return exp.JMPF(L, R, Mode, 0) * dest.BoolAnswer


	function Compare (|asmreg| dest,  |asmreg| L,  |asmreg| R,  |message| exp,  |int| Mode,  |asmreg|)
		if dest.IsInt
			return .CompareInt(dest, l, r, exp, mode)
		return .CompareFloat(dest, l, r, exp, mode)
/*				(A >  B)    (A <= B)    (A == B)    (A != B)			*/
	
	function More   (fn_opasm): return .compare(dest, l, r, exp, 0)
	function LessEq (fn_opasm): return .compare(dest, l, r, exp, 1)
	function Less   (fn_opasm): return .compare(dest, r, l, exp, 0)
	function MoreEq (fn_opasm): return .compare(dest, r, l, exp, 1)



	// MATH 
	function QuickFloat32Plus    (fn_OpASM)
		if  r is constany // a + 1,   a - 1
			|| QQ = .QuickFloatPlusConstSub(dest, l, r, exp)
				return qq
		if  l is constany  and  l isnt Alternate   // 1 + a --> a + 1
			return .QuickFloatPlusConstSub(dest, r asnt asmreg.alternate, l, exp)
	
			
	function QuickFloatPlusConstSub    (fn_OpASM)
		|| k = r.Const|uint64|
		k ^= (l is Alternate)<<31
		require (k >> 18) << 18 == k
		r.nop
		return exp.FADK(dest, l, k) * Dest


	function message.DivByZero
		problem (self, "Divide by zero???")	


	function QuickFloatDiv    (fn_OpASM)
		require r is ConstAny
		|| v = r.float
		if v == 0.0
			Exp.DivByZero
		if !v.fract
			r.nop
			return .Quick1Or1Sub(dest, l, v|int|, exp)


	// from 1 to -1
	function Quick1Or1Sub (|&ASMState| self, |asmreg| dest, |asmreg| L, |int| ptoi, |message| exp, |asmreg|)
		if ptoi == -1										//   x * -1  -->  0-x 
			return .subtract(dest, asmreg(), l, exp)
		if ptoi == 1										//   x * 1   -->  x 
			return l
		return dest.µtype.zero								//   x * 0   -->  0
		

	function QuickIntMul    (fn_OpASM)
		|| Pow2 = .IntPowerOfTwo(r)
			if Pow2 <= 1
				return .Quick1Or1Sub(dest, l, Pow2, exp)
			return exp.BFLG(dest, l,  Pow2-1, 0, 0) * Dest	// y = x * 4  -->  y = x << 2

		|| five = .IntPowerOfTwo(r, 1) // 5 or 9 or 17 or 33, etc
			return exp.ADD(dest, l, l, five-1) * dest


	function QuickFloatMul    (fn_OpASM)
		require r is ConstAny	
		|| v = r.float
		if  v == 1.0 or 0.0 or -1.0
			r.nop
			return .Quick1Or1Sub(dest, l, v|int|, exp)
		if  v == 2.0
			r.nop
			return .plus(dest, l, l, exp)
		
		if r.Fourbytes
			|| x = r.const
			|| y = x >> 18
			if x == y << 18
				r.nop
				return exp.FMLK(dest, l, x) * Dest

	
	function ASMReg.LeftScore (|int|) // we want pointers on the left... ints on the left, and consts on the right
		return (.µtype.ispointer<<1) + (self isnt constany)
	
	
	function asmreg.PointerMul (|message| exp, |int| swapped, |int|)
		if !.µtype.ispointer
			return 1
		
		|| fn = exp.Func
		if fn == @opp
			debugger 
			exp = exp.parent
			if swapped
				exp = exp.last
			else
				exp = exp.first
			
		  elseif fn == @arel or @brel
			debugger
			exp = exp.first
		
		  elseif fn == @acc
			debugger 
			exp = exp.first
			
		  else
			// should never happen. Even @thg shouldn't.
			error "aaargh"
			debugger 
		return 1
	

	function AddOrSubtractInt     (fn_opasm)
		//		a - 1,		a + 1		//
		//		1 - a,		1 + a		//
		if dest is ConstAny // perhaps we can do this in domathsub?
			return .MakeConst2(dest, l, r, exp, &fatasm.ConstifyIntPlus)

		if l.reg == r.reg // shift
			if l is Alternate
				return .zeros(dest, exp)
			  else
				return .BFLG_Const(exp, dest, l, 1, 0) // shift up

		|| swapped = 0
		if  l.LeftScore < r.LeftScore // put consts into R
			if l is alternate
				target debug
					if r.µtype.IsPointer				// 1 - ptr??? we blocked this already.
			  else
				swapped = 1
				swap (l) (r) // a+b == b+a
	
		if !r.reg // add or subtract zero
			return .Assign(dest, l, exp)
		
	
		|| lmul = l.PointerMul(exp, swapped)
		if r is ConstAny
			|| RR = r.const
			if l is alternate
				RR = -RR
			rr *= lmul
			if r.CanAddK(RR)							// a - 1,  a + 1
				r.nop
				return exp.ADDK(dest, L, RR) * dest

		|&fatasm| fat
		if lmul.ispow2		  							// x = int1 + int2
			fat = exp.ADD(dest, L, R, lmul.log2)		// x = lptr + r<<3
			if l is alternate
				fat.op ++
		  else
			|| v = .numtoreg(exp, lmul)
			if l isnt alternate							// x = lptr + r*12
				fat = exp.MULT(dest, R, V, L)
			  else										// x = (lptr - rptr)/12
				exp.SUB(dest, L, R, ASMReg())
				|| uint64type = 0, debugger
				fat = exp.DIVV(dest, ASMReg(), dest, V, uint64type)  
			  
		return fat * Dest
		

	function FloatPlus   (fn_opasm)
		if dest is ConstAny
			return .makeconst2(dest, l, r, exp, &fatasm.ConstifyFloatPlus)

		|| IsF32 = dest.FourBytes
			|| qq = .QuickFloat32Plus(dest, l, r, exp)
				return qq

		|| ss = (l is alternate)
		|| rr = r.reg
		return exp.fADD(dest, l, r*!ss, r*ss, 1-IsF32) * Dest


	function BoolMul (|asmreg| dest, |asmreg| Boo, |asmreg| V, |message| exp, |asmreg|)		
		if dest is ConstAny
			return .MakeConst2(dest, boo, V, exp, &fatasm.ConstifyBoolMul)
			
		|| fat = exp.TERN(dest, boo, V, asmreg())
		if dest is ConstAny
			fat.const = boo.const * v.const
		return fat * Dest

		
	function Plus   (fn_opasm) // add (
		if !r.reg
			return l
		if !l.reg and (l isnt alternate)
			return r
		if l.isint
			return .AddOrSubtractInt(dest, l, r, exp)
		return .FloatPlus(dest, l, r, exp)


	function Subtract  (fn_opasm)	// x = a - b
		(l is alternate) // the logic ismostly shared...
		return .plus(dest, l, r, exp)


	function Minus  (fn_opasm)		// x = -b
		// not the same as negative, which is an fn_asm... that means it works on the exp
		// where as fn_opasms work on the op.
		(l is alternate)
		return .plus(dest, l, r, exp)


	function IntMul (fn_opasm)
		if dest is ConstAny
			.MakeConst2(dest, l, r, exp, &fatasm.ConstifyIntMul)
		rz = .QuickIntMul(dest, R, L, exp)
		if rz == nil
			rz = .QuickIntMul(dest, L, R, exp)
			if rz == nil
				rz = exp.MULT(dest, L, R, asmreg()) * Dest


	function FloatMul (fn_opasm)
		if dest is constany
			return .makeconst2(dest, l, r, exp, &fatasm.ConstifyFloatMul)
		rz = .QuickFloatMul(dest, R, L, exp)
		if rz == nil
			rz = .QuickFloatMul(dest, L, R, exp)
			if rz == nil
				rz = exp.FMUL(dest, L, R, asmreg(), l.IsBig|int|) * Dest


	function Multiply (fn_opasm)
		if l isa DataTypeCode.bool
			return .BoolMul(dest, l, r, exp)
		if r isa DataTypeCode.bool
			return .BoolMul(dest, r, l, exp)
			
		if l is ConstAny
			swap (l) (r)
		if l.isint
			return .IntMul(dest, r, l, exp)
		return .floatmul(Dest, l, r, exp)
	

	function Divide (fn_opasm)
		if r.reg == l.reg											// return 1
			return .SelfDivide(dest, exp)
		if dest.isint
			return .DivInt(dest, l, r, exp)
		return .DivFloat(dest, l, r, exp)


	function DivFloat    (fn_opasm)
		if (dest is ConstAny) and (l isnt alternate)
			return .MakeConst2(dest, l, r, exp, &fatasm.ConstifyFloatDiv)
		rz = .QuickFloatDiv(dest, L, R, exp)
		if rz == nil
			rz = exp.FDIV(dest,  L,  R,  asmreg(), dest.isbig|int|) * dest


	function SelfDivide (|asmreg| dest, |message| exp, |asmreg|)
		if dest.IsInt
			return .NumToReg(exp, 1, dest) // x/x=1
		if !dest.Isbig
			return .NumToReg(exp, 0x3f800000 /*1.0*/, dest)
		return .NumToReg(exp, 0x3ff0000000000000 /*1.0 double*/, dest)
		

	function QuickIntDiv    (fn_opasm)
		|| PToi = .IntPowerOftwo(r)
			if ptoi <= 1											//   x/1  x/0  x/-1
				if ptoi == 0
					exp.DivByZero
				return .Quick1Or1Sub(dest, l, ptoi, exp)
			if !l.Signed
				return exp.BFLG(dest, l, 0, ptoi-1, 0) * Dest		// u / 2 --> u >> 1
			return exp.DIV2(dest, l, ptoi-1, ptoi-1) * Dest			// nice
	
	
	function DivInt    (fn_opasm)
		if dest is ConstAny
			return .MakeConst2(dest, L, R, exp, &fatasm.ConstifyIntDiv)
		|| q = .QuickIntDiv(dest, l, r, exp)
			return q
		return exp.DIVV(dest, ASMReg(), l, r, dest.IntDivType) * dest
	
	
	function ModFloat    (fn_opasm)
		if dest is ConstAny
			return .MakeConst2(dest, L, R, exp, &fatasm.Constifyfloatmod)
		|| big = dest.IsBig|int|
		exp.fdiv(dest, asmreg(), L, R, big)
		return exp.FFRC(dest, asmreg(), dest, R, big) * dest
	
	
	function ModInt    (fn_opasm)
		if dest is ConstAny
			return .MakeConst2(dest, L, R, exp, &fatasm.ConstifyIntMod)
		|| fat = exp.DIVV(asmreg(), dest, L, R, dest.IntDivType)
		return fat * Dest
	
	
	function Mod    (fn_opasm)
		if dest.isint
			return .ModInt(dest, l, r, exp)
		return .ModFloat(dest, l, r, exp)
		


	// BITS
	function BitNot (fn_opasm)
		if r.reg // what?
		if dest is ConstAny
			return .makeconst2(dest, l, asmreg(), exp, &fatasm.ConstifyBitNot)
		return .BitMaker(dest, l, asmreg(), exp, asm.bnot)

	
	function BitXor (fn_opasm)
		if dest is ConstAny
			return .MakeConst2(dest, l, r, exp, &fatasm.ConstifyBitXor)
		if (l.reg == r.reg)
			return .Zeros(dest, exp)
		if !l.reg
			return .Assign(dest, r, exp)
		if !r.reg
			return .Assign(dest, l, exp)
		return .BitMakerSwap(dest, l, r, exp, asm.bxor)


	function BitOr (fn_opasm)
		if dest is ConstAny
			return .MakeConst2(dest, l, r, exp, &fatasm.ConstifyBitOr)
		if (l.reg == r.reg) or l.IsConst(-1)
			return .Assign(dest, l, exp)
		if r.IsConst(-1)
			return .Assign(dest, R, exp)
		return .BitMakerSwap(dest, l, r, exp, asm.bor)

			
	function BitAnd (fn_opasm)		
		if dest is constany
			return .MakeConst2(dest, l, r, exp, &fatasm.ConstifyBitAnd)
		if (l.reg == r.reg) or r.IsConst(-1)
			return .Assign(dest, l, exp)
		if l.IsConst(-1)
			return .Assign(dest, r, exp)
		if l.IsConst(0) or r.IsConst(0)
			return .Zeros(dest, exp)
		return .BitMakerSwap(dest, l, r, exp, asm.band)


	function BitMakerSwap (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asm| Op, |asmreg|)
		if (l is ConstAny)
			swap (l) (r)
		return .BitMaker(Dest, l, r, exp, op)
	
	
	function BitMaker (|asmreg| dest, |asmreg| L, |asmreg| R, |message| exp, |asm| Op, |asmreg|)
		|| K = r.const
		if (r is ConstAny) and k.fits(9, op!=asm.bor) // we want bitor unsigned
			r.nop
			r = ASMReg()
		  else
			k = 0
		|| fat = exp.BAND(dest, L, R, K)
		fat.op = op
		return fat * Dest


	function SHR  (fn_opasm)
		if dest is constany
			return .MakeConst2(dest, l, r, exp, (&fatasm.ConstifyBitSHS, &fatasm.ConstifyBitSHR)(l.signed))
		if r is ConstAny
			return .BFLG_Const(exp, dest, l, 0, r.nop)
		
		|| op = (asm.BSHS, asm.BSHR)(l.signed)
		return .BitMaker(dest, l, r, exp, op)
	

	function SHL  (fn_opasm)
		if dest is constany
			return .MakeConst2(dest, l, r, exp, &fatasm.ConstifyBitSHL)
		if r is ConstAny
			return .BFLG_Const(exp, dest, l,  r.nop, 0)
		
		return .BitMaker(dest, l, r, exp, asm.bshl)


	function BFLG_Const  (|message| exp, |asmreg| dest, |asmreg| Src, |uint| up, |uint| down, |asmreg|)
		|uint| srcbits = src.BitCount
		if up > srcbits or down > srcbits			// wierd?
			problem (exp, "Shift too far")
		if !up and !down
			return .assign(dest, src, exp)
		
		|| extra = 64 - srcBits // handle extra bits.
		up += extra
		down += extra
		if  !src.reg  or  (up >= 64)  or  (!src.signed and down >= 64)
			return .zeros(dest, exp)
	
		return exp.BFLG(dest,  src,  up,  down,  src.signed|int|) * dest


	function Zeros (|asmreg| dest, |message| exp, |asmreg|)
		return .Assign(dest, asmreg(), exp)	
	
	
	function Assign (|asmreg| dest, |asmreg| L, |message| exp, |asmreg|)
		if l.reg != dest.reg
			return .BitOr(dest, l, asmreg(), exp)
		return dest		// hmmm... we are already there



function int64.Fits (|int| amount, |bool| signed=true,  |bool|)
	|| sh = 64 - amount
	|| x = self << sh
	if signed
		x >>= sh
	  else
		x = self|uint64| >> sh
	return x == self


function asmreg.IsConst (|int64| val, |bool|)
	return  (self is ConstAny) and (.const == val)
	
	
