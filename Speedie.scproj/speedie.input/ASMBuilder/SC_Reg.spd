


// we need to return both a type, and a fat.
// or perhaps even an asmreg and a fat.
// i think... returning both is better.
// we can return it in a struct... which is fine i guess
// its just done via pointers always.
// couldnt an ASMReg refer to a fat? like... it could, right? we have some spare bits?
prototype fn_asm (|&ASMState| self=asmstate.sh, |message| exp, |AsmReg| Dest=asmreg(), |int| Mode = 0, |AsmReg|)


datatype AsmReg (uint64)
	default 0|AsmReg|
	linkage
		cpp_part Reg
		numeric false
	
	Flags
		FatRef			= 1<<30
		|asmreg|
		(1<<16)		// starting point
//		SrcConst    // what does this do? remove it?
		NotYetUsed	// for   || y = a + b,   y is (currently) unused.
		FromMath
		StayOpen
		AlreadyNegated
		Alternate
		Cond
		Negate
		Discard
		Set
		ForReturn
		Temp
		SingleExpr
		NewCondRequest
		ConstAny
		ContainsAddr
		AddrRequest
		BitCorrect
		LabelRequest
		CorrectAddr		= ContainsAddr ||| bitcorrect
		Arg				= Discard         // ||| SrcConst
		CondRequest		= NewCondRequest  // ||| SrcConst
		MathConst		= ConstAny ||| FromMath
		Zero			= ConstAny ||| BitCorrect
	
	

	
	function F32 (|float|)
		return .needfat.F32
		
	function F64 (|float|)
		return .needfat.F64

	function Const (|int64|)
		if .reg == 0
			return 0
		return .needfat.const

	function Const (assigns:|int64|)
		.needfat.const = value

	function Negate (|asmreg|)
		(self is negate) = (Self isnt negate)
		return self
	
	function Expect (|message| where)
		opt inline
		check (.reg) (asmstate.sh, where, "Internal ASM Error: Setting to register 0")
	
	function BitCount (|int|)
		return .µtype.bitcount
	
	function FatIndex (|uint|)
		return self|uint64| >> 48
	
	function FatIndex (assigns:|uint|, |asmreg|)
		return (((self|uint64| << 16) >> 16) ||| (value|uint64| << 48))|asmreg|
	
	function FAT (|&fatasm|)
		|| i = .FatIndex
			return i.fat

	function NeedFAT (|&fatasm|)
		|| i = .FatIndex
			return i.fat
		error "reg is too skinny"
		return asmstate.sh.zero

	operator isa (|datatypecode| M, |bool|)
		return .µtype == M

	function FourBytes (|bool|)
		opt inline
		return self|datatypecode|._shift == 2

	function IsBig (|bool|)
		opt inline
		return self|datatypecode|._shift == 3

	function IsFloat (|bool|)
		opt inline
		return self|datatypecode|.isfloat

	function IsInt (|bool|)
		opt inline
		return self|datatypecode|.isfloat

	function IntDivType (|int|)
		|| FourBytes = .µtype._shift == 2
			rz = 2
		rz += !.signed
	
	function Signed (|bool|)
		return self|DataTypeCode|.issigned
		
	function GapBits (|int|)
		return 64 - .bitcount
	
	function µType (|DataTypeCode|)
		return self|DataTypeCode|
	
	function µType (assigns:|DataTypeCode|, |asmreg|)
		return ((self|int64| &~ 0xFFFF) ||| value)|asmreg|
	
	syntax is (|AsmReg| R, |bool|)
		return (self & R) == r
	
	syntax is (|AsmReg| R, assigns:|bool|, |AsmReg|)
		if (value)
			return self as r
		  else
			return self asnt r
			
	function RequestPos (|asmreg|)
		return self as addrrequest
	
	function Fatasm.HaveAddr (|&fatasm|)
		.info = .info.HaveAddr
		return self
		
	function HaveAddr (|asmreg|)
		rz = self asnt AsmReg.AddrRequest
		rz = rz as AsmReg.CorrectAddr
		
	function Reg (|int|)
		return .µType.Register
	
	function Reg (assigns:|int|, |AsmReg|)
		|| t = .µType
		t.Register = Value
		.µType = t
		return self
	
	operator mul (|bool| b, |asmreg|)
		return (self, nil)(b)
	
	syntax cast (|bool|)
		return .reg
	
	syntax cast (|int|)
		cpp_part ToInt
		return .reg
	
	syntax append (|asmreg| m, |asmreg|)
		return self as m
		
	operator bitand (|asmreg| A, |asmreg|)
		return (self|int64| & A|int64|)|asmreg|
	
	operator as (|asmreg| A, |asmreg|)
		return (self|int64| ||| A|int64|)|asmreg|

	operator asnt (|asmreg| A, |asmreg|)
		return (self|int64| &~ A|int64|)|asmreg|

	operator • (|asmreg| dest, |asmreg|)
		if .reg == dest.reg
			return asmreg()
		return dest
		
	module
		function New (|asmreg|)
			return Zero
		
	

// we could do this instead
// (self-1)&31!!
// this will "rotate" 0 back to 31 and 1 to 0.
function int.asm (|asmreg|)
	cpp_part ToASM
	rz.reg = self // (self - 1)&31 // would be better?


/*  function IncrAmount (|asmreg| dest, |scdecl| D, |int64|)  */
//			LL = LL.VarMul(subtract)
