


prototype fn_asm (|&ASMState| self, |message| exp, |AsmReg| Dest=asmreg(), |int| Mode = 0, |AsmReg|)


datatype ASMMath (uint)
	constants
		|ASMMath|
		Add
		Sub
		Mul
		Div
		Mod

		ROL
		ROR
		Shl
		Shru
		Shrs
		BNt
		BAn
		BOr
		Xor
		Xnr


datatype AsmReg (uint)
	default 0|AsmReg|
	linkage
		cpp_part Reg
		numeric false
	
	constants
		MathTypes		= (31 << 16)
		|asmreg|		// start at 21!
		Set = 4
		FromMath		= (1 << 21)
		StayOpen		= (1 << 22)
		Alternate		= (1 << 23)
		Const			= (1 << 24)
		MathConst		= (1 << 21) ||| (1<<24)
		Cond			= (1 << 25)
		PositionRequest = (1 << 26)
		ForReturn		= (1 << 27)
		ForReturnReg	= (1 << 27) ||| (1<< 11)
		Discard			= (1 << 28)
		Zero			= Const
	
	function Math (|asmmath|)
		return (self|asmmath| >> 16) & 31
		
	operator isa (|ASMMath| M, |bool|)
		return .math == M

	operator as (|ASMMath| M, |asmreg|)
		return ((self|uint| &~ MathTypes) ||| (M<<16))|ASMReg|

	function Exists (|bool|)
		return self|int|
		
	function FourBytes (|bool|)
		pragma inline
		return self|datatypecode|._shift == 2

	function EightBytes (|bool|)
		pragma inline
		return self|datatypecode|._shift == 3

	function IsFloat (|bool|)
		pragma inline
		return self|datatypecode|.isfloat

	function IsInt (|bool|)
		pragma inline
		return self|datatypecode|.isfloat

	function IntDivType (|int|)
		|| FourBytes = .µtype._shift == 2
			rz = 2
		rz += !.signed
	
	function Signed (|bool|)
		return self|DataTypeCode|.issigned
		
	function µType (|DataTypeCode|)
		return self|int| & 0xFFFF
	
	function µType (assigns:|DataTypeCode|, |asmreg|)
		return ((self|int| & ~0xFFFF) ||| value)|asmreg|
	
	syntax is (|AsmReg| R, |bool|)
		return (self|uint| & R|uint|) == r|uint|
	
	syntax is (|AsmReg| R, assigns:|bool|, |AsmReg|)
		if (value)
			return self as r
		  else
			return self asnt r
			
	function RequestPos (|asmreg|)
		return self as positionrequest
			
	function Unrequest (|asmreg|)
		return self asnt positionrequest
		
	// we could do this instead
	// (self-1)&31!!
	// this will "rotate" 0 back to 31 and 1 to 0.
	function Reg (|int|)
		return .µType.Register
	
	function Reg (assigns:|int|, |AsmReg|)
		|| t = .µType
		t.Register = Value
		.µType = t
		return self
	
	syntax cast (|bool|)
		return .reg
	
	syntax cast (|int|)
		cpp_part ToInt
		return .reg
	
//	function SameType (|AsmReg| t, |message| err, |bool|)
//		|| T1 = .µType
//		|| t2 = t.µType
//		// what about a < b with diferent types?
//		// i guess speedie should sort that out on the C-level...
//		return ((t2 == t1) or  (t1.IsInt and t2.IsInt)) #expect (err, "Type-mismatch")

	operator as (|asmreg| A, |asmreg|)
		return (self|uint| ||| A|uint|)|asmreg|

	operator asnt (|asmreg| A, |asmreg|)
		return (self|uint| &~ A|uint|)|asmreg|

	function Set (|asmreg|)
		return self as set
	
	operator • (|asmreg| dest, |asmreg|)
		if .reg == dest.reg
			return asmreg()
		return dest
		
//	function TryUse (|asmreg|)
//		if self is ReallyTemp
//			return (self|uint| &~ ReallyTemp|uint|)|asmreg|
	
	module
		function New (|asmreg|)
			return const
		
	

function int.Reg (|asmreg|)
	cpp_part ToReg // (self - 1)&31 // would be better?
	rz.reg = self
