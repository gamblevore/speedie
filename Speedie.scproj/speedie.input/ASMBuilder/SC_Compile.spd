

// ADDC? optimise stages... OR special-case it specifically.

struct ASMState
	linkage: cpp_part Pac
	|SCFunction--|				fn       
	|µFunc|						Out      
	|&FatASM|					Start		// a "slice" feature would be nice    
	|&FatASM|					Curr    
	|&FatASM|					End    
	|&FatASM|					WhileStart
	|bool|						OK       
	|byte|						LabelCount
	|datatypecode|				ReturnType
	|byte|						VDecls
	|byte|						VTmps
	|uint64[32]|				TrueBits 
	|uint64[32]|				FalseBits
	|uint|						AnyTrue	 

	function AddFuncParams (|SCFunction| fn)
		opt norefcounts
		for a in fn.Args
			a.dreg = .DeclareMe(nil, a.RegType|asmreg|)


	function Last (|&FatASM|)
		return .curr-1
	
	
	function AddASM (|int| SM, |message| dbg, |int| a, |int| b, |int| c, |int| d, |&FatASM|)
		pragma inline,  opt norefcounts,  cpp_part AddASM4
		rz = self <~ (sm, dbg)
		rz.r[0] = a
		rz.r[1] = b
		rz.r[2] = c
		rz.r[3] = d

	function AddASM (|int| SM, |message| dbg, |int| a, |int| b, |int| c, |&FatASM|)
		pragma inline,  opt norefcounts,  cpp_part AddASM3
		rz = self <~ (sm, dbg)
		rz.r[0] = a
		rz.r[1] = b
		rz.r[2] = c

	function AddASM (|int| SM, |message| dbg, |int| a, |int| b, |&FatASM|)
		pragma inline,  opt norefcounts,  cpp_part AddASM2
		rz = self <~ (sm, dbg)
		rz.r[0] = a
		rz.r[1] = b

	function AddASM (|int| SM, |message| dbg, |int| a, |&FatASM|)
		pragma inline,  opt norefcounts,  cpp_part AddASM1
		rz = self <~ (sm, dbg)
		rz.r[0] = a		

	syntax append (|uint| code, |message| dbg, |&FatASM|)
		cpp_part RequestOp
		|| p = .curr
		if p < .end
			.curr = p + 1
			P.op = code
			p.debug = dbg
			return p
		if .OK
			error "ASM is out of memory."
			.OK = false
		return .start
	
		
	function DeclareMe (|Message| where,  |asmreg| T,  |asmreg|)
		|| d = .VDecls|int| + 1
		if (d + .vtmps <= 31)
			t.reg = d
			.vdecls = d
			return t
		error (where, "No free local variables.")			
	
	
	function TempMe (|Message| where,  |asmreg| T=asmreg.discard,  |asmreg|) // .needreg
		if t.reg
			return t
		
		|| d = .vtmps|int| + 1
		if (d + .vtmps <= 31)
			t.reg = 32-d
			.vtmps = d
			return t
		error (where, "No free local variables.")


	function OpenVars (|uint|)
		return .vdecls ||| (.vtmps << 8)


	function CloseVars (|uint| Old)
		.vdecls = old & 255
		.vtmps  = old >> 8


///////////////////////////////////////////////////////////////////////////////////////////////////

	function µ (|message| exp,  |AsmReg| R = 0.reg, |AsmReg|)
		opt norefcounts
		pragma inline
		cpp_part Get
		|| T = exp.ASMType
		|| fn = fn_asm_table[T]!
		target debug
			if !T
				fn = fn_asm_table[exp.func|int|]!
				debugger
		return (fn)(self, exp, R)


	function prm (|message| prm, |asmreg|)
		|| r = prm.SimpleVar
		if r.Exists
			return r
		return .µ(prm, .DeclareMe(prm, asmreg.empty))
		
		
	function DoFunc (|message| prms, |asmreg| dest, |asmreg|)
		opt norefcounts
		|| fn = prms.asfunc
		expect (fn and fn.args <= 10) (prms, "Too many parameters")
		// speedie's function histogram:  0:410, 1:1656, 2:1464, 3:713, 4:167, 5:61, 6:10, 7:3, 8:1
		// there are only 4 above 6!, only 14 above 5

		if dest is discard
			dest = asmreg.empty
		  else
			dest = .TempMe(prms, dest)
			dest.µtype = fn.RegType

//		debugger
		|uint| Reg2
		|uint| Reg3
		|| sh = 0
		|| OV = .OpenVars

		for p.fast in prms
			|| v = .prm(p).reg
			if sh <= 25
				reg2 |= (v << sh)
			  else
				reg3 |= (v << (sh-30))
			sh += 5
		.closevars(OV)

		|| OP = (asm.func2, asm.func3)(sh <= 25)
		|| SaveAmount = 0
		if dest isnt forreturn
			SaveAmount = .vdecls
		|| Fnc = .AddASM(OP,  prms.parent!,  save: SaveAmount,  incr: 0,  jump: fn.TableId)
		fnc.r[3] = reg2
		fnc.r[4] = reg3 

		return dest


	module
		|FatASM[]|					JSMSpace = FatASM[0]!
		|[memory of fatasm]|		Olds
		|ASMState|					Sh
		

function ASMState.PackMakerInit (|bool|)
	opt norefcounts
	VM_Builder.JustMakeMyBread
	treeassembler.InitTree
	return .alloc(ASMState.JSMSpace)


function ASMState.Alloc (|memory of fatasm| j, |bool|)
	if j.setcap(4MB)
		.start = j.ptr
		.curr = .start
		.end = j.Capacity + .start
		.AddASM(asm.EROR, compiler.InternalFile.ast!, 0)
		olds <~ j
		return true


function @ASMState.Expand (|bool|)
	require olds < 16
	|| j = FatASM[4MB]
		.JSMSpace = j
		return sh.Alloc(j)
	// actually will leak on purpose. Which is good. Advanced memory managment :)
	


function ASMState.InitState (|scfunction| fn)
	if fn.ASM
		.out = fn.asm!
	  else
		.out = µfunc(fn, .curr)
	.LabelCount = 0
	.fn  = fn
	.returntype = fn.returntype.regtype
	.ok  = true
	.vdecls = 0
	.vtmps = 0
	.AddFuncParams(fn)


function ASMState.FinishASM
	opt norefcounts
	ifn .last isa asm.ret
		debugger
	.out.length = .curr - .out.IR

