unfinished

extend SCFunction
	function µTest (|memory of asm|)
		description "Generate some code (only for testing)."
		rz = asm[.µfatlength|int|*3]!
		|| p = rz.ptr
		|| after = .ASMBake(P, P+rz.Size, nil)
		rz.Size = after-p
		rz.length = rz.Size   //   ::::::]]]]]]]


	function FatFirst (|&fatasm|)
		return Assembler.sh.start + .µstart
	
	
	function Message.SrcMap (|uint|)
		return (.Position<<11) ||| (.tag >> 8)
	
	
	function ASMBake (|!&ASM| Where,  |!&asm| After,  |?&uint| debug,  |!&asm|)
		opt norefcounts
		rz = where
			
		|uint64| HasLabel
		for (fat.fat in self) (i)
			haslabel |= fat.JumpPrm << (i&63)
			|| n = rz - where
			if n > 250K/4
				error (.source, "Function too large (over 250K)")
			fat.ASMIndex = n
			fat.CheckHasOutput

			|| Curr = rz
			rz = fat.µRenderInto(rz, after)
			if debug
				|| pos = fat.msg.SrcMap
				while (Curr++ < rz)
					debug++[] = pos

		if HasLabel // Use this as a jump-map??? To skip huge sections?
			for fat.fat in self
				|| r = fat.JumpPrm
					fat.FillLabelRequest(where, rz, r-1)	
	
	target debug
		render µRender (|bool| RenderBlocks = false)
			fs <~ "asm ${.exportname}   ${.µASMLength*4}\n" // " asm " "asm"
			|| bb = 0
			for µ.fat in self
				if RenderBlocks and µ.BasicBlock != bb
					bb = µ.BasicBlock
					fs <~ "//$bb\n"
				µ.RenderFat(fs)
				fs <~ '\n'


	function µPrint 
		target debug
			printline self
			printline .µRender


	iterator fat
		|| FirstInst = .FatFirst
		|| CurrInst = FirstInst
		|| AfterInst = Currinst + .µfatlength
		while currinst < Afterinst
			yield currinst (CurrInst - FirstInst)
			++currinst



function fatasm.FillLabelRequest (|!&asm| Start, |!&asm| After, |int| reg)
	|| SelfOut = start + .ASMIndex
	target debug: debug_require  SelfOut >= start  and  SelfOut < after 

	// this needs fixing.
	|int| J = .prms[reg]
	|| OldDest = self + J + 1
	|| DestOut = OldDest.asmindex
	|| dest = start + DestOut

	target debug: debug_require dest >= start and dest <= after
	|| J2 = (dest - SelfOut) -1
	check (J2 != -1) (self, "Infinite loop")
	.prms[reg] = J2
	.µRenderInto(SelfOut, SelfOut+1)		// rerender
	.prms[reg] = J							// restore!


function packmaker.BakeAllFuncs (|jbin| j, |bool|)
	opt norefcounts
	for f in .PackFuncs
		if .PackSaved.AddOrShrink(j, f is UsedByASM)
			require j.BakeASM(f)
	return true


function JBin.BakeASM (|SCFunction| fn, |bool|)
	if  (options.targetdebug)  and  (fn isnt usedbyasm)  and  (!fn.isbehaviour)
		.add(@tmp, fn.exportname)
		return true
	
	scfunction.currfunc = fn

	|| t = fn.µASMLength
	Compiler.stTotalASMOps += t
	|| tmp = Options.TargetDebug
		.enter(@tmp, fn.exportname)			// maybe string.Length better than JB_Str_Length
		.SyntaxAppend(0, 2)

		
	|| Start = .ReserveMemory(@bin, t*4, 4)|&asm|
	|| dbg = SourceMap.Align(self, t*4)
	|| after = fn.ASMBake(Start, Start+t, dbg)
	expect (after-start == T)			(fn, "ASM length corrupt")
	if tmp
		.exit
	return true


