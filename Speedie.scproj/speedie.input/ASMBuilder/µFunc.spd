unfinished

extend SCFunction
	function µTest (|memory of asm|)
		description "Generate some code (only for testing)."
		rz = asm[.µfatlength|int|*3]!
		|| p = rz.ptr
		|| after = .ASMBake(P, P+rz.Size, nil)
		rz.Size = after-p
		rz.length = rz.Size   //   ::::::]]]]]]]


	function FatFirst (|&fatasm|)
		return Assembler.sh.start + .µstart
	
	
	function Message.SrcMap (|uint|)
		return (.Position<<11) ||| (.tag >> 8)
	
	
	function ASMBake (|!&ASM| Where,  |!&asm| After,  |?&uint| Positions,  |!&asm|)
		opt norefcounts
		rz = where		
		|uint64| HasLabel
		for (fat.fat in self) (i)
			haslabel |= fat.JumpPrm << (i&63)
			if (rz - where) > 63K
				error (.source, "Function too large (over 63K)")
			fat.ASMIndex = rz - where
			fat.CheckHasOutput

			|| Curr = rz
			rz = fat.µRenderInto(rz, after)
			if Positions
				if (fat is NewlyDeclared)
					fat.DumpDecl
				|| pos = fat.msg.SrcMap
				while (Curr++ < rz)
					Positions++[] = pos

		if HasLabel // Use this as a jump-map??? To skip huge sections?
			for fat.fat in self
				|| r = fat.JumpPrm
					fat.FillLabelRequest(where, rz, r-1)	
	
	target debug
		render µRender (|bool| RenderBlocks = false)
			fs <~ "asm ${.exportname}   ${.µASMLength*4}\n"
			|| bb = 0
			for µ.fat in self
				if RenderBlocks and µ.BasicBlock != bb
					bb = µ.BasicBlock
					fs <~ "//$bb\n"
				µ.RenderFat(fs)
				fs <~ '\n'


	function µPrint 
		target debug
			printline self
			printline .µRender


	iterator fat
		|| FirstInst = .FatFirst
		|| CurrInst = FirstInst
		|| AfterInst = Currinst + .µfatlength
		while currinst < Afterinst
			yield currinst (CurrInst - FirstInst)
			++currinst



function fatasm.FillLabelRequest (|!&asm| Start, |!&asm| After, |int| reg)
	|| SelfOut = start + .ASMIndex
	target debug: debug_require  SelfOut >= start  and  SelfOut < after 

	// this needs fixing.
	|int| J = .prms[reg]
	|| OldDest = self + J + 1
	|| DestOut = OldDest.asmindex
	|| dest = start + DestOut

	target debug: debug_require dest >= start and dest <= after
	|| J2 = (dest - SelfOut) -1
	check (J2 != -1) (self, "Infinite loop")
	.prms[reg] = J2
	.µRenderInto(SelfOut, SelfOut+1)		// rerender
	.prms[reg] = J							// restore!


function packmaker.BakeAllFuncs (|jbin| j, |bool|)
	opt norefcounts
	for f in .PackFuncs
		if .PackSaved.AddOrShrink(j, f is UsedByASM)
			SCFunction.currfunc = f
			require j.BakeASM(f)
	return true


function JBin.BakeASMSub (|SCFunction| fn, |int| T,  |bool|)
	opt norefcounts
	|| Start = .ReserveMemory(@bin, t*4)|&asm|
	|| Positions = SourceMap.Reserve(t*4)
	|| after = fn.ASMBake(Start, Start+t, Positions)
		
	expect (after-start == T)									(fn, "ASM length corrupt")
	return sourcemap.check(fn, T*4)


function JBin.BakeASM (|SCFunction| fn, |bool|)
	opt norefcounts
	if  (options.targetdebug)  and  (fn isnt usedbyasm)  and  (!fn.isbehaviour)
		.AddFunc(fn)
		return true
	

	|| t = fn.µASMLength
	Compiler.stTotalASMOps += t
	|| j = assembler.DebugDecls
	if !j
		return .BakeASMSub(fn, t)

	.AddFunc(fn, true)
	if j.Length
	for a in fn.args	
		a.DumpDecl(j, 0)
	rz = .BakeASMSub(fn, t)
	.add(@bin, j)
	j.Length = 0
	.exit


function jbin.AddFunc (|scfunction| fn, |bool| Enter=false)
	opt norefcounts
	|| fs = FastString.new(nil|faststring|)
	|| cls = fn.cls
		fs <~ cls.name
		fs <~ '.'
	fs <~ fn.name
	.add(@tmp, fs, enter)
	self <~ '\0'
	

