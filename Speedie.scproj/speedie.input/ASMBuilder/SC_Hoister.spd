
struct SC_Hoister
	linkage
		cpp_part Hoi
	|SCDecl--[6]|	Items
	|byte|			Count
	|byte|			Spare
	
	constants
		MinHoistScore	= 3

	helper GotGlobal (|message| prop,  |scdecl| decl)
		opt NoRefCounts
		|| dest = ASMState.sh.DeclareMe(prop, decl)		// Pre-declare glob.
		|| Addr = ASMType.GlobAddr(decl, prop, dest)
		.got(dest, Decl, addr)

	helper Got (|asmreg| dest, |scdecl| decl, |asmreg| K)
		opt NoRefCounts
		decl.FatConstIndex = K.FatIndex
		decl.NilReg = dest.reg ||| nilreg.Hoisted
		.Items[.Count++] = decl
		.spare--

	helper WriteConst (|message| prop,  |scdecl| decl)
		opt NoRefCounts
		|| dest = ASMState.sh.DeclareMe(prop, decl)		// Pre-declare glob.
		|| K = asmstate.sh.NumToReg(prop, decl.exportposition, dest, dest.µtype)
		.got(dest, Decl, k)

	helper ReuseConst (|message| prop,  |scdecl| decl)
		opt NoRefCounts
		|| k = decl.ExportPosition
		for i in .Count
			|| Old = .items[i]!
			if old.ExportPosition == K
				old.fat!.µRefCount++
				decl.FatConstIndex = old.FatConstIndex
				decl.NilReg = old.NilReg
				return
		.WriteConst(prop, decl)
	
	function Unhoist (|asmreg|)
		opt NoRefCounts
		for d in .Items
			loop d
			d.nilreg = 0

	function HoistNormal (|message| exp)
		.spare = –count .items
		.Hoist(exp, 0)
		
	function HoistLoop (|message| exp)
		.spare = 2
		.Hoist(exp, 1<<20)

	function TryHoist (|message| exp, |int| Score, |scdecl| prop)
		if prop.nilreg isnt hoisted
			if prop is const
				if (score >= 1<<20) and prop.exportposition
					.ReuseConst(Exp, prop)
			  elseif prop is Global
				.GotGlobal(exp, prop)
		  else
			|| fat = prop.fat
				fat.µRefCount++
	
	
	function Hoist (|message| exp, |int| Score)
		opt norefcounts
		|| fn = exp.func
		if fn == @type
			exp = exp.first! // no go into the type!
			fn = exp.func
		if fn == @func
			exp = exp.second!
			fn = exp.func
		
		if  (fn == @dot or @thg or @num)  and  (.spare > 0)
			|| prop = exp.ASMDecl
			if (score >= 0)
				.TryHoist(exp, score, prop)
			if prop is const or global
				return
	
		for ch.fast in exp
			|| add = 0
			if ch.func == @tmp
				|| ty = ch.ASMType
				add += (ty is kWhile) * MinHoistScore
				add -= (ty is kIf)
				
			.Hoist(ch, add + score)

