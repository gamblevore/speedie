


dispatch fn_asm_table (fn_asm)
	@arg:  ASMType.Arg
	@decl: ASMType.decl
	@num:  ASMType.num
	@rel:  ASMType.DoMath
	@thg:  ASMType.Thg
	@bra:  ASMType.First
	@sheb: ASMType.Sheb
	@sstr: ASMState.Str
	@list: // vectors
		debugger
	@func: ASMType.ASMFunction
	@dot:  ASMType.Dot
	@acc:  ASMType.Access
	@brel: ASMType.Brel		// ++, --, -, &
	@arel: ASMType.arel     // ++, --, 
	@type:					ASMType.TypeCast
	@tmp:					ASMType.unexpected

	(ASMType.kif):			ASMstate.If
	(ASMType.kwhile):		ASMstate.While
	(ASMType.kTern):		ASMstate.Ternary
	(ASMType.kContinue):	ASMstate.Continue
	(ASMType.kExit):		ASMstate.Exit

	(ASMType.kDebugger):	ASMType.Debugger
	(ASMType.krejoin):		ASMType.First
	(ASMType.kreturn):		ASMType.return

	(ASMType.kSetRel):		ASMType.SetRel
	(ASMType.kStatExpr): 	ASMType.StatExpr
	(asmtype.kRefCount):	asmstate.RefCount

	(ASMType.kIgnore):		ASMType.Ignore
	(ASMType.kFail):		ASMType.Ignore

	63:						ASMType.unexpected



extend_module ASMType {
constants
	IncrAfter = 2
	IncrBefore = 0
	
|asm_Read[10]|	ReadASM
|asm_Write[5]|	WriteASM
|message--|		LastDebugMark


function InitAccess
	|| R = &ASMType.ReadASM[0]
	|| W = &ASMType.WriteASM[0]
	R++[] = ASM.RD1U, W++[] = ASM.WR1U
	R++[] = ASM.RD1S, W++[] = ASM.WR2U
	R++[] = ASM.RD2U, W++[] = ASM.WR4U
	R++[] = ASM.RD2S, W++[] = ASM.WR8U
	R++[] = ASM.RD4U, W++[] = ASM.WR16
	R++[] = ASM.RD4S
	R++[] = ASM.RD8U
	R++[] = ASM.RD8U
	R++[] = ASM.RD16
	R++[] = ASM.RD16	


function Debugger (fn_asm)
	if Options.targetdebug
		return exp.TRAP(asmreg(), 0, 2)*asmreg() // 2 == sigint
	return asmreg()
	

function StatExpr (fn_asm)
	opt norefcounts
	for ch.fast in exp
		if ch.islast
			return .µGetASM(ch, dest)
		.µ(ch, asmreg.Discard)

	
function First (fn_asm)
	opt norefcounts
	return .µGetASM(exp.first!, dest)


function Sheb (fn_asm)
	opt norefcounts
	LastDebugMark = exp
	return exp.noop * asmreg()


function InlinedReturn (|&ASMState| self, |message| exp, |asmreg| dest,   |AsmReg|)
	opt norefcounts
	// What about getting the address of registers?
	dest = dest as asmreg.ExitFunction * .TailInlineable
	dest = dest as asmreg.ExitAtAll
	|| f = exp.first
		dest = .µinto(f, dest)
	
	|| fat = exp.reT(Dest, asmreg(), asmreg(), 0, 0)
	fat.µRefCount = 1
	(fat is CondAnswer)
	return fat*Dest



function Return (fn_asm)
	opt norefcounts
	|| L = .returndest		// can remove constants, so try first
		return InlinedReturn(self, exp, L)

	|asmreg| V 
	|| f = exp.first
		V = .ExistingVar(f, VarUseMode.return)
		if !v
			v = .µGetASM(f, ASMReg.ExitFunction)
		
	|| amount = (f!=nil)|int|
	|| D0 = .SafeDecr
		if d0.reg != V.reg // problem
	|| D1 = .decr
	|| D2 = .decr
	|| K = (.Const(V, 8, true), -1)(!D0)
	|| fat = exp.RET(V, D1, D2, (D0!=nil)|int|, 0)
	fat.µRefCount = 1
	if K
		fat.r[4] = K
		fat.r[0] = 0
	
	return fat * asmreg.Exit
	


function TypeCast (fn_asm)
	opt norefcounts
	|| f = exp.first!
	dest.µtype = exp.asmdecl.datatype
	|| freg = .µGetASM(F, dest)
	if freg.µtype == dest.µtype
		return freg // already done!
	return TypeCastSub(self, exp, dest, freg)


function TypeCastSub (|&asmstate| self, |message| exp, |ASMReg| dest, |ASMReg| from, |ASMReg|)
	opt norefcounts
	|| NewDecl = exp.ASMDecl
	|| NewType = NewDecl.regtype
	if !from.Reg												// x = 0|float|
		from.µtype = NewType
		return from

	|| OldType = from.µtype	
	if NewType is bool
		if (OldType is bool) and (dest isnt negate)				// x = true|bool| // why?
			return from
		return .NotEq(dest, asmreg(), from, exp)

	|| OC = OldType.BitCount
	|| NC = NewType.BitCount
	|| IsInt = NewType.IsInt

	|| Floatness = (NewType.IsFloat<<1) + oldtype.isfloat
	if Floatness == 0 or 3
		if (nc == oc) and (oldtype.IsSigned == newtype.IsSigned)
			from.µtype = newtype								// same all. maybe quicker to test in one go?
			return from
		if isint  and  (nc > oc  or  oc == 64)
			from.µtype = NewType
			return from
	
	dest = .TempOnly(exp, dest)
	dest.µType = NewType
	if from is ConstOutput
		return ConstConvert(self, exp, dest, from, oldtype, newtype)
		
	if oldtype.isfloat or newtype.isfloat
		return exp.CONV(dest, from, oldtype.asmconv(newtype))*dest
	|| sh2 = 64 - oc
	return exp.BFLG(dest, from, sh2, sh2, from.signed|int|) * dest



function ConstConvert  (|&asmstate| self, |message| exp, |ASMReg| dest, |ASMReg| from, |Datatypecode| Old, |Datatypecode| New, |ASMReg|)
	// this converts in place...	// we don't want that.
	
	|| srf = from.NeedFAT
	|| NC = New.BitCount
	|int64| K
	if old.isint!=new.isint
		K = srf.FloatIntConvConst(old, new)
	  elseif new.isfloat
		K = srf.FloatConvConst(NC)
	  else
		|| sh = 64 - NC
		|| oof = srf.Const << sh
		K = (oof|int64| >> sh, oof|uint64| >> sh)(dest.Signed) // shift up and down

	.NopConst(from)
	return .NumToReg(exp, K, dest, new)


function Decl (fn_asm)
	if options.Optimise >= 4
		return declsub(self, exp)
		
	rz = declsub(self, exp)
	|| fat = rz.fat
		fat.µRefCount++		// Keep vars viewable for debugging
								// Actually its not an issue for even release builds!
								// just don't add vars you don't need!
	


function SetRel (fn_asm)						// function relset (
	opt	norefcounts								//     x[func()] = y++
	|| A = exp.first!.asmunbra
	|| B = exp.last!
	|| X = .ExistingVar(A, VarUseMode.set)
		dest = .µInto(B, X)
		|| D = a.ASMDecl
		.MakeConstFromASM(d, nil, dest)
		return dest

	// probably x.y = z, or x[y] = z 
	dest = .µGetASM(B, asmreg()) 
	dest = (dest asnt ASMReg.RealConst) as AsmReg.set
	return .µ(A, dest as asmreg.AlwaysSet)


function DeclSub (fn_asm)
	opt norefcounts
	|| rel = exp.last!
	|| ty = exp.fastdecl
	if !exp.obj
		debugger
		ty = rel.first!.fastdecl
	ty.BranchDepth = .BranchDepth
	dest = .DeclareMe(rel, ty)
	|| item = rel.last!

	if ty isnt local							// a carray or somthing?? idk
	if (ty is alteredinbranch) or (ty isnt local)
		return .µinto(item, dest)

	|| K = .µinto(item, dest)
	if k is ConstOutput
		.MakeConstFromASM(ty, k, nil)
	return k


	
function ArgSub (|&ASMState| self, |message| exp, |&sc_hoister| H, |AsmReg|)
	opt norefcounts
	|| OV = .OpenVars
	
	for ch.fast in exp
		if h
			h.HoistNormal(ch)
		if ch.func == @decl
			ASMType.Decl(self, ch, AsmReg.RealDiscard)
		  else
			|| What = .µ(ch, AsmReg.RealDiscard)
			if what & asmreg.exit
				rz = what
				exit							// for if-branch that get consted, always that just returns.
	.CloseVars(OV)
	



function Arg (fn_asm)
	return ArgSub(self, exp, nil)




function Access (fn_asm)									// a[b], struct.mem[b]
	opt	NoRefCounts
	if dest isnt set
		dest = .TempTyped(exp, dest)
	
	|| f = exp.first!
	target debug
		|| d = f.asmdecl
		if !d.pointercount and !d.iscarray
	|| base = .µGetASM(f, asmreg.WithinAccess)				// x = a.b[0]
	|| sc = exp.Second.first!
	if base is InlinedAccess
/		return base

	|| vara = .µGetASM(sc)
	
	if dest isnt AddrRequest
		dest.µType = exp.ASMDecl.DataType
		return .ReadOrWrite(Dest, exp, base, vara)
		
	dest = dest.HaveAddr
	base = base.HaveAddr
	dest.µType = DataTypeCode._voidptr
	return .Plus(dest, base, vara, exp)
	


function Dot (fn_asm)										// a.b
	opt	NoRefCounts
	|asmreg| SubDest // fool around with optimising this
	if dest isnt set // **once** everything is working
		dest = .TempTyped(exp, dest)
		subdest = dest
	
	|| prop = exp.ASMDecl
	if prop is const // wait? don't these all get pre-transformed? SHOULD they be?
		return ASMConst(self, exp, dest, prop)
	if prop is global
		return DoGlobal(self, exp, dest, prop)
	|| F = exp.first!		 
	|| Obj = .µGetASM(F, subdest)
	|| T = prop.typeinfo
	dest.µtype = T
	
	|int64| Pos = prop.ExportPosition
	if (pos < 0 or pos > 1K) // what?
	
	if (dest is AddrRequest) or prop.IsCarray
		pos *= t.ByteCount
		if prop.iscarray
			dest.µtype = datatypecode._voidptr
		dest = dest.haveaddr
		return .AddToReg(exp, dest, obj as ASMReg.NoScale, pos)
	
	return .ReadOrWrite(Dest, exp, obj, ASMReg(), pos)


function Num (fn_asm)
	opt norefcounts
	|| d = exp.ASMDecl
	|| T = d.typeonly
	dest.µtype = T
	(dest is RealConst)
	
	if d.NilReg is hoisted
		Dest.reg = d.nilreg&31
		return Dest
	
	return .NumToReg(exp, d.ExportPosition, dest, T)


function Thg (fn_asm)
	|| thg = asmtype.ThgSub(self, exp, dest)
	|| reg = thg.reg
	if (reg-1)|uint| < 31							// what?
		return thg
	if reg == 0  and  thg is RealConst
		return thg // its still ok.   nil/false
	error (exp, "Register out of bounds")


function ASMConst (|&ASMState| self, |message| exp, |AsmReg| Dest, |scdecl| d, |asmreg|)
	(dest is RealConst)
	if dest isnt ConstRequest
		if d.nilreg isnt hoisted
			return .NumToReg(exp, d.exportposition, dest, dest.µtype)
/		Dest.reg = d.nilreg&31
	return dest


function ThgSub (|&ASMState| self, |message| exp, |AsmReg| Dest, |asmreg|)
	opt norefcounts
	|| d = exp.ASMDecl
	if d is local
		|| T = d.ThgASM
		if dest isnt addrneed
			return T
		if dest is DeclareInlineParam // &A
			(t is InlinedAccess)
			return T
		return exp.GRAB(dest, t, dest, t) * dest.HaveAddr
	
	if d is const
		return ASMConst(self, exp, dest, d)

	if dest isnt ConstRequest
		check (d is global) (Exp)
		return DoGlobal(self, exp, dest, d)


function GlobAddr (|scdecl| D, |message| exp, |AsmReg| Dest, |asmreg|)
	if d.NilReg is Hoisted
		Dest = d.nilreg|int|.asm
	  else
		|int64| iTable = d.ExportPosition
		expect (iTable > 0)          (exp, "No global address".internal)
		expect (iTable <= 18~bits)   (exp, "Used over 256K of global data")
		Dest = exp.GTAB(dest,  (!d.IsLib)|int|,  iTable) * dest
	dest.µtype = DataTypeCode._voidptr
	return dest.HaveAddr


function DoGlobal (|&ASMState| self, |message| exp, |AsmReg| Dest, |scdecl| D, |asmreg|)
	opt	NoRefCounts
	target debug
		|| d2 = exp.asmdecl
		if !d.minimatch(d2) // Remove d2. it the same as D.
	if dest isnt set
		dest = .TempTyped(exp, dest)
	
	|| addr = GlobAddr(d, exp, dest)
		if dest is AddrRequest
			return addr
		
		dest.µtype = d.DataType
		dest = dest asnt asmreg.ContainsAddr
		return .ReadOrWrite(dest, Exp, addr, ASMReg(), 0)
	


function AddressOf (fn_asm)
	rz = .µGetASM(exp, dest as asmreg.addrneed)
	if (rz is ContainsAddr)
		return rz
	error (exp, "ASM fail")
	.µGetASM(exp, dest as asmreg.addrneed)


function BoolNot (fn_asm)							// !a
	dest = dest.negate
	|| src = .µGetASM(exp, dest)
	if src is AlreadyNegated
		return src
	return .Exists(dest, src, exp)
	

function BitNot (fn_asm)							// ~a
	return .DoMathSub(exp, dest as asmreg.SingleExpr, scoperator.bnot.asm!)


function Negative (fn_asm)							// -a
	return .DoMathSub(exp, dest as asmreg.singleexpr, scoperator.Negative.asm!)


///   ///

function IncrSlowOnAddr (|&ASMState| self, |message| f, |ASMReg| Dest, |int| Mode, |ASMReg| Addr, |int64| Amount, |ASMReg|)	
	opt	NoRefCounts									// *a, x = *(a.b)
	|| TmpCloser = .OpenVars
	.Read(Dest, F, dest, addr)						// READ

	|| NewValue = (.Temptyped(f), dest)(mode & IncrAfter)
	.IncrPre(f, NewValue, dest, amount)				// ADD

	|| fat = .write(dest, f, newvalue, addr)		// WRITE BACK

	// the new value should exist! now lets close
	.CloseVars(TmpCloser)
	return fat * Dest


function IncrOnAddr (|&ASMState| self, |message| f, |AsmReg| Dest, |int| Mode, |AsmReg| Addr, |int64| Amount, |asmreg|)
	ifn  dest.isint  and  amount.fits(7)
		return IncrSlowOnAddr(self, f, dest, mode, Addr, amount)
	|| bc  = dest.µtype.ByteCount
	|| log = bc.Log2
	if dest.BitCount * (1<<log) != bc<<3 // hmm? maths bug?
	|| fat = f.CNTC(Addr, dest, 1, amount, log)
	if mode & IncrAfter
		fat._op = asm.cntd
	return fat * dest


function IncrAmount (|asmreg| upon, |int| IsPlus, |scdecl| D,  |int64|)
	opt norefcounts
	rz = (1,-1)(IsPlus & 1)
	if upon.isfloat
		if upon.FourBytes
			rz = rz|float|.asint
		  else
			rz = rz|Float64|.asint
	  elseif D.TypeSuffers
		rz *= D.PointerIncrement



function Incrementatulatorifier		(fn_asm)				// --a++
	visible
	opt	NoRefCounts
	|| Src = .µGetASM(exp, dest as asmreg.addrrequest)
	|| Amount = IncrAmount(src, mode, exp.ASMDecl)
	if src is ContainsAddr
		return IncrOnAddr(self, exp, dest, mode, src, amount)
	
	check (src isnt temp) (exp, "Can't increment temporary value.")
	ifn mode & IncrAfter
		return .IncrPre(exp, dest, src, Amount)				// ++x,	 y = ++x

	return .IncrPost(exp, dest, src, amount)				// x++,	 y = x++



function ARel (fn_asm)
	opt	NoRefCounts
	|| F = exp.name[]
	if F == '?' or '!'
		return .µGetASM(exp.first!, dest)
	|| M = (F == '+')|int|
	if dest isnt discard
		M += IncrAfter
	return Incrementatulatorifier(self, exp.first!, dest, M)



function BRel (fn_asm)						//  --a,  ++a,  &a,  --glob,  --a.b, -a
	opt	NoRefCounts
	|| n = exp.name[]
	|| F = exp.first!
	if n == '&'
		return AddressOf(self, f, dest)
	if n == '~'
		return BitNot(self, f, dest)
	if n == '!'
		return BoolNot(self, F, dest)
	if exp.name == 1								// -a
		return Negative(Self, f, dest)
	
	|| IsPlusPlus = n == '+'						// ++a, --a
	return Incrementatulatorifier(self, F, dest, IsPlusPlus|int|)



function DoMath (fn_asm)
	opt norefcounts
	|| op = exp.second!
	|| scop = op.obj|scoperator|
		if scop is AndOr
			return .ASMBoolBadnessMadness(exp, dest, scop.Kind)
		|| fn = scop.asm
			return .DoMathSub(exp, dest, fn)
	error (op, "No ASM for this operator?")
	debugger // what?



function Ignore			(fn_asm)
	return asmreg()


function Unexpected		(fn_asm)
	if !exp.asmtype
		|| f = exp.func|int|
			exp.asmtype = f
			return .µGetASM(exp, dest)
	error (exp, "bad ASMType")
}



extend ASMState {
function message.ASMUnbra (|message|)
	opt	norefcounts
	real self
	|| fn = .func 
	while (fn == @bra) or ((fn == @type) and .TypeCastDoesnothing)
		self = .first!
		fn = .func
	return self

// would be nice to allow --X or ++X
// however, what if its being passed to a function?
// and the function is inline?
// in fact, what if it isnt inline? I think we need tests for those?
function ExistingVar (|message| m, |VarUseMode| Mode = 0, |asmreg|)
	opt	norefcounts
	real self
	m = m.ASMUnbra
	|| fn = m.func 
	if fn == @thg
		return .ASMLocal(m, mode)
	if fn != @num
		return nil
	|| d = m.ASMDecl
	if d.nilreg is hoisted
		rz = d.nilreg|int|.asm
		rz.µtype = d.DataType


function Str (fn_asm)
	opt	norefcounts
	dest = .TempOnly(exp, dest)
	|| real = exp.name|messageid|
	dest.µtype = datatypecode._voidptr
	return exp.gstr(dest, 0, real.ID) * dest // seems good!


helper DoMathSub (|message| exp,  |ASMReg| Dest,  |fn_opasm!| fn, |ASMReg|)
	opt norefcounts
	|| ml = ASMReg()
	|| mr = ASMReg()
	if dest is SingleExpr
		ml = .µGetASM(exp, dest)
	  else
		|| Closer = .OpenVars
		|| discardmode = dest & AsmReg.Discard
		|| Mode = discardmode with dest
		
		ml = .µGetASM(exp.first!, mode)
		mode = DiscardMode.with(dest, ml)
		mr = .µ(exp.last!, mode)
		.CloseVars(Closer)
	
	dest = .TempTyped(exp, dest)
	(dest is constoutput) = (ml&mr is constoutput)
	rz = (fn)(self, dest, ml, mr, exp.second!)
	rz = rz.ConstInputFromMath(ml, mr)
	rz.µtype = dest.µtype


function asmreg.ConstInputFromMath (|asmreg| L, |asmreg| R, |asmreg|)
	if self is constoutput
		(self is constinput) = l.ConstEnough & r.ConstEnough
	return self


function asmreg.ConstEnough (|bool|)
	return self|uint64| & asmreg.NotUnConst|uint64|


function ASMLocal (|message| s, |VarUseMode| mode, |asmreg|)
	opt	norefcounts
	|| d = s.ASMDecl
	if d is local
		if mode is Return
			.fn.TrackReturns(d)
		rz = d.ThgASM


function SetDeclToConst (|scdecl| decl, |asmreg| Initial, |&fatasm| f, |bool|)
	if (Initial is ConstOutput) and (decl isnt AlteredInBranch)
		if f
			(decl is const)
			// || k = 1, k = 2, k = k + 3
			decl.ExportPosition = f._const
			if !Initial.FatIndex
			decl.FatConstIndex = Initial.fatindex
			return true
		debugger 


function MakeConstFromASM (|scdecl| decl, |asmreg| Initial, |asmreg| NewValue)
	description "Marks Decl as a const, if it definitely is a const. Otherwise marks as NOT CONST. Also nops unused consts. Also updates new const value."
	
	// uncompleted code?

	opt NoRefCounts
	|| f = Initial.fat
	if initial
		require !.SetDeclToConst(decl, initial, f)

// || k = 1, k = X	// clear here
// y = k			// nice.
	if decl is Const
		(decl isnt const)
		if f
			.AskNop(f)
		decl.ExportPosition = 0
		|| DK = decl.FatConstIndex|uint|
			decl.FatConstIndex = 0
			.AskNop(DK.fat)
	
	if NewValue
		.SetDeclToConst(decl, newvalue, newvalue.fat)
}



function SCFunction.TrackReturns (|scdecl| D)
	if d isnt ReturnedVar
		.ReturnedVars++
		(d is ReturnedVar)	


function message.HasAsmStuff (|bool|)
	opt norefcounts
	for ch in self
		|| fn = ch.BraFunc
		if fn != @sheb and @num and @char
			return true


function message.BraFunc (|syntax|)
	opt norefcounts
	while self
		|| fn = .func
		if fn != @bra
			return fn
		self = .first


function FatASM.FloatConvConst (|int| DestBitSize, |int64|)
	|| f = &._Const
	if DestBitSize == 64				// f32 to f64
		return (f|&float|[]|Float64|).asint
										// f64 to f32
	return f|&f64|[]|float|.asint


function FatASM.FloatIntConvConst (|datatypecode| src, |datatypecode| dest, |int64|)
	|| dbc = dest.ByteCount
	|| f = (&._Const)|&nil|
	if src.IsInt
		|| v = .Const
		if src.IsSigned
			if dbc == 4
				return v|float|.asint			// i to f32
			return v|Float64|.asint				// i to f64
		if dbc == 4
			return v|uint64||float|.asint		// u to f32
		return v|uint64||Float64|.asint			// u to f64

	// src.isfloat
	if src.ByteCount == 4
		|| f32 = f|&float|[]
		if dest.IsSigned
			return f32|int64|					// f32 to i
		return f32|uint64|						// f32 to u
	
	|| f64 = f|&f64|
	if dest.IsSigned
		return f64|int64|					// f32 to i
	return f64|uint64|						// f32 to u


function DataTypecode.ASMConv (|Datatypecode| To, |int|)
	return to.ASMPart<<2 ||| .asmpart


function DataTypecode.ASMPart (|int|)
	if .isfloat
		return 0 + (.bitcount > 32)
	return 2 + .issigned


function Message.TypeCastDoesNothing (|bool|)
	opt norefcounts
	|| Output = .ASMDecl.typeonly
	|| Input = .first!.ASMDecl.typeonly
	require  Output.Count    == Input.Count				// vectors
	require  Output.IsFloat  == Input.isfloat			// floatness
	|| BoolOut = Output is bool
	require (BoolOut) == (Input is bool)				// boolness

	|| BytesLost = Input.ByteCount - Output.ByteCount
	if output.isfloat
		return byteslost == 0
	require byteslost <= 0



function fatasm.FuncPrmBits (|uint64|)
	// i don't really get this.
	// its also only usd for tail, which is unused
	// is this out of date code?
	rz = .r2
	|| r3 = .r3|uint64| << 32
	rz |= r3
	rz >>= 7	
	require !(rz>>31)
	for i in 6
		|| p = (rz >> (5*i)) & 31
		if p > i
			rz |= (1|uint64|<<31)
			exit
	// ffff(r4, r3, r2, r1)


function SCDecl.ThgASM (|asmreg|)
	|| T = .WholeType as ASMReg.FromExistingVar
	(t is param) = (self is param) // unused?
	if (self isnt alteredinbranch)
		|| fi = .FatConstIndex
			|| f = fi|uint|.fat
			if f is ConstOutput
				(T is ConstOutput)
				t.FatIndex = fi
	return T
	
	
function string.trip (|bool|)
	return asmtype.lastdebugmark ~= self 

