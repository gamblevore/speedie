

// ADDK? optimise stages... OR special-case it specifically. 

/*
struct RegState // seems we only use this for consts? lets... just keep it that way
				// then we don't need to go back to stuff.
	|?&fatasm|			Creator
	|?scdecl--| 		Type
	|int|				NilCount
	|int|				WriteCount
*/


struct ASMState
	linkage: cpp_part Pac
	|uint16| 					BlockNum
	|uint16| 					ParentBlock

	|uint|						VAvail
	|byte|						VDecls
	|byte|						VTmps
// OK, can we do this... just remove it and... make it work. We just need to be able 
// to add and remove. We also need some way to say that a regs exists, as a returned reg.
// So we can overwrite it even. But not written to. Basically a returned result.
// how to write that a reg is "Returned"? in closetmps?
	|datatypecode|				ReturnASM
	|bool|						OK    
	|bool|						Inited   

	|SCFunction--|				fn       
	|µFunc|						Out      
	|&FatASM|					FuncStart
	|&FatASM|					Start		// a "slice" feature would be nice    
	|&FatASM|					Curr    
	|&FatASM|					End    
	|uint64[32]|				RegConsts
	|FATASM| 					Zero

	
	function CloseVDecls (|int| i)
		|| v = .VDecls
		while v > i
			.CloseReg(v--)
		.VDecls = v

	function CloseVTmps (|int| i, |asmreg| dest=ASMReg())
		if dest isnt StayOpen
			|| v = .VTmps
			while v < i
				.CloseReg(v++)
			.VTmps = v

	function MarkAvailableValues
		.vAvail  = (2<<.vdecls)-1
		|| sh = .vtmps
		if sh < 32
			.vAvail |= -1<<sh
	
	function CloseReg (|int| i)
		.RegConsts[i] = 0
	
			
	function AddFuncParams (|SCFunction| fn)
		opt norefcounts
		for a in fn.Args
			.DeclareMe(a.source, a)

	function Last (|&FatASM|)
		return .curr-1

	function RegPlace (|int| a, |&uint64|)
		opt norefcounts
		|| InBounds = a|uint| <= 31
			if (1<<a) & .vavail
				return .RegConsts[a]
		
		|| msg = (": Unassigned register",  ": Out of bounds register")(InBounds)
		error (self, errorreceiver.NonFatal + msg)
		return .RegConsts[0]
			
		
	function GetConst (|int| RegNum, |&uint64|)
		// How do i say that some regs are "closed for write, but still readable".
		// Then each reg needs two bits. not just 1!
		// We can do this by using 64-bits per thingy. Or two 32-bits.
		opt inline
		|| r = .regplace(RegNum)
		if  !r[]  ==  !regnum
			return r
		debugger 
		return .zero.const


	helper FoundCode (|int| i, |uint64| Value, |asmreg| typeinfo, |asmreg|)
		opt norefcounts
		|| place = .regplace(i)
		|| K = place[]
		if K == Value
//			|| ty = place.Type
//			if ty is Altered
//				#!hmmm. now what? need to understand blocks and if/while things?
//				debugger 
			typeinfo.reg = i
			(typeinfo is ConstAny)
			return typeinfo

	
	function IntPowerOfTwo (|asmreg| r,  |FailableInt|)
		// -1 --> -1,  0 --> 0,  1 --> 1,  2 --> 2,  4 --> 3,  8 --> 4
		// this is used for multiplication mostly. like x*-1, or x*0, or x*1
		if r is ConstAny
			|| v = .GetConst(r)[]
			if v == -1
				return v
			if v.IsPow2
				return v.Log2+1

		if (!r.reg) // what?


	function AddConstantAndReturnOld (|message| exp,  |AsmReg| Dest,  |AsmReg| ToGrow,  |int64| Value,  |asmreg|)
		|| reg = .TempMe(exp, dest)
		if togrow.isint and value.CanStoreAsIntImmediate
			if 0
				exp.adpk(reg, togrow, togrow)
			return exp.ADPK(reg, ToGrow, Value) * reg
		.Assign(reg, ToGrow, asmreg(), exp)
		rz = .AddConstant(exp, togrow, ToGrow, value)


	function AddConstant (|message| exp,  |AsmReg| Dest,  |AsmReg| Src,  |int64| Value,  |asmreg|)
		if dest.isint
			if Value.CanStoreAsIntImmediate
				return exp.ADDK(dest, src, Value) * dest
		  elseif dest.FourBytes
			if value|uint64|.CanStoreAsfloatImmediate
				return exp.FADK(dest, src, value) * dest
			debugger // how is this possible? we are adding a 64 bit float to a 32-bit?
		debugat
		|| where = .NumToReg(exp, value)
		return .Plus(dest, src, where, exp)


	syntax append (|int| code, |message| dbg, |&FatASM|)
		cpp_part RequestOp
		|| p = .curr
		if p < .end
			.curr = p + 1
			P.op = code
			p.debug = dbg
			p.BlockNum = .BlockNum
			p.ParentBlock = .ParentBlock
			return p
		.OK = false
		return .start


	syntax expect (|message?| where=nil, |string| msg)
		|| f = .fn.asm
			f.length = .curr - .funcstart
			printline f.render
		debugger
		error (where, msg)


	function DeclareMe (|Message| where,  |scdecl| Type,  |asmreg|)
		|| d = .VDecls|int| + 1
		if (d < .vtmps)
			|| t = Type.CalculateASMType
			t.reg = d
			Type.µType = t|DataTypeCode|   		// right?
			.VAvail	|= 1 << d
			.vdecls = d
			target debug: Type.ASMSanity
			return t
		error (self, where, "No free local variables.")


	function TempMe (|Message| where, |asmreg| T=asmreg.Temp, |asmreg|)				// .needreg
		opt norefcounts
		|| r = t.reg
		if !r
			r = .vtmps - 1
			check (r > .vdecls) (where, "No free local variables.")
			t.reg = r
			.vtmps = r
			.VAvail	|= 1 << r
		if t isnt textual
			t.µtype = where.asmdecl.µtype
		return t


	function RegMap (|int|)
		|| dcls = (.VDecls+1)~bits
		if .vtmps >= 32
			return dcls
		|| tmps = .vtmps~bits
		tmps = ~tmps
		return dcls ||| tmps


	function FindConstDecl (|scdecl| d, |asmreg|)
		return .findconst(d.exportposition, d.CalculateASMType)

	function FindConst (|uint64| Value, |asmreg| typeinfo, |asmreg|)
		// our vars will need checking by the nil-checker...
		// to see if loops/ifs harm this thing.
		// technically... the asm thing can handle the ifs...
		// but loops must be pre-checked!
		
		if !value
			typeinfo.reg = 0
			return typeinfo
		|| R = .regmap &~ 1
		for (b.bits in R) (i)
			|| f = .FoundCode(i, value, typeinfo)
			if f != nil
				return f


///////////////////////////////////////////////////////////////////////////////////////////////////
	
	function Branch (|message| cond,  |bool| neg=false,  |!&fatasm|)
		|| cmu = .µ(cond, asmreg.CondRequest)
		if cmu is CondAnswer
			return cmu.needfat
		|| req = asmreg.CondRequest
		if cmu isnt alreadynegated
			(Req is negate)
		debugger
		cmu = .equals(req, asmreg(), cmu, cond)
		return cmu.needfat


	function µ (|message| exp, |AsmReg| Dest = asmreg(), |asmreg|)
		opt norefcounts
		target !debug
			opt inline
		cpp_part Get
		|| fn = fn_asm_table[exp.ASMType]!
		|| TmpCloser = .VTmps
		|| ss = (fn)(self, exp, dest)
		.CloseVTmps(TmpCloser, dest)
		.MarkAvailableValues

		target debug
			if exp != @sheb
				|| ty = exp.ASMDecl
				if ty.µType != ss.µType
					debugger
					(fn)(self, exp, dest)
		return ss


	function FuncPrm (|message| prm, |scdecl| DestType,  |asmreg|)
		|| r = .ExistingVar(prm)
		|| OldTmps = .VTmps
		if r != nil
			return r
		return .µ(prm, r)


	module
		|FatASM[]|					JSMSpace = FatASM[0]!
		|[memory of fatasm]|		Ancients
		|ASMState|					Sh
		


		
function @ASMTmp.DoFunc (fn_Asm)
	opt norefcounts
	|| prms = exp.last!
	|| fn = prms.asfunc
	ifn  fn and fn.args <= 10
		error (prms, "Too many parameters")
		return AsmReg()

	// speedie's function histogram:  0:410, 1:1656, 2:1464, 3:713, 4:167, 5:61, 6:10, 7:3, 8:1
	// there are only 4 above 6!, only 14 above 5

	if dest.reg == 0
		dest = AsmReg()
	  else
		dest = .TempMe(prms, dest)
	dest.µtype = fn.RegType

	|int| Reg2
	|int| Reg3
	|| sh = 0
	|| TmpCloser = .VTmps

	|| p = prms.first
	for fnarg in fn.args
		expect (p) (exp, "Compiler did not fully type this function.") 
		|| v = .FuncPrm(p, FnArg).reg
		if sh <= 25
			reg2 |= (v << sh)
		  else
			reg3 |= (v << (sh-30))
		sh += 5
		p++
	.CloseVTmps(TmpCloser)
	// we still need to clear variables passed by address
	// like x = 12345, f(&x), if y == 12345 // can't reuse x as a const
	

	|| SaveAmount = .vdecls * (dest isnt forreturn)
	|| fat = exp.FNCX(SaveAmount,  fn.TableId,  reg2,  reg3) // exp.func2, exp.func3

	|| OP = (asm.FUNC2, asm.FUNC3)(sh <= 25)
	if fn is Wrapper
		op = (asm.FNCX2, asm.FNCX3)(sh <= 25)
	fat.op = op

	return fat * dest


function ASMState.PackMakerInit (|bool|)
	opt norefcounts
	if .Inited
		return true
	.inited = true
	VM_Builder.JustDefineInstructions
	SpdAssembler.InitAss
	return .alloc(ASMState.JSMSpace)


function ASMState.Alloc (|memory of fatasm| j, |bool|)
	if j.SetCap(4MB)
		.start = j.ptr
		.curr = .start
		.end = j.Capacity + .start
		spdassembler.Guard
		Ancients <~ j
		return true


function @ASMState.ExpandJSM (|bool|)
	require .Ancients < 16
	|| j = FatASM[4MB]
		.Ancients <~ .jsmspace
		.JSMSpace = j
		return sh.Alloc(j)
	

function ASMState.StartFunc (|scfunction| fn)
	if fn.ASM
		.out = fn.asm!
	  else
		.out = µfunc(fn, .curr)
	.zero = nil
	.zero.info = AsmReg()
	for i in 32
		.CloseReg(i)
	.VAvail = 1
	.FuncStart = .curr
	.fn = fn
	.ReturnASM = fn.ReturnType.regtype
	.OK = true
	.VDecls = 0
	.VTmps = 32
	.AddFuncParams(fn)


function ASMState.FinishASM
	opt norefcounts
	if .zero.Const // what?
	|| l = .last
	ifn (l isa asm.ret) or (l isa asm.trap) 
		debugger
	.SetLength


function ASMState.SetLength
	.out.length = .curr - .FuncStart


//function int64.CanInt32 (|bool|)
//	return  (self <<32)>>32 == self
//	
//
//function int64.CanFloat32 (|bool|)
//	return  (self|uint64|>>32)<<32 == self
//	
