

struct ASMState
	|scfunction!--|	Fn    
	|asmreg|		Return
	|uint64|		ParentVars
	|byte|			BranchDepth
	|byte|			LoopDepth
	|byte|			InlineDepth
	|bool|			TailInlineable


struct Assembler
	linkage: cpp_part Pac
	|DataTypeCode|				ReturnASM
	|uint16| 					BasicBlock

	|ASMState|					State
	|byte|						DeepestInline
	|byte|						InlineDepthLimit
	|byte|						StackSize
	|uint|						VDecls
	|uint|						VTemps
	|bool|						IsTextual
	|int|						CurrFuncGrab
	|bool|						Inited
	|bool|						NeedsRework

	|scfunction--|				Out

	|?&FatASM|					InlineEnd
	|uint|						KnownValuesMap
	|?&FatASM|					Orig
	|&FatASM|					FuncStart
	|&FatASM|					LastDebug
	|&FatASM|					Start		// a "slice" feature would be nice    
	|&FatASM|					_Curr    
	|&FatASM|					End    

	|(?&FATASM)[32]|			Registers
	|FatASM| 					Zero

	constants
		kContinue		= 254
		kExit			= 255
	

	module
		|Assembler|					Sh
		|memory of fatasm|			JSM = FatASM[0]!
		|memory of fatasm|			HoistSpace = FatASM[0]!


	function fn (|scfunction!--|)
		return .state.fn
	

    function Curr (|&fatasm|)
        return ._curr

	function Length (|int|)
		return .curr - .funcstart
	
	function AddFuncParams (|SCFunction| fn)
		opt NoRefCounts
		for a in fn.Args
			.DeclReset(a)
			.DeclareMe(a.source, a)
	
	
	function Index (|int|)
		return ._curr - .start
	
	
	function Last (|&FatASM|)
		return ._curr - 1


	function Last (|asm| type, |&FatASM|)
		|| L = ._curr - 1
		if (l isa type) and .IsCurr(L)
			return L
	
	
	function IsCurr (|&fatasm| f, |bool|)
		return .basicblock == f.basicblock
	
	
	function IntPowerOfTwo (|asmreg| r, |int| Sub=0, |FailableInt|)
		// -1 --> -1,  0 --> 0,  1 --> 1,  2 --> 2,  4 --> 3,  8 --> 4
		// this is used for multiplication mostly. like x*-1, or x*0, or x*1
		|| v = r.const
		if v == -1
			if sub
			.NopConst(r)
			return v
		v -= Sub
		if v.IsPow2
			.NopConst(r)
			if !v
				return 0
			return v.Log2+1

	

	function IncrPost (|message| exp,  |AsmReg| Dest,  |AsmReg| ToGrow,  |int64| Value,  |asmreg|)
		if togrow.isint and value.CanStoreAsAddK and !dest.IsVec
			return exp.ADAK(dest, ToGrow, Value) * Dest
		.Assign(dest, ToGrow, exp)
		rz = .IncrPre(exp, togrow, ToGrow, value)


	function AddToReg (|message| exp, |asmreg| Dest, |asmreg| Orig, |int64| Amount, |asmreg|) // addnum
		if !dest or dest == asmreg()
			dest.µtype = orig.µtype
		dest = .TempOnly(dest)
		|| Where = orig • dest
		(where is realconst)
		|| K = .NumToReg(exp, amount, where, orig.µtype)
		return .Plus(dest, Orig, K, exp)
	
	
	function IncrPre (|message| exp,  |AsmReg| Dest,  |AsmReg| Src,  |int64| Value,  |asmreg|)
		if !src.reg // what?
		if !dest.reg
			dest = src
		src = .AddToReg(exp, src, src, value) 
		if dest.reg==src.reg
			return src
		return .Assign(dest, src, exp)


	function DeclareMe (|Message| where,  |scdecl?| Type=nil,  |asmreg|)
		if type
			rz = Type.CalculateASMType
			|| ret = .state.return
			if  ret.reg  and  (type is VarThatGotReturned)  and  (.state.fn.ReturnedVars == 1)
				ret.µtype = rz|DataTypeCode|
				Type.wholetype = ret
				return ret
		
		|| d = .VDecls
		check (d != -1) (where, "No free local variables.")
		
		|| FreeBit = (~d).LowestBit		// Find lowest free bit.
		d |= FreeBit
		rz.reg = freeBit.log2

		if type
			Type.WholeType = rz
			target debug: Type.ASMSanity
			(rz is notalteredinbranch) = (type isnt alteredinbranch)

		.Vdecls = d
		check (!(.vdecls & .VTemps)) (where, "No free local variables.")	// entirely full


	function TempTyped (|scdecl| Type, |asmreg| T,  |asmreg|)
		t.µtype = type.mu.DataType
		return .TempOnly(t)
	
	
	function TempTyped (|Message| where, |asmreg| T=asmreg.Temp, |asmreg|)				// .needreg
		opt norefcounts
		if t isnt textual
			// what if there is a type and its different?
			// and if its NEVER different... why add it?
			t.µtype = where.asmdecl.mu.DataType
		return .TempOnly(t)


	function TempOnly (|asmreg| T=asmreg.Temp, |asmreg|)				// .needreg
		opt norefcounts
		if t.reg
			return t

		|| r = .VTemps
		if r&1 // how?
/			r&=~1
		
		
		|| Free = (~r).Log2		// Find lowest free bit.
		r |= 1<<Free
		t.reg = Free
		.VTemps = r
		(t isnt realconst) // why would it be?
		(t is temp)
		check (!(.vdecls & .VTemps)) (.fn.sourcearg, "No free local variables.")	// entirely full
		return t
	
	
	function NopReg (|asmreg| R)
		|| f = r.fat
			return .Nop(f)
		if r.reg and r isnt param
			debugat // we don't know what made it, but we should!!
	
	
	function Nop2Consts (|asmreg| A, |asmreg| B)
		|| Further = a.FatIndex > b.FatIndex
			.NopConst(A)
		.NopConst(B)
		if !Further
			.NopConst(A) // in case of swapped order
	
	
	function Const (|asmreg| R, |int| bits, |bool| signed, |ind|)
		if r.reg == 0
			return 0 // sigh. I DIE.
		
		if R is ConstOutput
			|| k = R.const
			if k.fits(bits, signed)
				.NopConst(R, AnyBlock)
				return k|uint64|.trim(bits)


	constants
		AnyBlock = 1

	
	function NopConst (|asmreg| R, |int| NopMode = 0, |bool|)
		require (r is RemoveableOutput) or (r is RealConst)
		|| f = r.fat #require
		require (NopMode&AnyBlock) or .IsCurr(f)
		require (f.µRefCount <= 0)
		.Nop(f)
		return true
	
	
	function Nop (|FATASM| ToNop)
		opt inline
		return .nopsub(ToNop, false, 0)
	
	
	function SoftNop (|FATASM| Fat)
		opt inline
		return .nopsub(fat, true, 0)
	
	
	helper NopSub (|FATASM| Fat, |bool| Soft,  |int| Depth)
		|| C = .curr - 1
		if fat == C
			._curr = C // do it before both kinds of nop...

		depth++
		if depth > 7
		
		for f.fatinputs in fat
			.Decr(f, Soft, depth)
		
		for v.regoutputs in fat
			v &= 31
			if .Registers[v] == fat
				.Registers[v] = nil
				.KnownValuesMap &=~ (1<<v)
		|| Type = asm.noop * (fat < ._curr)
		fat.setop = Type


	function RegIsConst (|asmreg| R, |int64| k, |bool|)
		|| f = .registers[r.reg]
		if f is constoutput
			return f.const == k
	
	
	function Decr (|?&fatasm| f, |bool| Soft, |int| Depth)
		if f
			|| C = f.µrefcount
			C -= (C > 0)
			f.µrefcount = C
			if !soft and C <= 0
				.NopSub(f, soft, depth)
	
	
	function AskNop (|fatasm| ToNop)
		if ToNop.µRefCount <= 0
			.nop(ToNop)
	
	
	function SoftNopRange (|fatasm| Start, |fatasm| After)
		while start < After
			.SoftNop(--after)
	
	
///////////////////////////////////////////////////////////////////////////////////////////////////
	
	function µBoolInto (|message| exp, |AsmReg| Dest, |asmreg|)
		dest.µType = DataTypeCode.bool
		|| x = .µInto(exp, dest as asmreg.PreferEqul) // easier to optimise.
		if x.IsBool
			return x
		(dest as= X bitand ASMReg.FromExistingVar)
		return .Exists(dest, X, exp)
	
	
	function µGetReal (|message| exp,  |scdecl| A, |asmreg| ModeOnly=nil,  |asmreg|)
		|| o = .OpenVars
		|| v = .DeclareMe(exp, a)
		(v as= ModeOnly asnt asmreg.GetReal)
		if ModeOnly is Discard
			return .µInto(exp, v)

		|| f = .µGetASM(exp, v)
		|| roar = f.reg
		if roar == v.reg
			return f
		
		if !roar and (modeonly is set)
			return .assign(v, f, exp)	
		.CloseVars(o) // close the var if v2 is unused.
		return f

	
	
	function µInto (|message| exp, |AsmReg| Dest, |asmreg|)
		target debug: if !dest.reg
		// OK... so... we should really do a type-cast, also.
		
		|| r = .µ(Exp, dest)		
		
		if r iz dest
			if dest.bitcount == r.bitcount
				return r
			|| f = r.fat
			if f and .iscurr(F)	and f.IsRead
				return dest
		
		if r is temp
			|| f = r.fat
				return .ReDest(f, dest)

		if "asknop".trap
		|| dest2 = r.into(dest)
		if dest is preferequl
			return .Exists(dest, r, exp)
		return .Assign(Dest2, r, exp)
	
	
	function µ (|message| exp, |AsmReg| Dest = asmreg(), |asmreg|)
		description "Same as µGetASM, except this closes temp vars for reuse." 
		|| Closer = .OpenVars
		|| v = .µGetASM(exp, dest)
		return .CloseVars(Closer, nil, v)


	function µReffed (|message| exp, |scdecl!| A, |asmreg|)
		rz = asmreg()
		(rz is AddrNeed) = (a is Reference)
		rz = .µGetReal(exp, nil, rz)
		if (a is reference  and  rz isnt containsaddr)
			error (exp, "Failed to get address of expression. (Compiler *should* have caught this in transform phase)")
		
		|| f = rz.fat
			f.µRefCount++


	function µGetASM (|message| exp, |AsmReg| Dest = asmreg(), |asmreg|)
		opt norefcounts
		cpp_part GetASM 
		
		|| fn = fn_asm_table[exp.ASMType]!
		target debug
			dest.TmpCheck
			|| StartStackSize = ++.stacksize
			if StartStackSize > 20 // ugh
			|| trap = ++ASMTrapper
			if trap == 120681
		  else
			opt inline
		
		|| ss = (fn)(self, exp, dest)

		target debug
			.StackSize--
			if !ss.tmpcheck
				(fn)(self, exp, dest)
			if !ss  or  (dest is set)  or  (ss is set or ok)
				return ss // no need test.
			if !ss.reg and (ss isnt condanswer)
				if (ss isnt ExitFunction) and (ss isnt constoutput)
					debugger
					PrintProgress()
					(fn)(self, exp, dest)
		return ss
	
	
	function PackMakerInit
		opt norefcounts
		require !.Inited
		.inited = true
		VM_Builder.JustDefineInstructions
		instruction.InstructionInit
		ASMType.InitAccess
		Instruction[asm.KNSR].Const
		Instruction[asm.KNST].Const
		asm.encoders[asm.KNST] = fatasm.KNST_Encoder
		asm.encoders[asm.noop] = fatasm.NOOP_Encoder
		return 
		TextAssembler.Requests		?= dictionary()
		TextAssembler.RequestTable	?= array()
	
	
	function InitAndStartFunc (|scfunction| fn) // init 
		// can't we just zero the entire thing?
		opt norefcounts
		
		target debug
			asm.noisyasm = 3

		|| j = jsm
		.end = j.NeedSpare(256K, j.Size)
		.start = j.ptr
		._curr = .start + j.Length
		
		// after zeroing, we could just set everything normally? It seems ._curr is set from the jsm.
		HoistSpace.Length = 0
		
		.out = fn
		.state.fn = fn
		SCFunction.CurrFunc = fn
		.state.Return = nil
		.StackSize = 0
		.state.inlinedepth = 0
		.state.BranchDepth = 0
		.state.TailInlineable = true
		.InlineDepthLimit = 3
		.BasicBlock = 0
		.VDecls = 1
		.IsTextual = false
		.VTemps = 0
		.NeedsRework = false
		.zero = nil
		.zero.info = AsmReg()
		.KnownValuesMap = nil
		for i in 32
			.registers[i] = nil

		.Guard
		fn.StartASM
		.LastDebug = .curr
		.InlineEnd = nil
		.FuncStart = .curr
		.ReturnASM = fn.ASMReturn.regtype
		.AddFuncParams(fn)


	function CapASM
		.out.µlength = .length
		jsm!.length = .curr - .start 

		
	function FinishASM 
		opt norefcounts
		if .zero.Const // what?
		|| l = .last
		if !l.IsFinisher
			debugger !.fn.ASMReturn
			|| dl = .fn.Source!.DeepLast
			.closevars(1, dl)
			|asmreg| dest = nil
			if !ASMType.ReturnOpt(self, dest)
				dl.RET()
		.Optimise


	function Guard
		opt norefcounts
		|| A = asmreg()										// GUARD
		|| arg = .fn.SourceArg!
		target debug
			if arg.position < 0
				arg.position = 0 // skip error detection
		Arg.EROR(A,A,A,A,0)

/* The .asint/.asfloat functions should be recognised as working on constants? Or is that just a general case of inline functions? Technically, its an inline func that does nothing, just returns the original... with a different type. */


	iterator
		|| h = .KnownValuesMap
		while h
			|| h2 = h.LowestBit
			yield .Registers[h2.log2]!
			h &= ~h2


target debug
	|| ASMTrapper = 0
// Speedie's function histogram:  0:410, 1:1656, 2:1464, 3:713, 4:167, 5:61, 6:10, 7:4, 8:0
// There are only 4 above 6!, only 14 above 5, 75 above 4
// almost all lib functions have 4 or less. 5 is for proj funcs.
	


function Assembler.Rework (|SCFunction| fn,  |bool|)
	HoistSpace.NeedSpare(.Length)
	HoistSpace.CopyFrom(.FuncStart, .Length)
	.Orig = .FuncStart
	.InitAndStartFunc(fn)
	asmtype.ArgumentSub(self, fn.SourceArg!)
	.FinishASM
	.Orig = nil


function Assembler.GenerateASM (|scfunction| fn,  |bool|) // AsmGenerate
	opt norefcounts
	
	.InitAndStartFunc(fn)
	asmtype.ArgumentSub(self, fn.SourceArg!)
	.FinishASM
	if .NeedsRework
		.Rework(fn)
	.CapASM

	if StdErr.ok
		|| max = fn.maxasm
			check (fn.µlength <= max) (fn.sourcearg, "ASM too large")
		target debug
			fn.µprint(++CountASMFuncPrint)
		return true


|| CountASMFuncPrint = 0

