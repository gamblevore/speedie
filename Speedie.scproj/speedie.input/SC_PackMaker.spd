

// some kinda function table... saved to a file

module PackMaker
	|[scfunction]|			LibFuncs
	|[SCDecl]|				LibGlobs
	|[scfunction]|			PackFuncs
	|[SCDecl]|				PackGlobs
	|int| 					ExportPos

	function AddAll
		opt norefcounts
		|| MakeExec = options.MakeInterpreter
		for f in Compiler.FuncList
			if !f.CanLibLoad
				0
			  elseif f.IsStdLib
				if MakeExec or f.needsexport
					.LibFuncs <~ f
			  elseif f.NeedsExport
				.PackFuncs <~ f
		.SortAll
	
		
	function AddLibGlob (|scdecl| d)
		.LibGlobs <~ d
		d.ExportPosition = ++.ExportPos
	
	
	function DoLibGlobs (|jbin| lib)
		opt norefcounts
		LibGlobs.sort(NodeSorter)
		
		lib.enter(@arr)
		for (f in LibGlobs)
			lib.Add(@thg, f.ExportName)
		lib.exit


	function SortAll
		opt norefcounts
		|| FnInit = compiler.xfunc("JB_Init_")
		|| FnMain = compiler.xfunc("JB_Main")
		require fninit and fnmain
		
		fninit.tableid = 0 // put init and main first!
		fnmain.tableid = 1

		.SortSub(.libfuncs)
		.SortSub(.Packfuncs)
	
	
	helper SortSub (|[scfunction]| list)
		opt norefcounts
		list.sort(SCFuncSorter)
		for (f in list) (i)
			f.TableID = i

	
	function WriteLibFuncs (|jbin!| j)
		opt norefcounts
		todo "Write the libfuncs via c++ structs, instead of jbin, like array of name/ptr pairs"
		
		j.Enter(@prm)
		for f in .libfuncs
			j.Add(@thg, f.exportname)
		j.Exit


	function MakePack 
		require ASMState.Sh.PackMakerInit // and fn_asm_table[@arg]

		|| upon = SpdAssembler.CollectFuncs("asm_test,asm_test_sub,asm_test_ptr,asm_test_flow")$
		for fn in upon
			require SpdAssembler.Vacuum(fn)

		|| j = jbin(@tmp, "xpd")
		j.enter
		j.enter(@tmp, "app")
		.OrderGlobals(j)
		j.enter
		.MiniTest(upon.first!)
		.ListPackFuncs(j, upon)
		.DumpStrings(j)
		.PackClass(j)
		.FuncLink(j)
		.GlobLink(j)

		Compiler.ProductFile.SmartData = j


	function OrderGlobals (|jbin| j)
		.PackGlobs.sort(DeclSizeSorter)
		|| g = .OrderGlobalsSub
		j.addint(G + 16)


	function OrderGlobalsSub (|int|)
		opt norefcounts
		|| G = 1				// So we can detect bugs easier... if something lacks an export position.
		for (d in .PackGlobs)
			|| s = d.DeclSize
			G = G align s
			d.ExportPosition = G
			G += s
		return g

	
	function MiniTest (|scfunction| fn1)
		|| args = {8725l, 1l, 275l, 1l}
		|| mu   = fn1.asm
			|| Result = mu.finish.run( (&args[0])|&ivec4|, â€“count args )
			if result != 9000
	

	function ListPackFuncs (|jbin| j, |[scfunction]| upon)
		j.tmp("fncs")
		for f in upon
			j.Add(@tmp, f.ExportName, true)
			j.add(f.ASM #expect)
			j.exit
		j.Exit(2)
		
	
	function DumpStrings (|jbin| j)
		j.tmp("strs")
		for s in StringCorrector.list
			j.add(@str, s)
		j.exit(2)



	function PackClass (|jbin| j)
		j.enter(@tmp, "clss")
		typeobject!.PackClassChildren(j)
		j.exit
	
	function FuncLink (|jbin| j)
		opt norefcounts
		j.tmp("flnk")
		for linked in .libfuncs
			j.add(@thg, linked.ExportName)
		j.exit(2)
	
	
	function GlobLink (|jbin| j)
		opt norefcounts
		j.tmp("blnk")
		for linked in .LibGlobs
			j.add(@thg, linked.ExportName)
		j.exit(2)


	function SCClass.HasPackChildren (|bool|)
		opt norefcounts
		for c in .children
			if !c.isrole and !c.isstdlib
				return true
			if c.HasPackChildren
				return true

	function SCClass.PackClassChildren (|jbin| j)
		opt norefcounts
		require .HasPackChildren
		j.enter
		for c in .children
			if !c.isrole
				c.packexport(j)
		j.exit
	
	
	function SCClass.PackExport (|jbin| j)
		opt norefcounts
		require .HasPackChildren or !.isstdlib

		j.enter(@tmp, .name)
		if !.IsSTDLib
			.DescribeInPack(j)
		.PackClassChildren(j)
		j.exit
		
		
	function SCClass.DescribeInPack (|jbin| j)
		opt norefcounts
		|| sz = (0, .size)(.iswrapper)
		j.addint(sz)
		
		|| fn_list = .behaviour!.WrapperFuncs
		j.enter(@list)
	
		|| name = "0"
		|| df = .DestructorFunc
			name = df.exportname
		j.add(@thg, name)
	
		|| b = .behaviour
		while (b)
			for (M in b.interfaces)
				|| fn = .BestBehaviourFor(m.name.InterfaceToBehaviour)
					|| BaseClass = .InterfaceForBehaviour(fn)
						j.add(@thg, fn.exportname)
			b = b.upwards
		
		j.exit

		
	function BuildInterpreter (|string|)
		|| pack = jbin()
		.WriteLibFuncs(pack)
		.DoLibGlobs(pack)
		return pack


function scfunction.WrappedName (|string|)
	if .iscppinbuilt
		return .exportname + "Wrap"
	  else
		return .exportname


function scfunction.CanLibLoad (|bool|)
	if !.ismacro and (self isnt disabled) and (.iscppinbuilt != 2)
		return true


/* Instead of this... SortAndPackFuncs could create some cpp... like
struct LibPack
	|int|			FuncCount
	|int|			GlobCount
	|LibEntry[1]|	Item

struct LibEntry
	|&nil|		Where
	|cstring|	Name

then we just return a LibPack* from C++.

Then exec can just... use the table directly, as it is already sorted. :)    */


function printfunc (|string| s)
	visible
	|| fn = compiler.xfunc(s)
		printline fn
