/*
	Jeebox internals. By Theodore H. Smith...
*/



class SCParamArray
	linkage
		cpp_part		PA
	contains message

	|Message[11]--|		Items
	|Message--|			Exp
	|int16|				Size
	|int16|				ErrCount
	|bool|				IsAssigns
	|bool|				HasProperParams
	|bool|				IsDot
	|bool|				IsAddress
	|bool|				IsNotModule
	|bool|				IsSelf
	|bool|				IsSuper
	|bool|				Found
	

	constructor (|Message| exp, |message|side=nil)
		.Exp = exp
		.side = side
		.IsDot = (exp == @dot)
		.IsAddress = (exp != nil) and (exp.niceparent Isbrel "&")
		.ErrCount = stderr.errorcount

	syntax append (|| m)
		expect (.size < 11) (m, "Too many parameters")
		|| i = .size++
		.items[i] = m
	
	syntax access (|int| i, ||)
		if (i < .size)
			return .items[i]
	
	function IsModule (|bool|)
		return !.IsNotModule
		
	function MadeError (|bool|)
		return StdErr.ErrorCount > .errcount
	
	function IgnoreSelfContain (|scclass|cls, |int|)
		require cls and true//cls.IgnoreContainedSelf
		if (.IsDot and .IsNotModule)
			return kTypeCastIgnoreContained

	function RenderKind (|string|)
		if (.IsNotModule)
			return "class"
		return "module"
	
	function ModuleName (|string|)
		if (.IsNotModule)
			return Self[0].RenderType
		return .exp.first.RenderType



	function PreReadTypes (|SCBase| Name_Space, |Message| P, |message|side, |bool|)
		.exp := p
		for  Item.fast in P  // in case '...' adds a param
			require  .AddTestedParam(item, name_space)
		.side = side
		return true


	function scbase.IsModuleFunc (|bool|)
		opt norefcounts
		if (self isa scarg)
			return !.IsClassArg
		return (self isa SCModule)


	function DetectDotSuper (|SCBase| curr, |SCBase|)
		opt norefcounts
		.IsNotModule = !Curr.IsModuleFunc
		if .IsDot
			|| Exp0 = .exp.first
			.isself = (exp0 ~= "self")
			if .IsNotModule
				self <~ exp0
				if (exp0 IsThing "super")
					.IsSuper = true
					exp0.name = "self"
					// setting isself would change property setters.
					return Curr.Lookup
		return Curr


	syntax cast (|bool|)
		return .HasProperParams


	function AddParam (|message| item)
		self <~ item
		.HasProperParams = true


	function AddTestedParam (|message| item, |scbase| name_space, |bool|)
		opt norefcounts
		expect TypeOfExpr( item, name_space ) ( item, "Canâ€™t find type." )
		.AddParam(Item)
		return true


	function Range (|IntRange|)
		|| y = .size
		|| x = (.isdot and .IsNotModule)|int|
		return (x,y)|intrange|


	function MacroSize (|int|)
		if .isassigns
			return .size-1
		return .size


	function Side (assigns:|Message|)
		if (Value)
			self <~ Value
			.IsAssigns = true


function scfunction.SelfMatch (|scparamarray| p, |scbase| name_space,  |bool|)
	rz = true
	|| SelfContain = .SelfContain
		|| ty = extractdecl(selfcontain, name_space)
			|| exty = TypeOfExpr(p.exp.first, name_space)
				|| m = ty.Match( exty, 0, nil )
				rz = m == kSimpleMatch or kSuperClassMatch
	
	
	
