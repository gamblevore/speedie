

module LinkMap 
	|int| 			CurrID

	function scfunction.CanLinkTo (|SCNode| To, |bool|)
		if to.LinkedFrom < LinkMap.CurrID
			to.LinkedFrom = LinkMap.CurrID
			return true
			
	function scfunction.DoLinkTo (|SCNode| To)
		if .canlinkto(to)
			linkmap.Store(.linkto, To)
	
	function scfunction.DoLinkBoth (|SCNode| To)
		if .canlinkto(to)
			linkmap.Store(.linkto, to)
			if compiler.InPerry
				linkmap.store(To.linkfrom, self)
	
	function Store (|&(array? of SCObject)| darr, |SCObject| obj)
		opt norefcounts
		|| arr = darr[]
		if arr == nil
			arr = []
			darr[] = arr
		arr <~ obj


	function CollectAll
		opt norefcounts
		for s in compiler.funclist
			.collect(s)


	function Collect (|scfunction| self)
		opt norefcounts
		scfunction.currfunc = self
		CurrID++
	
		for d in .args
			.CollectLinks( d )
	
		|| r = .returntype
			.CollectLinks( r )
	
		collectfromsource(self, .sourcearg, false)
	
	
	function CollectFromSource (|scfunction| self, |message| src, |bool| InBranch)
		opt norefcounts
		for msg in src
			|| o = msg.obj
				.CollectLinks( o, InBranch )
			if msg.HasAny
				if !inbranch
					|| T = msg.ASMType
					inbranch = (t == ASMType.kIf or ASMType.kwhile or ASMType.ktern)
								// how do we check that this isnt recursive?
								// seems to do this, we need to go over the entire program
								// anytime we wanna add a function! like if we call .parse
								// now we gotta test every other function... its not so simple is it.
								// We could just do it 1 level deep? 
				collectfromsource(Self, msg, inbranch)
		


extend scfunction {

function CollectLinks (|object!| obj, |bool| InBranch=false)
	opt norefcounts
	
	while 
		if obj isa SCFunction
			return .DoLinkBoth( obj )
	
		if obj isa SCClass
			return .DoLinkTo( obj )
		
		if obj isa SCDecl
			.DoLinkTo( obj.type )
			obj = obj.IsLookupOnly|scdecl| #require
		  else
			return


function ReachFunc (|SCNode| from, |bool|)
	opt norefcounts
	flow off // memory order dependant!
	
	if .borked
		error (.source,    "Borked function reached!")
		error (from.source, from.ReachedName)
	
	require !.MyReacher
	.MyReacher = from
	Compiler.stReachedFunc++

	.cls.Reach(self)?
		
	for o in .LinkTo
		if (o.MyReacher)
			//
		  elseif (o isa SCFunction)
			o.ReachFunc(self)
			rz = true
		  elseif (o isa SCClass)
			o.Reach(self)
	
	
function IsUnused (|bool|)
	opt norefcounts
	|| p = .project
		require p.WarnUnusedFuncs
	return !.MyReacher and (self isnt Disabled) and !.isvisible and (self isnt ConOrDes) and !.IsAllocOrNew

}


function message.HateUnusedFuncs
	opt norefcounts
	|| imp = scimport.findimport(.name)
		imp.warnunusedfuncs = true
	  else
		.expect(.func, "Can't find project")



function scclass.Reach (|SCNode| from, |message| src=nil)
	opt norefcounts
	flow off
	require !.MyReacher
	.MyReacher = from
	Compiler.stReachedClass++
	
	if Options.BannedClasses
		.BannedCheck(src)
	|| s = .super
		s.Reach(self)
	|| b = .Behaviour
		b.FuncTable!.cls!.reach(self)
	
	|| df = .DestructorFunc
		df.ReachFunc(self)
	
	for d in .Properties
		d.TypeReach(self, d.source!)
		d.contains!.TypeReach(self, d.source!)


function scclass.ReachBehaviours
	flow off
	for f in .Access
		if f isa SCFunction
			if f.IsBehaviour
				f.ReachFunc(self)


function compiler.DeadStrip
	opt norefcounts
	Compiler.VisibleFuncs = SCNode("Visible",	  false)
	Compiler.Interpreter  = SCNode("Interpreter", false)

	compiler.MainFunc.ReachedDetect?
	
	for f in .FuncList
		f.ReachedDetect

	if options.MakeInterpreter // must come before the next loop
		for c in .classlist
			c.reach(Compiler.Interpreter)
	
	for c in .classlist
		if c.MyReacher // shorten-discovery-chains, 
			c.ReachBehaviours // behaviour chains tend to be long.

	CodeSorter.LeafsFirst		// super-refcounter wants this
	

function scdecl.TypeReach (|SCNode| from, |message| src)
	opt norefcounts, flow off
	if self
		return .type.reach(from, src)

	
