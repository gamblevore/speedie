

extend_module ArchonPurger {
	dispatch NilTable (NilTrackerFn)
		@thg:	Thing
		@bra:	Bracket
		@type:	Type
		@func:	Function
		@tmp:   StatementJump
		@dot:	Property
		@acc:	Access
		@item:	Item
		@brel:	brel
		@arel:	arel
		@rel:	Rel
		@list:	List
		@decl:	Declaration
		@arg:	Argument
		@sheb:  Ignore
		@sstr:	String
		@str:	String
		@num:	Number
		@char:	Char
		(ASMType.kif):			If
		(ASMType.kdebugger):	ignore
		(ASMType.kWhile):		While
		(ASMType.kContinue):	Continue
		(ASMType.kExit):		Exit
		(ASMType.kSetRel):		SetRel
		(ASMType.kSwap):		Swap
		(ASMType.kReturn):		Return
		(ASMType.kTern):		Tern
		(ASMType.kStatExpr): 	StatExpr
		(ASMType.kPointer):		Pointer
		(ASMType.kIgnore):		Ignore
		(ASMType.krejoin):		Rejoin
		(ASMType.kFail):		Fail
		(ASMType.kRefCount):	Function // sigh

	
	TrackerDummies()
	function TrackerDummies
		for i in 64
			NilTable[i]     ?= &Dummy
			fn_asm_table[i] ?= &ASMType.unexpected



function Dummy (NilTrackerFn): debugger


function Ignore (NilTrackerFn)

		
function StatementJump (NilTrackerFn)
	|| fn = NilTable[msg.ASMType]!
	if fn != StatementJump
		return (NilTable[msg.ASMType]!)(msg)
	if stderr.ok
		error (msg, "".internal)
		debugger // what? how?


function SyncJump (NilTrackerFn)
	PostIncrementNil.Sync = true
	rz = .jump(msg, test)
	PostIncrementNil.Clear


function JumpWithinBool (niltrackerfn)
	t.InsideBoolLogic++
	rz = .jump(msg, test)
	t.InsideBoolLogic--


function JumpSub (NilTrackerFn)
	opt inline
	|| t = msg.ASMType
		return (NilTable[t]!)(msg, test)
	t = msg.func|ASMType|
	msg.ASMType = t						// main setter here.
	return (NilTable[t]!)(msg, test)

function Jump (NilTrackerFn)
	cpp_part FlowJump
	target flow
		if FlowControl.Active
			FlowControl <~ msg.func|byte|
			FlowControl.input("", msg.name)
	rz = .JumpSub(msg, test)
	target flow
		if FlowControl.Active
			FlowControl <~ rz|byte|


function PropertyToFunc (|message!| dot, |scfunction!| fn, |nilstate|)
	opt norefcounts
	// a.b( c )
	// b(a, c)
	
	dot.func = @func
	dot.ASMType = (@func)|ASMType|
	|| upon = dot.First!
	|| Prms = dot.Last!
	Prms.obj = fn
	Prms.Func = @prm
	Prms.First = upon

	dot.memorylayer.use
	dot.First = message(nil, @thg, dot.position, fn.exportname, 0)
	return nilstate.real
	


function UseChildAsReal (|message| msg, |nilcheckmode| test, |nilreason| reason, |NilState|) 	// a.b, *a, a[0], (a)()
	opt norefcounts

	|| dcl = msg.FastDecl
	if dcl is Global
		return dcl.NilDeclared
	|| ch = msg.First
		return .UseAsRealSub(ch, test, reason, dcl)
	debugger // what?


function UseAsReal (|message| msg, |nilcheckmode| test, |nilreason| reason, |NilState|) 	// a.b, *a, a[0], (a)()
	return .useasrealsub(msg, test, reason, msg.fastdecl)


function UseAsRealSub (|message| ch, |nilcheckmode| test, |nilreason| reason, |scdecl| dcl, |NilState|) 	// a.b, *a, a[0], (a)()
	opt norefcounts
	|| actual = .Jump(ch)
	if actual is nilish
		|| ddd = dcl.hiderfunc
		if ddd and (reason is property) and !ddd.isassigns and (test isnt assigns)				// OK... 
			return .PropertyToFunc(ch.parent!, ddd)
			
		.BecomeReal(ch.FastDecl, ch, reason)
	|| nd = dcl.nildeclared
	if  test isnt active  or  test is soft
		return nd
	if nd.statedreal and dcl.typesuffers
		return ch.RedundantDeclare(nd)
	return nd


function Rel (NilTrackerFn)									// x = y
	opt norefcounts
	|| ll = msg.first!
	|| op = ll.next!
	|| scop = op.obj!|SCOperator|
	|| rr = op.next!

	if scop Is Compare
		return .CompareObjs(ll, rr, test, scop)
	
	|| AndOr = scop is AndOr
		(test is AND) = (andor == opmode.AND)
		return .BooleanLogic(msg, test.activate)

	|| L = .Jump(ll) & NilState.Either		// (ptr+1) or (1+ptr) still needs nilness check
	|| R = .Jump(rr) & NilState.Either
	|| D = msg.FastDecl
	if !D.TypeSuffers						// (ptr-ptr) is actually a number... so thats OK.
		return nilstate.real
	if NilState.Nilish == l or r
		return nilstate.nilish
	return l ||| r


function List (NilTrackerFn)
	opt norefcounts
	for s in msg
		.jump(s)
	return nilstate.Real


function Item (NilTrackerFn)
	return .jump(msg.last!, test)


function Access (NilTrackerFn)
	return .UseChildAsReal(msg, test, nilreason.accessing)


function Property (NilTrackerFn)
	return .UseChildAsReal(msg, test, nilreason.property)


function RealBool (|bool| b, |nilstate|)
	rz = nilstate.real
	rz |= nilstate.truevalue >> b


function Number (niltrackerfn)
	return .RealBool(msg.name.iszero)


function Char (niltrackerfn)
	return .RealBool(msg.name.isnull)


function String (niltrackerfn)
	StringCorrector.CollectString(msg, scfunction.currfunc!.isstdlib)
	return .RealBool(!msg.length)


function Function (NilTrackerFn)
	opt norefcounts
	test &=~ NilCheckMode.Disappears
	|| fp = msg.first!
	|| prms = fp.next!
	
	|| fn = prms.obj|SCFunction|
	if !fn
//		expect (fp.func == @bra) (prms, errorreceiver.NonFatal) // no longer true.
		fn = fp.GetFunctionPointer$
		fp = fp.unbra
		require .UseAsReal(fp, 0, nilreason.accessing)					// (abc)()

	expect ((fn != scfunction.currfunc) or t.anyreturns or t.nestdepth > 0) (msg, "Recursion bug. !!!!!!! QUICKLY DELETE THIS.")
	
	if (test is active) and fn.IsNilChecker
		if fn.isnilchecker != 3
			Test |= NilCheckMode.Soft
		if t.FnAbleToNilCheck
			rz = nilstate.FnBecomesNilCh
	  else
		Test = 0
	
	|| P = prms.first
	|| HasAddrs = false
	for recv in fn.args
		if (recv.nildeclared is optional) and (fn.IsNilChecker != 3)
			test |= nilcheckmode.Soft
			
		expect (p) (msg, "Compiler did not fully type this function.") 
		|| v = .Jump(p!, test)
		if rz and !(v is FnBecomesNilCh) and p!.isfirst
			rz = 0 // can't work
		|| sent = p!.FastDecl
		HasAddrs ?= sent.CanUpgradeInternalPointer
		if (v is nilish) // and recv.NilReg	
			.NilParamPass(recv, sent, p!, fn, v)

		Test = 0
		p++
	
	if HasAddrs
		p = prms.first
		for recv in fn.args
			|| infernal = p!.FastDecl.CanUpgradeInternalPointer
				|| ri = recv.internal!
				.VariableSet(infernal, recv.source!, ri, p!, ri.nilcurr)
			p++

	if fn Is Killer
		.DeclKill
		if (t.nestdepth == 0)  and  !t.AnyReturns // also if no returns happened...
			(scfunction.currfunc! is killer)
		return nilstate.Killer
	
	|| ret = fn.ReturnType
		rz |= ret.NilDeclared
		check ((rz is stated) or !ret.TypeSuffers) (msg, "Nil-decoration needed on return type of function: " + fn.RenderName)


function Rejoin (NilTrackerFn)
	opt norefcounts
	return .jump(msg.first!, Test) 


function DetectStillChecks (|message| first, |nilstate|)
	require first
	rz = .jump(first)
	if !t.FnAbleToNilCheck or (rz is FnBecomesNilCh)
		return rz

	|| F = first.TrueNess(rz)
	|| SelfNil = t.self is nilish 
	if f.Knownfalse
		if !SelfNil
			t.FnAbleToNilCheck = false		// also require that false returns ALWAYS have self nil!
		// optional is allowed to return false
	  elseif rz is FnBecomesNilCh			// its ok
		0
	  elseif SelfNil
		t.FnAbleToNilCheck = false			// Nilcheckers require only return true if self is not nil!


function Return (NilTrackerFn)
	opt norefcounts
	|| v = .DetectStillChecks(msg.first)
	.Loops.ExitRecord |= nilstate.Basic		// mark as exitting safely.
	.DeclKill
	t.anyreturns = true
	|| ret = t.return
	require ret and ret.typesuffers

	if ret.StatedReal
		expect (v isnt nilish) (msg, "Declaration does not allow returning " + v.rawname)
	ret.NilDeclared = ret.nildeclared ||| (v&nilstate.either)				// assume real, unless it becomes optional??
	target debugnil
		ret.NilPrint("return")


function Exit (NilTrackerFn)
	.loops.ExitRecord |= .EndBlock
	.loops.hasEscape = true


function Continue (NilTrackerFn)
	.loops.ContRecord |= .EndBlock


function While (NilTrackerFn)
	opt norefcounts
	|| cond = msg.first!
	|| CondTest = cond.IsAlwaysBool

	require !CondTest.KnownFalse and T.BranchEnter(msg)

	|| W = .loops.BeginLoop(!condtest.KnownTrue)

	for i in 3 
		.SyncJump(cond, NilCheckMode.whilepos)
		.Argument(cond.next)
		.Loops.NextLoop

	if !condtest.KnownTrue
		.SyncJump(cond, NilCheckMode.whileneg)
		.loops.exitrecord |= .value // merge this
	
	.loops.FinishLoop(msg)
	ArchonPurger.loops = W

	return t.BranchExit
	


function If (NilTrackerFn)
	opt norefcounts
	todo "we could store numbers on the "if" tmp... to check what decls have been altered"
	
	|| cond = msg.first!,  || arg1 = cond.next
	expect (arg1) (msg)
	|| Else = arg1.next,   || arg2 = else.first
	|message| ElseIfCond
	if arg2 and arg2 != @arg
		ElseIfCond = arg2
		arg2 = arg2.next

	|| Orig = T.BranchEnter(msg)				#require
	#!"First Branch"
	.SyncJump(Cond, NilCheckMode.positive)
	.Argument(arg1)
	
	|| First = T.BranchSwap(Orig)

	#!"Second Branch"
	.SyncJump(cond, NilCheckMode.negative) // we need the negative, for the merging.
	if ElseIfCond
		.if(Else)
	  elseif arg2.HasAny
		.Argument(arg2)
	return t.BranchExit(First)



function Tern (Niltrackerfn)
	opt norefcounts
	test &=~ NilCheckMode.Disappears
	msg = msg.second!

	|| Orig = T.BranchEnter(msg)			#require
	|| tC = msg.first!,  || tA = tC.next!,  || tB = tA.next!

	#!"First Branch"
	.jump(tC, NilCheckMode.positive)
	rz = .JumpWithinBool(tA, test)
	|| First = T.BranchSwap(Orig)

	#!"Second Branch"
	.jump(tC, nilcheckmode.negative)
	rz |= .JumpWithinBool(tB, test)
	t.BranchExit(First)
	rz &= ~nilstate.FnBecomesNilCh	// in theory if BOTH were nil-testers... its OK
									// but thats sooo awkward to even think of a real-case
									// where I want this! Just don't do it!



function BooleanLogic (niltrackerfn)
	opt norefcounts
	|| AA = msg.first!
	|| BB = msg.last!
	|| IsAnd = test is AND
	(test is AND) = false 
	if IsAnd == (test is positive)
		|| NC = .Jump(AA, test)						// x = (!a or !a.y)
		NC   |= .JumpWithinBool(BB, test)
		return nilstate.real ||| (NC & nilstate.FnBecomesNilCh)

	|| Orig = T.BranchEnter(AA)				#require
	
	#!"test A+"
	.Jump(AA, test)
	|| First = T.BranchSwap(Orig)

	#!"test A-B+"
	.Jump(AA, test.negate)
	.JumpWithinBool(BB, test)

	return t.BranchExit(First)



function SuperSmartMagicCompare (|message| ll, |message| rr, |nilcheckmode| test, |scoperator| scop,   |nilstate|)
	opt norefcounts
	|| todo = 3
	
	if  ll.func == @thg and rr.func == @thg and test is Active
		if (scop Is EqualOrNot) and ((test is positive) == (scop is ExactlyEquals)|bool|)
		//	if real == x
		//		x becomes real
		// if real != x
		//     0
		//   else
		// 		x becomes real
			|| ld = ll.obj|scdecl|!
			|| rd = rr.obj|scdecl|!
			|| ldl = ld is local
			|| rdl = rd is Local
			if ldl or rdl
				if rdl and ld.NilCurr == NilState.Realish
					T.SetNilness(rd, NilState.real)
					todo &= ~2
				if ldl and rd.NilCurr == NilState.Realish
					T.SetNilness(ld, NilState.real)
					todo &= ~1
	
	if todo & 1
		.jump(ll, NilCheckMode.soft)
	if todo & 2	
		.jump(rr, NilCheckMode.soft)
	return nilstate.real


function CompareObjs (|message| ll, |message| rr, |nilcheckmode| test, |scoperator| scop,  |nilstate|)
	opt norefcounts
		
	|| LNil = ll.IsNowNil
	|| RNil = rr.IsNowNil
	if !lnil and !rnil
		return SuperSmartMagicCompare(ll, rr, test, scop)

	if lnil and rnil
		|| lld = ll.FastDecl
		if lld.NilAllocDepth >= .Loops.NestDepth
			return ll.Redundantvar(lld, test)
		if lnil + rnil != 3
			return nilstate.real
	
	  elseif (2 == (lnil + rnil))		// || x = (a.b.c == objThatisCurrentlyNil)
		.jump(ll)
		.jump(rr)
		return nilstate.real

	if scop Is NilTest
		|| side = (rr, ll)(lnil == 1)
		if test is active
			(test is reversed) = scop Is ExactlyEquals
			test = test.activate
		return .jump(side, test)
	
	error (ll.next, "Nil can't accept this operator. Use != or ==.")


function Thing (NilTrackerFn)
	opt norefcounts
	|| dcl = msg.FastDecl
	rz = .ThingSub(msg, test, dcl)
	msg.Indent = rz				// The msg now knows the current state! Nice!
	if t.FnAbleToNilCheck and dcl is self
		rz |= nilstate.FnBecomesNilCh


function ThingSub (|message| msg,  |nilcheckmode| test,  |scdecl| dcl,  |nilstate|)
	opt norefcounts										// Handles 10 different cases :O

	if !dcl.NilReg
		return dcl.nildeclared
	
	|| Y = dcl.NilCurr
	|| Unknown = dcl.IsUnknownParam 
	if Unknown and (test isnt soft and disappears)
		if test isnt Active							// set var A to unknown prm B, so default to real
			return .BecomeReal(dcl, msg, NilReason.Reading)
		dcl.NilDeclared = nilstate.optional

	if  test isnt active  or  test is soft and Negative
		return Y

	if Unknown or Y != test
		return T.SetNilness(dcl, Test)

	return msg.Redundantvar(dcl, test)


function StatExpr (Niltrackerfn)
	opt norefcounts
	return argument(msg.first, test)
 

function Argument (Niltrackerfn)
	opt norefcounts
	|| v = .value.depthofvars + 1
	for s.fast in msg
		|| t = test & -s.islast
		if (rz is borked) and !stderr.ok
			exit // needs a clean space for borking
		|| prev = rz
		rz = .SyncJump(s, T ||| nilcheckmode.Disappears)
		if prev is borked
			error.autoprint = .oldprint
			expect !(stderr.ok) (s, "Borking but no zergs found?")
			stderr.clear
		if rz is Killer
			exit
	
	|| Allowed = (1|uint64| << (v<<1)) - 1|uint64|
	t.SetAllNil(ArchonPurger.value & Allowed)


function Bracket (Niltrackerfn)
	opt norefcounts
	if msg.length					// a? --> (a) // but the bra has a name
		test |= NilCheckMode.Soft
	return .Jump(msg.first!, test)


function Type (NilTrackerFn)
	opt norefcounts
	|| F = msg.first!
	|| ns = .Jump(F, test) &~ nilstate.FnBecomesNilCh
	|| d = msg.FastDecl
	if !d.TypeSuffers
		return nilstate.real
	|| fd = f.FastDecl
	if fd.TypeSuffers // return inner suffering
		return ns
	
	return d.nildeclared // we created new suffering, so lets pass that back.


function Pointer (Niltrackerfn)
	return .UseChildAsReal(msg, test, nilreason.accessing)				// *abc


function Brel (Niltrackerfn)
	opt norefcounts
	|| b = msg.name[]
	if b == '*'
		return pointer(msg, test)				// *abc

	|| ch = msg.first!
	if b == '-' or '+'							// for ++ or --
		return .jump(ch, test)
	if b == '!'
		return .Not(ch, test)
	
	.Jump(ch, test)
	return nilstate.real						// & ~ both return real


function Not (niltrackerfn)
	test = (test ||| nilcheckmode.reversed).activate
	|| a = .jump(msg, test)
	return a &~ nilstate.FnBecomesNilCh


function arel (Niltrackerfn)
	opt norefcounts
	|| ch = msg.first
		rz = .jump(ch, test)
		|| b = msg.name[]
		if b == '!'
			rz &=~ nilstate.nilish
			rz |=  nilstate.realish
		  elseif b == '?' // won't it be transformed already?
			0
		  else
			if PostIncrementNil.Sync
				PostIncrementNil.Check(ch)
			if t.InsideBoolLogic
				error (ch, "Can't post-increment within boolean-logic")


function Declaration (niltrackerfn) // function decl 
	opt norefcounts
	|| rel = msg.last
	target debug: if rel!=@rel
	|| thg = rel.first!
	//thg.resetasm
	|| d = thg.FastDecl
	|| J = .Jump(rel.last!)
	if d.IsReg
		return T.Declare(D, J)



function Swapity (|scdecl| LD, |message| L, |scdecl| RD, |message| R, |nilstate| RN, |nilstate|)
	opt norefcounts
	|| LL = l.Unwrap
	if ll != @acc // memory access... must work
		check (LD is swappable) (L, l.origrender + " can't be swapped. Must be a real variable somewhere.")
	return .VariableSet(LD,L,  RD,R,  RN)


function Swap (NilTrackerfn)
	opt norefcounts
	|| P = msg.last
	|| R = P.last!
	|| L = P.first!
	|| LD = L.FastDecl
	|| RD = R.Fastdecl
	|| RN = .Jump(R)
	|| LN = .Jump(L)

	.swapity(LD,L,  RD,R,  RN)
	.swapity(RD,R,  LD,L,  LN)


function SetRel (NilTrackerfn)
	opt norefcounts
	|| R = msg.last!
	|| L = msg.first!
	|| LD = L.FastDecl
	test = (nilcheckmode.soft) * (ld isnt local  and  ld.nildeclared == nilstate.optional)
	|| RN = .Jump(R, test)
	// to run this:  *a = *b
	// we would need to run *b first to get the value. So right-side has to be run first.
	.Jump(L, nilcheckmode.Assigns)
								
	return .VariableSet(LD, L, R.FastDecl, R, RN)


function Fail (NilTrackerFn)
	.OldPrint = error.autoprint 
	error.AutoPrint = 0
	return nilstate.borked


function NilParamPass (|scdecl| Recv, |scdecl| Sent, |message| where, |scfunction| f, |nilstate| v)
	opt norefcounts
	if recv.StatedReal
		.BecomeRealSub(sent, where, nilreason.passing, recv, f, v)
	  elseif !recv.nilstated and f != scfunction.currfunc  // haha
		recv.NilPrmFail(f)
	

function VariableSet (|scdecl| Recv, |message| RecvMsg, |scdecl| Sent, |message| SentMsg, |nilstate| SentState, |nilstate|) // setrel, relset
	opt norefcounts
	
	if !recv.typesuffers
		return nilstate.real
	expect (!recv.IsUnknownParam) (recvmsg, Recv.PrmStateMsg("Need to set it into another var."))

	if recv.StatedReal and recv isnt local
		if sentstate isnt nilish
			return sentstate
		if (SentState is realish) 
			return .BecomeReal(sent, SentMsg, nilreason.using)
		if (recv is property) and (recvmsg.func == @dot)
			if (scfunction.currfunc is constructor) and (recvmsg.first!.fastdecl is self)
				error (RecvMsg, "Property should be non-nil in constructor: " + recv.name)
				return nilstate.real
		
		error (RecvMsg, "Nil is not allowed on: " + recvmsg.origrender)
		return nilstate.real
		
	if recv.NilReg
		return T.SetNilness(recv, SentState)


function BecomeReal (|scdecl| sent,  |message| where,  |nilreason| reason,  |scdecl?| recv = nil,  |scfunction?| To = nil, |nilstate|)
	opt norefcounts
	|| Curr = sent.NilCurr
	if curr == NilState.Realish
		return curr
	
	return .BecomeRealSub(sent, where, reason, recv, to, curr)


function BecomeRealSub (|scdecl| sent,  |message| where,  |nilreason| reason,  |scdecl?| recv,  |scfunction?| To, |nilstate| curr, |nilstate|)
	if (Curr == nilstate.either) and sent.IsUnknownParam
		T.BecomeRealSub(sent)
		return T.SetNilness(sent, NilState.real)

	if recv and !recv.TypeSuffers		// passing nil to int
		return NilState.Real

	return .FailedReal(to, where, reason, curr)


function FailedReal (|scfunction| To, |message| where, |nilreason| reason, |nilstate| state, |nilstate|)
	|| fs = faststring()					// "Passing optional: S.pop to RefCount"
	fs <~ nilreason.values[reason]!
	|| raw = state.RawName
	ifn where isthing raw
		fs <~ ' '
		fs <~ raw
	fs <~ ':'
	fs <~ ' '
	fs <~ where.OrigRender
	if To
		fs <~ " to "
		if where.isfirst and to.cls
			fs <~ to.Name
		  else
			to.rendertitle(fs)
		problem (to.source, kUsingStr)
	error (where, fs)
	
}


function scdecl.IsUnknownParam (|bool|)
	opt inline
	return (!.nilstated) and (self is param)


function message.Trueness (|nilstate| found, |maybebool|)
	opt norefcounts
	real self
	|| d = .FastDecl
	|| ret = ArchonPurger.t.Return$
	if d.FastMatch(ret)
		|| n = d.nildeclared
		if n & nilstate.KnownValue
			return maybebool.Known(n is TrueValue)
		if found & nilstate.knownValue
			return maybebool.Known(found is TrueValue)

	  elseif Ret.isdatatypeonly and d.isdatatypeonly // nothing special
		while self == @arg or @bra
			self = self.last
		|| rtf = ret.type.false
		if rtf and self and self matches rtf
			return maybebool.Known(false)
	// return maybe

