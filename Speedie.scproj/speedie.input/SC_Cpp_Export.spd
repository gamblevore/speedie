

/*
	Jeebox internals. By Theodore H. Smith...
*/


// cexport cppexport exportcpp
class Cpp_Export {
linkage
	cpp_part		Cpp


|[FastStringCpp]|					Outputs_cpp
|[FastStringCpp]|					Outputs_h

|FastStringCpp|						T
|FastStringCpp|						AllTypes
|FastStringCpp|						TypesProj
|FastStringCpp|						HeaderProj
|FastStringCpp|						Syxes
|FastStringCpp|						Inlines
|FastStringCpp|						ModuleProperties

constructor
	.Outputs_cpp = []
	.Outputs_h = []
	.T = FastStringCpp.new
	.AllTypes   = .StreamChild( "AllTypes.h"  )
	.syxes		= .StreamChild( "SyntaxConstants.h" )
	.TypesProj  = .StreamChild( "Proj.h" )
	
	.HeaderProj = .StreamChild( "JB.h" )
	.Inlines	= .StreamChild( "Inlines.h" )
	.ModuleProperties = .StreamChild( "ModuleProperties.cpp" )
	Init()


function PreRun
	.AllTypes <~ "
#ifndef ObjCID
	#define ObjCID void*
#endif
"

	.Headerproj <~ '\n'
	
	.ModuleProperties <~ `// Globals put into a separate file to make things faster.
#include "JB_Umbrella.hpp"
#include "JB.h"
#pragma GCC visibility push(hidden)

`
	|| str = "const char* JB_CrashLogFileName = \"/tmp/logs/${compiler.projectname}.crash.txt\";\n\n"
	.ModuleProperties <~ str


function CanOpt (|bool| force, |bool|)
	opt norefcounts
	if force
		return true
	|| c = CurrClass
		return !c.MinOpt


function OptStart (|FastStringCpp| fs, |int| m, |bool| force = 0)
	opt min 3
	if m and .CanOpt(force)
		|| s = "s"
		if m <= 3
			s = m.render
		fs <~ "#pragma GCC optimize (\"O$s\")\n"


function OptEnd (|FastStringCpp| fs, |int| m, |bool| force=0)
	if m and .CanOpt(force)
		fs <~ "\n#pragma GCC reset_options\n\n"
	

function FuncStart (|FastStringCpp| fs, |scfunction| f, |string| funcheader)
	if f is api
		fs <~ "__lib__ "
	
	.OptStart(fs, f.minopt)
	fs <~ FuncHeader
	.T.Length = 0
	
	fs <~ ' ' // space between arg.



function CanExportFunc (|SCFunction| F, |bool|)
	// (.MyReacher) or .isapi
	require (f.MyReacher or f is api)
	require (f isnt disabled)
	if f Isnt ExternalLib
		require (f Isnt Wrapper) and !f.isMacro
	return f isnt NewStruct  and  f isnt alreadyexported


function FuncHeader (|SCFunction| F, |string|)
	WriteFunctionHeader( F, .T )	
	rz = .T.GetResult
	.HeaderProj <~ rz
	.HeaderProj <~ ";\n\n"
	if f Is ExternalLib
		rz = ""


function CppFunc (|FastStringCpp| fs, |SCFunction| F)
	opt norefcounts	
	scfunction.currfunc = f
	(f is AlreadyExported)
	|| FuncHeader = .FuncHeader(f)$
	.SetupFlow(F)

	.funcStart(fs, f, funcheader)
	f.Sourcearg!.Export( fs )

	.OptEnd(fs, f.minopt)
	fs <~ "\n\n"


function SCClass.NeedsExport (|bool|)
	return super.NeedsExport or .IsBehaviourTable


function Codify (|SCModule| scb, |string| fName)
	// class and modules come through here.
	|| cls = scb.Cls
	if !fName
		if (cls)
			fName = cls.ExportName
		  else
			fName = scb.ExportName

	if (scb == Compiler.program) // doesn't really matter...
		fName = "main"
	
	|| ModuleFuncs = scb.ListFunctions
	require (cls or ModuleFuncs or scb.access)
		
	cpp_Export.currclass = cls
	|| File = .StreamChild( fname + ".cpp" )
	.HeaderProj <~ "\n\n// "
	.headerproj <~ fname
	.headerproj <~ "\n"

	.ExportModuleProperties( scb, .ModuleProperties )


	if (Cls and cls.NeedsExport)
		.ExportClass( cls, .ModuleProperties )
		.optstart(file, cls.minopt, true)
		.CodifyFuncs( file, cls.ListFunctions )
		.optend(file, cls.minopt, true)
	
	.CodifyFuncs( file, ModuleFuncs )
	cpp_Export.currclass = nil


function SetupFlow (|SCFunction| f)
	scfunction.CurrFunc = f
	if Options.GenFlowControlCode and f isnt FlowDisabled
		Cpp_Export.IfName = "iif"
		Cpp_Export.WhileName = "wwhile"
		compiler.TernaryFunc!.exportname = "JB_Ternaryy"
	  else
		Cpp_Export.IfName = "if"
		Cpp_Export.WhileName = "while"
		compiler.TernaryFunc!.exportname = "JB_Ternary"


function CodifyInlines
	opt norefcounts
	for (f in compiler.funclist)
		if (f is inline) and .CanExportFunc(f)
			.cppfunc( .Inlines, f )
	scfunction.CurrFunc = nil


function CodifyFuncs (|FastStringCpp| file, |[SCFunction]| list)
	opt norefcounts
	for (f in list)
		if (f isnt inline) and .canexportfunc(f)
			.cppfunc( file, f )
	scfunction.CurrFunc = nil


function StreamChild (|String| s, |FastStringCpp|)
	rz = FastStringCpp(s.TrimExtAndPath)
	if s isa "h"
		.Outputs_h <~ rz
		rz <~ "//// HEADER "
		rz <~ s
	  else
		.Outputs_cpp <~ rz
	rz <~ '\n'


function WriteAPIFuncHeader (|SCFunction| F, |FastStringCpp| fs)
	Cpp_export.WriteAPI = true
	WriteFunctionHeader( F, fs )
	Cpp_export.WriteAPI = false


function SCClass.APIStructName (|string|)
	if (.isobject or .super)
		return .sourcearg.APICppProject + .name.lowercase
	return .Cstructname


function WriteAPIHeader (|file| destfile)
	// CreateApi (Cpp export)
	require destfile!=nil
	
	|| fs = FastStringCpp.new
	fs <~ cpp_export.CppLicence
	fs <~ `

		// API


extern "C" {

		// Types

typedef const char* _cstring;
typedef void* _voidptr;
#ifndef u8
	typedef unsigned char  u8; 
#endif
#ifndef byte
	typedef u8  byte; 
#endif
#ifndef int64
	typedef long long  int64; 
#endif
#ifndef uint64
	typedef unsigned long long  uint64; 
#endif

struct jbobject {int RefCount;};
`

	for (m in compiler.api_list)
		if (m == "class_api")
			|| f = m.first
			|| cls = compiler[f]#require
			if (cls.isobject)
				fs <~ "struct ${cls.APIStructName} : jbobject {};\n"
			  elseif (cls.isdatatypeOrFP)
				fs <~ "typedef ${cls.super.CStructName} ${cls.APIStructName};\n"
			  else
				error (m, "Can't export this.")


	|SCClass| PrevCls
	for (f in compiler.api_funcs)
		if (f.Cls != PrevCls)
			Prevcls = f.cls
			fs <~ "\n\n\t\t// "
			if (prevcls)
				fs <~ prevcls.name
			  else
				fs <~ "Global"
			fs <~ " functions\n\n"
		
		if (f Isnt Wrapper)
			.WriteAPIFuncHeader( f, fs )
		fs <~ ';'
		if (f.description)
			fs <~ " /* ${f.description} */"
		fs <~ '\n'
	
	
	if (Compiler.SyxArray)
		fs <~ "\n\n\t\t// Jeebox Syntax\n\n"
		for (s in Compiler.SyxArray)
			|| slower = s.lowercase
			|| id = _syxdict_[slower]!.id|int|
			fs <~ "#define JB_Syx$s $id;\n"
			fs <~ "#define \$$slower $id\n"
	
	fs <~ `

#ifdef __nodebug
	#undef __nodebug
#endif
#if defined(__linux__)
	#define __nodebug inline __attribute__((__always_inline__)) // __nodebug__ doesn't exist on linux?
#else
	#define __nodebug inline __attribute__((__always_inline__, __nodebug__))
#endif
__nodebug void jb_incr(jbobject* s) {
	if (s) {s->RefCount++;}
}

__nodebug void jb_decr(jbobject* s) {
	if (s) { if (--s->RefCount <= 0) {jb_delete_(s);} }
}

} // end-extern
`
	
	SmartWrite(FS, destfile)



function Run 		
	for c in Compiler.ClassList
		if !c.IsFunc
			WriteType( c, .AllTypes, true )
	
	for c in Compiler.ClassList
		if c.IsFunc
			WriteType( c, .AllTypes, false )
	
	for c in Compiler.ModuleList
		.Codify( c, "" )
	
	for c in Compiler.ModuleList
		.ExportModuleProperties( c, nil )
	
	.CodifyInlines
	
	if options.ProjectIsLibrary
		|| WriteTo = Cpp_Export.APIHeader
			|| File = .StreamChild( "API.cpp" )
			file <~ "\n//// API START! ////\n\n"
			file <~ "#pragma GCC visibility push(default)\n"
			.CodifyFuncs( file, compiler.api_funcs )
			file <~ "#pragma GCC visibility pop\n"
			file <~ "//// API END! ////\n"
			.WriteAPIHeader(WriteTo)


function SCClass.CSuperStructName (|string|)
	|| sup = .realSuper
	if (!sup)
		return "0"
	return "JB_AsClass(${sup.CStructName})"


function AppendPropField (|SCdecl|prop, |FastStringCpp| fs)
	fs <~ "\n\t"
	WriteDecl( prop, fs )
	fs <~ ';'


function AppendCppField (|string|prop, |FastStringCpp| fs)
	fs <~ "\n\t"
	fs <~ prop
	fs <~ ';'


function ExportProperties (|SCClass| cls, |FastStringCpp|fs, |string?| Ignore=nil)
	for prop in cls.Properties
		if prop.name != Ignore
			.AppendPropField(prop, fs)


function ExportOneProperty (|SCClass| cls, |FastStringCpp|fs, |string| Only, |bool|)
	for prop in cls.Properties
		if prop.name == Only
			.AppendPropField(prop, fs)
			return true


function scclass.WriteStructOrUnion (|faststringcpp| fs)
	opt norefcounts
	if .iswrapper == 2
		fs <~ "\nunion "
	  else
		fs <~ "\nstruct "


function ExportStruct (|SCClass| cls, |bool| IsBehaviour=false)
	|| fs = .TypesProj
	cls.WriteStructOrUnion(fs)
	if Cls.IsModel
		fs <~ "__attribute__((__packed__)) "
	
	fs <~ cls.cstructname
	if cls.super
		fs <~ ": "
		fs <~ cls.super.cstructname
	
	fs <~ " {"

	if (IsBehaviour)
		if (!cls.super)
			.AppendCppField("void* __destructor__", fs)
			.ExportOneProperty(cls, fs, "render")
		.ExportProperties(cls, fs, "render")
	  else
		.ExportProperties(cls, fs)
	
	fs <~ "\n};\n"


function ExportBehaviourHeader (|SCClass| cls)
	require !cls.IsRole
	|| b = cls.Behaviour
		.ExportStruct( B.FuncTable!.cls!, true )


function FastStringCpp.AppendBehaviour (|string| name, |SCFunction| fn, |SCClass| Type)
	opt norefcounts
	self <~ "\tResult."
	if name
		self <~ name
	  else
		self <~ "__destructor__"

	self <~ " = "
	if !fn
		self <~ "nil"
	  else
		self <~ '('
		if Type
			self <~ (Type.CStructName #expect)
		  else
			self <~ "void*"
		self <~ ')'
		self <~ fn.exportname
	self <~ ";\n"


function ExportBehaviourInstance (|SCClass| cls,  |FastStringCpp| fs,  |bool|)
	|| b		= cls.behaviour									#require
	|| TableCls = b.FuncTable!.cls
	|| N		= TableCls.CstructName
	|| CN		= cls.CStructName

	fs <~ N
	fs <~ " InitTable"
	fs <~ CN
	fs <~ "_() { // Behaviour \n\t"
	fs <~ N
	fs <~ " Result;\n"
	
	// behaviours are exported here! 679
	fs.AppendBehaviour("", cls.destructorfunc, nil)
	
	while (b)
		for (M in b.interfaces)
			|| s = m.name.InterfaceToBehaviour
			|| fn = cls.BestBehaviourFor(s)
				|| BaseClass = cls.InterfaceForBehaviour(fn)
					if (M.IsRequiredInterface and !cls.Behaviour![s])
						|| msg = "Missing behaviour on class: " + cls.name
						error (cls.source, msg)
						error (baseclass.funcproto!.source, msg)
					fs.AppendBehaviour(s, fn, Baseclass)
			  else
				error (cls.source, "Missing behaviour on class: " + s)
		b = b.upwards
	
	fs <~ "\treturn Result;\n}\n\n"
	fs <~ "$N ${CN}_FuncTable_ = InitTable${CN}_();\n"
	return true


function ExportClass (|SCClass| cls, |FastStringCpp| CppFile)
	require !cls.IsDataTypeOrFP
	require !cls.isbehaviourTable
	require cls.IsRole != 1

	if cls.isrole != 2
		if cls.iswrapper
			.ExportBehaviourHeader(cls)
		require !cls.IsWrapper
		if cls.IsStruct
			.ExportStruct( cls )
			return

	|| HFile = .TypesProj
	
	if !cls.isrole
		.ExportBehaviourHeader(cls) // can we move this lower?
	
	HFile <~ "\nJBClass ( "
	hfile <~ cls.CStructName
	hfile <~ " , "
	if (cls.super)
		hfile <~ cls.super.CStructName
	  else
		hfile <~ "JB_Object"
	hfile <~ " , "
	
	for (prop in cls.Properties)
		hfile <~ "\n\t"
		WriteDecl( prop, hfile )
		hfile <~ ';'
	
	hfile <~ "\n);\n"

	if !cls.isrole
		|| HasTable = .ExportBehaviourInstance(cls, cppfile)
		CppFile <~ "JBClassPlace4(${cls.CStructName}, ${cls.CSuperStructName}, "
		if (!HasTable)
			cls = TypeObject
		cppfile <~ cls.CStructName
		cppfile <~ "_FuncTable_);\n\n"


function MakeWrapperFor (|FastStringCpp| fs, |SCFunction| f)
	|| n = f.WrappedName
	WriteFuncSub( f, n, fs )
	fs <~ " {\n\t"
	if f.returnType
		fs <~ "return "
	
	fs <~ f.ExportName
	
	cpp_export.WriteFuncArgs( f, fs, true )
	fs <~ ";\n}\n\n"


function FillInterpreter
	|| lib_pack = PackMaker.BuildInterpreter
	.ListAllFuncs(.ModuleProperties, lib_pack)



function faststring.appendvoid (|string| s, |byte| extra=0)
	self <~ "\t(void*)"
	if extra
		self <~ extra
	self <~ s
	self <~ ",\n"


function ListAllFuncs (|FastStringCpp| fs, |string| lib_pack)
	fs <~ "// entire_function_list\n\n"
	for f in PackMaker.libFuncs
		if (f.iscppinbuilt == 1)
			.MakeWrapperFor(fs, f)
	
	fs <~ "\n__lib__ void* JB_PackPtrs[] = {\n"

	for f in PackMaker.libFuncs
		fs.appendvoid(f.wrappedname)
	
	fs.appendvoid("0")
	
	for g in packmaker.LibGlobs
		fs.appendvoid(g.exportname, '&')

	fs.appendvoid("0")
	fs <~ "};\n"

	|| pack_str = lib_pack.compress
	check (pack_str.parse) ("Invalid libpack")
	
	fs <~ `__lib__ const char* JB_LibFuncInfo = `
	fs.AppendCpp(pack_str)
	
	fs <~ ";

extern \"C\" JB_String* JB_App__LibPack(void*** Funcs) { // rename this to libpack
	*Funcs = JB_PackPtrs;
	return JB_StrCN(JB_LibFuncInfo, ${pack_str.length});
}
"


function ExportModuleProperties (|SCModule| modl, |FastStringCpp?| IntoModule)
	|| fs = IntoModule
	if !fs
		fs = .TypesProj

	for prop.vars in modl
		if !prop
			0
		  elseif prop is Const
			if !IntoModule
				WriteConst( prop, fs )
				fs <~ '\n'

		  elseif prop.TypeIsReached
			if !IntoModule
				fs <~ "extern "
			  elseif prop is api
				fs <~ "__lib__ "

			if IntoModule and prop is GameFlyingMem
				prop.StringData.replaceall("$(N)", prop.name, fs)
			  else
				WriteDecl( prop, fs, nil )
			fs <~ ';'
			fs <~ '\n'



function ExportSyx
	require Compiler.SyxArray
	|| fs = .syxes
	for (syx in Compiler.SyxArray) (i)
		fs <~ "#define kSyx$syx $i\n"


function FinalMergeOutputIntoACpp
	|| fs = faststringcpp.new
	fs <~ "\n// Auto-generated file, by speedie.\n"
	fs <~ Cpp_Export.CppLicence

	fs.includeh "JB_Umbrella.hpp"
	fs.includeh "JB.h"

	for h in Cpp_Export.H_PostInput
		fs.includeh(h)


	fs <~ `
#pragma GCC visibility push(hidden)
extern "C" {

`
	fs <~ "extern "
	Cpp_Export.GenLub(fs)

	
	fs <~ "extern ${TypeObject!.Behaviour!.FuncTable!.cls!.cstructname} JB_Object_FuncTable_;\n"
	if Cpp_Export.DoSavers
		Cpp_Export.CreateClassListHeader(fs, ";\n")

	cpp_export.GenStrings(.ModuleProperties)
	Cpp_Export.SmartWrite(.ModuleProperties, Cpp_Export.product("JB_Globals.cpp"))
	.ModuleProperties.length = 0

	for f in .Outputs_cpp
		fs <~ Cpp_Export.splitcpp(f, f.cpp_name)
	
	if Cpp_Export.DoSavers
		Cpp_Export.CreateClassListFunc(fs)
	
	fs <~ "}\n\n//"
	
	for i in cpp_Export.Cpp_Includes
		fs <~ ' '
		fs <~ i
	
	Cpp_Export.SmartWrite(FS, Cpp_Export.CppProduct)


function WriteHeader
	|| fs = FastStringCpp.new
	fs <~ "\n\n // Auto-generated file, by speedie.\n"
	fs <~ Cpp_Export.CppLicence	
	if Linkage.VM
		fs <~ "\n#define __VM__ 1\n"
	
	fs <~ "\n#define __SPEEDIE__ 1\n"
	for h in Cpp_Export.H_Input
		fs.includeh(h)
	
	fs <~ `

#ifndef __JB_H__
#define __JB_H__

extern "C" {

`
	
	for (f in .Outputs_h)
		fs <~ f
		fs <~ '\n'
	
	fs <~ "\n}\n#endif // __JB_H__\n"
	Cpp_Export.SmartWrite(fs, Cpp_Export.HProduct)


module
	|bool|						WriteAPI 
	|bool|						WroteAny
								
	|[string]|					H_Input
	|[string]|					H_PostInput
	|[string]|					Cpp_Input
	|[string]|					Cpp_Includes


	|| IfName = ""
	|| WhileName = ""

	|SCClass| 	CurrClass
	|uint64| 	LubHash
	|string| 	FindGlobalsCpp = (`		

extern "C" JB_StringC** JB_Str__FindGlobals(const char** src, uint64* Hash) {
	*src = JB_LUBSrc2;
	*Hash = JB_LUBHash;
	return JB_LUB;
}
`)
	
	function compiler.Reachedfuncs (|int|)
		opt norefcounts
		for f in compiler.funclist
			rz += f.MyReacher!=nil


	function GenLub (|faststring| fs)
		fs <~ "JB_StringC* JB_LUB["
		fs <~ stringcorrector.Count + 1 // + 1 for the emptystring
		fs <~ "];\n\n"


	function GenStrings (|faststring| fs)
		|| sl2 = FastString()
		|| blob = .WriteStrings(sl2)
		|| lengths = sl2.GetResult
		.GenLub(fs)
		
		|| blob2 = FastString()
		blob2.EncodeLength(lengths.length)
		blob2.EncodeLength(blob.length)
		blob2.EncodeLength(stringcorrector.Count)
		blob2 <~ lengths
		blob2 <~ blob
		blob = blob2.GetResult
		.LubHash = blob.hash
		fs <~ "static const uint64 JB_LUBHash = ${.LubHash|int64|};\n"
		fs <~ "static const char* JB_LUBSrc2 = "
		fs.AppendCppAll(blob)
		fs <~ ";\n"
		|| FlowStr = Options.GenFlowControlCode.Render
		fs <~ .FindGlobalsCpp.ReplaceAll("@flow", FlowStr)



	function WriteStrings (|faststring| Lengths, |string|)
		opt norefcounts
		|| fs = FastString()
		for s in StringCorrector.list
			.WriteName(s, fs, lengths)
		return fs


	function WriteName (|string| name, |faststring| blob, |faststring| lengths)
		blob <~ name
		blob <~ 0|byte|
		Lengths.EncodeLength(name.length)


	function CppProduct (|File|)
		return Compiler.GeneratedCppsFolder[ "JB.cpp" ]!


	function HProduct (|File|)
		return Compiler.GeneratedCppsFolder[ "JB.h" ]!


	function Product (|string| name, |File|)
		return Compiler.GeneratedCppsFolder[ name ]!


	function APIHeader (|File|)
		|| arg = compiler.api_place.last(@arg)
		|| ForLib = arg.find(@tmp, "libraryname")[@str]
			|| FolName = arg.find(@tmp, "exportplace")[@str]
				|| Fol = Compiler.BaseProjectPath![ FolName.name ]
				if fol.mustexist("Create API-Header for API: ${arg.filelocation}")
					rz = Fol[compiler.ProjectName + "_api.h"]
	
	
	function WriteFuncSub (|SCFunction| F, |string| E, |FastStringCpp| T)
		.WriteDecl( f.ReturnType, T )
		T <~ ' '
		T <~ E
		.WriteFuncArgs( F, T )
		
		
	function WriteFunctionHeader (|SCFunction| F, |FastStringCpp| T)
		// int example(int a, int b)
		if f Is Inline
			t <~ "inline "
		
		.WriteFuncSub(F, f.exportname, t)


	function WriteDecl (|SCDecl| D, |FastStringCpp| T, |Message| equality = nil) 
		opt norefcounts
		if (!d)
			t <~ "void"
			Return
		
		if d.iscarray
			.WriteDeclNoName( d.internal!, T )
		  else
			.WriteDeclNoName( d, T )
		
		if (d.exportName)
			T <~ ' '
			d.exportName.safe(t, d is local)
			if d.IsCArray
				T <~ '['
				T <~ d.C_Array
				T <~ ']'
		
		if (equality)
			T <~ " = "
			equality.Export( T )


	function SmartWrite (|string| S, |File| F)
		flow off
		FlowControl.Disabled++ // we don't care about previous file contents
		.cpp_includes <~ s.hash|int64|.render
		if F and !F.CompareData(S)
			.wroteAny = true
			F.Data = S
		FlowControl.Disabled--
	
	
	function DoSavers (|bool|)
		return compiler.Reached("SaverClassInfo") and compiler.Reached("saverloadclass")


	|| CppLicenceStr = ""
	function CppLicence (|string|)
		if !.CppLicenceStr
			|| f = compiler.InputFile(nil, "licence.txt", nil)
				.Cpplicencestr = f.readall(16KB, true)
			.CppLicenceStr := " "
		return .CppLicenceStr

	
	function ExportAllSource
		require StdErr.ok
		
		|| exp = Cpp_Export()
		exp.PreRun
		exp.ExportSyx
		exp.run
		exp.WriteHeader
		if Options.MakeInterpreter
			exp.FillInterpreter
		exp.FinalMergeOutputIntoACpp
	

	function CreateClassListHeader (|FastString| fs, |string| s)
		fs <~ "void JB_InitClassList(SaverLoadClass fn)"		
		fs <~ s
	
	
	function FastString.IncludeH (|string| name)
		self <~ `#include "`
		self <~ name 
		self <~ '"'
		self <~ '\n'


	function CreateClassListFunc (|Faststring| dest)
		|| fs = FastStringCpp.new
		.CreateClassListHeader(fs, " {\n")

		for ( c in compiler.ClassList )
			if (c.IsSaveableObject and c.MyReacher)
				fs <~ "\tfn(&"
				fs <~ c.CStructName
				fs <~ "Data, (char*)" 
				c.RenderSaveable(fs)
				fs <~ ");\n"
		
		fs <~ "}\n"
		dest <~ .splitCpp(fs, "saver_funcs")


	function SplitCpp (|faststringcpp| fs, |string| name, |string|)
		if options.SingleCppOutput
			return fs

// if the file is too big, we can split here. Just search for "}\n\n" //{
		require fs.length > 1
		name += ".i"
		.SmartWrite(fs, .product(name))

// reuse fs
		fs.includeh(name)
		return fs
		

	function WriteType (|SCClass| c, |FastStringCpp| fs, |bool| always)
		require c isnt Builtin
		if !always
			require c.NeedsExport
		|| f = c.FuncProto
			fs <~ "\ntypedef "
			.WriteDecl( f.ReturnType, fs )
			fs <~ " (*${c.CStructName})"
			.WriteFuncArgs( f, fs )
		  else
			if c.IsDataTypeOrFP
				|| name = c.super.CStructName #require // can be nil!
				fs <~ "\ntypedef $name "
			  else
				require !c.iswrapper
				c.WriteStructOrUnion(fs)
			fs <~ c.CStructName
		
		fs <~ ";\n"


	function WriteClass (|SCClass| C, |FastStringCpp| T)
		if (.writeapi)
			T <~ C.APIStructName
		  else
			T <~ C.CStructName
		if (C.IsObject)
			T <~ '*'


	function Init
		WroteAny = false
		Cpp_Includes = []
		@Acc.ExportAddr  = C_RenderAcc
		@Item.ExportAddr = C_RenderItem

		@list.exportaddr = C_RenderList
		@prm.exportaddr  = C_RenderList
		@Arg.ExportAddr  = C_RenderArg
		@Dot.ExportAddr  = C_RenderDot
		@Decl.ExportAddr = C_RenderDcl
		@Func.ExportAddr = C_RenderFnc
		@Thg.ExportAddr  = C_RenderThg

		
		@Type.ExportAddr = C_RenderTyp
		@Tmp.ExportAddr  = C_RenderTmp
		@opp.ExportAddr  = C_RenderOpp
		@brel.ExportAddr = C_RenderBrel
		
		@Rel.ExportAddr  = C_RenderRel
		
		@Num.ExportAddr  = C_RenderNum
		@unit.ExportAddr = C_RenderUnt
		@Bra.ExportAddr  = C_RenderBra
		@ARel.ExportAddr = C_RenderArel

		@Char.ExportAddr = C_RenderChr
		@Str.ExportAddr  = C_RenderStr
		@SStr.ExportAddr = C_RenderSStr
		@Back.ExportAddr = C_RenderBak
		
		@Sheb.ExportAddr = C_RenderShe
	
	
	function C_RenderAcc (fpMsgRenderCpp) // name, params
		opt norefcounts
		.first!.Export(fs)
		fs <~ '['
		.second.first!.Export(fs)
		fs <~ ']'
	
	
	function C_RenderItem (fpMsgRenderCpp)
		opt norefcounts
		// func(a:b, c:d)
		.second!.Export( fs )

	
	function C_RenderList (fpMsgRenderCpp)
		opt norefcounts
		|| d = .obj
		if (d isa scdecl)
			fs <~ d.type.name // vec4
			.MiniArgCpp(fs)

	
	function Message.MiniArgCpp (|faststring| fs)
		opt norefcounts
		fs <~ '{'
		for (ch in self)
			fs <~ ", "
			start
			ch.Export(fs)
		fs <~ '}'

	
	function C_RenderArg (fpMsgRenderCpp)
		opt norefcounts
		fs <~ '{'
		fs.indent++
		
		for (ch in self)
			fs.IndentLine
			ch.Export( fs )
			ifn (ch isTmp "if" or "elseif" or "else")
				fs <~ ';'
		
		fs.indent--
		fs.IndentLine
		fs <~ '}'
		if .next
			fs.IndentLine
	
	
	function message.DotMustBeProperty (|scdecl|)
		opt norefcounts
		|| obj = .obj
		if obj mustbe SCDecl
			if obj.name
				return obj
			|| Backup = .Last!.obj as scdecl
				if backup.name
					return Backup
			error (self, "Missing property name")


	function C_RenderDot (fpMsgRenderCpp)
		opt norefcounts
		|| obj = .DotMustBeProperty						#require
		|| ch = .first!
		
		if (ch.obj isa SCModule)
			obj.exportName.safe(fs)
		  else
			|| CloseStr = cpp_export.CDotType( self, obj, fs )
	
			ch.Export( fs )
			|| ch_decl = DeclOfObjForC( ch )
				if NeedsDotForC( ch_decl )
					fs <~ '.'
				  else
					fs <~ "->"

			obj.exportName.safe(fs)
			fs <~ CloseStr
	

	function message.BraDeclfind (|scdecl|)
		opt norefcounts
		
		while
			|| d = .AsDecl
				rz = d
			self = .parent
			loop self == @bra

				
	function C_RenderFnc (fpMsgRenderCpp)
		opt norefcounts
		|| F = .first!
		|| ch1 = f.next!
		|| fn = ch1.obj|scfunction|
		|| PostBracket = false
		
		if (fn isa SCFunction) and !fn.prototype   // easy
			if Fn.ReturnType and (.Parent != @arg)
				PostBracket = C_OneParamType(fs, self, .BraDeclFind)

			fs <~ fn.ExportName
		  else
			F.Export( fs )
		
		C_ParamsRender( ch1, fs )
		if PostBracket
			fs <~ ')'
		
		// what if it's not a known function, but
		// from a function pointer?
		
		// NormalFunc()
		// |fp| y,  y()
		// x[1]()


	function C_RenderDcl (fpMsgRenderCpp)
		opt norefcounts
		|| name = .GetDclName!
		|| d = name.AsDecl				#expect
		WriteDecl( d, fs, DeclsDefault(.last) )
			
	
	function C_RenderTmp (fpMsgRenderCpp)
		opt norefcounts			
		|| name = .name
		|| needbra = false
		
		if (name == "exit")
			fs <~ "break"
			
		  elseif (name == "if")
			fs <~ Cpp_Export.IfName
			needbra = true	

		  elseif (name == "while") 
			fs <~ Cpp_Export.WhileName
			needbra = true	

		  elseif (name == "elseif")
			fs <~ "else "
			fs <~ Cpp_Export.IfName
			needbra = true	

		  elseif (name == "ignore")
			return

		  elseif (name == "rejoin")
			0

		  elseif (name == "swap")
			fs <~ "JB_Swap"
			debugat
			C_ParamsRender(self, fs)
			return
			
		  else
			fs <~ name
		
		if needbra
			|| f = .first!
			needbra  =  f != @bra and @brel
		
		for mv in self
			fs <~ ' '
			if needbra
				fs <~ '('
			mv.Export( fs )
			if needbra
				fs <~ ')'
			needbra = false
		

	function C_RenderOpp (fpMsgRenderCpp)
		opt norefcounts
		|| s = .name
		if s == "(|)"
			fs <~ "|"
		  elseif s == "&"
			fs <~ "&"
		  elseif s == "^"
			fs <~ "^"
		  elseif s == "~"
			fs <~ "~"
		  elseif s == "mod"
			fs <~ "%"
		  else
			fs <~ s


	function C_RenderBrel (fpMsgRenderCpp)
		opt norefcounts
		fs <~ '('
		|| s = .name
		if (s == "~")
			s = "~"
		
		fs <~ s
		RenderInBracketsIfRel( .first!, fs )
		fs <~ ')'


	function WriteConst (|SCDecl| D, |FastStringCpp| T)
		opt norefcounts
		T <~ "#define "
		D.exportName.safe(T)
		t <~ " ("
		D.Default!.Export( T )
		t <~ ')'


	function C_RenderUnt (fpMsgRenderCpp)
		expect (self == "x")
		|| num = .first!
		|| s = num.name.ReplaceAll( "_", "" )

		fs <~ "0x"
		if (s.length mod 2) and (*s).isletter
			fs <~ '0'
		fs <~ s


	function C_RenderBak (fpMsgRenderCpp)
		opt norefcounts
		fs <~ .name // assume pre-rendered in C!


	function C_RenderThg (fpMsgRenderCpp)
		opt norefcounts
		|| o = .obj
		if o isa SCDecl
			WriteThgMaybeTypeToo( self, o, fs )
			Return
		
		printline "Found @thg with .obj=nil: " + .objectid
		debugger
		.safe(fs)


	function C_RenderRel (fpMsgRenderCpp)
		opt norefcounts
		|| first = .first!
		|| op = first.next!
		|| last = op.next!
		|| scop = op.obj|scoperator| #expect (op, "Missing scop".internal)
		if scop is NeedsCppFuncOnFloats
			if first.fastdecl.isfloat
				fs <~ "std::fmod("
				first.export(fs)
				fs <~ ", "
				last.export(fs)
				fs <~ ")"
				return
		
		RenderInBracketsIfRel( first, fs )

		fs <~ ' '
		op.Export( fs )
		fs <~ ' '
		
		RenderInBracketsIfRel( last, fs )


	function RenderInBracketsIfRel (fpMsgRenderCpp)
		opt norefcounts
		if self == @Rel
			fs <~ '('
		
		.Export( fs )
		
		if self == @Rel
			fs <~ ')'


	function WriteFuncArgs (|SCFunction| F, |FastStringCpp| T, |bool| names_only=false)
		// int example(int a, int b)
		opt norefcounts
		T <~ '('
		|| N = false
		for d in f.args
			if n
				T <~ ", "
			n = true
			if names_only
				d.exportname.safe(T, true)
			  else
				.WriteDecl( D, T )
		
		T <~ ')'


	function C_OneParamType (|FastStringCpp| fs, |Message| Curr, |SCDecl| D, |bool|)
		// because C doesn't do type-inferencing
		require d // sigh
		|| ActualType = ClassOfObjForC( Curr )$
		if ActualType != D.type
			fs <~ '('							//  wrapper calls ')' after
			fs <~ '('
			WriteDeclNoName( d, fs )
			fs <~ ')'
			return true
	
	
	constants
		kNeedsOuterBrackets = 0
		kNeedsInnerBrackets = 1
		kNeedsNoBrackets	= 2
	
	
	function DontNeedMoreBrackets (|Message| msg, |int|)
		opt norefcounts
		if msg.first == @rel
			return kNeedsOuterBrackets
			
		|| P = Msg.Parent
		|| Result = kNeedsOuterBrackets
		
		while  P == @bra
			P = P.parent
			Result = kNeedsInnerBrackets
		
		if  P IsBrel "!"
			return kNeedsNoBrackets
		
		if  P isTmp "if" or "while"
			return kNeedsNoBrackets

		return Result


	function C_RenderTyp (fpMsgRenderCpp)
		|| Saveness = DontNeedMoreBrackets(self)
		|| fc		= .first!
		if (Saveness == kNeedsNoBrackets)
			fc.Export(fs)
			return
		
		if (Saveness == kNeedsOuterBrackets)
			fs <~ '('
		WriteDeclBrackets( self, fs )

		if (fc == @Rel)
			fs <~ '('
		if (fc == @arg)
			fc.MiniArgCpp( fs )
		  else
			fc.Export( fs )
		if (fc == @Rel)
			fs <~ ')'
		if (Saveness == kNeedsOuterBrackets)
			fs <~ ')'


	function MsgOKToImprove (|Message| msg, |bool|)
		opt norefcounts
		|| p = msg.parent
		if (p == @prm)
			return (p.AsFunc != Compiler.RefSetRef)
		if p == @rel and msg == p.first
			return false
		Return true


	function NeedsDotForC (|SCDecl| d, |bool|)
		require !d.PointerCount
		if d.type.isstruct
			Return true


	function CDotType (|Message| msg, |SCDecl| Type, |FastStringCpp| fs, |String|)
		opt norefcounts
		require type Is UpgradeableContained

		if msg.IsFirstOfSetRel
			return ""

		|| p = msg.parent
			|| fn = p.obj
			if fn isa scfunction
				if fn is Reffer
					return ""
		
		fs <~ "(("
		.WriteDeclNoName( Type, fs )
		fs <~ ")("
		return "))"


	function WriteDeclNoName (|SCDecl| D, |FastStringCpp| T)
		WriteClass( d.Type, T )
		T <~ ( '*', D.PointerCount )


	function C_RenderNum (fpMsgRenderCpp)
		opt norefcounts
		|| d = .FastDecl
		|| Num = d.ExportPosition|int64|
		if d.IsFloat
			// no point converting it ourselves...
			fs <~ .name
			fs <~ 'f'
		  else
			if num
				fs <~ num
			  else
				fs <~ .name


	function C_RenderBra (fpMsgRenderCpp)
		opt norefcounts
		|| f = .first!
		ifn self in @arg and f == @thg
			fs <~ '('
			f.Export( fs )
			fs <~ ')'


	function C_RenderArel (fpMsgRenderCpp)
		opt norefcounts
		// ("arl" @x "++" )								x++
		.first!.Export( fs )
		|| s = .name.first
		if (s != '&' and '!')
			fs <~ .name


	function C_RenderChr (fpMsgRenderCpp)
		// ("chr" "x")									'x'
		fs <~ '\''
		
		|| b = *.name
		if b < 32 and b != 9 and 10
			fs <~ "\\x"
			fs.appendhex(b)
		  else
			fs <~ .name.EscapeChr // C_Unescape?
		fs <~ '\''
	

	function C_ParamsRender (fpMsgRenderCpp)
		// assume we are an LST
		fs <~ '('
		C_FSListComma( self, fs )
		fs <~ ')'
	

	function C_FSListComma (fpMsgRenderCpp)
		opt norefcounts
		for (item in self)
			fs <~ ", "
			start
			item.Export( fs )


	function C_RenderSStr (fpMsgRenderCpp)
		// so... convert it to an obj
		opt norefcounts
		|| n = .name
		if !n
			fs <~ "JB_LUB[0]"
		  else
			fs <~ "JB_LUB["
			|uint| ID = 0
			if n isa Messageid
				id = n|messageid|.ID + 1
				if ID > stringcorrector.Count|uint| // now what?
					ID = 0
					debugger 
					error (self, "Invalid String ID".internal)
			  else
				error (self, "This isnt even a string ID!!".internal)
				
			fs <~ ID
			fs <~ "]"

	function string.internal (|string|)
		return "InternalError: " + self

	function C_RenderStr (fpMsgRenderCpp)
		fs.AppendCpp(.name)

	
	function Faststring.AppendCppAll (|string| s)
		self <~ '"'
		for (c in s) (i)
			self <~ `\x`
			.appendhex(c, 2)
		self <~ '"'

		
	function Faststring.AppendCpp (|string| s)
		self <~ '"'
		for (c in s) (i)
			if c == '?'
				self <~ `\?`
			  elseif c == '\\'
				self <~ `\\`
			  elseif c == 9
				self <~ `\t`
			  elseif c == 10
				self <~ `\n`
			  elseif c == 13
				self <~ `\r`
			  elseif (c >= 32) and (c != '"') and (c <= 127)
				self <~ c
			  else
				self <~ `\x`
				.appendhex(c, 2)
				if s[i+1].IsHex
					self <~ `""`
		self <~ '"'


	function WriteDeclBrackets (fpMsgRenderCpp)
		fs <~ '('
		
		|| d = .AsDecl // it should be?
			WriteDeclNoName( d, fs )
		  else
			.second!.Export( fs )
		
		fs <~ ')'


	function C_RenderShe (fpMsgRenderCpp)
		fs <~ "//"
		fs <~ .name


	function WriteThgMaybeTypeToo (|Message| msg, |SCDecl| d, |FastStringCpp| fs)
		// ADD MORE ❤️‍🔥FIRE❤️‍🔥
		
		if !d.exportName
			error (msg, errorreceiver.nonfatal) // should never happen
			msg.safe(fs)

		  elseif (d is TypeImprove) and (MsgOKToImprove(msg))
			fs <~ '('
			WriteDeclBrackets( msg, fs )
			d.exportname.safe(fs, d is local)
			fs <~ ')'

		  else
			d.exportname.safe(fs, d is local)

		// plink plink plonk
		// plantasy star is so beautiful.
		// and i was... too. 
		// ❤️‍🔥 ❤️‍🔥 ❤️‍🔥 ❤️‍🔥 ❤️‍🔥 ❤️‍🔥
}

function message.Safe (|faststring| fs)
	.name.safe(fs)


function string.Safe (|faststring| fs, |bool| Local=false)
	if .isascii
		if local and .islower
			fs <~ .first.uppercase
			fs.appendsection(self, 1)
		  else 
			fs <~ self
		return
		
	for s in self
		if s.isupper or s.islower or s.IsInt or s == '_'
			fs <~ s
		  else
			fs <~ 'x'
			fs.appendhex(s)


prototype fpMsgRenderCpp (|Message| self, |FastStringCpp| fs)
class FastStringCpp (FastString)
	|string|		Cpp_Name

	constructor (|string| name = "")
		.cpp_name = name


