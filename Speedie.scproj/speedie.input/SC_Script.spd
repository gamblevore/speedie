

function Compiler.ScriptLoc (|string| f, |file|)
	rz = feedback.TryUseProject(f, true).ScriptLocation.file
	options.output_path = rz


function compiler.UsingScript (|file| f, |file|)
	require options.compile and !f.isdir
	require f.Read(16) starts "#!/"
	
	rz = .ScriptLoc(f)
	
	if error.autoprint == errorflags.printandkeep
		error.AutoPrint = 0
	
	options.scripting = true
	options.silent = true
	options.PrintCompileString = false
	options.OperationCount++
	app.rawargs.remove(0)


function compiler.ScriptRecompile (|file| f, |file|script_build, |bool|)
	if !script_build.exists
		return true
	if (script_build.modified < f.modified)
		return true


function compiler.TryVariousStartModes (|bool|)
	require !spdprocess.parent
	require !app["perry"] and !app["nocompile"]
	|| p = app.rawargs[0]
	require p and !p.argname
			
	|| f   = p.file
	|| it  = compiler.UsingScript(f)$
	|| cwd = app.cwd
	if compiler.ScriptRecompile(f, it)
		compiler.CompileTime
	if it.exists and stderr.ok
		app.cwd = cwd
		app.ChildEnv("OrigScriptPath") = f
		app.turninto(it.path, app.rawargs)
	return true



function string.ContainerName (|string|ext, |string|)
	|| After = .find(ext.PathDir)
		return self[0, after].name


function string.SpdScriptName (|string|)
	// if we are in fizz.scproj... use fizz.
	rz  = .ContainerName(".scproj")
	rz ?= .ContainerName(".uwu")
	rz ?= .name.trimextandpath


function string.ScriptContainer (|string| container = "/tmp/SpeedieApps/",  |string|)
	return container[.hash|int64|.abs.render]


function string.ScriptLocation (|string| container = "/tmp/SpeedieApps/",  |string|)
	|| full = .Resolve ?? self
	|| dest = full.Scriptcontainer(container)
	dest.makeentirepath
	return dest[full.SpdScriptName]

