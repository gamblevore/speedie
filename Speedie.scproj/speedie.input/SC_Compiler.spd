
/*
	Speedie Internals. By Theodore H. Smith...
*/

|SCClass|	TypeFloat64
|SCClass|	Typefloat
|SCClass|	TypeSaveable
|SCClass|	TypeInt
|SCClass|	TypeInt16
|SCClass|	TypeDate
|SCClass|	TypeDuration
|SCClass|	TypeInt64
|SCClass|	TypeuInt16
|SCClass|	TypeuInt
|SCClass|	TypeuInt64
|SCClass|	TypeString
|SCClass|	TypeStringZero
|SCClass|	TypeSyntax
|SCClass|	TypeBool
|SCClass|	TypeChar
|SCClass|	TypeByte
|scclass|	TypeCodePoint
|SCClass|	TypeObject
|SCClass|	TypeWrapper
|SCClass|	TypeArray
|SCClass|	TypeDictionary
|SCClass|	TypeJBClass
|scclass|	TypeTask
|SCClass|	TypeVec4
|SCClass|	TypeVec3
|SCClass|	TypeVec2
|SCClass|	TypeiVec4
|SCClass|	TypeiVec3
|SCClass|	TypeiVec2
|SCDecl|	TypeNil
|SCClass|	TypeVoid_
|SCDecl|	TypeVoid
|scdecl|	TypeVoidPtr
|SCDecl|	TrueBool
|SCDecl|	FalseBool



function Compiler.SpeedieDir (|string| s, |file|)
	return file.Speediedir.Child(s).file
	

module Compiler {
linkage
	cpp_part Comp

|SCNode|					VisibleFuncs = SCNode("Visible")
|SCNode|					Interpreter = SCNode("Interpreter")
|File|						BaseProjectPath  // projectfolder, projfol
|string|					OriginalInputPath
|File|						_BuildFolder
|String?|					_Projects = nil
|String?|					_SpeedieProj = nil
|File|						TempFolder
|SCFile|					InternalFile = SCFile.new(nil, SCImport(nil, true))
|string|					ProjectName
|scimport|					Project
|scfunction|				MainFunc
|message|					ConfMsg
|dictionary|				InsecureWords
|dictionary of scdecl|		Numbers = dictionary()
|[message]| 				TodoList

|Dictionary of SCFile|		Files = nil
|[SCImport]|				ImportedList
|Dictionary of SCImport|	ImportedNames = Dictionary.new


|SCModule|					program = InternalFile.Proj.Mod

|Message|					AppInitCode = (`{cpp_name JB_SP_AppInit, return InitAppGlobals_()}`~parse).WipePos
|Message|					AppInitGlobals = (`{cpp_name JB_SP_AppInitSub_}`~parse).WipePos

|[SCClass]|					ClassList
|[SCModule]|				ModuleList
|[SCFunction]|				FuncList
|[SCFunction]|				ProtoTypes
|[scdecl]|					ActualSyxes

|Dictionary of SCNode|		ExportNames = Dictionary()
|dictionary of dictionary of macro|	AdjDicts = dictionary()

//|SCFunction|				PrivateProperty
|SCFunction|				RefIncr
|SCFunction|				RefIncr2
|SCFunction|				RefDecr
|SCFunction|				RefClear
|SCFunction|				RefDecrMulti
|SCFunction|				RefFreeIfDead
|SCFunction|				RefSafeDecr
|SCFunction|				RefSetRef

//|SCFunction|				NewFunc
|SCFunction|				SwapFunc
|SCFunction|				TernaryFunc
|SCFunction|				fnFastIsa
|SCFunction|				fnFastAs
|SCFunction|				fnAs
|SCFunction|				fnIsa
//|SCFunction|				fnMustbe
//|SCFunction|				fnOpMustBe

|[String]|					SyxArray
|Message|					ExtendList = message()
|Message|					ConstantsList = message()
|[scdecl]|					DeclConstants
|[scclass]|					TasksList
|[Message]|					TasksTodo
|[Message]|					ExtendModuleList
|[Message]|					API_List
|[SCFunction]|				API_Funcs
|Message|					API_Place

|SCModule|					DisamClasses
|SCModule|					DisamModules
|SCModule|					LibInit

|byte|						InPerry
|SCNodeInfo|				InBuiltType
|bool| 						HasFunny
//|bool|					HasMainFunc
|byte|						During // unused but i don't want to remove. it could be useful?
|bool| 						InitedOK = true
|int|						stClasses
|int|						stFuncs
|int|						stLibFuncs
|int|						stReachedFunc
|int|						stReachedClass
|int|						stParseTime
|int64|						stTotalSourceSize
|int|						stTotalFileCount
|int|						stParseTimeTotal
|int|						stTotalASMOps

|int|						CurrStage
|string|					StageName
//|SCFunction|				ClassListInitFunc


constants
	kTimeWidth = 19
	Initialising
	Import
	Transform
	NilChecking
	RefCounts
	Packing
	CppExport


function BuildFolder (|file|)
	opt norefcounts
	|| b = ._BuildFolder!
	if !B.Exists
		B.MakeDir
	return B!




function CompileAll (|int|)
	require .stage("Compilation")
	.InitCompiler
	.ImportAll
	
	ErrorDelayer.ReturnErrors
	require .InitedOK
	
	.CheckIsGoodLibrary

	if .Stage( "Stripping dead code" )
		.DeadStrip
	
	if .Stage( "NilChecking" )
		.Timer( "NilChecking" )
		ArchonPurger.LaunchMothership
	
	require .Stage( "RefCounting" )
	.Timer( "RefCounting" )
	.DoRefAnalysis				// do actual refcounts.

	require .Stage( "String Correction" )
	StringCorrector.CorrectConsts
	
	rz += stderr.ok
	target debug
		if Compiler.InPerry: app.SetASMBreak(false)
	
	if (compiler.InPerry or Options.ModePack)
		.Timer( "Assemble" )
		PackMaker.MakePack	

	target debug
		if Compiler.InPerry: app.SetASMBreak(true)
	
	if !compiler.inperry
		if Options.ModeCpp
			compiler.during = compiler.CppExport
			.Timer( "Export Cpp" )
			Cpp_Export.ExportAllSource

		SCImport.ExportShaders

		if .Stage( "Finished" )
			.TimerSub("Parse", .stParseTimeTotal)
			.Timer( "" )
	rz += stderr.ok


function Projects (|String|)
	return ._projects init file.Speediedir


function SpeedieProj (|String|)
	return ._SpeedieProj init .projects["Speedie.scproj"]


function TestDate
	|int64| day  = 5662310400
	|int64| hour = 235929600
	|int64| min  = 3932160
	|int64| sec  = 64*1024
	if sec  != 1s
	if min  != 1mins
	if hour != 1hours
	if day  != 1days
	

function compiler.ReachedClassCount (|int|)
	opt norefcounts
	for C in compiler.classlist
		RZ += ((C.MyReacher) and c.isobject)


function FastString.AppendWidth (|string| s, |int| Width)
	self <~ s
	self <~ (' ', Width - s.length)


function Reached (|string| s, |bool|)
	flow off
	|| obj = .SoftClass(S)
		return (obj.MyReacher)


function InitTypes (|bool|)
	.InBuiltType = SCNodeInfo.InBuilt
	TypeInt				= compiler["int"]
	TypeInt16			= compiler["int16"]
	TypeInt64			= compiler["int64"]
	TypeuInt			= compiler["uint"]
	TypeuInt16			= compiler["uint16"]
	TypeuInt64			= compiler["uint64"]
	compiler["float16"]
	Typefloat			= compiler["float"] 
	Typefloat64			= compiler["float64"]
	TypeBool			= compiler["bool"]
	TypeString			= compiler["string"]
	TypeStringZero		= compiler["stringzeroterminated"]	
	TypeByte			= compiler["byte"]
	TypeChar			= Compiler["int8"] // int8 would be a better name
	TypeCodePoint		= compiler["codepoint"]
	compiler["charset"]
	Typevec2			= compiler["vec2"]
	Typevec3			= compiler["vec3"]
	Typevec4			= compiler["vec4"]
	Typeivec2			= compiler["ivec2"]
	Typeivec3			= compiler["ivec3"]
	Typeivec4			= compiler["ivec4"]
	Typedate			= compiler["date"]
	TypeDuration		= Compiler["duration"]
	TypeJBClass			= Compiler["classdata"]
	TypeTask			= Compiler["task"]
	TypeObject			= Compiler["object"]
	TypeSyntax			= Compiler["syntax"]
	TypeWrapper			= Compiler["dataobject"]
	TypeDictionary		= Compiler["dictionary"]
	Compiler["faststring"]
	TypeArray			= Compiler["array"]
	TypeSaveable		= Compiler["saveable"]
	if StdErr.ok and .GetTypeVoid
		TypeStringZero!.DowngradeTo = TypeString
		(TypeString! is HasSubClass) // compiler doesn't know about internal-subclasses of string.
		for s in .actualsyxes
			s.type = typesyntax! // sigh
	.InBuiltType = 0

	return StdErr.ok


function InitBasicStuff (|int|) // compiler.init
	opt norefcounts
	rz = stderr.errorcount
	if rz > 0
		stderr.LowerErrorsTo = ErrorSeverity.Warning
		compiler.InitedOK = false
	(Message).Restore
	.ModuleList <~ .program
	
	if !.InitTypes
		Compiler.InitedOK = false
	
	TypeNil   = .AddGlobalConst( "nil",		TypeObject!,	NilState.ActuallyNil )
	TrueBool  = .AddGlobalConst( "true",	TypeBool!,  	NilState.real ||| NilState.trueValue )
	FalseBool = .AddGlobalConst( "false",	TypeBool!,  	NilState.real ||| NilState.falseValue )
	
	TrueBool!.numberconst = 1
	.AddGlobalConstNum( "__time_id__", 		typeint! , 		scimport.recent.timeid )
	.AddGlobalConstNum( "__now__",     		typedate!,		date.now )
	.AddGlobalConstNum( "__modified__",		typedate!,		scimport.recent)
	.AddGlobalConstFloat( "π",				typefloat64!,	"3.1415926535897932")
	.AddGlobalConstFloat( "τ",				typefloat64!,	"6.2831853071795864")
	if stderr.ErrorCount > rz
		compiler.InitedOK = false
		

function LoadTypeTest (|string| s, |scfunction|)
	opt norefcounts
	|| f = TypeObject![ s ]
	if f isa SCFunction
		(f is TypeTest)
		return f


function InitBasicFuncs (|bool|)
	.RefSetRef		= .LoadRefFunc( "JB_SetRef" )
	.RefIncr		= .LoadRefFunc( "JB_Incr" )
	.RefIncr2		= .LoadRefFunc( "JB_Incr2" )
	.RefDecr		= .LoadRefFunc( "JB_Decr" )
	.RefClear		= .LoadRefFunc( "JB_Clear" )
	.RefDecrMulti	= .LoadRefFunc( "JB_DecrMulti" )
	.RefFreeIfDead	= .LoadRefFunc( "JB_FreeIfDead" )
	.RefSafeDecr	= .LoadRefFunc( "JB_SafeDecr" )
	
	.SwapFunc		= .FindFunction( "JB_Swap" )
	.FindFunction( "JB_New" )
	.TernaryFunc	= .FindFunction( "JB_Ternary" )
	.fnAs			= .LoadTypeTest( "operatoras" )
	.fnFastAs		= .LoadTypeTest( "operatorfast_as" )
	.fnFastIsa		= .LoadTypeTest( "operatorfast_isa" )
	.fnIsa			= .LoadTypeTest( "operatorisa" )
	.LoadTypeTest( "mustbe" )
	.LoadTypeTest( "operatormustbe" )
//	.PrivateProperty = .CreateFuncFromSource("function Private () {disabled `This property is private.`}")
	return StdErr.OK


//function CreateFuncFromSource (|String| Src, |SCFunction|)
//	|| msg = src.ParseClean
//		rz = SCFunction.NewFunc( msg, nil, msg )|SCFunction|
//		if rz
//			rz.IsVisible = false


function Library (|file|)
	return .projects.filech("Library")


function Jeebox (|file|)
	return .Projects.FileCh("jeebox.scproj")


function Speedie (|file|)
	return .Projects.FileCh("Speedie.scproj")


function ImportLibs
	SCImport.STDLibTime = true
	SCImport.Importpath( "Library",  .Library,	1,  true )
	SCImport.Importpath( "jeebox",   .Jeebox,	1,  true )
	
	compiler.program.LoadInitFuncSub(.AppInitGlobals, "InitAppGlobals_")
	compiler.program.LoadInitFuncSub(.AppInitCode,    "InitAppCode_")


function ImportProj 
	SCImport.STDLibTime = false
	|| ToUse = (options.SingleFileInput.file, .BaseProjectPath!)(options.SingleFileInput)
	compiler.Project = SCImport.ImportPath( "",  ToUse,  0, false )
	expect compiler.Project ("No user-project found.")
	if !compiler.ConfMsg										// we need a conf, for prefs to work
		|| conf = @tmp+"conf"
		conf["prefs"] = Compiler.ProjectName + ".box.pref"
		.SetConf(conf)


function ConfBool (|string| name, |bool| Default=true, |bool|)
	return .ConfMsg[name].Yes(Default)


function PrepareAST 
	opt norefcounts
	for rz in .ImportedList
		rz.PrepareAll


function ImportAll 
	compiler.during = compiler.Import
	.Timer( "Read" )
	require .Stage( "Finding Files" )
	
	.ImportLibs
	.ImportProj
	SCImport.AllFiles.sort(&scimport.sorter) // or else file-dates can mess things up.
	.PrepareAST
	if options.SelfReplacement
		require VM_Builder.MakeTheVM

	require .Stage( "Import" )
	.Timer( "Import" )
	
	for L in SCImport.AllFiles
		L.Proj.use
		compiler.program.ImportFile( L )

	.Stage( "Initialising Compiler" )

	|| mark = .InitBasicStuff
	require .AddMain(mark) and .InitedOK		
	
	parser.using.Flags = MsgParseFlags.Inserted // noice
	scimport.noproj
	
	sctasks.CollectAll
	for c in .classlist
		C.TryAddToProject
	
	require .Stage( "Processing Delayed Imports" )
	for l in .ExtendList
		SCClass.ProcessExtend( l, true )

	for l in .ExtendModuleList
		SCClass.ProcessExtend( l, false )

	require .initbasicfuncs
	require .Stage( "Creating Class Hierarchy" )
		

	for c in .ClassList
		c.CreateBehaviour	// creates new classes, put before loadsuperclass
	require .Stage( "Loading Supers" )
	
	.LoadSuperClasses
	
	require .Stage( "Sorting" )
	require .SortModulesAndClasses

	require .Stage( "Loading DataTypes" )
	for c in .ClassList
		c.DataTypePostLoad
	
	require .Stage( "Savers and behaviours" )
	for c in .ClassList
		c.LoadSaver

	.DoSavers(0)

	for c in .ClassList
		c.ProcessBehaviours
	
	// ProcessBehaviours generates classes
	
	require stderr.ok
	for c in .ClassList
		c.LoadClassType
		c.ProcessInterfaces
	
	require .Stage( "Creating Class Allocators And Stuff" )
	for c in .ClassList
		c.ContainedTypeLoad

	require .Stage( "Parsing Class Properties" ) // just moved this... used to come after
	for c in .ClassList							 // handling function prototypes.
		c.CollectProperties
		require !stderr.TooMany


	require .SortModulesAndClasses	// need to re-sort array by depth


	for c in .ClassList
		c.CalculateSize
		c.LoadConstructors
	
	require .Stage( "Collecting Constants" )
	.CollectConstants
	(Message).Restore
	scimport.noproj


	// we need decls for this.
	// can we move decls earlier?
	if .API_Place
		|| Proj = .API_Place[@thg]
			for m in .api_list
				sc_api.DelayedClassAPI(m, proj.name)

	.DoSavers(1)


	require .Stage( "Creating Class Exportnames" )
	for c in .ClassList: c.LoadExportName

	
	require .Stage( "Initialising Modules And More" )
	.LoadInitsAndMore


	scimport.noproj
	

	require .Stage( "Handling Function Prototypes" )
	for f in .ProtoTypes
		f.ProtoExportName
		f.FuncParamsLoad
		f.MakeParamsReal
	scfunction.currfunc = nil // set in funcdecls

	require StdErr.OK
	

	.DoSavers(2)

	
	require .Stage( "Parsing Function Parameters" )
	for f in .FuncList
		f.FuncParamsLoad
		f.LoadExportName
		f.StatelessAccumulate
		require !stderr.TooMany
	scfunction.currfunc = nil


	|| fn = .XFunc("JB_App__Quit")
		(fn is killer)
	

	require .Stage( "A Bit More Stuff On Classes" )
	for c in .ClassList
		c.ExportName = c.StructName // minifier...
		c.AfterFuncsDecled //
	  

	require .Stage( "Even More Stuff On Classes" )
	for c in .ClassList
		c.AfterAfterFuncs


	require .Stage( "Collecting Globals" )
	for m in .ModuleList
		m.InitDecls
	for F in SCImport.AllFiles
		F.CollectGlobals Compiler.Program

	scimport.noproj
	(Message).Restore

	for m in .ModuleList
		if m != Compiler.program
			m.BaseDecls
		m.GlobalsFix
		|| c = m.cls
			c.RenameInterfaces

	for m in SortInitOrder(.ModuleList)
		m.DelayedInitFuncTransform
	.PostInitCodeCall
	
	packmaker.LibGlobs.Sort(SimplestFirst)

	.Stage( "Creating Exportnames For Function Prototypes" )
	for f in .FuncList
		f.BeforeTransform
//		f.CheckStuff
	
	.Stage( "Checking Iterators" )
	for c in .ClassList
		c.CheckIterator

	.Timer( "Transform" )
	.Stage( "Transforming Functions" )
	flowcontrol.InputLine(scfunction.NameList, "Funcs: ")
	scfunction.TransformAll

	.Stage( "Building Function Link Map" )
	LinkMap.CollectAll
	scfunction.currfunc = nil
	
		
function LoadSuperClasses
	opt norefcounts
	for c in .ClassList
		c.LoadSuperclass
	for c in .classlist
		c.LoadSuperBehaviours


function LoadInitsAndMore
	opt norefcounts
	for m in .ModuleList
		m.Use
		m.LoadExportName
		m.LoadInitFunc


function PostInitCodeCall
	.program.initvarsarg <~ "initcode_()"~parse


function InitCompiler
	compiler.During = Compiler.Initialising
	require (.Projects.fileCH("Library").mustexist) and stderr.ok
	scoperator.Init
	Cpp_Export.Init

	.files ?= Dictionary.new
	
	////////////////////////////////////////////////
	.TimerOutput <~ "\n"
	StringCorrector.CollectString(@str.msg, true)
	.CurrStage = 1
	SCGlobals.UniqueNum = 100
	////////////////////////////////////////////////
	
	if options.SingleFileInput
		if app.parent
			._BuildFolder = compiler.originalinputpath.ScriptContainer.file
		  else
			._BuildFolder = .BaseProjectPath
	  else
		._BuildFolder = .BaseProjectPath![ "Build" + Compiler.VariantSuffix ]

	|| t = TransCompiler.ProjTmpPath.file!  /// Tmp/Speedie/Proj
	.TempFolder = t
	t.MakeEntirePath
	.GeneratedCppsFolder.makeEntirePath
	
	.CreateRoot
	.CreateDisambiguation
	SCImport.AllFiles <~ .InternalFile



function GeneratedCppsFolder (|File!|)
	// * For most projects (Galaxxies), we build jb.cpp in the temp folder, when built from terminal.
	// * For xcode compiles, we use ExternalCompile, which creates a "Cpp" folder for the jb.cpp to be in
	//   keeping it nice and separate from the builds that we want to use.
		// * we COULD build straight from the temp-folder? I don't see why not...
	// * For self-replacement, well... we use the buildfolder, which is versioned: Build, BuildTest, BuildTest2. 
	
	// the question is, what happens to compiling speedie from xcode? :D
	// I guess it's considered "self-replacement". It's OK.

	if !Options.ExternalCompile
		return .TempFolder!
	if  Options.SelfReplacement
		return .BuildFolder
	return .BaseProjectPath!["Cpp"]



function CreateDisambiguation
	// classes.string, module.string, globals.whatever, etc.
	// make a module...

	.DisamClasses = SCModule.NewContainer("__classes__")
	.DisamModules = SCModule.NewContainer("__modules__")
	.LibInit = SCModule.NewContainer("__libinit__")


function SoftClass (|string| name, |scclass|)
	return compiler[name, SCNodeFindMode.NoErrors]


syntax access (|string| name, |SCNodeFindMode| Mode=0, |SCClass|)
	cpp_part FindClassName
	return compiler.program.FindClass(name, mode)


syntax access (|Message| name, |SCClass|)
	if name
		return compiler.program.FindClassMsg( name, 0 )


function FindClassType (|message| n, |scdecl|)
	opt norefcounts
	|| m = SCModule.curr
	if !m
		m = compiler.program
	|| c = m.findclassmsg(n)
		return c.typenormal


function FindModuleMsg (|Message| where, |SCNodeFindMode| Mode=0, |SCModule|)
	return compiler.program.FindModulemsg(where, Mode)


function CreateRoot
	|| M = .InternalFile.Proj.Mod
	.program = M

	M.ExportName = "JB"
	M.Name = "main"
	M.parent = nil


function LoadRefFunc (|String| name, |SCFunction|)
	opt norefcounts
	|| o = .FindFunction(name)
		(o is Reffer)
		return o

		
function XFunc (|string| name, |message| m=nil, |SCFunction|)
	opt norefcounts
	|| fn = .ExportNames[name]
	if fn isa scfunction
		return fn
	if m
		if fn
			error (m, "This isn't a function")
		  else
			error (m, "Can't find this function")


function FindFunction (|string| Name, |SCFunction|)
	opt norefcounts
	return Compiler.program.findfunction(name)


function SCFunction.IsAllocOrNew (|bool|)
	return self is Constructor


function NewConst (|scdecl| d)
	d.nildeclared = nilstate.real
	d <~ scdeclinfo.Const // ||| scdeclinfo.Global
	.DeclConstants <~ d
	

function DoRefAnalysis
	opt norefcounts
	compiler.during = compiler.RefCounts
	|[message]| list // avoid reading same thing that we alter.
	for f in .FuncList
		if f.IsARealSpdFunc
			list.clear
			f.AnalyseRefs(list)
	scfunction.currfunc = nil


function SCFunction.IsARealSpdFunc (|bool|)
	if (self Is Wrapper) or .IsMacro
		return false
	scfunction.currfunc = self
	return true


function SCFunction.CanBuildConstructor (|bool|)
	return (self is ConOrDes) and .IsARealSpdFunc


function SortModulesAndClasses (|bool|)
	opt norefcounts
	for c in .ClassList
		c.depth = 0
	for c in .ClassList
		c.GetDepth
	
	.ModuleList.sort(ModulesSorter)
	for (m in .modulelist) (i)
		m.ModuleNum = i+1
	.ClassList.sort(ClassSorter)
	return stderr.ok
	

function ClassSorter (SorterComparer of scclass)
	return ModulesSorter(.modul, b.modul)


function ModulesSorter (SorterComparer of scmodule)
	opt norefcounts
	|| ca = .cls
	|| cb = b.cls
	
	if !ca or !cb // a module
		if ca or cb
			return (cb!=nil)
	  else
		|| tt = ca.basetype
		|| D = cb.basetype|int| - tt|int|
			return D
		
		D = cb.depth - ca.depth
		if D
			return D
		
	return .name < b.name


function SCFunction.ReachedDetect
	opt norefcounts
	|| Base = Compiler.VisibleFuncs!
	
	if !.IsVisible	
		require options.MakeInterpreter and (self Isnt Disabled)
		base = Compiler.Interpreter!
	
	.ReachFunc(base)
			

function CheckIsGoodLibrary
	if options.ProjectIsLibrary
		expect compiler.api_place ("No 'main' or 'export_api' found. Create 'export_api' to make this a library, create 'main' to make this an app.")


function InputFile (|String| s, |message| where,  |File|)
	expect !(s contains "../") (where, "Insecure path")
	return .InputFileAlways(s)


function InputFileAlways (|String| s,  |File|)
	opt norefcounts
	// find the "highest" file...
	|| c = SCImport.Curr
		|| found = c.Resources.ValueLower(s)
			return found
			
	|| best = int.max
	for imp in .ImportedList
		if (imp.depth < best)
			|| f = imp.Resources.ValueLower(s)
				rz = f
				best = imp.Depth
	rz ?= Compiler.BaseProjectPath![s]


function AddGlobalConstNum (|string| name, |SCClass| c, |int64| value)
	|| d = .AddGlobalConst(name, c)
	d.IsLookupOnly = @num + value.render
	d.numberconst = value


function AddGlobalConstFloat (|string| name, |SCClass| c, |string| value)
	|| d = .AddGlobalConst(name, c)
	d.IsLookupOnly = @num + value
	d.numberconst = value.float.asint


function AddGlobalConst (|string| name, |SCClass| c, |nilstate| dcl = NilState.real, |SCDecl|)
	opt norefcounts
	|| d = SCDecl(c)
	(D is const)
	(D is NumberConst)
	d.NilDeclared = dcl
	d.IsLookupOnly = "" // it is lookuponly... but what for? "" i guess... whatever.
	d.Name = name
	d.default = @thg + name
	Compiler.program.TryAdd( nil, d, name )
	Return d


function MakeMainFunc (|message|)
	|| f = compiler.MainFunc 
		f.blindcasts = errorseverity.error
		return f.sourcearg
	"Compiling as library!"
	Options.ProjectisLibrary = true
	return .program.CollectStr( "main:\n" ).last				// mainproj.collect


function AddMain (|int| mark, |bool|)
	|| b = (Truebool!, falsebool!)(Options.ProjectisLibrary) 
	|| m = .MakeMainFunc
		|| conf = .ConfMsg
			using conf
			m.first = "app.configure = __AppConfString"~parse
	
	.program.TryAdd( nil, b, "__IS_LIBRARY__" )

	stderr.LowerErrorsTo = 0
	return mark == stderr.errorcount


syntax append (|message| m)
	.InternalFile.LiveAST <~ m


////  ////  ////  ////  ////  ////  ////  ////  ////  ////  ////  ////  ////  

|Date|			LastTime
|string|		LastTimeName
|FastString|	TimerOutput = FastString.new

function AppendCompilerTime (|string| s, |int| Durr)
	compiler.TimerOutput.appendwidth(s, compiler.kTimeWidth)
	if options.color
		compiler.TimerOutput <~ errorcolors.good
	compiler.TimerOutput.appenddurr(Durr)
	if options.color
		compiler.TimerOutput <~ errorcolors.normal
	compiler.TimerOutput <~ '\n'


function TimerSub (|string| s, |int| Durr)
	Durr -= .stParseTime
	.stParseTimeTotal += .stParseTime
	.stParseTime = 0
	.AppendCompilerTime(s, durr)


function Timer (|string| name)
	|| Curr = Date.Now
	if (.LastTime)
		.TimerSub( .LastTimeName, (Curr - .LastTime)|int| )

	if !name and !options.silent
		print .TimerOutput
	
	.LastTimeName = name
	.LastTime = Date.now


function Stage (|string| name, |bool|)
	require StdErr.OK
	if (Options.PrintStages)
		|| space = " "*(Compiler.CurrStage <= 9)
		" ${.CurrStage}$space  :: $name ::  "
	.CurrStage++
	.Stagename = name
	return true


function @transcompiler.PrintProduct (|string| s, |bool| ActualCompile)
	print s
	print " `"
	if ActualCompile
		print compiler.ProductPath
	  else
		print cpp_export.CppProduct
	printline "`"


function IsTest (|bool|)
	return (App.fileName contains "Test")


function VariantSuffix (|string|)
	if (Options.SelfReplacement) and Options.Compile
		return "Test" + Options.Variant
	return Options.Variant


function AddSCProj (|string| Path, |string|)
	|| name = path.name
	|| dot = name.find('.', int.max, 0)
	
	if (dot > 0)
		return path
	return path + ".scproj"


function FindProjNoSuffix (|string| Path, |string|)
	|| spd = path + ".spd"
	if spd.FileExists
		return spd
	return path + ".scproj"


function FindProject (|string| Path, |File|)
	|| p = .BaseProjectPath!.parent
	|| B = .FindProjectSub(p, path)
		return B
	|| A = .FindProjectSub(.projects, path)
		return A

	
helper FindProjectSub (|string| base, |string| Path, |File|)
	if (Path[] == '/')
		return Path.FileThatExists
	
	|| First = Path
	|| SlashPos = Path.find('/')
		First = Path[0, SlashPos]
	first = .AddSCProj(first)
	
	if SlashPos
		First = First + Path[SlashPos...]

	rz = base.FileCh(First)
	if !rz.Exists
		rz = nil
}

