


///// LEAF SORTING
// perhaps not needed right now. We can move some other sort functions in here.

module CodeSorter
	function LeafsFirst
		opt norefcounts
		for f in compiler.FuncList
			f.FindDepths
			if !f.IsSTDLib and !f.IsVisible
				deprecate (f.source, "Unused function")
		compiler.funclist.sort(LeafFunctionsSorter)


	function LeafFunctionsSorter (sortercomparer)
		opt norefcounts
		return a|scfunction|.depth < b|scfunction|.depth
	
	
function scfunction.MarkRecursive (|scfunction| EndAt)
	opt norefcounts
	if (endat == self)
		.alloccode |= kIsRecursive
		return
	
	|| f = self
	while f
		f.alloccode |= kIsRecursive
		f = f.DepthFinder
		if f == EndAt
			exit


function scfunction.FindDepths (|int|)
	flow off
	// Orders the functions in terms of which ones need others.
	// So we can process leaf functions first. Maybe easier to optimise
	if .depth
		return .depth
	
	rz = 1
	for f in .LinkTo
		if f isa SCFunction
			if f.DepthFinder
				.MarkRecursive(f)
			  else
				f.DepthFinder = self
				|| D = f.FindDepths+1
				(rz max= D)
				f.DepthFinder = nil
	.depth = rz

	
