

function compiler.PrintCompileTime (|date| durr) 	
	if StdErr.OK and !options.silent
		print "\nStages:            "
		print Compiler.CurrStage
		print "\nClasses:           "
		compiler.ReachedClassCount.render.printlinecolor(errorcolors.good)
		print   "Funcs:             "
		"${compiler.reachedfuncs} / ${compiler.funclist.length}".printlinecolor(errorcolors.good)

		print   "Total elapsed:	   "
		(durr.renderdurr+"s").printlinecolor(errorcolors.good)

		Print   "Input source code: "
		"${Compiler.stTotalSourceSize/1024} KB\n".printcolor(errorcolors.good)


function compiler.PrintStats
	require options.argstats
	|| fs = faststring()
	fs <~ "FuncStats: "
	for i in 12
		|| c = scfunction.FuncStats[i]
			if i
				fs <~ ", "
			fs <~ "$i:$c"
	printline fs


function compiler.PrintCompileErrors
	if Options.Warnings
		require stderr.hasanything
	  else
		require StdErr.hasproblems
	.PrePrintErrors

	if (SCFunction.DisabledPoints)
		"Disabled functions called ${scfunction.DisabledPoints}x!".printlinecolor(errorcolors.error)
	
	|| MinSev = (ErrorSeverity.warning, ErrorSeverity.problem)(Options.Warnings) 
	.PrintErrors(MinSev)
	stderr.clear


function compiler.VariousSelfTests
	|| t = Jeebox.test // bug if put into compiler.run!. Wierd!
	if (options.perry < 2) and !options.scripting and !interact.shouldenter
		if (Cpp_Export.FindGlobalsCpp*12).CompressTest(false) and options.PrintLibraries
			"Compression: OK"


function compiler.CompileTime 
	if options.SelfReplacement // avoid failing needlessly
		.FileSanityTests // sometimes this fails due to lack of permissions in xcode...
						 // xcode gives lower perms to it's builder scripts, strangely.
	.VariousSelfTests
	SC_Saver.TestSaver~quietly

	|| elapsed = .CompileAll~timeit
	.PrintCompileTime(elapsed)

	require stderr.ok

	
	|| caninstall = true
	if Options.SelfReplacement
		if platform.release
			caninstall = elapsed < 2.5s
			check (caninstall) ("Not installing as compile duration was over 2.5s. Perhaps this speedie has a bug leading to excessively slow compiles?")
	TransCompiler.ExportAndInstall(caninstall)

