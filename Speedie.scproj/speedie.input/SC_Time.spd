

function compiler.PrintCompileTime (|date| durr) 	
	require StdErr.OK and !options.silent
	|| fs = faststring()
	fs.normal  "Stages",		fs <~ Compiler.CurrStage
	fs.normal  "Classes",		fs <~ compiler.ReachedClassCount
	fs.normal  "Funcs",			fs <~ "${compiler.reachedfuncs} / ${compiler.funclist.length}"
	fs.normal  "Strings"
	|| avg = stringcorrector.TotalSize|float| / stringcorrector.list.length|float|
	fs <~ "${stringcorrector.list.length} / "	
	stringcorrector.TotalSize.strsize(fs)
	fs <~ " // (avg $avg)"
	fs.normal  "Total Elapsed",	durr.renderdurr(fs), fs <~ "s"
	fs.normal  "Input Src",		Compiler.stTotalSourceSize.strsize(fs)
	fs <~ errorcolors.normal
	printline fs


function compiler.PrintStats 
	require options.argstats
	|| fs = faststring()
	fs <~ "FuncStats: "
	for i in 12
		|| c = scfunction.FuncStats[i]
			if i
				fs <~ ", "
			fs <~ "$i:$c"
	printline fs


function compiler.PrintCompileErrors
	if Options.Warnings
		require stderr.hasanything
	  else
		require StdErr.hasproblems
	stderr.PrePrintErrors

	if (SCFunction.DisabledPoints)
		"Disabled functions called ${scfunction.DisabledPoints}x!".printlinecolor(errorcolors.error)
	
	|| MinSev = (ErrorSeverity.warning, ErrorSeverity.problem)(Options.Warnings) 
	.PrintErrors(MinSev)
	stderr.clear


function compiler.VariousSelfTests
	|| t = Jeebox.test // bug if put into compiler.run!. Wierd!
	if (options.perryoutput < 2) and !options.scripting and !SpdProcess.Parent
		if (Cpp_Export.FindGlobalsCpp*12).CompressTest(false) and options.PrintLibraries
			"Compression: OK"


function compiler.CompileTime 
	if !options.output_path and options.UseScriptLoc
		.ScriptLoc(compiler.originalinputpath)		
	
	if options.SelfReplacement // avoid failing needlessly
		.FileSanityTests // sometimes this fails due to lack of permissions in xcode...
						 // xcode gives lower perms to it's builder scripts, strangely.
	.VariousSelfTests
	SC_Saver.TestSaver~quietly

	|| elapsed = .CompileAll~timeit
	.PrintCompileTime(elapsed)

	require stderr.ok

	
	|| caninstall = true
	if Options.SelfReplacement
		if platform.release
			caninstall = elapsed < 2.5s
			check (caninstall) ("Not installing as compile duration was over 2.5s. Perhaps this speedie has a bug leading to excessively slow compiles?")
	TransCompiler.ExportAndInstall(caninstall)

