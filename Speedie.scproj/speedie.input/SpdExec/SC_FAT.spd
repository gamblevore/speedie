

struct FatASM 
	linkage
		cpp_part FAT
	|ASMReg| 			Info
	|message--| 		Msg
	|uint64| 			_Const
	|uint[7]|			R
	|uint|				ASMIndex	
	|uint16| 			BlockNum
	|byte| 				Outputs
	|byte| 				Nothing
	|uint16|			RefCount
	|byte|				Op
	|byte|				JumpReg
	
	
	function BaseOp (|int|)
		rz = .op
		if rz == asm.FNCX3
/			rz = asm.FNCX
		  elseif rz == asm.FNC3
/			rz = asm.FNC

	
	function Form (|µform|)
		|| i = instruction.typelist[.op]
			return i.Sizes
		debugger
		return instruction.typelist[0]!.Sizes

	operator mul (|asmreg| info, |asmreg|)
		cpp_part AsReg
		
		info.FatIndex = .index
		.info = info
		return info
	
	function Const (|int64|)
		opt norefcounts
		return ._const
		
	function Const (assigns:|int64|)
		opt norefcounts
		._const = value

	function F32 (assigns:|float|)
		if !value: value = 0			// rationalise
		.const = value.AsInt

	function F64 (assigns:|Float64|)
		if !value: value = 0			// rationalise
		.const = value.AsInt
		
	function F32 (|float|)
		rz = ((&._const)|&float|)[]
		if !rz: rz = 0					// rationalise

	function F64 (|Float64|)
		rz = ((&._const)|&f64|)[]
		if !rz: rz = 0					// rationalise
		
	setter Debug (|message|)
		.msg = Value

	function has (|int| a, |int| b, |bool|)
		|| out = .r0
		return out == a or b
	
	function ResetOutput (|int| reg, |bool|)
		|| o = .Outputs
		if (o&1) and .r[0]
			.r[0] = reg
			return true
		if (o&2) and .r[1]
			.r[1] = reg
			return true


	function SmartFatness (|int| reg, |maybebool|)
		opt norefcounts
		|| f = .form
		for i in f.Count
			if .Prm(i) == reg
				|| p = f.Params[i]
				if p.IsReg
					return (p is Output)|maybebool|


	function Index (|uint|)
		opt inline
		return 1 + self - asmstate.sh.funcstart

	function uint.FAT (|&FATASM|)
		opt inline
		return asmstate.sh.funcstart + (self - 1)
	
	function FileNum (|int|)
		return .msg.filenum & 0x4095
	
	function BytePos (|int|)
		return .msg.position
	
	operator isa (|int| m, |bool|)
		nil checker
		if self
			return m == .op
	
	syntax is (|asmreg| flags, |bool|)
		if self
			return (.info & flags)|uint64| != 0

	syntax is (|asmreg| flags, assigns:|bool|)
		if value
			(.info as= flags)
		  else
			(.info asnt= flags)
		
	function OptNop (|bool|)
		.op = asm.noop
		.JumpReg = 0
		return true
	
	function LinkedFrom (|fatasm| New)
		.refcount++

	function asmreg.treg (|uint|)
		return .reg ||| ((self is Temp)<<31)

	function RegInput (|int| a, assigns:|asmreg| info)
		cpp_part Prm
		opt inline
		.r[a] = info.treg
//		|| Src = info.fat
//			Src.LinkedFrom(self) // what?
	
	
	function Dest (|uint| a, |asmreg| info)
		opt inline
		.reginput(A) = info
	
	
	function File (|string|)
		opt norefcounts
		|| s = scimport.allfiles[.filenum]
			return s
	
	Syntax Expect (|string| Error)
		|| f = .file|file--|
		|| err = Error(nil, error, ErrorSeverity.Error, f)
		err.position = .BytePos
		nil|errorreceiver| <~ err
		.DebugPrint
	
	function NumInput (|int| a, assigns:|int| v)
		.r[a] = v

	function JumpInput (|int| a, assigns:|int| v)
		.r[a] = v
		.jumpreg = a + 1
		ASMState.sh.BasicBlock++
	
	function Jump (|int|)
		|| j = .jumpreg
			return .r[j]

	function JumpTo (|&fatasm|)
		|| j = .jumpreg
			return self + 1 + .r[j-1]
	
	
	function JumpTo (assigns:|&fatasm|)
		|| j = .jumpreg
			|| D = (value - self) - 1
			check (D != 0 and -1) (self, "bad jump")
			.r[j-1] = D
		  else
			debugger

	function r0 (|uint|): return .r[0]
	function r0 (assigns:|asmreg|): .r[0] = value.treg
	function r1 (|uint|): return .r[1]
	function r1 (assigns:|uint|): .r[1] = value
	function r2 (|uint|): return .r[2]
	function r2 (assigns:|uint|): .r[2] = value
	function r3 (|uint|): return .r[3]
	function r3 (assigns:|uint|): .r[3] = value
	function r4 (|uint|): return .r[4]
	function r4 (assigns:|uint|): .r[4] = value

	function Swap (|int| a, |int| b)
		swap (.r[a-1]) (.r[b-1])
	
	helper Step (|int| dir, |&FatASM|)
		real self
		|| b = .blocknum
		while
			self += dir
			if .op != asm.noop
				return (self, nil)(b == .BlockNum)
		
	function Prev (|&FatASM|)
		opt inline
		return .step(-1)
		
	function Next (|&FatASM|)
		opt inline
		return .step(1)

	function match3_2 (|int| reg, |bool|)
		// prefers the value to be at 3.
		if reg == .r1
			.swap(2, 3)
			return true
		return reg == .r2
	
	
	function Prm (|int| i, |int|)
		return .r[i] & ~(1<<31) // remove tmpbit.
		
	function CheckHasOutput 
		opt inline, opt norefcounts
		target debug
			// why not use .outputs?
			if !.Prm(0)
				|| P = .form.params[0]
				if  p is output//  and  p isnt optional 
					error (self, "Missing ASM output")
	
		
	function µRenderInto (|!&asm| Where, |!&asm| After, |!&asm|)
		opt inline
		return (ASM.encoders[.op]!)(self, where, after)

	
	function Renda (|FastString| fs)
		opt norefcounts
		if (self isa asm.noop)  and  (.msg.func == @sheb)
			.RendaMsg(fs, 0)
			return 
		
		|| M = fs.mark
		|| op = instruction.typelist[.baseop]!
		fs <~ op.name
		fs <~ ':'
		fs <~ (' ', 5-op.name.length)
		|| GotAny = ""
		|| Remain = 24
		|| f = op.sizes
		/// 
		
		for i in f.count
			|| bs = f[i].BitSize
			if bs and (remain > 0)
				fs <~ GotAny
				GotAny = ", "
				|int| v = .r[i]
				if f[i].isreg
					if v & (1<<31)
						v &=~ (1<<31)
						fs <~ 't'
					  else
						fs <~ 'r'
				fs <~ v
				remain += bs
			if .op == asm.knst
				exit
		///
		
		if (self isa asm.knst or asm.knsr)
			.RendaKnst(fs, fs.mark-m)
			fs <~ ' '
		.RendaMsg(fs, fs.mark-m)
	
	
	function TmpRender (|faststring| fs, |message| msg)
		|| arg = msg.Find(@arg)
		if !arg
			msg.Render(fs)
			return 
		
		|| c = msg.copy
		while arg
			arg.Remove
			arg = c.find(@arg)
		c.Render(fs)
			
	
	function RendaMsg (|faststring| fs, |int| sofar)
		opt norefcounts
		|| msg = .msg
		if msg == @sheb
			msg.render(fs)
			return
		fs <~ (' ', 47-sofar)
		require msg != @arg

		fs <~ "/*" // reuse /* string lol.
		fs <~ " "
		if msg == @prm
			msg = msg.parent
		  else
			while msg == @num or @thg or @opp or @bra or @char
				loop !(msg in @prm)
				msg = msg.parent
		if (msg == @tmp)
			.TmpRender(fs, msg)
		  else
			msg.render(fs)
		
		fs <~ " " // reuse "*/" string
		fs <~ "*/"
	
	
	function RendaKnst (|faststring| fs, |int| sofar)
		|| ty = .info.µType
		fs <~ (' ', 26-sofar)
		fs <~ "/# "
		if ty.IsInt
			fs <~ .const|int64|
		  else
			|float64| num
			if ty.ByteCount == 4
				num = .F32
			  else
				num = .f64
			fs.AppendDouble(num, 9, true, true)
			if ty.ByteCount == 4
				fs <~ "f"
			  else
				fs <~ "d"
		fs <~ " #/"


	function DebugPrint 
		visible
		if asm.NoisyASM >= 3
			|| fs = faststring()
			.Renda(fs)
			printline fs

 
