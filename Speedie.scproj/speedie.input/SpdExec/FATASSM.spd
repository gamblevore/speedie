

struct FatASM 
	linkage
		cpp_part FAT
		sort_properties true
	
	// phi-nodes can actually be done simply by linking one fat to the prev
	|byte|				InputFats
	|byte|				InputRegs
	|byte| 				Outputs
	|byte|				_Op
	|byte|				JumpReg
	|byte| 				BranchDepth
	|byte| 				LoopDepth
	|ASMReg| 			Info
	|message--| 		Msg
	|uint64| 			_Const
	|uint[6]|			Prms
	|uint16| 			BasicBlock
	|int16|				µRefCount		// could make this a byte and stick at 255.
	

	function ASMIndex (|uint16|)
		return .basicblock

	setter ASMIndex (|uint16|)
		.basicblock = value
		
	
	function Op (|byte|)
		return ._op
	
	setter SetOp (|byte| value)
		opt norefcounts
		.InputFats = 0
		.InputRegs = 0
		._op = value
		.jumpreg = 0
		.outputs = 0
		.BranchDepth = 0
		.LoopDepth = 0
		.info = nil // ??? nothing needs this?
		// leave msg, and _const. Other things may need them still.
		for i in –count .prms
			.prms[i] = 0
		.BasicBlock = 0
		.µrefcount = 0
		
	function Input (|int| i, |&fatasm|)
		|| r = (.prms[i] << 1) >> 16
			return r.fat
	
	function IsRead (|bool|)
		|| op = .op
		return op >= asm.RD1U and op <= asm.RD16 
	
	function IsFunc (|bool|)
		|| b = .baseop
		return b == asm.fncx or asm.fnc
	
	function IsUnused (|bool|)
		return .µRefCount <= 0
	
	function BaseOp (|int|)
		rz = .op
		if rz == asm.FNCX3
			rz = asm.FNCX
		  elseif rz == asm.FNC3
			rz = asm.FNC

	
	function Form (|µform|)
		|| i = instruction.typelist[.op]
			return i.Sizes
		debugger
		return instruction.typelist[0]!.Sizes


	function Vectorise (|asmreg| Dest, |asm| VOpp,  |asmreg|)
		dest.FatIndex = .index
		.info = dest
		if dest.IsVec
			._op = VOpp
		return dest
	
	
	function VectoriseSmall (|asmreg| Dest, |asm| VOpp, |asm| SOpp, |asmreg|)
		dest.FatIndex = .index
		.info = dest
		if dest.IsVec
			if !vopp // oof?
			._op = VOpp
		  elseif dest.small
			._op = sopp
		return dest
	
	
//	function Small (|asmreg| Dest, |asm| SOpp, |asmreg|)
//		dest.FatIndex = .index
//		.info = dest
//		if dest.small
///			._op = sopp
//		return dest
	

	operator mul (|asmreg| info, |asmreg|)
		cpp_part AsReg
		
		info.FatIndex = .index
		.info = info
		return info
	
	function Const (|int64|)
		opt norefcounts
		return ._const
		
	function Const (assigns:|int64|)
		opt norefcounts
		._const = value

//	function F32 (assigns:|float|)
//		if !value: value = 0			// rationalise
//		.const = value.AsInt
//
//	function F64 (assigns:|Float64|)
//		if !value: value = 0			// rationalise
//		.const = value.AsInt
		
	function F32 (|float|)
		|&uint64| A = &._Const 
		rz = (A|&float|)[]
		if !rz: rz = 0					// rationalise

	function F64 (|Float64|)
		rz = ((&._const)|&f64|)[]
		if !rz: rz = 0					// rationalise
		
	setter Debug (|message|)
		.msg = Value
	
//	function ResetFunctionCall (|int| reg, |bool|)
//		// we will be the last instruction
//		// so how can we reset this? I thinkonly by comparing the
//		// state's vdecls. Temps will disappear.
//		|| v = Assembler.sh.VDecls
//		if v == reg and v == (.r0 - 1)
//			.Outputs = v|int|.log2
//			.r0 = v
//			return true


/*
	function SmartFatness (|int| reg, |maybebool|)
		opt norefcounts
		|| f = .form
		for i in f.Count
			if .Reg(i) == reg
				|| p = f.Params[i]
				if p.IsReg
					return (p is Output)|maybebool|
*/

	syntax equals (|int| ID, |bool| aware, |bool|)
		(aware), return .id == ID
	
	function ID (|uint|)
		return self - Assembler.sh.start

	function Index (|uint|)
		opt inline
		return 1 + self - Assembler.sh.funcstart

	function SCdecl.Fat (|&fatasm|)
		|| K = .au.FatConstIndex|uint|
		return K.fat

	function uint.FAT (|&FATASM|)
		opt inline
		if self
			return Assembler.sh.funcstart + (self - 1)
	
	function FileNum (|int|)
		return .msg.filenum & 0x4095
	
	function BytePos (|int|)
		return .msg.position
	
	operator isa (|int| m, |bool|)
		nil checker
		if self
			return m == .op
	
	syntax is (|asmreg| flags, |bool|)
		if self
			return (.info & flags)|uint64| != 0

	syntax is (|asmreg| flags, assigns:|bool|)
		if value
			(.info as= flags)
		  else
			(.info asnt= flags)
			
	function asmreg.treg (|uint|)
		return .reg ||| ((self is Temp)<<31)

	function CopyFrom (|fatasm| D)
		(self)[0] = (d)[0]
		for i.FatInputs in self
			if i 
				i.µRefCount++
	
	function RegInput (|int| a, assigns:|asmreg| Input)
		cpp_part Prm
		opt inline
		
		|| i = input.FatIndex
		.prms[a] = input.treg ||| (i << 15)
		.InputRegs |= 1<<a
		if i
			|| src = i.fat // we refcount dests too. shouldn't!
			if src != self
				.InputFats |= 1<<a
				if src
					src.µRefCount++
			  else
				debugger // what?
	
	
	function Dest (|uint| a, |asmreg| info)
		opt inline
		.prms[a] = info.treg
		.Outputs |= 1<<a

	
//	function Altered (|uint|) 
//		for prm.regoutputs in self
//			rz |= 1<<(prm&31)
	
	
	function File (|string|)
		opt norefcounts
		|| s = scimport.allfiles[.filenum]
			return s
	
	syntax Using (|&fatasm|)
		rz = Assembler.sh.curr
		Assembler.sh._curr = self
		
	syntax UsingComplete (|&nil| idk)
		Assembler.sh._curr = self, (idk)
	
	
	Syntax Expect (|string| Error)
		|| f = .file|file--|
		|| err = Error(nil, error, ErrorSeverity.Error, f)
		err.position = .BytePos
		nil|errorreceiver| <~ err
		.DebugPrint
	
	function NumInput (|int| a, assigns:|int| v)
		.prms[a] = v

	function JumpInput (|int| a, assigns:|int| v)
		.prms[a] = v
		.JumpReg = a + 1
		.µRefCount = 1 max .µRefCount // control flow is always used?
		Assembler.sh.BasicBlock++
		
	function Jump (|int|)
		|| j = .JumpReg
			return .prms[j-1]


	function JumpTo (|&fatasm|)
		|| j = .JumpReg
			return self + 1 + .prms[j-1]|int|
	
	
	function JumpTo (|&fatasm| value, |&Assembler| A)
		cpp_Part JumpToSet
		|| j = .jumpreg
			|| D = (value - self) - 1
			check (D != -1) (self, "bad jump")
			.prms[j-1] = D
			if d == 0
				a.nop(self)
		  else
			j = .id
			debugger

	function r0 (|uint|): return .prms[0]
	function r1 (|uint|): return .prms[1]
	function r2 (|uint|): return .prms[2]
	function r3 (|uint|): return .prms[3]

	function r0 (assigns:|uint|): .prms[0] = value
	function r1 (assigns:|uint|): .prms[1] = value
	function r2 (assigns:|uint|): .prms[2] = value
	function r3 (assigns:|uint|): .prms[3] = value

	function r0 (assigns:|asmreg|): .prms[0] = value.treg
	function r1 (assigns:|asmreg|): .prms[1] = value.treg
	function r2 (assigns:|asmreg|): .prms[2] = value.treg
	function r3 (assigns:|asmreg|): .prms[3] = value.treg


//	function Swap (|int| a, |int| b)
//		swap (.prms[a-1]) (.prms[b-1])
// doesnt take fatinputs or other inputs into account anyhow
	
	helper Step (|int| dir, |&FatASM|)
		real self
		|| b = .BasicBlock
		while
			self += dir
			if .op != asm.noop
				return (self, nil)(b == .BasicBlock)
		
	function Prev (|&FatASM|)
		opt inline
		return .step(-1)
		
	function Next (|&FatASM|)
		opt inline
		return .step(1)

	function OutReg (|uint|)
		return .prms[0] & 31

	function Reg (|int| i, |int|)
		return .prms[i] & 31 // remove tmpbit + fatindex
		
	function CheckHasOutput 
		opt inline
		target debug {
		opt norefcounts
		for (b.bits in .outputs|int|) (i)
			if !.reg(i)
				error (self, "Missing ASM output")
		}
		
	function µRenderInto (|!&asm| Where, |!&asm| After, |!&asm|)
		opt inline
		return (ASM.encoders[.op]!)(self, where, after)


	function FuncPrms (|scfunction| fn, |uint64|)
		if self isa asm.tail
			return .r1 >> 5

		|| R = .r2|uint64| ||| (.r3|uint64| << 32)
		if !fn.hascversion
			return R >> 5
		return R >> 12

	
	function PrmCollectCounterPart (|FastString| fs)
		opt norefcounts
		|| fn = .msg.Obj as SCFunction 		#require		
		|| regs = .FuncPrms(fn)
		|| n = fn.Args.Length

		fs <~ "("

		while n
			fs <~ 'r'
			if fn.HasCVersion
				fs <~ (regs>>((--n)*5))&31
			  else
				fs <~ regs & 31
				regs >>= 5
				n--
			if n > 0
				fs <~ ", "
		
		
		fs <~ ")"
	
	
	function Renda (|FastString| fs, |bool| Simpler=false)
		opt norefcounts
		|| obj = .msg.obj
		
		if (!.exists)  and  (.msg.func == @sheb)
			.RendaMsg(fs, 0)
			return 
		
		|| M = fs.mark
		|| op = instruction.typelist[.baseop]!
		|| NL = op.name.length
		if .baseop == asm.noop
			NL = -1
		  else
			fs <~ op.name
			fs <~ ':'
		fs <~ (' ', 5-op.name.length)
		|| GotAny = ""
		|| Remain = 24
		|| f = op.sizes
		/// 
		
		for i in f.count
			|| bs = f[i].BitSize
			if bs and (remain > 0)
				fs <~ GotAny
				GotAny = ", "
				|int| v = .prms[i]
				if (.IsFunc and (i == 2 or 3)) or (self isa asm.tail and i == 1)
					if i != 3
						.PrmCollectCounterPart(fs)
					  else
						fs.Length-=2
				  else
					if f[i].isreg
						fs <~ ('r' + v|uint| >> 30)|byte|
						v &= 31
					fs <~ v
				remain += bs
			if .op == asm.knst
				exit
		
		if (self isa asm.knst or asm.knsr or asm.gtab)
			.RendaKnst(fs, fs.mark-m)
			fs <~ ' '
		
		|| n = fs.mark - m
		if n <= 2
			fs.length--
			fs <~ "..."
			n += 2
		if !Simpler
			.RendaMsg(fs, n)
	

	function Exists (|bool|)
		return .op != asm.noop

	
	function TmpRender (|faststring| fs, |message| msg)
		|| arg = msg.Find(@arg)
		if !arg
			msg.Render(fs)
			return 
		
		|| c = msg.copy
		arg = c.find(@arg)!
		while arg
			arg.Remove
			arg = c.find(@arg)
		fs <~ c.Render.replaceall( "\n", ": " )
			
	
	function RendaMsg (|faststring| fs, |int| sofar)
		opt norefcounts
		|| msg = .msg
		if msg == @sheb
			msg.render(fs)
			return
		fs <~ (' ', 47-sofar)
		require msg != @arg

		fs <~ "/*" // reuse /* string lol.
		fs <~ " "
		if msg == @prm
			msg = msg.parent
		  else
			while msg == @num or @thg or @opp or @bra or @char
				loop !(msg in @prm)
				msg = msg.parent
		if (msg == @tmp)
			.TmpRender(fs, msg)
		  else
			msg.render(fs)
		
		fs <~ " " // reuse "*/" string
		fs <~ "*/"
	
	
	function RendaKnst (|faststring| fs, |int| sofar)
		|| ty = .info.µType
		fs <~ (' ', 26-sofar)
		fs <~ "/# "
		if ty.IsInt
			fs <~ .const|int64|
		  else
			|float64| num
			if ty.ByteCount == 4
				num = .F32
			  else
				num = .f64
			fs.AppendDouble(num, 9, true)
			if ty.ByteCount == 4
				fs <~ "f"
			  else
				fs <~ "d"
		fs <~ " #/"


	function JumpImprove (|bool|)
		|| D0 = .jumpto!
		|| D = D0
		|| n = 0
		while d isa asm.jump
			d = d.JumpTo!
			expect (++n < 30) (.msg)
		
		if  (d isa asm.ret or (d isa asm.tail and options.optimise >= 4))  and  (self isa asm.jump)
			.CopyFrom(d) // always jumping into an exit.
			return true
		
		if D != D0 // faster jump
			.JumpTo(D, Assembler.sh)
			return true
		
		
//	function IsReturn (|bool|)
//		|| o = .op|asm|
//		if o == asm.ret or asm.TAIL
//			return true
	

	function IsFinisher (|bool|)
		|| o = .op|asm|
		if o == asm.ret or asm.trap or asm.TAIL
			return true
		if o == asm.jump
			return .jump < 0


	function DebugPrint 
		visible
		nil self
		if !self
			"(nil)"
			return
		if asm.NoisyASM >= 3
			|| fs = faststring()
			.Renda(fs)
			printline fs


	iterator RegInputs
		|| ir = .InputRegs
		|| i = 0
		while (i < –count .prms)
			ifn ir & (1 << i)
				i++
				continue
			yield i
			i++
			
	iterator FatInputs
		|| ir = .InputFats
		|| i = 0
		while (i < –count .prms)
			ifn ir & (1 << i)
				continue
			yield (.input(i))
			i++
			
	iterator RegOutputs
		|| oo = .outputs|int|
		while oo
			|| i = oo.lowestbit
			oo &=~ i
			yield .prms[i.log2]

 
