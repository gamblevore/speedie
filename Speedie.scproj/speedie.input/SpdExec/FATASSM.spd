

struct FatASM 
	linkage
		cpp_part FAT
		sort_properties true
	
	// phi-nodes can actually be done simply by linking one fat to the prev
	|byte|				InputFats
	|byte|				InputRegs
	|byte| 				_Outputs
	|byte|				_Op
	|byte|				JumpReg
//	|byte| 				BranchDepth
//	|byte| 				LoopDepth
	|ASMReg| 			Info
	|message--| 		Msg
	|uint64| 			_Const
	|ASMParam[6]|		Prms
	|uint16| 			BasicBlock
	|int16|				µRefCount		// could make this a byte and stick at 255.
	

	function ASMIndex (|uint16|)
		return .basicblock

	setter ASMIndex (|uint16|)
		.basicblock = value
		
	
	function Op (|byte|)
		return ._op
	
	setter SetOp (|byte| value)
		opt norefcounts
		.InputFats = 0
		.InputRegs = 0
		.jumpreg = 0
		._Outputs = 0
//		.BranchDepth = 0
//		.LoopDepth = 0
		.info = nil // ??? nothing needs this?
		// leave msg, and _const. Other things may need them still.
		for i in –count .prms
			.prms[i] = 0
		.BasicBlock = 0
		.µrefcount = 0
		._op = value
		
	function InputFat (|int| i, |&fatasm|)
		|| r = (.prms[i] << 1) >> 16
			return r.fat
	
	function IsRead (|bool|)
		|| op = .op
		return op >= asm.RD1U and op <= asm.RD16 
	
	function IsFunc (|bool|)
		|| b = .baseop
		return b == asm.fncx or asm.fnc
	
	function IsUnused (|bool|)
		return .µRefCount <= 0 and self!=asm.NOOP
	
	function BaseOp (|int|)
		rz = .op
		if rz == asm.FNCX3
			rz = asm.FNCX
		  elseif rz == asm.FNC3
			rz = asm.FNC

	
	function Form (|µform|)
		|| i = instruction.typelist[.op]
			return i.Sizes
		debugger
		return instruction.typelist[0]!.Sizes


	function Vectorise (|asmreg| Dest, |asm| VOpp,  |asmreg|)
		dest.FatIndex = .index
		.info = dest
		if dest.IsVec
			._op = VOpp
		return dest
	
	
	function VectoriseSmall (|asmreg| Dest, |asm| VOpp, |asm| SOpp, |asmreg|)
		dest.FatIndex = .index
		.info = dest
		if dest.IsVec
			if !vopp // oof?
			._op = VOpp
		  elseif dest.small
			._op = sopp
		return dest
	

	operator mul (|asmreg| info, |asmreg|)
		cpp_part AsReg
		
		|| info2 = info.FatIndex(.index)
		.info = info2
		if info
			return info2
		return info
	
	function Const (|int64|)
		opt norefcounts
		return ._const
		
	function Const (assigns:|int64|)
		opt norefcounts
		._const = value
		
	function F32 (|float|)
		|&uint64| A = &._Const 
		rz = (A|&float|)[]
		if !rz: rz = 0					// rationalise

	function F64 (|Float64|)
		rz = ((&._const)|&f64|)[]
		if !rz: rz = 0					// rationalise
		
	setter Debug (|message|)
		.msg = Value
	

	syntax equals (|int| ID, |bool| aware, |bool|)
		(aware), return .id == ID
	
	function ID (|uint|)
		return self - Assembler.sh.start

	function Index (|uint|)
		opt inline
		return 1 + self - Assembler.sh.funcstart

	function uint.FAT (|&FATASM|)
		opt inline
		if self
			return Assembler.sh.funcstart + (self - 1)
	
	function FileNum (|int|)
		return .msg.filenum & 0x4095
	
	function BytePos (|int|)
		return .msg.position
	
	operator isa (|asm| m, |bool|)
		nil checker
		if self
			return m == .op
			
	syntax is (|asmreg| flags, |bool|)
		nil checker
		if self
			return (.info & flags)|uint64| != 0

	syntax is (|asmreg| flags, assigns:|bool|)
		if value
			(.info as= flags)
		  else
			(.info asnt= flags)


	function CopyFrom (|fatasm| D, |bool|)
		(self)[0] = (d)[0]
		for i.FatInputs in self
			if i 
				i.µRefCount++
	

	function assembler.ReDest (|fatasm| f, |asmreg| info, |asmreg|)
		|int| o = f._Outputs
		target debug: if !o.IsPow2
		return .ReDest(f, info, o.log2)
		
		
	function assembler.ReDest (|fatasm| f, |asmreg| info, |uint| a, |asmreg|)
		// lets see.
		|| U = f.info // save old info. probably quite good.
		|| old = f.prms[a] & 31 							// Need to remove the old output.
			.RemoveReg(old, f)
		
		U.Reg = f.Dest(a, info, self)						// Then we set it to the new value
		f.info = u // update reg here.
		return u
	
	
	function assembler.RemoveReg (|int| i, |fatasm| fat)
		require .Registers[i] == fat
		|| NewFat = fat.FindOlder
		.KnownValuesMap &=~ (1<<i)
		.Register(i) = NewFat
	

	function FindOlder (|&fatasm|)
		|| reg = .Output.reg$
		|| b = .BasicBlock
		while 
			self--
			require .BasicBlock == b

			if .Output.reg == reg
				return self
		
		
	function Output (|asmparam|)
		|| o = ._Outputs|uint|
			o = o.log2
			return .Prms[o]


	function Dest (|uint| a, |asmreg| info, |&assembler| sh, |asmreg|)
		|| changed = info.prm
		.prms[a] = changed
		._Outputs |= 1 << a
		changed &= 31
		if changed
			sh.Register(changed) = self
			|| map = 1 << changed
			if info is KnownConst		// Lets see
				sh.KnownValuesMap |= map
			  else
				sh.KnownValuesMap &= ~map

		|asmreg| x // in case i forget to do it later via: fat * asmreg
		x.FatIndex = .index
		x.reg = info.reg
		.info = x
		
		return info

	
	function File (|string|)
		opt norefcounts
		|| s = SCImport.AllFiles[.filenum]
			return s
	
	
	Syntax Expect (|string| Error)
		|| f = .file|file--|
		|| err = Error(nil, error, ErrorSeverity.Error, f)
		err.position = .BytePos
		nil|errorreceiver| <~ err
		.DebugPrint
	
	function Jump (|int|)
		|| j = .JumpReg
			return .prms[j-1]


	function JumpTo (|&fatasm|)
		|| j = .JumpReg
			return self + 1 + .prms[j-1]|int|
	
	
	function JumpTo (|&fatasm| value, |&Assembler| A)
		cpp_Part JumpToSet
		|| j = .jumpreg
			|| D = (value - self) - 1
			check (D != -1) (self, "bad jump")
			.prms[j-1] = D
			if d == 0
				a.nop(self)
		  else
			j = .id
			debugger

////////////////////////////////////////////////////////////
	function r0 (|uint|): return .prms[0]
	function r1 (|uint|): return .prms[1]
	function r2 (|uint|): return .prms[2]
	function r3 (|uint|): return .prms[3]

	function r0 (assigns:|uint|): .prms[0] = value
	function r1 (assigns:|uint|): .prms[1] = value
	function r2 (assigns:|uint|): .prms[2] = value
	function r3 (assigns:|uint|): .prms[3] = value
	
	
	function RegInput (|int| a, assigns:|asmreg|)
		.ASMReg(a, value)
	
	
	function RegOnly (|int| i=0, |int|)
		return .prms[i] & 31 // remove tmpbit + fatindex		


	function ASMReg (|int| a=0, |asmreg|)
		return .prms[a].ASMReg
	
	
	function ASMReg (|int| a, assigns:|asmreg| Input)		// prm (
		cpp_part Prm
		opt inline
		
		|| i = input.FatIndex
		.prms[a] = input.Prm
		.InputRegs |= 1<<a
		if i
			|| src = i.fat // we refcount dests too. shouldn't!
			if src < self
				.InputFats |= 1<<a
				if src
					src.µRefCount++
			  else
				debugger // what?


	function NumInput (|int| a, assigns:|int| v)
		.prms[a] = v


	function JumpInput (|int| a, assigns:|int| v)
		.prms[a] = v
		.JumpReg = a + 1
		.µRefCount = 1 max .µRefCount // control flow is always used?
		Assembler.sh.BasicBlock++
		


////////////////////////////////////////////////////////////


	function CheckHasOutput 
		opt inline
		target debug {
		opt norefcounts
		for (b.bits in ._outputs|int|) (i)
			if !.regonly(i)
				error (self, "Missing ASM output")
		}
		
	function µRenderInto (|!&asm| Where, |!&asm| After, |!&asm|)
		opt inline
		return (ASM.encoders[.op]!)(self, where, after)


	function FuncPrms (|scfunction| fn, |uint64|)
		if self isa asm.tail
			return .r1 >> 5

		|| R = .r2|uint64| ||| (.r3|uint64| << 32)
		if !fn.hascversion
			return R >> 5
		return R >> 12

	
	function PrmCollectCounterPart (|FastString| fs)
		opt norefcounts
		|| fn = .msg.Obj as SCFunction 		#require		
		|| regs = .FuncPrms(fn)
		|| n = fn.Args.Length

		fs <~ "("

		while n
			fs <~ 'r'
			if fn.HasCVersion
				fs <~ (regs>>((--n)*5))&31
			  else
				fs <~ regs & 31
				regs >>= 5
				n--
			if n > 0
				fs <~ ", "		
		fs <~ ")"
	
	
	function RenderFat (|FastString| fs, |bool| Simpler=false)
		opt norefcounts		
		if (!.exists)  and  (.msg.func == @sheb)
			.RendaMsg(fs, 0)
			return 
		
		|| M = fs.mark
		|| op = instruction.typelist[.baseop]!
		if .baseop != asm.noop
			if self is constoutput
				fs.appendlower(op.name)
			  else
				fs <~ op.name
			fs <~ ':'
		fs <~ (' ', 5-op.name.length)
		|| GotAny = ""
		|| Remain = 24
		|| f = op.sizes
		/// 
		
		for i in f.count
			|| bs = f[i].BitSize
			if bs and (remain > 0)
				fs <~ GotAny
				GotAny = ", "
				|int| v = .prms[i]
				if (.IsFunc and (i == 2 or 3)) or (self isa asm.tail and i == 1)
					if i != 3
						.PrmCollectCounterPart(fs)
					  else
						fs.Length-=2
				  else
					if f[i].isreg
						fs <~ ('r' + v|uint| >> 30)|byte|
						v &= 31
					fs <~ v
				remain += bs
			if .op == asm.knst
				exit
		
		if (self  isa  asm.knst or asm.knsr or asm.gtab)
			.RendaKnst(fs, fs.mark-m)
			fs <~ ' '
		
		|| n = fs.mark - m
		if n <= 2
			fs.length--
			fs <~ "..."
			n += 2
		if !Simpler
			.RendaMsg(fs, n)
	

	function GuessSize (|int|)
		// don't really know the size, as consts are awkward to guess
		// and long-jumps might require more awkward encoding.
		if .Exists
			if ._op < 32
				return 2 + (._op&1)
			return 1
	
	
	function Exists (|bool|)
		return .op != asm.noop

	
	function TmpRender (|faststring| fs, |message| msg)
		|| arg = msg.Find(@arg)
		if !arg
			msg.Render(fs)
			return 
		
		|| c = msg.copy
		arg = c.find(@arg)!
		while arg
			arg.Remove
			arg = c.find(@arg)
		fs <~ c.Render.replaceall( "\n", ": " )
			
	
	function RendaMsg (|faststring| fs, |int| sofar)
		opt norefcounts
		|| msg = .msg
		if msg == @sheb
			msg.render(fs)
			return
		fs <~ (' ', 47-sofar)
		require msg != @arg

		fs <~ "/*" // reuse /* string lol.
		fs <~ " "
		if msg == @prm
			msg = msg.parent
		  else
			while msg == @num or @thg or @opp or @bra or @char
				loop !(msg in @prm)
				msg = msg.parent
		if (msg == @tmp)
			.TmpRender(fs, msg)
		  else
			msg.render(fs)
		
		fs <~ " " // reuse "*/" string
		fs <~ "*/"
	
	
	function RendaKnst (|faststring| fs, |int| sofar)
		|| ty = .info.µType
		fs <~ (' ', 26-sofar)
		fs <~ "/# "
		if ty.IsInt
			fs <~ .const|int64|
		  else
			|float64| num
			if ty.ByteCount == 4
				num = .F32
			  else
				num = .f64
			fs.AppendDouble(num, 9, true)
			if ty.ByteCount == 4
				fs <~ "f"
			  else
				fs <~ "d"
		fs <~ " #/"
		
	
	function JumpNegate (|assembler| sh,  |fatasm| mid,  |bool|)
		|| m = .op
		if m == asm.jmpi or asm.jmpf
			.prms[2] xor= 1
		  else
			._op = m xor 1
		|| j = .jumpreg - 1
		.prms[j] += mid.Jump
		sh.nop(mid)
		return true
		
	
	function JumpImprove (|assembler| sh, |bool|)
		|| D = .JumpTo!					// extend the destination, in case of multiple jumps
		|| n = 0
		
		while d isa asm.jump
			d = d.JumpTo!
			expect (++n < 30) (.msg)
		
		|asm| m = .op
		if  m == asm.jump
			if  d isa asm.ret  or  d isa asm.tail  
				return .CopyFrom(d)		// jumping to a function-exit.

		  elseif (d == self + 2) and (m != asm.LUPU and asm.LUPD)
			|| mid = self + 1
			if mid isa asm.JUMP
				return .JumpNegate(sh, mid)
		
		if n > 0
			.JumpTo(D, sh)
			return true
	

	function IsFinisher (|bool|)
		|| o = .op|asm|
		if o == asm.ret or asm.trap or asm.TAIL or asm.rfrt
			return true
		if o == asm.jump
			return .jump < 0


	function DebugPrint 
		visible
		nil self
		if !self
			"(nil)"
			return
		if asm.NoisyASM >= 3
			|| fs = faststring()
			.RenderFat(fs)
			printline fs


	iterator RegInputs
		|| ir = .InputRegs
		|| i = 0
		while (i < –count .prms)
			ifn ir & (1 << i)
				i++
				continue
			yield i
			i++
			
	iterator FatInputs
		|| ir = .InputFats
		|| i = 0
		while (i < –count .prms)
			ifn ir & (1 << i)
				continue
			yield (.inputfat(i))
			i++
			
	iterator RegOutputs
		|| oo = ._Outputs|int|
		while oo
			|| i = oo.lowestbit
			oo &=~ i
			yield .prms[i.log2]


	module
		function VerifyNumbers (|bool|)
			require (asm.jmpe xor 1) == asm.jmpn
			require (asm.jmkm xor 1) == asm.jmkl
			require (asm.jmke xor 1) == asm.jmkn
			require (asm.jbor xor 1) == asm.jban
			require (–sizeof FatASM) == 64
			return true



datatype ASMParam (uint)
	function ASMReg (|asmreg|)
		(rz is temp) = (self>>31)
		rz.reg = self&31
		|uint64| fatindex = (self >> 15)&0xFFFF
		rz = rz as (fatindex << 48)|asmreg|
	function Reg (|int|)
		return self&31


function asmreg.Prm	(|asmparam|)
	|| i = .FatIndex << 15
	|| r = .reg
	|| t = (self is Temp)<<31
	return r ||| t ||| i

