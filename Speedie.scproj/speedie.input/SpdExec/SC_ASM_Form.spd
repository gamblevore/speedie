

// vm.box


class µForm 
	|int|			Count
	|string|		Name
	|µParam[6]|		Params
	|int|			TotalBits
	|int|			Index
	
	flags
		(32)		// starting point
		Jump
		Num
		Signed
		NoExpect
		PositionBits
		Output
	
	
	render
		fs <~ .name
		for i in .count
			fs <~ " "
			fs <~ .params[i].BitSize
		
	constructor (|string| data, |message?| tmp)
		opt norefcounts
		.name = tmp.name
		for i in 6
			.params[i] = 0
		for line in tmp.last(@arg)
			|| f = line.needfirst
			.loadparam(f.name, f, line.IsLast)
//		  else
//			debugat // remove this? I think we don't do this anymore.
//			for param in data
//				.loadparam( string.byte(param), nil )
		
		.index = ++µform.count


	function AddP (|int| Size, |µParam| P=0)
		if size <= 0 or size > 32 // hmm??
		|| i = .count++
		.totalbits += size
		expect (i < 6) ("Too many params.")
		p |= size-1
		.params[i] = p
			

	function LastField (|µParam|)
		return self[.Count-1]
	
	syntax Access (|int| i, |µParam|)
		cpp_part AccessInt
		return .params[i]
	
	syntax Access (|int| i, assigns: |µParam|)
		.params[i] = Value
	
	
	iterator
		|| i = 0
		while (i < .count)
			yield self[i] (i)
			++i

	function AddRemainder (|int| U)
		.addp( 32 - .TotalBits, µform.num ||| U)
	
	function LoadParam (|string| pl, |message| place, |bool| IsLast)
		if (pl.isint)
			|| i = pl.int
			check (i > 0 and i <= 24) (place, "Number out of range (1-24)")
			.addp(i, µform.num)
			return
		expect (pl == 1) (self)
		|| p = pl[]
		if  p == 'r'
			.AddP(5)
		  elseif p == 'R'
			.AddP(5, µform.Output)
		  elseif p == 'x' // for 64/96/128 sized instructions
			.AddP(32)
		  elseif p == 'n'
			.AddP(5, µform.num)
		  elseif p == 'l'
			.addremainder(0)
		  elseif p == 'L'
			.addremainder( µform.NoExpect )
		  elseif p == 's'
			.addremainder( µform.signed )
		  elseif p == 'S'
			.addremainder( µform.signed + µform.NoExpect )
		  elseif p == 'j'
			.addremainder( µform.signed + µform.jump )
		  elseif place
			error (place)
		  else
		    error (self)
	

	module
		|dictionary of µForm|				Forms = dictionary()
		|int| Count
		function NeedForm (|string| Form, |µform|)
			opt norefcounts
			rz = forms.valuelower(form)
			if !rz
				error "Missing form: " + form
				rz = µform(form, nil)
