
/*
	Speedie Internals. By Theodore H. Smith...
*/

constants
	kUsingStr = "Function here:"




prototype fnPreReader (|scfunction| self, |message| msg, |bool|)


function SCFunction.PreRead (|message| Arg)
	opt NoRefCounts

	.NilSelf = 0			// need to clear stuff that could be error-checked, or removed.
	isnt disabled			// Autocomplete, will call .PreRead
	
	for s in arg
		if s == @tmp
			|| fn = FuncPreReader[s.name]
				(fn[])(self, s)
				s.remove


dispatch FuncPreReader (fnPreReader)
	+'asm'
		opt norefcounts
		.intrinsic = msg[@func]
		.intrinsic.ExpectLast
	
	+'trin'
		.AddTrin(msg)

	+'disabled'
		opt norefcounts
		is disabled
		.description = "Called a disabled function."
		|| s = msg.first
			s.ExpectString
			.description = s.name

	+'todo'
		opt norefcounts
		msg[@nil].expectlast
		compiler.TodoList <~ msg
	
	
	+'numeric'
		opt norefcounts
		|| f = msg.first
			error f
		(self is numbercreator)
		
	+'inline'
		opt norefcounts
		msg.expectempty
		.MakeLinkInline
		
	
	+'opt'
		opt norefcounts
		|| f = msg[@thg]
			if f ~= "min"
				|| Min = f.next(@num)
					.minopt = min.int
			  else
				f.expectlast
				if f ~= "NoRefCounts"
					is NotRefCounted
				  elseif f ~= "inline"
					is OptInline
				  else
					error f

	
	+'nil'
		opt norefcounts
		|| f = msg[@thg]$
		if f ~= "self"
			.NilSelff(msg, nilstate.optional)
		  elseif f ~= "checker"
			.MakeNilChecker(msg)
		  elseif f ~= "safe"
			.MakeNilChecker(msg)
			is HidesProperties
			is OptInline
			.isvisible = true
			
		  else
			error f
	
	
	+'real'
		opt norefcounts
		|| f = msg[@thg]$
		if f ~= "self"
			.NilSelff(msg, nilstate.real)
		  else
			error f
	

	+'description'
		expect (self isnt disabled) (msg, "Already disabled")
		.ReadDescription(msg)
	


function SCFunction.MakeLinkInline
	check (.islibrary) (self, "Only library funcs can use this.")
	is OptInline
	is LinkInline


function SCFunction.MakeNilChecker (|message| msg)
	.NilSelff(msg, nilstate.optional)
	.IsNilChecker = 1
	

function SettingSelfProperty (|Message| Rel, |bool|)
	// .bla = X
	opt norefcounts
	|| Dot = SettingAProperty(rel)
		return (Dot.first IsThing "self")


function SettingAProperty (|Message| Rel, |Message|)
	// bla.bla = X
	opt norefcounts
	require (Rel.IssetRel)
	|| f = Rel.first
	if f and f.DotType
		return f


function SettingMemory (|Message| Rel, |bool|)
	// bla.bla = X
	opt norefcounts
	require Rel.IsSetRel

	|| f = Rel.first$
	if f.func == @acc
		return true
	|| d = f.obj
	if d isa scdecl
		return  d is Property  or  d is Global


function SCFunction.NilSelff (|message| where, |nilstate| v)
	expect (!.nilself) (where, "Nilness of self, already stated")
	.NilSelf = v


function string.UnicodeSafeName (|faststring| fs_in=nil, |string|)
	if .isascii
		if fs_in
			fs_in <~ self
		return self
	
	|| fs = faststring(fs_in)
	for s in self
		if C_Letters[s]
			fs <~ s
		  else
			fs <~ 'x'
			fs.appendhex(s)
	return fs.GetResult(fs_in)




datatype SCBlockage (int)
	flags
		Bits		= 0b111100000	// reserve 1 more bit
	
		Continue	= 0x0040
		Exit		= 0x0080
		Return		= 0x00C0
		Reserved    = 0x0100
	
		Quit		= Return		// for now
	
	syntax is (|SCBlockage| b, |bool|)
		return self == b
	
	

//function fdb (|[scfunction]| r)
//	opt norefcounts
//	cpp_name fdb
//	visible
//	for f in r
//		printline f.Exportname


function @scfunction.PrintStats (|faststring| fs)
	fs.PrintStatType("int",				FuncArgAndReturnTypes[1] + FuncArgAndReturnTypes[0])
	fs.PrintStatType("float",			FuncArgAndReturnTypes[2])
	fs.PrintStatType("float/int",		FuncArgAndReturnTypes[3])
	fs.PrintStatType("vector",			FuncArgAndReturnTypes[4])
	fs.PrintStatType("vector/int",      FuncArgAndReturnTypes[5])
	fs.PrintStatType("vector/float",    FuncArgAndReturnTypes[6])
	fs.PrintStatType("vector/float/int",FuncArgAndReturnTypes[7])


