
/*
	Speedie Internals. By Theodore H. Smith...
*/


class SCObject 
	|string|					Name
	|string|					ExportName
	|[SCFunction]?|				LinkFrom
	|message?|					Source
	|byte|						NoAutoComplete
	|byte|						AllocSource
	|uint16|					CurrFuncLink
	|SCNodeInfo|				NodeInfo
	
	constructor
		.NodeInfo = Compiler.InBuiltType
		.linkfrom = nil
	
	function AutoCompleteKind (|string|)
		if self isa SCModule
			return "module"
		if self isa scclass
			if .FuncProto
				return "function prototype"
			if .IsDataTypeOnly
				return "datatype"
			if .IsStruct
				return "struct"
			if .IsObject
				return "class"
		if self isa SCDecl
			if self is Const
				return "Constant"
			if self is Global
				return "Global"
			if self is Property
				return "Property"
			if self is Param
				return "Parameter"
			return "Variable"

		return "unknown"
	
	function Name (|string|) 
		nil safe
		if self
			return .name
		return "(nil)"
		
	render
		|| cname = ((.class.name|uint64|)+2)|cstring|
		fs <~ cname
		fs <~ ": "
		fs <~ .name

	function SyntaxExpect ( |string| Error )
		cpp_part Fail
		errorreceiver.NewError( .source, Error )

	function File (|SCFile?|)
		opt norefcounts
		|| src = .Source
			return src.File
		if self isa SCModule
			return .cls.file?

	syntax is (|SCNodeInfo| i, |bool|)
		return .NodeInfo&i

//	function TestASM (|SCNodeInfo| i, |bool|)
//		return .NodeInfo[i]
	
	syntax is (|SCNodeInfo| i, assigns:|bool|)
		|| x = .NodeInfo
		if value
			x |= i
		  else
			x &=~ i
		.NodeInfo = x

	function IsVisible (|bool|)
		return self is visible
	
	function IsVisible (assigns:|bool|)
		(self is visible) = value

