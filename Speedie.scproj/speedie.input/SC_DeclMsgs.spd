


function DeclsDefault (|Message| def, |Message|)
	opt norefcounts
	if def.IsSetRel
		return def.Last


function MakeDecl (|string| Type, |string| Name, |Message| Value, |SCDeclInfo| Purpose = 0, |Message!|)
	opt NoRefCounts
	rz = @decl.msg
	
	|| f = rz
	if purpose is Borrowed // seems to make no difference? sigh.
		f = f.msg(@arel, "--")

	f = f.msg(@emb)
	if Type
		f.func = @name
		f.name = type
	
	if !Value
		rz <~ (@thg, name)
		return rz
	
	|| Rel = rz.msg(@rel)
	|| NameMsg = rel.msg(@thg, name)
	rel <~ (@opp, "=")
	rel <~ value
	
	|| o = value.obj as scdecl
		|| dcl = o.copydecl(true)
		dcl.mu.info |= purpose
		dcl.Name = name
		dcl.Default = value
		NameMsg.obj = dcl		
		Rel.obj = dcl
		rz.obj = dcl
		f.obj = dcl
	

function MakeDeclOrRel (|Message| Into, |message| namemsg, |Message| value, |Message|)
	require into
	rz = @decl.msg
	rz <~ @emb
	
	if value
		Value = NeweqRel( NameMsg, value )
	  else
		Value = NameMsg
	rz <~ value
	into.First = rz



function SCDecl.IntsOnly (|Message| exp, |bool|)
	|| T = .type.TypeInfo
	if (.isnormalnumber and t.isint)
		return true // for constants... isreallynumeric allows constants a bit better.
	
	expect (T is numeric or t is bool)			(exp, "Can’t do bit ops on this")
	expect (!T.isfloat)							(exp, "Can’t do bit ops on floats")
	return true



function DefaultStitch (|Message| Default, |Message| dcl, |message|)
	opt norefcounts
	using (dcl)
	rz = dcl.msg(@rel)
	rz <~ dcl.second
	rz <~ (@Opp, "=")
	rz <~ Default


function SCFunction.CollectDeclsParams (|Message| prms, |SCNode| AddToSpace)
	opt norefcounts
	scmodule.curr = .parent|scmodule|
	|| cls = .Cls
	if !cls
		cls = .parent|SCModule|!.Cls
	  elseif !prms.HasSelfAsFirst
		expect (AddToSpace) (prms)
		.AddSelfToFunc(cls, AddToSpace)

	for item in prms
		item.CollectOneParam(self, AddToSpace, cls)

	|| ns = .NilSelf
		.WantAs(ns)

	|| r = .ReturnType
		if r.IsBareStruct
			(r is ReturnedStruct)
		if self is HidesProperties
			.ReturnType = r.makereal
		check (r isnt Reference) (r.source, "Only parameters can be references")


function Message.CollectOneParam (|scfunction| func, |SCNode| Recv, |scclass| cls,  |bool|)
	opt norefcounts
	real self
	if (func.isassigns == 2) and .islast
		if !cls
			error (self, "Can’t use this here." )
		  else
			// setter x (|int|) --> fn x (|int| value)
			func.IsAssigns = 3
			if cls.IsDataTypeOrFP
				.next = .copy // fn x (|int|) --> fn x (|int| value, |int|)
			.AddValue(func)
		
	if  self == @Item  and  .first == "assigns"
		self = .Last
		func.IsAssigns = 1
		.addvalue(func)
	
	require .Expect(@decl)

	|| dcl = .CollectDecl(func, recv, declmode.FuncParam, cls, func)		#require
	if dcl.default isthing "nil"
		if dcl.TypeSuffers
			// seems to be taking the default type... for string/array, making it "real"
			// for example function abc (|object|A=nil, |string|B=nil)
			// it complains about B, but not A!
			
			check (!dcl.StatedReal) (self, "Can't be both real and nil!")
		dcl.NilDeclared = nilstate.optional


	func.args <~ dcl
	check (func.args <= 8)    (self, "Too many parameters defined. (8 max)")
	if !dcl.type.isstruct
		check (dcl.name ~!= "rz") (self, "Using reserved name.")
	if dcl.name
		(dcl is Param)
	  else
		(dcl is return)		// This is for struct returns, right? which are always params.
	(dcl isnt Altered)		// SCDecl defaults as being altered... not sure why.
	(dcl isnt NumberConst)
	func.AddTypeInfo(dcl)
	return true



function scclass.CollectProp (|Message| msg, |bool|)
	opt norefcounts
	|| dcl = msg.CollectDecl( .modul, self, DeclMode.properties, self)
		.properties <~ dcl
		(dcl is property)
		dcl.becomereal
		return true


// remove these... but later.
function CollectDeclsGlobals (|Message| arg, |SCNode| SCArg)
	opt norefcounts
	if scarg isa SCModule
		SCModule.Curr = SCArg
	for item in arg
		if item == @decl
			require item.CollectAGlobalDecl(scarg)


function scdecl.ForContained (|scdecl|) // notglobal, noglobal
	|| Strong = (.NilDeclared isnt nilish) and .typesuffers
	if (self isnt global) and !.name and !strong 
		return self
	// strange that it can have a name and be marked as !
	// surely... all contained classes can be nil?
	rz = .copydecl
	rz.name = ""
	rz.nildeclared |= nilstate.nilish
	(rz isnt global)


function scdecl.NotLocal (|scdecl|)
	if (self isnt local) and !.name 
		return self
	rz = .copydecl
	rz.name = ""
	(rz isnt local)


function scdecl.AsLocal (|scdecl|)
	opt norefcounts
	if (self is local) 
		return self
	rz = .copydecl
	rz.name = ""
	(rz is local)


function Message.CollectAGlobalDecl (|SCNode| scarg, |bool|)
	opt norefcounts
	|| f = .file
		f.proj.decllayer.use
	
	|| dcl = .CollectDecl(scarg, scarg, declmode.Globals)		#require
	|| def = dcl.Default
	if def and !dcl.nilstated
		|| d = def.MsgDecl
		if d and d.StatedReal
			dcl.NilDeclared = nilstate.real			// in case of something like |object| x = real()

	return dcl.AddToGlobs


function scdecl.AddToGlobs (|bool|)
	opt norefcounts
	is Global
	
//	|| n = .name
//	if ('_' == n[]) ||| ('_' == n.Last)
//		is hidden
// maybe just do visibility textually?
	
	if self is Library
		PackMaker.LibGlobs <~ self
	  else
		packmaker.PackGlobs <~ self

	return true


function CollectManyDeclsFromBody (|Message| arg, |SCNode| scarg) 	// remove later... :)
	opt norefcounts	// basically... the caller needs fixing.
	for item in arg
		if item == @decl
			item.CollectFromBody(scarg) // keep on going even if one failed. good for autocomplete


function scdecl.AsBody
	is body
	isnt altered
	isnt NumberConst
	.DepthOfBranch = SCFunction.InBranch
	.DepthOfLoop = SCFunction.InLoop


function Message.CollectFromBody (|SCNode| Name_space)
	opt norefcounts
	if .obj // when does this happen? hopefully not on anything that escaped .collectdecl?
		return

	|| dcl = .CollectDecl(Name_space, Name_space, DeclMode.FunctionBody)
		dcl.AsBody
		StructStuff.DelayedFix
		|| def = dcl.Default
			check (def != @thg  or  def.name ~!= dcl.name) (def, "Same name!")
		.TryAddBareStruct(name_space, dcl)


function Message.CollectDecl (|SCNode| P, |SCNode| Recv, |declmode| Mode, |scclass| cls=nil, |scfunction| FuncPrmz=nil,  |scdecl|)
	|message--| Type = .first!
	|message--| Name = type.Next
	
	expect (FuncPrmz or Name)  (self, "No name found.")
	|| Default = DeclsDefault( name )
	|| Sdcl = ExtractDecl( Type, P, mode )
		check ((sdcl isnt reference) or (mode is FuncParam)) (Type, "Only parameters can be references")
	  elseif !default  and  (P isa SCArg)
		Sdcl = P.ParentFunc!.ReturnType

	|message--| NameItm = name.CollectDclName
	|SCDecl--| DefaultDcl
	if FuncPrmz
		0
	  elseif (Default and mode isnt Properties)
		DefaultDcl = TypeOfExprNeeded( Default, P ) #require
		DefaultDcl = DefaultDcl.Downgrade
		if (sdcl)
			default = sdcl.ExpectMatchForDecl( DefaultDcl, default )
		
	  elseif (!cls) and (sdcl) and (sdcl.CanUseDefault)
		Default = sdcl.CreateDefault(type, mode >= 4)
		if (Default)
			TypeOfExpr( default, P )
			name = DefaultStitch( Default, self )
	
	|| NoOriginalDcl = false
	if !Sdcl
		NoOriginalDcl = true
		if (cls) and (cls.Contained)
			sdcl = cls.Contained! // (|| x = Y), (|object| x = Y)
		  else
			expect (!FuncPrmz and defaultdcl) (self, ("Missing type", "Missing type, or default value.")(!FuncPrmz))
			sdcl = DefaultDcl
	
	if sdcl.NeedsContainedfix
		sdcl = sdcl.Containedfix
	
	|| dcl = Sdcl.CopyDeclsStructFixer(self, mode * (nameitm!=nil), type)
	dcl.source = self
	if !NameItm
		check (.islast) ( self, "Parameter is missing a name." )
		if !FuncPrmz
			return nil
		if FuncPrmz.CollectReturnAsReal // allocs dont fail (assumed so)
			dcl.nildeclared = nilstate.real
			

		check !dcl.IsCArray (self, "Can't return c-arrays")
		FuncPrmz.ReturnType = dcl // we need the copy
		if !dcl.isbarestruct or funcprmz is constructor
			funcprmz.AddTypeInfo(dcl)
			(dcl is return)
			return nil


		(dcl is ReturnedStruct)
		dcl = dcl.getaddress
		nameitm = .msg(@thg,"rz")
		(dcl is IntendedAsReturn)
		funcprmz.StructReturnPos = funcprmz.args.Length+1 // sheesh a lot of work!


	if !Default
		name.ExpectLast
	
	if mode isnt Properties // the old .obj is needed
		nameitm.obj = dcl
	
	if (Mode & 8) and NoOriginalDcl // functionbody
		if sdcl isnt StayBorrowed
			(Dcl Isnt Borrowed)
		dcl.nildeclared = NilState.real >> dcl.TypeSuffers 
	
	if (nameitm in @Rel)
		nameitm.Parent!.obj = dcl
	
	.obj = dcl
	type.obj = dcl // might as well set it everywhere.
	dcl.Default = Default
	dcl.Name = nameitm.name
	(dcl Isnt Const) // on "|| bla = true", make "bla" editable.

	if default and defaultdcl
		dcl.AssignabilityCheck(nil, default, DefaultDcl)

	if Recv // nil for prototypes
		Recv.TryAdd( self, dcl, dcl.Name )
	
	return dcl



function Message.CollectFuncTableName (|string|)
	//	@msg.ID-->"msg"
	//	"abc"-->"abc"
	//	why? why not?
	if self == @name
		return .name.UpperCase
	if .name and self != @dot
		Return .Name

	Return .Firstname 
	

function scdecl.NeedsContainedfix (|bool|)
	opt NoRefCounts
	if !.AllocSource and !.contains
		return .ReallyContains


function scdecl.Containedfix (|scdecl|)
	opt norefcounts
	|| t = .type
	rz = (.copydecl, self)(T.typenormal == self)
	rz.contains = t.SelfDecl.Contains


function scdecl.DownGrade (|scdecl|)
	opt norefcounts
	if !.IsNormalObject
		return self
	|| y = .type.DowngradeTo
		return y.TypeNormal
	return self
	
	
function message.AddValue (|scfunction| f)
	opt norefcounts
	opt inline
	if !.HasChildCount(2)
		using f.source!
			self <~ (@thg, "Value")


datatype DeclMode (int)
	constants
		|DeclMode|
		FuncParam		= 4
		Properties		= 3
		Globals			= 1
		FunctionBody	= 8
		Local			= FuncParam+FunctionBody
		Proto			= 16
		TypeCast		= 32
		ProtoParam		= FuncParam + Proto
		Always			= 64

	syntax is (|DeclMode|o, |bool|)
		return self == o



