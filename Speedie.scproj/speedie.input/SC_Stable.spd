

module Stability
	|| LastLibID = 0

	function StabiliseFuncs
		if !packmaker.libfuncs
			PackMaker.AddAll
		.StabiliseSub("stable_id_bank.txt", packmaker.libfuncs, true)
		.Debug
	
	
	function StabiliseGlobs 
		return .StabiliseSub("stable_glob_bank.txt", packmaker.libglobs, false)
	
	
	function StabiliseSub (|string| name, |[scobject]| list, |bool| IsFuncs)
		.LastLibID = 0
		|| funcs = Compiler.library[name]
		|| Data = funcs.ReadAll.trim
		stability.UseBank(data, IsFuncs)
		|| fs = (faststring(), nil)(options.MakeExec)
			fs <~ data ?? "// beta-file. Unstable."
		.StabliseSub2(fs, list, isfuncs)
		if fs
			funcs.SmartData = fs|string|
		
	
	function scfunction.NeedsID (|bool|)
		return (!.IsMacro) and (self isnt disabled and LinkInline)
	
	
	function Debug 
		target debug
			return
			for f in packmaker.libfuncs
				print f.packid(true)|int|
				print " "
				printline f.exportname

	
	function StabliseSub2 (|faststring| fs, |[scobject]| list, |bool| IsFunc)
		opt norefcounts
		|&&nil| Table		// just pass 4096. Saves all the fÃ¼cking around with calculating sizes.
		target debug
			if !options.MakeExec
				Table = jb_vm.vm.InitTable(4096, 64K)

		|| sorter = codesorter.PackID(isfunc)
		list.Sort(sorter)
		
		for obj in list
			if (obj isnt private) and (!isfunc or obj|scfunction|.needsID)
				obj.MakeID(fs, table, isFunc)
			
		
	function packmaker.OrderLibGlobs // why is it done from the main compile func and not in the execmaker?
		opt norefcounts
		.LibGlobs.Sort(SimplestFirst)				// sort them so empty values come after
		.LibGlobSize = .GlobalsSize(.libglobs) 		// rebuild size
	
	
	function SCObject.PackID (|bool| isFunc, |int|)
		if IsFunc
			return .FPackID
		return .GPackID
	
	function SCObject.PackID (|bool| isFunc, assigns:|int|)
		if IsFunc
			.FPackID = value
		  else
			.GPackID = value
	
	function scobject.MakeID (|faststring| fs, |?&&nil| table, |bool| IsFunc)
		|| id = .packid(isfunc)
		if !id
			if fs
				fs <~ "\n$.ExportName"
			id = Stability.LastLibID++
			.PackID(isfunc) = id
		
		if table
			|| fn = jb_vm.Load(.exportname) // an actual function pointer for testing
				Table[id+31] = fn

	function Find (|string| str, |bool| Funcs, |scobject|)
		if funcs
			return Compiler.ExportNames[str]
		return compiler.ExportGlobs[str]
	
	function UseBank (|string| Data, |bool| Funcs)
		|| ID = .LastLibID
		for str.lines in data
			id++
			|SCObject--| fn = .Find(Str, Funcs)
				check (!fn.PackID(funcs)) (str + " replicated ID.")
				fn.PackID(funcs) = id
		.LastLibID = ID	
	
