

module LibStability
	|| LastLibID = 0

	function StabiliseFuncs
		if !packmaker.libfuncs
			PackMaker.AddAll
		.StabiliseSub("stable_id_bank.txt", packmaker.libfuncs, true)
	
	
	function StabiliseGlobs 
		return .StabiliseSub("stable_glob_bank.txt", packmaker.libglobs, false)
	
	
	function StabiliseSub (|string| name, |[scobject]| list, |bool| IsFuncs)
		.LastLibID = 0
		|| funcs = Compiler.library[name]
		|| Data = funcs.ReadAll.trim
		.UseBank(data, IsFuncs)
		|| fs = (faststring(), nil)(options.MakeExec)
			fs <~ data ?? "// beta-file. Unstable."
		.StabiliseSub2(fs, list, isfuncs)
		if fs and !Options.TargetDebug
			funcs.SmartData = fs|string|

	
	function StabiliseSub2 (|faststring| fs, |[scobject]| list, |bool| IsFunc)
		opt norefcounts
		|&&nil| Table		// just pass 4096. Saves all the fÃ¼cking around with calculating sizes.
		target debug and !perrydbg
			if !options.MakeExec
				Table = testvm.TestInit(4096, 64K)
			
		|| ObjSort = codesorter.PackID(isfunc)
		list.Sort(ObjSort)
		
		for obj in list
			if (obj isnt libonly) and (!isfunc or obj|scfunction|.NeedsStableID)
				obj.MakeID(fs, table, isFunc)
			
		
	function packmaker.OrderLibGlobs // why is it done from the main compile func and not in the execmaker?
		opt norefcounts
		if !.LibGlobSize
			.LibGlobs.Sort(SimplestFirst)				// sort them so empty values come after
			.LibGlobSize = .GlobalsSize(.libglobs) 		// rebuild size
	
	
	function SCObject.PackID (|bool| isFunc, |int|)
		if IsFunc
			return .FPackID
		return .GPackID
	
	function SCObject.PackID (|bool| isFunc, assigns:|int|)
		if IsFunc
			.FPackID = value
		  else
			.GPackID = value
	
	function scobject.MakeID (|faststring| fs, |?&&nil| table, |bool| IsFunc)
		|| id = .packid(isfunc)
		if !id
			if fs
				fs <~ "\n$.ExportName"
			id = ++LibStability.LastLibID
			.PackID(isfunc) = id
		
		if table
			if !SelfLib.IsOpen
				selflib = DynamicLibrary.new(nil)
			|| fn = SelfLib[.exportname] // an actual function pointer for testing
				Table[id+31] = fn

	function Find (|string| str, |bool| Funcs, |scobject|)
		if funcs
			return Compiler.ExportNames[str]
		return compiler.ExportGlobs[str]
	
	function UseBank (|string| Data, |bool| Funcs)
		|| ID = .LastLibID
		for str.lines in data
			if str[0] != '/'
				id++
				|SCObject--| fn = .Find(Str, Funcs)
					check (!fn.PackID(funcs)) (str + " replicated ID.")
					fn.PackID(funcs) = id
		.LastLibID = ID	
	

|dynamiclibrary| SelfLib
