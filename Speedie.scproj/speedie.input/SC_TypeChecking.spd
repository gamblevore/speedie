


function Message.ExpandToBool (|SCNode| name_space, |Message|)
	opt norefcounts
	|| decl = TypeOfExpr( self, name_space )
		if decl.isnormal and !decl.type.theboolfunc
			return self
		
		|| upon = .unbra
		rz = TypeBool!.TypeNormal.ExpectMatch( decl, kTypeCastTrue, upon )
	
		while (rz == @bra) and (rz.first == @bra)
			rz = rz.ReplaceWithChild


function RelSetOrExpansion (|Message| exp)
	// (x = a) or (x = b)
	// --> x = a, if (!x) {x = b}
	|| L = exp.first
	|| R = exp.last						// should probably go within collectdecls at least?
	using (exp)
	exp.clear
	l.AppendAllInto(exp)
	|| cond = r.first!.copy
	|| tmp = «tmp "if"
		cond.Negate
		«arg, R »
	»
	
	|| P = exp.parent
	((p, exp)(P == @decl)).next = tmp



function Or_And_Expansion (|SCDecl| LC, |SCDecl| RC, |Message| exp, |SCNode| name_space, |SCDecl|)
	|| LBool = LC.Minimatch( TypeBool!.TypeNormal )
	|| RBool = RC.Minimatch( TypeBool!.TypeNormal )
		
	if RBool and LBool
		Return LC // easy

	// if  !RBool  and  !lbool  and  comp is OR
		//return lc.highestmatch(lc)
/*
		We got 15 of these only in speedie. And they are all within "if" or AND
		We COULD make this an upgrade, so you can do this:
			strx = str1 or str2 
*/

	// x = (mybool and myint)		// convert to bool
	|| ch0 = exp.first!
	|| ch2 = ch0.next.next!			// ch0 could change
	if !LBool and lc.type.TheBoolFunc
		ch0.ExpandToBool( name_space )
	if !RBool and rc.type.TheBoolFunc
		ch2.ExpandToBool( name_space )
	
	Return TypeBool!.TypeNormal


function scdecl.FastMatch (|SCDecl| O, |bool|)
	require o.type == .type
	require o.pointercount == .pointercount
	return o.c_array == .c_array


function scdecl.TypeMatch (|SCDecl?| O, |int| TypeCast, |Message?| exp = nil, |int|)
// Make TypeCast = true able to deal with... pointers to structs!
	real self
	if (o == nil)
		require .default
		Return kUseDefaultParams

	if (o == self)
		Return kSimpleMatch		

	if (o.IsNil and (TypeCast & kTypeCastTrue))		// nil cast
		if .CantBeNilInCpp							// |mystruct| x = nil
			if !exp
				return kNeedsTypeCast
			|| NilStruct = .CreateStructNil(exp)
			exp.replacewith(nilstruct)
			return kCastedMatch
		Return kSimpleMatch // will this work?

	// Don't .Match on the contains? It would run into "array covariance" errors?
	if O.PointeryMatch(self)
		|| ooop = .PointeryMatchSub(o, typecast)
		if !ooop and (TypeCast & kTypeCastBothways) and o.IsNormalObject and .IsNormalObject
			ooop = o.PointeryMatchSub(self, typecast)
		if (ooop)
			return ooop

	if (TypeCast & kTypeCastTrue) and (.IsvoidPtr) and (o.pointercount or o.type.funcproto)
		if (!exp)
			return kNeedsTypeCast ||| kVoidPtrMatch
		if (o.type.funcproto)
			return exp.CastFPToVoidPtr // actually do it.
		return kCastedMatch

	if TypeCast & kActualTypecasts
		Return .TryTypeCast( O, exp, TypeCast )



function scdecl.SafelyWrappable (|bool|)
	opt norefcounts
	|| n = .IsNumeric
	if (n <= 1) or (.isbool)
		return true
	
	|| fns = TypeWrapper!.modul["new"]|scfunction|
	require fns isa scfunction

	for f in fns
		|| p = f.args
		if (p.length == 1) and p[0]!.TypeMatch(self, 0)
			return true
 

function scdecl.TryTypeCastPointer (|SCDecl| O, |Message| exp, |int| TypeCast, |bool| CArray, |int|)
	opt norefcounts
	|| i = .Internal
	if i and i.MiniMatch(O)	
		|| OAddr = O.GetAddress
		if CArray
			oaddr.C_Array = 0
		
		if .MiniMatch( OAddr, TypeCast ) and (!exp or exp.CanGetAddress(nil))
			rz = exp.GetAddressOf(OAddr, CArray)


function scdecl.TryTypeCastAccess (|SCDecl| O, |Message| exp, |int| TypeCast, |int|)
	opt norefcounts
	|| i = o.Internal
	if i and .MiniMatch(i, typecast)
		return exp.MakeDereference(i)


function scdecl.TryTypeCast (|SCDecl| O, |Message| OExp, |int| TypeCast, |int|)
	opt norefcounts

	|| t = .type
	|| ot = o.type
	if (.PointerCount == O.PointerCount + 1)
		|| found = .TryTypeCastPointer(o, oexp, typecast, false)
			return found

	if (o.IsCArray) and (.pointercount == 1)
		|| found = .TryTypeCastPointer(o, oexp, typecast, true)
			return found
	
	
	/// hmmmm... need to test it... without the c-array? how?
	ifn typecast & knobooltypecast
		if (t == TypeBool) and (.IsNormal) and (o.pointercount)
			return oexp.CastToBool(nil)
	
	if (.PointerCount + 1 == O.PointerCount)
		|| found = .TryTypeCastAccess(o, oexp, typecast)
			return found

	if TypeCast & (kTypeCastMost|||kTypeCastNumbers)
		|| CounterPart = t.Signed				// int vs uint
			if (CounterPart.type == ot) and (CounterPart.TypeMatch(o, typecast, oexp).isnormalmatch)
				return ksimplematch


	|| NoBools = (TypeCast&kTypeCastBothWays) or (typecast & knobooltypecast) 
	if (TypeCast & kTypeCastMost)				// Type Casting
		// we do bool casting after, as its usually a sign of a bug.
		for (b.Upwards in Ot)
			for (C in b.Casts)
				ifn NoBools and (C.returnType.isbool)
					if .TypeMatch( C.ReturnType, 0 ).IsNormalMatch
						return C.CreateTypeCast( O, oexp )
		
		
		|| IsaBool = (t == TypeBool) and (.IsNormal)
		if (!NoBools) and isabool
			return oexp.CastToBool(nil)

	
		if (!isabool or !NoBools) and O.canwrap
			if (Typewrapper!.typenormal.TypeMatch( self, 0 ).IsNormalMatch)
				// Already have all the type info! So check the contained type is correct!
				|| it = .contains
					require it.TypeMatch(O, kTypeCastNumbers)
					return oexp.WrapExpr(O)

			if .TypeMatch( Typewrapper!.typenormal, 0 ).isNormalMatch and O.SafelyWrappable 
				return oexp.WrapExpr(o)  // only OK to turn to a bool if O is a bool.

	
	if TypeCast & kTypeCastNumbers
		|| OIsNum = O.IsNormalNumber
		|| SIsNum =  .IsNormalNumber
		if sisnum and (o.isnil or TypeCast & kTypeCastFromzero)
			Return kNumericMatch
		
		//beepw()
		if Oisnum and sisNum
			|| ff = o.IsFloat + .IsFloat
			if ff == 0
				return kNumericMatch
			if (ff == 2)
				|| L = .typeinfo
				|| R = o.typeinfo
				if (L.ByteCount == R.ByteCount)
					return kNumericMatch
				if (typecast&kTypeCastSetToMemory)
					return .FloatCast(o, oexp)
				if (o is const) and Typecast&kTypeCastForSetRel
					return oexp.CastToClass(.type)
				if (self is const) or (o is const)
					if Typecast&kTypeCastForSetRel
						return oexp.CastToClass(.type)
					return kNumericMatch // Just do this at ASM time. makes no sense otherwise.
			  elseif TypeCast & kTypeCastFromSmallInt // int to float
				return oexp.CastToClass(.type)
 
		
		if TypeCast&kTypeCastBothWays
			if Oisnum and  .IsNormalBool
				return kNumericMatch
			if sisnum and o.IsNormalBool
				return kNumericMatch

	// so much nicer!
	if ot.FuncProto.EqualOrMoreSpecific(t.FuncProto) and .PointeryMatch(O)
		return oexp.CastToClass(t, nil)



function scdecl.FloatCast (|scdecl| orig, |message| exp, |int|)
	/* floats suck :D thats why I prefer Posits
	 ideally... heres my plan
	 
	 1) Remove floats. Just relegate them to "IEEFloat" name. This way, its still available.
	 2) Float and float64 just become posits. I think hfloat can become Float16 (posit16)
	 3) We can also have a "FastFloat" type, which is EITHER. Depending on your hardware. (floats only for now.)
	*/
	if .IsNormal
		return exp.CastToClass(.type, nil)
	.ExpectFail(orig, exp, nil)

