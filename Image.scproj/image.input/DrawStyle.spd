

prototype ImageDrawStyleEvent (|Image| self,  |ivec4| Rect, |drawstyle| G, |bool| EdgeOnly)

class DrawStyle
	linkage
		cpp_part Sty
	default DrawStyle.new 
	|imagePixel|				BG
	|imagePixel|				Edge
	|imagePixel|				Seps
	|imagePixel|				Text
	|imagePixel|				_BG
	|imagePixel|				_Edge
	|ImageDrawStyleEvent?|		Drawer
	|Byte|						CornerSize
	|byte| 						CornersCut
	|Byte|						LineWidth

		
	constructor
		.LineWidth = 1
		.clear
		.text <~ vec4(1.0)

	constructor (|DrawStyle| c)
		cpp_part ConstructorCopy
		.bg = c.bg
		.edge = c.edge
		.seps = c.seps
		.text = c.text
		.CornerSize = c.CornerSize
		.CornersCut = c.CornersCut
	
		
	render
		fs <~ "BG: "
		.bg.Render(fs)
		fs <~ "\nEdge: "
		.edge.Render(fs)
		fs <~ "\nSeps: "
		.seps.Render(fs)
		fs <~ "\ntext: "
		.text.Render(fs)
	
	function Overlay (|vec4| C, |float| a=0)
		|| T = C.PreMul
		.BG.overlay(T)
		.Edge.overlay(T)
		if a
			.bg.AddAlpha(a)
			.Edge.addalpha(A)

	function Clear
		.solid = (0.0, 0.0, 0.0, 0.0)
	
	function Copy (|DrawStyle|)
		return DrawStyle(self)
	
	function Solid (assigns:|vec4|c)
		.inside = c
		.edge = .bg
		
	function Inside (assigns:|vec4|c)
		.bg <~ c
		.seps = .bg
		
	function SafeEdge (|imagepixel|)
		if self
			return .edge
		return (0.5, 0.5, 0.5, 1.0).px
	
	function SafeBG (|imagepixel|)
		if self
			return .Bg
		return (0.0, 0.0, 0.0, 1.0).px
		
	function Width (|string| s, |int|)
		return s.length * font.width
		
	function Height (|string| s, |int|)
		return font.height

	syntax using (|drawstyle|)
		._bg = .bg
		._Edge = .edge
		return self

	syntax UsingComplete (|object| idk)
		.Edge = ._Edge
		.bg = ._BG

	function Strengthen (|float| s)
		require s!=1.0
		.edge.alpha *= s
		.bg.alpha *= s
		if s > 1.0
			|| a = .bg.a
			.bg <~ (.bg.Color * s)
			.bg.a = a



