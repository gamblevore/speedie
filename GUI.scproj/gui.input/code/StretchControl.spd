

role StretchControl (GuiControl)
	linkage
		cpp_part SC
	helper Add (|string| name)
		|| rz = guicontrol(self)
		rz.ID = name
		self <~ rz

	setter Collapsed (|bool|)
		opt norefcounts
		.div.setcollapsed(false, value)
	function Collapsed (|bool|)
		opt norefcounts
		return !.div.ActiveEnd.length
		
	function GuiControl.PrintSizes
		print .Sizer.GSize.Render + " : "

	function Reset
		opt norefcounts
		.div.ActiveEnd = .div.start
	
	function Collapse (|bool| soft = false)
		opt norefcounts
		.div.Collapse(soft)
	
	function AddHigh (|guicontrol| G)
		opt norefcounts
		.div.high <~ g
		
	function AddLow (|guicontrol| G)
		opt norefcounts
		.div.low <~ g

	helper div (|stretchdivider|)
		return .last|stretchdivider|
	
	function Show
		opt norefcounts
		.div.show	
		
	module
		function Horz (|guicontrol| p, |controlpoint| start, |int| min=0, |int| max=0, |stretchcontrol|)
			return .Build(p, start, min, max, 0)

		function Vert (|guicontrol| p, |controlpoint| start, |int| min=0, |int| max=0, |stretchcontrol|)
			return .Build(p, start, min, max, 1)

		function Build (|guicontrol| p, |controlpoint| start, |int| min, |int| max, |int| d, |stretchcontrol|)
			cpp_part It
			|StretchControl| sG    = StretchControl(p)
			sG.add(("bottom", "left" )(d))
			sG.add(("top",    "right")(d))
			|| SD   = stretchdivider(sG, d) 
			SD.start= start
			SD.min  = min
			SD.max  = max
			sd.InitGroup
			return sG



class StretchDivider (GuiControl)
	linkage:cpp_part Div
	|guiAction|					OnHide 
	|guiAction|					OnShow
	|guiAction|					OnStretch
	|int|						Min
	|int|						Max
	|controlpoint|				Start
	|date|						Entered
	|int|						d


	constructor (|guicontrol| p,  |int| d)
		super.constructor(p)
		isnt drawsfocus
		.d = d
		.takesfocus = 0
		.gnormal.solid = (0.65, 0.75, 0.0, 1.0)

	
	function InitGroup
		opt norefcounts
		cpp_part SSST
		.id = "stretch"
		|| A = .low
		|| C = .high
		|| s = .start
		if A == "left" // noice
			|| H = the~height
			A.size = the~left to s		by  H
			C.size = the~rest			by  H
		  else
			|| W = the~width
					 // down/up controls split by horz-line
			A.size = W  by  the~bottom	to  s
			C.size = W  by  the~rest
		.initdiv


	function InitDiv	
		|| W = 3~before  for 6
		if .collapsed
			W = 0 ~ before for 4
		|| H = W
		if .d
			W = the~width
		  else
			H = the~height
		.size = W by H
		(.OnStretch)(self)?


	function Collapsed (|bool|)
		return !.ActiveEnd.length


	function Collapse (|bool| soft = false)
		opt norefcounts
		if !.collapsed
			.start = .ActiveEnd
			.ActiveEnd = (.min, 0~right)(soft)


	helper SetCollapsed (|bool| Soft, |bool| value)
		if value
			.collapse(soft)
		  elseif .collapsed
			.show


	helper ClickedFlip
		if .Collapsed
			if .min > 0 // shouldnt be possible
				(.onshow)(self, 1)?
			  else
				.setCollapsed(true, false)
		  else
			if .min > 0
				(.onhide)(self, 0)?
			  else
				.setCollapsed(true, true)

	function Show
		opt norefcounts
		|| pr = .parent.lastrect
		|| s = .start.length
		if s < 10
			s = mix(pr[.d], pr[.d+2], 0.333)|int|
		if .ActiveEnd.length < s
			.ActiveEnd = (s)~up

		
	function High (|guicontrol|)
		opt norefcounts
		return .parent.second
	function Low (|guicontrol|)
		opt norefcounts
		return .parent.first

	function ActiveEnd (|controlpoint|)
		opt NoRefCounts
		return .low.sizer[2+.d]
	
	setter ActiveEnd (|controlpoint|)
		opt NoRefCounts
		.low.sizer.g(2+.d) = value
		.InitDiv
		
	behaviour MouseDown
		require .InTest(P, false)
		if keys.cmd or (clicks == 2)
			.ClickedFlip
			return 1
		|| pr = .parent.lastrect Â± 1
		|| x  = p[.d] - pr[.d]
		(x max= .min)
		(x min= pr.size[.d]-.max) 
		.ActiveEnd = (x)~up
		return 4


	helper InTest (|ivec2|P, |bool| MightBeOut, |bool|)
		|| Out = MightBeOut and !(.lastrect contains P)
		|| dateout = !.entered
		if Out != dateout
			sdlapp.touch(Self)
			.Entered = (0,date())(out)
		if sdlapp.MouseButtons is left
			|| d = date()-0.75s
			if .Entered > d
				.Entered = d
		return !Out


	behaviour MouseMove
		opt norefcounts
		if .MouseNotDownOverOther
			return .intest(p, true)


	helper FadeAlpha (|float|)
		|| now  = date()
		|| diff = now - .entered
		|float| f = (diff.seconds - 0.6)*2.0
		rz = f.clamp
		if rz >= 0 and rz < 1
			sdlapp.touch(Self)


	behaviour Draw
		opt norefcounts
		require .MouseNotDownOverOther and (.entered)
		|| a    = .FadeAlpha * 256.0
		|| rect = .lastrect
		|| h    = rect.size[1-.d]
		|| xy   = rect.xy
		|| d    = .d
		|| w	= (3,2)(.collapsed) 
		xy[d]  += 4-w
		require where contains xy
		|| m    = h*0.5
		|| md   = 1.0/m
		|| add  = (d, 1-d)
		||stride= (where.width, 1)(d)
		|| col  = .gnormal.edge
			
		for j in h
			|| p  = where[xy]
			xy += add
			|| hd = (j - m).abs
			col.a = ((1.0-hd*md)*a).clamp(255)
			for w
				p.overlay(col)
				p += stride


function GuiControl.MouseNotDownOverOther (|bool|)
	opt norefcounts
	return (.window.CurrMouseDown == self or nil)
