 
// confirm, ask, confirmit, beep, alert
module beep
	|date| OK = 1
	syntax expect (|string| s=nil)
		app.beep(s)


function errorlist.beep (|bool|)
	if .ok
		return true
	|| err = .FirstError()
	|| s = (err.render, "Unknown error occurred.")(err)
	app.beep(s)


extend_module app
	function BeepSub
		cpp_Wrapper JB_App__Beep
	
	function beep (|string| q="")
		if beep.OK.fired
			.beepsub
			beep.OK = date() + 1.5s
		if q
			.alert(q)
	
	function Alert (|string| Q)
		if sdlapp.StartedProperly
			ConfirmIt(q, "", ["OK"])
		  else
			sdlapp.pendingalerts <~ q


function ConfirmCancel (|string| Q,  |string| Title="",  |int|)
	|| Answers = ["Yes", "No", "Cancel"]
	return ConfirmIt(q, title, answers)


function AskSaveOrCancel (|string| Q,  |string| Title="",  |int|)
	|| Answers = ["Save", "Dont", "Cancel"]
	return ConfirmIt(q, title, Answers)


function string.confirm (|string| Title="", |ConfirmCanceller| Canceller=nil, |bool|)
	|| Answers = ["Yes", "No"]
	return ConfirmIt(self, title, Answers, Canceller) == 1


function string.ask_named (|string| Answers,  |int|)
	return ConfirmIt(self, "", Answers.Components)


function string.Ask (|string| response="",  |string| Title="",  |string|)
	|| Answers = ["OK", "Cancel"]
	if ConfirmIt(self, title, Answers, response) == 1
		return ConfirmWindow_.asker.response.text


function guicontrol.VisText (assigns:|string|s)
	.textset(s)
	(self is visible) = s != ""


function ConfirmWindow_.Resize (|string| s)
	|| desired = .question.TextSize(s) + (4, 34)
	|| actual = desired max (200, 100)
	actual = actual min (350, 450)
	actual = actual min (SDLApp.Screen.x2y2 - 8)
	.size = actual.layout


function window.CanJustCloseForQuit (|bool|)
	opt norefcounts
	|| count = 0
	if self isa ConfirmWindow_
		for b in .Buttons
			if b is visible
				count++
		return count == 1


function ConfirmIt (|string| Q,  |string| Title,  |[string]| answers,  |string| Response=string.byte(0), |ConfirmCanceller| Cancel = nil, |int|)
	|| w = ConfirmWindow_.asker init ConfirmWindow_()
	if !q or w is visible		// recursive... sigh
		if answers[2]
			return -1			// cancel
		return 0

	w.DelayUntil = date() + 0.5s
	with w
		.text = title
		.Resize(q)
		.question.text = q
		for i in 3
			|| b  = .buttons[i]
			|| bw = b.textwidth+8 max 40
			b.vistext = answers[i]
			b.size    = 3~after for bW  by 3~up  for 17
		.SetResponse(response)
		
		|| p = (W)(Cancel)
		ifn (.response isnt visible) or (p != .yes) or (.response.text)
			beep
			
		.text = nil
		.question.text = nil // clear memory :)

		if p == .yes
			return 1
		if p == .cancel or nil
			return -1

	
	
class ConfirmWindow_ (Window)
	linkage
		cpp_part cwin
    |textview|			Question
    |textview|			Response
    |Buttonview|		Yes
    |Buttonview|		No
    |Buttonview|		Cancel
    |[Buttonview]|		Buttons
    |date|				DelayUntil // for accidental presses.

    constructor
		super.constructor
		.id				= "confirm"
		.Yes			= buttonview(self, OnConfirmAnswer)
		.No				= buttonview(self, OnConfirmAnswer)
		.Cancel			= buttonview(self, OnConfirmAnswer)
		.response		= textview(self, nil)
		.Question		= textview(self, nil)
		.gnormal        = Â¥.systemstyle 
		.fullscreen		= (300, 150)
		.AddButton(.yes, "Yes", true)
		.AddButton(.No,	 "No" , false)
		.AddButton(.Cancel, "Cancel", false)
		
		isnt visible
		.init
		

	function SetResponse (|string| response)
		opt norefcounts
		|| R = .response
		|| vis = response != string.byte(0)
		r.visible = vis
		if vis
			r.text = response
			r.SelectAll
			r.SetFocus


	function Init
		with .question
			 is AlwaysScrollable // why isnt
			.id			= "question"
			.size 		= 2~inside  by  1~above to the~top-2
			.takesfocus = 0
			.gnormal.edge<~ (0,0,0,0)
			.gnormal.bg  <~ ((0.11,0.11,0.11,1.0))
		
		with .response
			is editable
			isnt drawsfocus
			.id			= "response"
			.singleline = true
			.gnormal	= self.question.gnormal
			.size		= 2~inside  by  2~above for 15

		
	behaviour KeyDown
		opt NoRefCounts
		
		|| cans = key.iscancel
		if (.cancel is visible) and cans
			.returnvalue = .cancel
			return true

		if (.no is visible)
			if cans or ((.response isnt visible) and (key.isdelete or key is n or d))
				.returnvalue = .NO
				return true

		if (.yes is visible) and (key.IsAccept or cans)
			.returnvalue = .YES
			return true
		return super.keydown(key)


	function AddButton (|ButtonView| BTN, |string| Oof, |bool| first)
		.Buttons <~ btn
		btn.ExpandOnMouseOver = 2
		btn.Text			= Oof
		btn.gnormal.solid	= (0.15, 0.15,0.15,1.0)
	
	module	
		|ConfirmWindow_| asker



function OnConfirmAnswer (GUIAction)
	opt norefcounts
	.window.ReturnValue = self // self isa button, not a window! (i made this mistake once)



prototype fnConfirmCanceller (|object| self, |bool|) 
class ConfirmCanceller
	linkage
		cpp_part cncl
	|object|					Owner
	|fnConfirmCanceller|		Func
	syntax call (|bool|)
		if self
			return (.func)(.owner)
	
