

class FileRow (ListViewRow)
// this would be better if it were simply a normal class set on message.Obj
// then fileviewer can handle it...
	linkage
		cpp_part FR
	|string|						Location
	|Document?|						_Document
	|date|							DateOfFileAtRead
	|string|						DirFile
	|int|							LastSize
	|int|							LastScroll
	|int|							LastFileCount
	|byte| 							FindID
	|bool|							HasError
	|bool|							InMainProj
	|bool|							AsDir
	|bool|							DoesntExist
	|bool|							SameNameAsPrev
	contains FileRow
	
	 
	constructor (|FileRow| R)
		cpp_Part ConstructorCopy
		.func = r.func
		.location = R.location
		.dirfile	= r.DirFile
		.asdir		= r.asdir
		.name		= r.name
		.lastsize	= r.lastsize
		.rangelength = r.rangelength // for the sorter
		.lastfilecount = r.lastfilecount
		.DoesntExist = r.DoesntExist
		.InMainProj = r.InMainProj
		is hierarchical
	
	constructor (|string| path, |string| name, |string| DirFile="")
		.func		= @file
		.rangelength = 0 // for the sorter

		.Location	= (path.PathDir, path)(DirFile)
		.dirfile	= DirFile

		.asdir		= dirfile
		.name		= name
		if path and !.asdir
			.lastsize = path.File.Size
		.lastfilecount = (!.asdir)|int|
		is hierarchical
	
	behaviour CopyID
		return .location
	
	render
		fs <~ "file: "
		fs <~ .location
		fs <~ .dirfile
		
	
	behaviour DisplayName
		if i == 0
			return .name

	operator isa (|string| s, |bool|)
		return .location isa s
	

	function AskDelete (|fileviewer| fv)
		|| ex = fv.extra
		if ex contains self
			.remove(fv)
			return
		|| l = .Location$
		require keys.shift or "Delete file: ${l.name}?\n\nAt ${l.parent}".confirm
		if l.file.MoveToTrash
			.remove(fv)
		fv.checksize
	
	
	function MoveTo (|string| v, |bool|) // rename
		if (v contains '/') or (v.FindBadUTF8) or (v.linecount > 1)
			beep v + " is not a valid filename!"
			return false
		
		if .location.name == v
			return false
		|| l = .location.file
		|| f = l.sibling(v)
		if .location != f
			if f.Exists
				beep v + " already exists!"
			  elseif l.moveto(f)
				.location = f.path
				.name = f.name
				.parent.filesort
				|| d = ._Document
					d.MoveTo(.readfrom)
				rz = true


	function IsDir (|bool|)
		disabled "use .Asdir. Not quite the same, but usually close enough"
	
	
	function ReadFrom (|string|)
//		.Sanity
		return .Location + .dirfile


	helper IsAlsoDir (|file| f, |bool|)
		return f.isdir == .asdir
	
	
	function TryReload (|fileviewer|fv)
		require .asdir
		|| l = .location
			|| f = l.file
			|| d = f.modified
			if d > .DateOfFileAtRead
				.DateOfFileAtRead = d
				.ReloadFiles(f, fv)
			fv.checksize
	

	function AddFile (|file| f, |fileviewer| o)
		|| Count = 2000
		.AddFileSub(f,  o.excludes,  o.folfile,  Count)


	helper AddFileSub (|file| f, |dictionary?| excl, |string| folfile, |$int| Count, |bool|)
		|| C = Count--
		require C > 0
		|| r = filerow(f.path, f.name)
		self <~ r
		if f.isdir
			r.location = r.location.pathdir
			r.dirfile = folfile

		if f.IsntAnOSXBundle and !f.issymlink
			r.asdir = true
			r.FillFilessub(f, excl, folfile, Count)
		return true


	function RemoveFile (|filerow| r, |fileviewer| fv)
		for c.flat in r
			|| d = c._document
				if d.ismodified
					r.DoesntExist = true
					fv.select(r)
					return
		r.remove


	function ReloadFiles (|file| dir, |fileviewer|o)
		|| excl = o.excludes
		|dictionary of filerow| found = dictionary()
		for s in self
			found[s.name] = s

		for name in dir
			|| ch = dir[name]!
			ifn (name[0] == '.') or (excl[name.ext])
				|| f = found[name]
				if f and f.IsAlsoDir(ch)
					found[name] = nil
					f.DoesntExist = false
				  else
					.addfile(ch, o)
		
		for lost in found
			.removefile(lost, o)
		.FileSort


	helper FillFilesSub (|file| fol, |dictionary?| excl, |string| folfile, |$int| Count)
		.DateOfFileAtRead = fol.modified
		for f.files in fol // merge these two
			|| name = f.path.name
			|| ext = name.ext
			if (name[0] != '.')  and  (!excl[ext])
				if !.AddFileSub(f, excl, folfile, count)
					exit
		.filesort


	function ReadName (|string|)
		|| n = .location.name
		if .dirfile
			return n.pathdir + .dirfile
		return n


	function IsModified (|bool|)
		return ._Document$.ismodified


	function Document (|document|)
		opt norefcounts
		|| s = ._Document
			return s
		s = document(.readfrom)
		._Document = s
		return s


	function FileData (|string|)
		|| d = ._Document
			if d.current and d.SimpleUpdateFromDisk
				return d.text
		|| f = .readfrom.file
		require !f.isdir and f.Exists

		// how do we know a file is binary? just... by checking it... really.
		|| x = f.open(file.readmode)
			|| preview = f.Read(4K)
			|| fsize = f.size
			|| badness = preview.UTF8Badness
				require  badness.x >= 4093  and  fsize > 4096 // heuristic, in case we cut a char in half.
			if fsize <= 4096
				return preview
			return f.ReadAll


	function FindAllParent (|message| FoundList, |error|)
		rz = error(nil, .location, errorseverity.ok)
		(rz|listviewrow| is expanded)
		rz.name = ("\11 ", "\19 ")(!.asdir) + .location.name
		rz.path = .location
		rz.obj = self
		rz.position = -1
		FoundList <~ rz


	function FindInFile (|string| text,  |message| FoundList,  |$int| Remain,  |bool| DoRepl)
		require text
		|| t = .FileData$
		
		|error| parent
		|| Lim = Remain
		|| i = 0
		|| LastBefore = -1
		
		while (i < t.length) and (lim > 0)
			|| R	= t.StretchyFind(text, i)
			|| p = r.x
			if p < 0
				exit
			i = r.x2
			|| after  = t.find(Jeebox.CSLine, i, i+30)   ??  (i+30)
			|| before = t.find(Jeebox.CSLine, p, p-30 max 0)   ??  (p-10)
			|| s = t[before, after]
			if DoRepl or (lastbefore != before)
				LastBefore = before
				
				if !text[0].iswhite
					s = s.trim.remove(jeebox.csline)
				|| result = error(nil, ErrorSeverity.OK)
				parent ?= .FindAllParent(FoundList)
				parent <~ result
				
				lim--
				
				result.range = R
				result.obj = self
				result.path = .location
				result.name = s
		Remain = lim


	syntax append (|filerow| r)
		super.syntaxappend(r)
		r.inmainproj = .InMainProj


	helper Match (|string| path, |bool| NameOnly, |int|)
		rz = .location.name(nameonly).FileViewMatch(path)
		if .dirfile
			|| r = .readfrom
			if rz and path.last != '/' and .readfrom.file.size == 0
				rz = 0 // folder matched uselessly
			  else
				rz = rz max r.name(nameonly).FileViewMatch(path)

	module
		function FindFileName (|listview| s, |string| name, |filerow|)
			for r.all in s
				if r isa filerow
					if r.location.name ~= name
						return r

		helper error.ViewDate (|date|)
			opt norefcounts
			|| f = .obj as FileRow
				|| d = f._Document
					return d.ViewDate
		
		function BatchFindSorter (SorterComparer of Error)
			// the document viewfrom date logic elsewhere
			// via load seems buggered somehow.... not sure why?
			|| an = .path.name 
			|| bn = b.path.name 
			|| ae = an.ext
			|| be = bn.ext
			// spds first.
			rz = isbetter(ae, be, "spd")
			if !rz
				|| af = .ViewDate
				|| bf = b.ViewDate
				rz = bf - af
			if !rz
				rz = isbetter(ae, be, "box")
			if !rz
				if be ~!= ae
					rz = ae.syntaxcompare(be)
				  else
					rz = an.syntaxcompare(bn)

		helper IsBetter (|string| a, |string| b, |string| ext, |int|)
			if (b == ext) != (a == ext)
				return (a==ext)+1

		
	

helper string.FindExistingPath (|string|)
	while (self > 1) and !.fileexists
		self = self.parent
	return self


helper ListView.ListFilesUp (|string| InPath, |string| types, |bool| Save, |int| max = 2K, |string|)
	|| path = InPath.TrimSlashes.FindExistingPath
	require .listfiles(path, types, save, max) and inpath // also calls clear
	|| r = filerow(path.parent, "..", ".nothing")
	.listsource.first = r
	|| f = path.file
	|| r2 = filerow.FindFileName(self, path.name) as filerow
	if f.exists and !f.isdir and r2
		.show(r2)
		r = r2
	.select(r)
	return path
			

function string.TypeContains (|string| s, |bool|)
	// maybe neater to do this with string matching.
	// its usally used for things ending with '/' anyhow
	|| Last = 0
	while
		|| Word = .NextField(',', Last)
		if Word == nil
			exit
		if word ~= s
			return true


function ListView.ListFiles (|string| InPath, |string| types, |bool| save, |int| max = 256, |bool|)
	|| s = .listsource
	s.clear
	|| path = InPath.Resolve(true)			#require
	|| AllSight = (path.name == ".")
	|| AtRoot   = (path == "/")
	|| pf		= path.file 
	if !pf.exists or !pf.isdir
		pf = pf.parent
	
	for f.files in pf // merge these two
		|| p = f.path
		|| n = p.name
		if AllSight or n.PlatformVisible(ATroot)
			if --max <= 0
				exit
			|| isdirlike = f.IsntAnOSXBundle
			|| ext = n.ext
			if isdirlike
				p = p.PathDir
			|| icon = filerow.FileIcon(p, types).Render
			n = icon + " " + n
			|| r = filerow(p, n, (".nothing", "")(isdirlike))
			self <~ r
			|| IsOK = isdirlike
			if !save and !isdirlike and !types and f.isdir
				isok = false // can't open package if we want files
			  elseif save and isdirlike and types.typecontains(ext + "/")
				isok = false
			  elseif !save and !isok
				isok = !types
				if !isok
					if isdirlike or f.isdir
						ext = ext.pathdir
					isok = types.typecontains(ext)
			(r isnt disabled) = IsOK 
			
	s.filesort
	return true


