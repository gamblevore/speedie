

// todo whenever its fun?
// * 4 threads, each can do one quad.
// * default RayTracer constructor uses block positioning system to match 2D/3D closely
// * App seems to still have "unstable" frames? (we need to log the framegap to figure out whats going on)
// * Eliminate drift by using "NextInSecond" number which is always stable
	// so for 10fps... it would be like 6553, 13107, 19660, 26214, 32K... etcetc

// advancements: (do AFTER we have perry+speedie in an awesome state)
// 1) Make SpaceTree work
// 2) Optimisations around unmoving objects
// 3) hit-detection? (surely its just chunk vs chunk anyhow?)
// 4) more shapes:
	// * box    / sloped voxels / blends with neighbours
	// * pipe   / half-pipes    / bends
	// * sphere / capsule       / blobs
	// * SDFs
	// (All shapes: Provide raydist for drawing, nearest point for hit-detection)

	



class RayTracer
	visible false
	|vec4|			Color
	|float|			Dist
	|float|			MaxViewDistSq
	//
	|vec4|			Size
	|vec2|			Scale
	|vec2|			Adjust
	|vec4[4]|		Planes
	//
	|Vec3|			_CamDir
	|vec3|			iDir
	|Mat4|			_LookAt
	|Vec3|			_Camera
	//
	visible true
	|[vobject]|		Shapes
	|float|			Pitch
	|float|			Spin
	
	constructor (|vec3| Camera)
		// Default RayTracer constructor should use the block positioning system
		// either by zpos or zoom
		._LookAt.reset
		.MaxViewDistSq = 1000.0.pow2
		.Camera(Camera)
	
	
	function Camera (|vec3|)
		return ._Camera

	setter Camera (|vec3|)
		._Camera = Value|vec4|.MakeSane|vec3|
	
	
	helper ViewMat (|&mat4|)
		.spin = .spin.Fract
		.Pitch = .Pitch.fract
		._LookAt = .spin.SinCos.RotateAroundY	// could make a faster version that creates already transposed thing
		|| M = .pitch.SinCos.RotateAroundX   	// or just creates needed values and applies directly
		._LookAt *= M							// and only needs a "virtual" vec3
		return ._LookAt
	
			
	helper AntiAlias (|vec2| XY, |!&raytracenode| curr, |&imagepixel| pxl)
		opt min 3
		|| ID = pxl.a
		|| C = pxl.Color
		for i in 4
			|| sh = (i&1, i>>1).Vec2 - 0.5
			c += .GetColor(XY + sh*0.8, curr)
		C *= 5th
		C.a = ID|float|*255th
		pxl <~ C
	
	helper GetColor (|vec2| XY, |!&raytracenode| Rt, |vec4|)
		// Should allow alpha for drawing over prev images...
		// Meaning that the alpha can't be the ID!
		opt min 3, opt norefcounts
		.Color = nil
		.Dist = float.max
		._CamDir = .Dir(xy)
		.idir = -1.0 / ._CamDir
		
		while rt
			|| sp = rt.draw!
			|| t = (sp.Shape)(sp, .Camera, ._CamDir, .idir)
			|| tx = t.x
			if (tx > 0) and (tx < .Dist)
				.Dist = t.x
				|| P = .Camera + ._CamDir*tx
				|| Norm = (P - sp.pos).normal
				|| C = (norm.abs * sp.Color.xyz)
				.color = (C, sp.shapeID|float|*255th)
			rt = rt.next
		return .Color
	

//	helper Dir (|int| X, |int| Y, |vec3|)
//		cpp_part DirInt
//		return .Dir((x,y).vec2)
	
	helper Dir (|vec2| XY, |vec3|)
		XY = (xy + .adjust) * .scale
		return (xy, 1.0).Normal * ._LookAt
	
	
	helper Plane (|vec4| P, |vec4|)
		|| D1 = .Dir(P.xy)
		|| D2 = .Dir(p.x2y2)
		|| C = ._Camera
		return Plane3D.normals(d1, d2, C)


	helper InPlanes (|vobject| b, |bool|)
		opt min 3
		|| D = (b.pos - ._Camera).lengthSq // should use the radius if a sphere, or at least be able to ask for "the nearest point" from the function.
		require D > 0.25 and D < .MaxViewDistSq
		|| R = (b.radius, 0.0)
		|| Pos = (b.pos, 1.0)
		
		for i in 4
			|| Q = .planes[i].BoxVsPlane(r, pos)
			require Q > 0
		// Could also (later) check if the nearest frustrum point is in the box.
		// Need points[8] and a way to find which is the nearest to the box, or just check all?
		return true
			
	
	helper DoFrustrum 
		.Planes[0] = -.plane(.size.x1y1x1y2) // 0001 // left
		.Planes[1] =  .plane(.size.x2y1x2y2) // 1011 // right
		.Planes[2] =  .plane(.size.x1y1x2y1) // 0010 // bottom
		.Planes[3] = -.plane(.size.x1y2x2y2) // 0111 // top
	
	
	function CameraChangedSinceLastTrace (|bool|)
		// return if campos+matrix is the same as last time...
		return true
	
	
	helper PlayQuidditch (|bool|)
		opt norefcounts,  opt min 3
		|| B = raytracer.GrabRoot(32)
			raytracer.currNode = raytracer.StartNodes
			.DoFrustrum
			if .CameraChangedSinceLastTrace
				b.FillQuads(self)
			for s in .shapes
				if .InPlanes(s)
					b.Quadditch(s)
			return true
	
	
	function RayTrace (|image| where)
		opt min 3
		.ViewMat
		.size = where.size.__xy.vec4
		.scale = 1.0/.size.x2x2
		.adjust = .size.x2y2 * -0.5
		if .PlayQuidditch
			|| rn = rootnode!
			|| C = Cells!
			|| LastCell = C+rn.area
			while C < LastCell
				if c.node
					c.Stride = where.width
					c.pxl = where[c.xy.ivec2]
					.TraceCell(C)
				c++


	helper TraceCell (|&raytracecell| cell)
		// super-awesome system to reduce stack use
		// it might seem like 6 vars is a lot, but it could easily be 14...
		// if I didn't work miracles to reduce all this!
		opt min 3
		|| P = cell.xy
		|| pxl = cell.pxl
		|| pxl3 = pxl - cell.stride*cell.h
		while pxl > pxl3
			|| pxl2 = pxl + cell.w
			while pxl < pxl2
				#! Main pixel color
				cell.Aliased = false
				pxl <~ .GetColor(P, cell.node!)
				
				|| Left = pxl-1
				if p.x and pxl.a != left.a
					#! Anti-alias the left??
					.AntiAlias(p-(1.0,0), cell.node!, left)
					.AntiAlias(p, cell.node!, pxl)
					cell.Aliased = true
				
				if p.y
					#! Maybe below too??
					|| below = pxl + cell.stride
					if pxl.a != below.a
						.AntiAlias(p-(0,1.0), cell.node!, below)
						if !cell.Aliased
							.AntiAlias(p, cell.node!, pxl)
				pxl++
				p.x++
			pxl -= (cell.stride + cell.w)
			p.y++
			p.x = cell.xy.x
		cell.node = nil
	
	
	module
		helper GrabRoot (|int| px, |int| Size=4MB, |&RayBoost|)
			|| s2 = (0,0, gui.Screen.Width, gui.Screen.height)
			px = 1<<px.log2

			|| root = .RootNode
				if root.size == s2 and root.px == px
					return root
				
			root = memory.Zalloc(size)|&RayBoost|$
			.RootNode = root
			.EndNodes = (root|&byte| + Size)|&RayTraceNode|
			root.px = px
			root.size = s2
			
			|| D = s2.x2.ContainingPowerOfTwo
			|| px2 = px.log2
			|| s3 = (s2.x2y2+(px-1)) >> px2
			root.cellswide = s3.x
			root.cellshigh = s3.y
			
			|| Last = root.InitialCreation(root+1, D-1, px2)
			.cells = last|&Raytracecell|
			.StartNodes = (.cells+root.area+1)|&RayTraceNode| // +1 for fakecell
			while last-- > root
				if !last.depth and last.px
					last.init
			return root

		
		|&RayBoost|			RootNode
		|&RayTraceCell|		Cells
		|&RayTraceNode|		StartNodes
		|&RayTraceNode|		CurrNode
		|&RayTraceNode|		EndNodes




struct RayBoost
	|vec4|					XDiv
	|vec4|					YDiv			
	|ivec4|					Size
	|!(&RayBoost)[4]|		Quad			// rather shrink Quad
	|byte|					Depth	
	|byte|					PX
	|byte|					CellsWide		// shared with root
	|byte|					CellsHigh		// shared with root
	|byte|					ThreadLock		// In case I want to multi-thread
	

	function Area (|int|)
		return .CellsWide|int| * .CellsHigh|int|


	helper Init 
		for i in 4
			.quad[i] = .InitCell(i&1, i>>1)|&rayboost|

//	function PrintStats
//		|| n = .area
//		|| Total = 0
//		for i in n
//			Total += raytracer.startnodes![i].Count
//		"$Total objects in $n chunks. (avg: ${total pc n}/chunk)"
			

	helper Quadditch (|vobject| v, |int| i=-1)
		real self
		if i >= 0
			|| D = .depth
			self = .quad[i]!
			if !D
				|| Cell = self|&raytracecell|
				|| RT = raytracer.currNode!++
				|| Old = cell.node
				cell.node = RT
				RT.next = old
				RT.Draw = V
				return
			require .PX
		
		// for big chunks, we should also test that the quad corners are in the object.
		// inigo quilez has a good article on this.
		|| Rd = (v.radius, 0.0)
		|| RX = Rd sign .xdiv
		|| RY = Rd sign .ydiv
		|| VP = (V.pos, 1.0)
		|| L = .xdiv • (VP + RX) > 0
		|| R = .xdiv • (VP - RX) < 0
		|| B = .ydiv • (VP + RY) > 0
		|| T = .ydiv • (VP - RY) < 0
		
		if (b|||t) & (l|||r)
			if (L+R)+(B+T) == 2
				return .Quadditch(V, R + (T<<1)) // tailcall
			
			if B
				if L
					.Quadditch(V, 0)
				if R
					.Quadditch(V, 1)
			if T	
				if L
					.Quadditch(V, 2)
				if R
					return .Quadditch(V, 3)

	function FillQuads (|raytracer| T)
		opt min 3
		|| S = .size.vec4
		require s.ispositive             // do nothing
		|| Sh = .px+.depth
		|| mid = s.xy+(1 << sh)|float|
		.yDiv = t.plane((  s.x2, mid.y,   s.x, mid.y)).makesane
		.xDiv = t.plane((mid.x,    s.y, mid.x,   s.y2)).makesane
		
		if .Depth
			for i in 4
				.quad[i].FillQuads(t)
	
	
	helper InitCell (|int| x, |int| y, |&RayTraceCell|)
		|| sz = .size
		|| CellStart = sz.xy + ((x,y) << .px)
		|| CellEnd = (CellStart + (1<<.px)) min sz.x2y2
		|| D = CellEnd - CellStart
		rz = RayTracer.Cells!
		if d.x > 0 and d.y > 0
			|| i = (CellStart>>.px)  fma  .CellsWide
			rz  +=  i
			rz.xy = CellStart.vec2
			rz.w = d.x
			rz.h = d.y
		  else
			rz  +=  .area // fake cell. should never be reached? but might be?
	
	
	helper InitialCreation (|!&RayBoost| Last, |int| H, |int| px, |!&RayBoost|)
		.depth = H-px
		.px = px

		if h <= px
			return last

		for i in 4
			.quad[i] = last++
		
		|| bounds = .Size
		|| xy = bounds.xy
		|| hh = 1<<H
		|| LB = (xy, xy+hh)
		for y in 2	
			for x in 2
				|| S = lb + hh * (x,y,x,y)
				|| Q = .Quad[x + y<<1]!
				Q.depth = 0
				Q.size = s.clip(bounds)
				q.CellsWide = .CellsWide
				q.CellsHigh = .CellsHigh
				if q.size.ispositive
					last = Q.InitialCreation(last, H-1, px)
				  else
					q.px = 0 // unreachable
		
		return last



struct RayTraceNode
	|?vobject--|				Draw
	|?&RayTraceNode|			Next
	// could be nice to put 3 items on here.
	// this makes it cache nicely! faster memory :)


struct RayTraceCell
	|vec2|						XY
	|?&RayTraceNode|			Node
	|&imagepixel|				pxl
	|uint16|					Stride
	|byte|						H
	|byte|						W
	|bool|						Aliased
	
//	function Count (|int|)
//		|| w = .node
//		while w
//			rz++
//			w = w.next


function int.ContainingPowerOfTwo (|int|)
	|| p = .log2
	|| rem = self - p
	return p + (rem!=0)


function Vec4.BoxVsPlane (|vec4| R, |vec4| Pos, |float|)
	description "Pos.w must be 1.0,  r.w must be 0.0"
	|| RB = R sign self
	|| PX = pos + RB
	|| Q = self • PX
	return Q

