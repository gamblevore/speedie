

// should really just be a module... no need to have a whole class for this
class AppMenu (Window)
	linkage:	cpp_Part AM
	|ListView|					Choices

    constructor
		super.constructor
		.id					= "appmenu"
		.ClickOutsideCloses = true
		.Choices			= ListView(self,	nil,	MenuReturnkey)
		.gnormal.edge = (0.5, 0.0, 1.0, 0.5).px
		.init

	
	libinternal ScreenModeStr (|string|)
		return "Go FullScreen"

	libinternal Init
		is visible
		isnt drawsfocus
		.id = "AppMenu"
		.setfocus
		|| first = "About " + app.filename
		|| it = .choices
		it <~ (first,						MenuAbout)
		it.addsep
//		it <~ (.ScreenModeStr,				MenuScreen) // doesn't quite work yet. locks up the mac!
		it <~ ("Help",						MenuHelp)
		it <~ ("Quit",						MenuQuit)
		it.gnormal = ¥.Menus 
		it.gRowSel = ¥.MenuRowSel 


	behaviour KeyDown
		if key is escape
			.cancelreturn
			.Choices.selectnone
			return true
		return super.keydown(key)


	function ShowAsDialog
		|| n	= .Choices.listsource.Count
		|| mh	= n * .Choices.rowheight + 4
		.size	= 160~wide by (mh)~high
		.Choices.size = the~area
		.Choices.selectfirst
		
		|| p = (self)()
		if p isa menuitem
			.Choices.selectnone
			(p.MenuEvent)(self, p, nil)?


	module
		|AppMenu|	Menu
		function SyntaxAppend (|string| name, |guimenuevent| ev)
			|| where = .GetAppMenu.Choices.listsource.second
				where.next = menuitem(name, ev)

		inline  AddSep
			self <~ ("", nil)

		libinternal GetAppMenu (|appmenu!|)
			rz = .menu
			if !rz
				rz = appmenu()
				.menu = rz
				rz.hide

		libinternal Show (|int|)
			|| m = .GetAppMenu
			m.choices.setfocus
			m.ShowAsDialog
			return SDLActivity.activity


		libinternal MenuScreen (GuiMenuEvent)
			|| W = GUI.ScreenWindow #expect beep
//			gui.SDL_SetWindowFullscreen(W, GUI.WINDOW_FULLSCREEN) // OSX doesn't like this!

		libinternal MenuHelp (GuiMenuEvent)
			|| it = app.Conf("HelpURL")
				app.showurl(it)
				return 
			if self isa listview
				menu.removequietly(self)
			beep "No help available."
		
		
		libinternal MenuQuit (GuiMenuEvent)
			GUI.QuitReason |= sdlactivity.exiting
		

		libinternal MenuReturnkey (GuiListEvent)
			(.window as appmenu)!.ReturnValue = .FirstSelected
			return true
		
		
		libinternal MenuAbout (GuiMenuEvent)
			|| aboutstr = app.Conf("about")
			if !aboutstr
				aboutstr = `$appname v$version $apptime.

By '$creator'.`
			aboutstr = aboutstr.ReplaceAll(`$appname`, app.filename)
			aboutstr = aboutstr.ReplaceAll(`$apptime`, app.TimeID|int64|.render)
			aboutstr = aboutstr.ReplaceAll(`$appdate`, app.Created.localtime)
			aboutstr = aboutstr.ReplaceAll(`$version`, app.Conf("version"))
			aboutstr = aboutstr.ReplaceAll(`$creator`, app.Conf("creator"))
			app.alert aboutstr

