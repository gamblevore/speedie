

class StretchControl (GuiControl)
	contains stretchdivider
	linkage
		cpp_part SC
	
	behaviour Resized
		if Initial
			.reset
		
	helper AddPane (|string| name)
		opt norefcounts
		|| rz = GUIControl(self)
		rz.ID = name
		(rz is PassThru)
	
	setter IsCollapsed (|bool|)
		opt norefcounts
		.Divider.setcollapsed(false, value)
	
	function IsCollapsed (|bool|)
		opt norefcounts
		return !.Divider.ActiveEnd.length
		
	function IsOpen (|bool|)
		opt norefcounts
		return !.IsCollapsed()
		
	function Reset
		opt norefcounts
		.Divider.ActiveEnd = .Divider.start

	function Collapse (|bool| soft = false)
		opt norefcounts
		.Divider.Collapse(soft)
	
	function AddHigh (|guicontrol| G)
		opt norefcounts
		.Divider.high <~ g
		
    function AddLeft (|guicontrol| G)
        .AddLow(g)

    function AddRight (|guicontrol| G)
        .AddHigh(g)
        
	function AddLow (|guicontrol| G)
		opt norefcounts
		.Divider.low <~ g

	function Divider (|stretchdivider!|)
		return .last|stretchdivider|!
	
	function Show 
		opt norefcounts
		.Divider.show
		
	module
		function Horz (|guicontrol| p, |controlpoint| start, |int| min=0, |int| max=0, |stretchcontrol|)
			return .Build(p, start, min, max, 0)

		function Vert (|guicontrol| p, |controlpoint| start, |int| min=0, |int| max=0, |stretchcontrol|)
			return .Build(p, start, min, max, 1)

		function Build (|guicontrol| p, |controlpoint| start, |int| min, |int| max, |int| d, |stretchcontrol|)
			|StretchControl| sG    = StretchControl(p)
			(sg is PassThru)
			sG.AddPane(("bottom", "left" )(d))
			sG.AddPane(("top",    "right")(d))
			|| SD   = StretchDivider(sG, d) 
			SD.start= start
			SD.min  = min
			SD.max  = max
			sd.InitGroup
			return sG




class StretchDivider (GuiControl)
	linkage:cpp_part Div
	|guiAction?|				OnHide 
	|guiAction?|				OnShow
	|guiAction?|				OnStretch
	|int|						Min
	|int|						Max
	|controlpoint|				Start
	|date|						Entered
	|int|						d
	|float|						DefaultFraction

	constructor (|guicontrol| p,  |int| d)
		super.constructor(p)
		isnt drawsfocus
		.d = d
		.DefaultFraction = 0.333
		.takesfocus = 0
		.gnormal.solid = (0.65, 0.75, 0.0, 1.0)

	
	function InitGroup
		opt norefcounts
		cpp_part SSST
		.id = "stretch"
		|| A = .low!
		|| C = .high!
		|| s = .start
		if A == "left"								// noice
			|| H = the~height
			A.size = the~left to s		by  H
			C.size = the~rest			by  H
		  else
			|| W = the~width						// down/up controls split by horz-line
			A.size = W  by  the~bottom	to  s
			C.size = W  by  the~rest
		(.OnStretch)(self)?
		.ResizeDivider


	function ResizeDivider
		// W basically overlaps the panes... unless collapsed.
		|| W = 3~before for 6
		if .collapsed
			W = 0~before for 4
		|| H = W
		if .d
			W = the~width
		  else
			H = the~height
		.size = W by H


	behaviour Resized
		.InTest(GUI.mousexy, true)
		

	function Collapsed (|bool|)
		return !.ActiveEnd.length


	function Collapse (|bool| soft = false)
		opt norefcounts
		if !.collapsed
			.start = .ActiveEnd
			.ActiveEnd = ((.min, 0)(soft)) ~ Right


	helper SetCollapsed (|bool| Soft, |bool| value)
		if value
			.collapse(soft)
		  elseif .collapsed
			.show


	function SlowFlip
		if .Collapsed
			if .min > 0 // shouldnt be possible
				(.onshow)(self, 1)?
			  else
				.Show
		  else
			if .min > 0
				(.onhide)(self, 0)?
			  else
				.Collapse(true)


	task SlowShow (taskgui, |int| s)
		constructor
			is Animation
		run
			// now what?
			|| p = .upon.parent$
			|| pr = p.LastRect
			if .upon.ActiveEnd.length < 10
				|| s = .upon.start.length
				if s < 10
					|| d = .upon.d
					s = 0.333.mix(pr[d]|float|, pr[d+2]|float|)|int|
				.upon.ActiveEnd = (.s)~up


	helper DefaultEnd (|guicontrol| P, |int|)
		|| pr = p.lastrect
		return .DefaultFraction.mix(pr[.d]|float|, pr[.d+2]|float|)|int|	


	function Show (|bool| anim=false)
		opt norefcounts
		|| p = .parent$
		require .ActiveEnd.length < 10
		|| s = .start.length
		if s < 10
			s = .DefaultEnd(p)
		if anim
			.slowshow(0.75s, s)
		  else
			.ActiveEnd = (s)~up


	function High (|guicontrol|)
		opt norefcounts
		return .parent.second

	function Low (|guicontrol|)
		opt norefcounts
		return .parent.first

	function ActiveEnd (|controlpoint|)
		opt NoRefCounts
		|| l = .low
			return l.sizer[2+.d]
	
	setter ActiveEnd (|controlpoint|)
		opt NoRefCounts
		|| l = .low
			l.sizer.g(2+.d, self) = value
			.ResizeDivider
	
	behaviour MouseDown
		require .InTest(P, false)
		if keys.cmd or (clicks == 2)
			.SlowFlip
			return mousedragging.handled
		
		.MoveTo(p)
		return mousedragging.Beyond


	function MoveTo (|ivec2| P)
		opt norefcounts
		|| pr = .parent!.lastrect ± 1
		|| d = .d
		|| x  = p[d] - pr[d]
		(x max= .min)
		(x min= pr.size[d]-.max) 
		.ActiveEnd = (x)~up


	helper InTest (|ivec2| P, |bool| MightBeOut, |bool|)
		|| Out = MightBeOut and !(.lastrect contains P)
		|| dateout = !.entered
		if Out != dateout
			.touch
			.Entered = (0, gui.FrameStart)(out)
		if GUI.MouseButtons is left
			|| d = gui.FrameStart-0.75s
			if .Entered > d
				.Entered = d
		return !Out


	behaviour MouseMove
		opt norefcounts
		if state is enter
			(SystemCursor.SizeWE+.d).Use
		
		if .MouseDownOnSelfOrNil
			return .InTest(P, true)


	helper FadeAlpha (|float|)
		|| now  = gui.FrameStart
		|| diff = now - .entered
		|| f = (diff.float - 0.6)*2.0
		rz = f.clamp
		if rz >= 0 and rz < 1.0
			.touch


//	function Image.DrawOutlinedCircle (|vec4| R, |vec4| Inner, |vec4| Edge, |float| EdgeWidth)
//		|| Back		= .Clip(R)$
//
//		|| mid		= r.Center
//		|| maxdist	= ((0.5 * r.width) + 0.5) * 0.15
//		|| scale	= (1.0, r.aspect)
//		|| Dim		= r.area.RangeConvertClamped(2000.0, 0.15, 8000.0, 0.26)
//		|| c1		= inner * inner.a
//		|| MD		= (maxdist / dim).pow2
//
//		for Pix.rect in Back.ivec4
//			|| V = (mid - pix.vec2)*scale
//			|| vv = v • v
//			if MD > vv
//				|| D = maxdist / vv.sqrt
//				self[Pix].add((D-dim)*c1)
	
	
	module
		constants
			kGrabDist = 9
		
		function DrawDash (GUIDrawShader)
			|| C = vec4(0)
			if P.y != rect.y
				C = colors.white(0.1)
			  else
				c.a = 0.05
			if (p.x == rect.x) or (p.x == rect.x2-1)
				c.a *= 0.5
			c.a *= Color.a
			pix[].overlay(C.premul)
	
	
	behaviour Draw
		opt norefcounts
		|| DrawColored = .MouseDownOnSelfOrNil and .entered
		|| rect = .LastRect
		|| xy   = rect.xy
		ifn where contains xy
			Drawcolored = false
		
		|| win = .Window
			|| u = win.currscroll
			if u and u.CanDrawScroll(win) and u.InScrollArea(win.mousexy)
				drawcolored = false
		
		|| a = .FadeAlpha
		|| scale = rect.size.Vec2
		|| h = scale[1-.d]
		|| w = scale[.d]
		scale = 2.5 / (w, h)
		w = 3.0
		xy[.d] += 1
		|| add = (.d, 1-.d)
		|| stride = (where.width, 1)(.d)
		|| col = .gnormal.edge.Color
		|| center = (w, h)*0.5 - 0.5

		// not sure why I neeed these wierd adjustments...
		if !.d
			center.x += 2
			scale.x *= 1.26th
			scale.y *= 2th
		  else
			xy.y+=(!.collapsed)*3
			scale.y *= 1.2th
			scale.x *= 1th

		
		if !.d
			|| TickCount = 5
			|| txy = (xy + add * (h*0.5)|int|) - (kGrabDist*add*TickCount)
			
			if !DrawColored
				a = 0
			
			for ticks in TickCount
				|| box = (txy, txy+(4,2))
				|| B = vec4(1.0)
				if ticks == 0 or ticks == tickcount-1
					b.a = 0.5
				b.a = b.a-a max 0.05
				where.DrawShader(DrawDash, box, b)
				txy += kGrabDist*add
		
		
		if DrawColored
			for j in h
				|| p = where[xy]
				for i in w
					|| diff2 = ((i,j)-center) * scale
					col.a = (1.0 - diff2.abs.sum)*a
					p.overlay(col.PreMul)
					p += stride
				xy += add



function GuiControl.MouseDownOnSelfOrNil (|bool|)
	opt norefcounts
	|| w = .window
		|| c = w.CurrMouseDown
		return (c == self or nil)


function window.CurrScroll (|GUIControl|)
	opt norefcounts
	|| x = ._Scroller
		return x.Upon

