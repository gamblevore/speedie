

module SDLApp
	linkage
		cpp_part App
	|image|						Screen = image("read")
	|error|						ToolTip = error()
	|date|						OpenedFirst
	|GUIPasteEvent|				AppDocumentOpen
	|string|					LastSearch
	|uint|						FrameCount

	private
	|SDL_Event|					Event
	|KeyMod|					KeyMod
	|[string]|					PendingAlerts
	|array?|					DrawDrag = nil
	|&SDL_Window|				ScreenWindow
	|&SDL_Renderer|				Renderer
	|&SDL_Texture|				Raw
	|ivec2| 					NewSize
	|ivec2| 					CurrMouseXY
	|ivec2|						TryResizeLater
	|date|						FrameLength
	|date|						LastFrameDate = 1 // wont fire otherwise
	|date|						LastWindowClose
	|date|						FixScreenBug
	|date|						FastToolTipDate
	|date|						ChangeSelFlash
	|date|						BlurBackgroundStart
	|float|						Scale
	|bool|						StartedProperly
	|bool|						IsBackground
	|bool|						FlashSelector
	|bool|						WantsQuit
	|bool|						IsQuitting
	|bool|						AControlWasResized
	|bool|						LastWasKeyWindowClose
	|bool|						TextInputEnabled
	|bool|						TextInputEnabledAtStart
	|byte|						TouchCount=3  // draw the first few frames always
											  // helps with startup events! at least on osx
	|byte|						SingleFrameNest
	|byte|						TouchFingersDown
	|byte|						CanDrag
	|byte|						ResizedCounter // for resize bug fix
	
	
	cpp_wrapper SDL_Init				(|uint| c)
	cpp_wrapper SDL_Quit				()

	cpp_wrapper SDL_CreateWindow		(|cstring| title, |int| x, |int| y, |int| w, |int| h, |uint| flags, |?&SDL_Window|) 
	cpp_wrapper SDL_SetWindowTitle		(|&SDL_Window| win, |cstring| title) 
	cpp_wrapper SDL_GetWindowTitle		(|&SDL_Window| win, |cstring|) 
	cpp_wrapper SDL_CaptureMouse		(|bool| state, |int|) 
	
	cpp_Wrapper SDL_CreateRenderer		(|&SDL_Window| window, |int| index, |Uint| flags, |&SDL_renderer|)

	cpp_Wrapper SDL_GetRendererOutputSize (|&SDL_Renderer| renderer, |&int| w, |&int| H, |int|)
	cpp_Wrapper SDL_CreateTexture		(|&SDL_Renderer| renderer, |Uint| format, |int| access, |int| w, |int| h, |&SDL_Texture|)
	cpp_Wrapper SDL_GL_GetDrawableSize	(|&SDL_Window| window, |&int| w, |&int| h)
	cpp_wrapper SDL_RenderCopy			(|&SDL_Renderer| renderer,  |&SDL_Texture| texture,  |?&SDL_Rect| srcrect, |&SDL_Rect| dstrect)
	cpp_Wrapper SDL_RenderPresent		(|&SDL_Renderer| renderer)
	cpp_Wrapper SDL_GetMouseState		(|?&int| x, |?&int| y, |mousebutton|) 
	cpp_wrapper SDL_EventState			(|int| event, |int| enable, |byte|)
	cpp_wrapper SDL_SetWindowPosition	(|&SDL_Window| w,  |int| x, |int| y)
	cpp_wrapper SDL_ShowCursor			(|int| show=-1, |int|)
	cpp_wrapper SDL_GetDisplayBounds	(|int| displayIndex, |&SDL_Rect| rect, |int|)
	cpp_wrapper SDL_SetWindowMaximumSize (|&SDL_Window| window, |int| max_w, |int| max_h)
	cpp_wrapper SDL_SetWindowMinimumSize (|&SDL_Window| window, |int| max_w, |int| max_h)
	cpp_Wrapper SDL_MaximizeWindow		(|&SDL_Window| window)
	cpp_wrapper SDL_SetWindowFullscreen	(|&SDL_Window| window, |int| Mode=0x1001, |int|)
	cpp_Wrapper SDL_RestoreWindow		(|&SDL_Window| window)
	cpp_wrapper SDL_GetWindowPosition	(|&SDL_Window| w,  |&int| x, |&int| y)
	cpp_wrapper SDL_GetWindowSize		(|&SDL_Window| w,  |&int| x, |&int| y)
	cpp_wrapper SDL_SetWindowSize		(|&SDL_Window| window, |int| w, |int| h, |int|)
	cpp_WRapper SDL_GetModState			(|keymod|)
	cpp_wrapper SDL_StartTextInput		()
	cpp_wrapper SDL_StopTextInput		()
	cpp_wrapper SDL_ShowSimpleMessageBox		(|int| Flags, |cstring| title, |cstring| msg, |?&SDL_Window| window)
	cpp_Wrapper JB_SDL_RemoveWindowBorder (|&nil| window)
	cpp_Wrapper JB_SDL_SetModified (|&nil| window, |bool| b)
	cpp_wrapper JB_SDL_FullScreen (|&nil| w, |bool| On=true)
	cpp_Wrapper SDL_GetCurrentDisplayMode (|int| displayIndex, |&SDL_DisplayMode| mode, |errorint|)

		
	function DocumentOpened  (|bool|Clear=true, |String|)
		cpp_Wrapper JB_App__DocumentOpened
		

	helper InitDelegates
		cpp_Wrapper JB_App__InitStuff


	function ScreenSize (|ivec4|)
		cpp_Part GetScreen
		return (.ScreenOrig.vec4 / .scale).ivec4 	


	function NativeWindow (|&nil|)
		|SDL_SysWMinfo| data
		|| s = .ScreenWindow
			s.SDL_GetWindowWMInfo(data)
			return data.window|&nil|


	function Alert (|string| msg, |string| title="")
		cpp_part MsgBox
		visible
		SDL_ShowSimpleMessageBox(0, title.cstr, msg.cstr, nil)

	
	function SelChanged
		.FlashSelector = true
		.ChangeSelFlash = 1


	function ScreenOrig (|ivec4|)
		|| P = .ScreenPosOrig
		return (p, p + .SizeOrig)


	function SizeOrig (|ivec2|)
		|int[2]| place
		|| s = .ScreenWindow
			.SDL_GetWindowSize(s,  &place[0],  place[1])
			return (place[0], place[1])


	function SizeOrig (assigns:|ivec2|)
		|| p = .ScreenPosOrig
		|| r = (p, p + value)
		.screenorig(r)


	function Title (|string|)
		|| s = .ScreenWindow
			return SDL_GetWindowTitle(s).Render


	function Title (assigns:|string|)
		|| s = .ScreenWindow
			SDL_SetWindowTitle(s, value.cstr)


	function ScreenPosOrig (|ivec2|)
		|int[2]| place
		|| s = .ScreenWindow
			.SDL_GetWindowPosition(s,  place[0],  place[1])
			return (place[0], place[1])

		
	function ShowCursor (|bool| v = true)
		SDL_ShowCursor(v|int|)


	function CursorVisible (|bool|)
		return SDL_ShowCursor()


	function ScreenOrig (assigns:|ivec4| result, |bool|)
		require .ScreenWindow
		if (result.x <= 0) and (result.y <= 0)
			|| n = .nativewindow
				.JB_SDL_FullScreen(n) // SDL2 had a bug in it... probably still exists
			return true
		|| Old = .SizeOrig
		|| aligned = .scale * (1.0+.IsHighDPI)
		|| wh = result.size-1
		if aligned > 1.0
			 wh = wh align aligned|int|
		.SDL_SetWindowSize(.screenwindow!, wh.x, wh.y)
		if .SizeOrig != wh
			.TryResizeLater = wh
		if result.xy
			.SDL_SetWindowPosition(.ScreenWindow!, result.x, result.y)

		if old != .SizeOrig
			.NewSize = .SizeOrig
		return true
		
	
	function Touch (|guicontrol| modified=nil)
		if .TouchCount < 1
			.touchcount = 1

	
	function Resized (|guicontrol| modified=nil)
		.AControlWasResized = true
		.Touch(modified)

	
	helper IsTyping (|bool|)
		return .event is TEXTINPUT or KEYDOWN
	
	
	function Run (|errorint|)
		target !gui
			disabled `Put "targets gui" in the app's conf section to use this!`
		app.ClearCaches
		|uint| n 
		while .SingleFrame
			if n++ isa 512
				app.ClearCaches //  :)
		.Quit
		
		if !StdErr.OK
			return 1

	
	helper PostLoads
		opt norefcounts
		|| s = .TryResizeLater
			.TryResizeLater = nil // clear... in case of inf loop
			.SizeOrig = s

		for w in window.all
			if !w.PostLoaded
				w.PostLoaded = true
				w.__PostLoad__


	function MouseXY (|ivec2|)
		|| x = 0,  || y = 0
		SDL_GetMouseState(&x, &y)
		return (x,  y).sdlxy
		
		
	function MouseButtons (|Mousebutton|)
		return SDL_GetMouseState(nil, nil)

	
	constants
		SDL_PIXELFORMAT_ARGB8888 = 372645892
		SDL_PIXELFORMAT_RGBA8888 = 373694468
		SDL_PIXELFORMAT_ABGR8888 = 376840196
		ChosenPixelFormat = SDL_PIXELFORMAT_ABGR8888
		SDL_INIT_TIMER =			0x00000001
		SDL_INIT_AUDIO =			0x00000010
		SDL_INIT_VIDEO =			0x00000020  /** implies SDL_INIT_EVENTS */
		SDL_INIT_JOYSTICK =			0x00000200  /** implies SDL_INIT_EVENTS */
		SDL_INIT_HAPTIC =			0x00001000
		SDL_INIT_GAMECONTROLLER =	0x00002000  /** implies SDL_INIT_JOYSTICK */
		SDL_INIT_EVENTS =			0x00004000
		SDL_INIT_SENSOR =			0x00008000
		SDL_WINDOWPOS_CENTERED =	0x2FFF0000
		SDL_TEXTUREACCESS_STREAMING = 1
		SDL_WINDOW_RESIZABLE =		0x20
		SDL_WINDOW_ALLOW_HIGHDPI =	0x00002000


	helper InitWindow (|ivec2| s, |string|name)
		SDL_Init(SDL_INIT_AUDIO ||| SDL_INIT_VIDEO ||| SDL_INIT_EVENTS ||| SDL_INIT_HAPTIC)

		.screenwindow = SDL_CreateWindow(name.cstr,
			SDL_WINDOWPOS_CENTERED, SDL_WINDOWPOS_CENTERED, s.x, s.y, SDL_WINDOW_RESIZABLE ||| SDL_WINDOW_ALLOW_HIGHDPI)
		if !.ScreenWindow
			app.quit("Can't init window", -1)

		.SDL_SetWindowMaximumSize(.screenwindow!, 2K, 2K)
		.SDL_SetWindowMinimumSize(.screenwindow!,  400,  200)
		.vsync = true
		.renderer = SDL_CreateRenderer(.screenwindow!, -1, 0)
		if !.Renderer
			app.Quit("Can't init renderer", -1)

		|| scr = .CurrDisplaySize
		if (s.x >= scr.x) and (s.y >= scr.y)
			|| n = .NativeWindow
				.JB_SDL_FullScreen(n)


	helper vsync (assigns:|bool|)
		|| v = ("1", "0")(value)
		.SetHint("SDL_RENDER_VSYNC".cstr, v.cstr)
	
	
	helper RemoveBorder
		|| n = .NativeWindow
			JB_SDL_RemoveWindowBorder(n)


	function SavePrefs
		if app._prefs
			app.pref("screen") = sdlapp.ScreenOrig.List
			app.SavePrefs


	function CurrDisplaySize (|ivec2|) // screensize
		// we call the monitor a "display"
		// and our SDL window we call it a "screen"
		// cos we have sub-windows. They can't both be windows
		|sdl_displaymode| mode = nil
		if SDL_GetCurrentDisplayMode(0, mode)
			return (mode.w, mode.h)


	helper IsHighDPI (|bool|)
		|int[2]| SmallPx
		|int[2]| BigPx
		SDL_GetWindowSize(.ScreenWindow!,  &BigPx[0],  &BigPx[1] )
		SDL_GL_GetDrawableSize( .screenwindow!,  &SmallPx[0],  &SmallPx[1] )
		|| s = SmallPx[0]
		|| b = BigPx[0]
		if s and b
			return s > b
	
	
	function IsFullScreen (|bool|)
		|| scr = .CurrDisplaySize
		return scr.x >= .ScreenOrig.x
		
		
	helper BumpSize
		description "Fixes a MacOSX bug. Not my fault!" 
		.FixScreenBug = 0
		|| was = .IsFullScreen
		|| n = sdlapp.nativewindow$
		.JB_SDL_FullScreen(n, false)
		if was
			.JB_SDL_FullScreen(n, true)
	
	
	function Scaling (assigns:|float| scale)
		|| bigPx = (.SizeOrig.vec2/scale).ivec2
		.Screen.sizeinfo = bigPx
		|| r = .Renderer
			.Raw = SDL_CreateTexture(r, ChosenPixelFormat, SDL_TEXTUREACCESS_STREAMING, bigPx.x, bigPx.y)
			.scale = Scale
			window.all.lastrect = bigPx.rect
		

	function GUICheck
		if app.yes("version")
			printline app.conf("version")
			app.quit
		if app.yes("created")
			printline __now__|date|.LocalTime
			app.quit
	
	function NiceAppName (|string|)
		with app.conf("selfname")
			return it
		with app.conf("name")
			return it
		return app.appname
	
	
	function Init (|ivec2| s=(414,256)) // startup
		pico.conf.SuicideIfParentDies = false
		pico.start
		target !gui
			disabled `Put "targets gui" in the app's conf section to use this!`
		require !.framelength
		.FrameLength = 180th.date
		.openedfirst = date.now
		app.PrefsInit(.openedfirst)
		|| PrevSize = app.prefmsg("screen").ivec4
			s = PrevSize.size
		  else
			s = (800, 600)
		s = s min (2K, 2K) max (500, 400)
		
		.GUICheck
		if !font.small
			sdlapp.Alert("Exitting...", "SmallFont not loaded.")
			app.quit
		.FastToolTipDate = 1

		.InitWindow(s, .niceappname)
		.scaling = 2.0 - (0.5*.ishighdpi)
		
		.CheckQuarantine
		¥.LoadDefaults

		appmenu.GetAppMenu
		.InitDelegates
		|| n = .nativewindow
		if !PrevSize and n
			.JB_SDL_FullScreen(n)
		.StartedProperly = true
		
	
	helper SetHint (|cstring| name, |cstring| value)
		cpp_wrapper SDL_SetHint
	
	
	helper TrySelectorFlash
		if !.IsBackground and (sdlapp.ChangeSelFlash.fired)
			.FlashSelector = !.FlashSelector
			.Touch
			if .ChangeSelFlash == 1
				.FlashSelector = true
			sdlapp.ChangeSelFlash = date.now + 1s


	function ClearAlerts
		|| it = .PendingAlerts
			.PendingAlerts = []
			for s in it
				app.alert(s)


	function DialogIsOpen (|bool|)
		opt norefcounts
		return .FrontDialog


	function FrontDialog (|window|)
		opt norefcounts
		for w.reverse in window.all
			if w.isdialog
				return w
	
	
	helper FrameAndEvents (|sdlactivity|) // frameevents
		while .Event.Poll
			rz |= .CheckEvent(.event)
			.Event.type = 0
		if .WantsQuit
			rz |= .TryQuit
		if .NewSize
			rz |= SDLActivity.activity

		if .TouchCount
			.TouchCount--
			rz |= SDLActivity.activity

		if rz is activity
			.framecount++
	
	
	function DroppingInto (|guicontrol|)
		opt norefcounts
		return __modules__.window.front.droppinginto?
	
	
	function UpdateTextFocus (|window| w)
		opt norefcounts
		|| f = w.realfocus // if theres nothing to focus, we can still type into the window!
		|| AllowTyping = !f or (f is editable) or (f isa menu)  or (f isa textview)
		if (f isa textview)
			if (!f.SelRange.width)
				sdlapp.TrySelectorFlash
		
		require .TextInputEnabled != AllowTyping
		.TextInputEnabled = AllowTyping
		if AllowTyping
			.sdl_starttextinput
		  else
			.sdl_stoptextinput
		
			
	function PreframeText (|window| W)
		.TextInputEnabledAtStart = .TextInputEnabled		
		if ListView.delayedItTill_.fired
			ListView.delayedItTill_ = 0
			|| lv = w.realfocus
			if lv isa ListView
				lv.TryDelayedEdit

	
	function OpenDocument (|string| path, |object|)
		require path
		|| d = .AppDocumentOpen
			|| f = path.file
			if f.exists
				rz = (d)(nil, nil, f.path, CopyPasteFlags.PasteFile)
	
	
	function HandleOpenFile
		|| d = .DocumentOpened$
		.OpenDocument(d)
		if .OpenedFirst.ago < 3s
			.FixScreenBug = date.now + 1s


	function OrderWindows (|window|)
		opt norefcounts
		|| i = 0
		for w in window.all
			if w is NewlyShown
				sdlapp.Resized(w)
				(w isnt newlyshown)
			if w is visible
				if rz
					w.position = i++
				rz = w
			  else
				w.position = -1
				window.all.first = w

		
	function PreFrame (|sdlactivity|)
		opt norefcounts
		if .FixScreenBug.Fired
			.BumpSize
			rz = SDLActivity.activity
		
		while (|| err = stderr.pop)
			notifications <~ err
			beep
			rz = SDLActivity.activity

		.HandleOpenFile
		
		|| w = .OrderWindows
			.UpdateTextFocus(W)
			.PreFrameText(w)
			
			|| ed = w.inline
			if ed.needshide
				ed.clear

			if .CanDrag >= mousedragging.EvenIfNoMovement
				.touch
				w.DoMouseDrag(.MouseXY, true)
				rz = SDLActivity.activity
	
	
	function DoDrawDrag (|window| w)
		|| s = .drawdrag
		|| d = w.DroppingInto
		require s and d and d != w.currmousedown
		|| P	= .mousexy
		|| sigh	= P.grow((6,6))
		|| img = .Screen
		img.DrawCircleStyled(sigh, ¥.DragAndDrop, 0.0)
		sigh	= P.grow((10,10))
		img.DrawCircleStyled(sigh, ¥.DragAndDrop, 7.0)
		
		img.controlclip = sigh
		|| t	= s.length.Render
		|| x	= t.length*font.width
		|| h	= font.height
		|| P2	= (x, h)/2
		font.small!.drawtext(t, img, P-P2, true)


	function DialogSorter (SorterComparerWindow)
		opt norefcounts
		if a and b
			return a.isdialog|int| - b.isdialog|int|


	function PutDialogsInFront
		opt norefcounts
		window.all.sort(DialogSorter|sortercomparer2|)


	function DrawWinFrom (|window|)
		opt norefcounts
		for w.reverse in window.all
			if w.isviewable and w.isfullscreen
				return w
		return window.all.first


	function StartResize
		opt norefcounts
		require .acontrolwasresized
		.acontrolwasresized = false
		for w.fast in __modules__.window.all
			w.reflow
		for w.fast in notifications.Alerts
			w.reflow(false)


	helper Window.Reflow (|bool| UsePrev = true, |ivec4|)
		opt norefcounts
		|| p = .parent ?? window.all
		|| D = sdlapp.Screen
		|| BigRect = d.rect
		|| rect = (0,0,0,0)
		|| b = .prev
		if b and UsePrev
			rect = b.lastrect
		return .reflowdoc(bigrect, rect, bigrect)

	
	function DrawFrame
		opt norefcounts
		|| CanBlur = !app.Pref("noblur")
		
		if !.DialogIsOpen
			.BlurBackgroundStart = 0
			
		.LastFrameDate = date.now
		|| w1 = .DrawWinFrom		#require
		.StartFrame(.Raw!)
		|| D = .DialogIsOpen
		while w1
			if w1.IsViewable
				if w1.IsDialog
					if CanBlur: .Screen.Blur(.BGBlurAmount)
					CanBlur = false
				w1.DrawWindow(.Screen)
			w1 = w1.next as window // :O
		
		notifications.Draw
		.DoDrawDrag(window.front!)
		.EndFrame(.Raw!)


	function BGBlurAmount (|float|)
		.BlurBackgroundStart := date.now
		|| Scale = 7.0
		|| ago = .BlurBackgroundStart.ago.seconds
		rz = ago*Scale*4.0
		if rz > Scale
			return scale
		sdlapp.Touch


	function FrameUpdatesAndResize
		opt norefcounts
		notifications.CheckForDisappear		
		.StartResize
		|| Dlg = .DialogIsOpen
		for w  in  window.all
			w.RunmyTasks(false)
			w.FixFocus
			if !dlg or w.IsLiveAction // stop needless updates
				w.__FrameUpdate__(dlg or w.openedmenu or .IsBackground)
		.StartResize
		
		
	function Mod (|int| period, |float|)
		opt min 3
		return (.FrameCount mod period)|float| / period|float|


	function Cycle (|date| amount, |float|)
		opt min 3
		return (.mod((amount*60).WholeSeconds).sin + 1.0) * 0.5
	

	helper NapTime (|date|)
		rz = .FrameLength
		if .IsBackground and !.DroppingInto
			rz *= 9

	
	helper SingleFrame (|bool|)
		opt norefcounts
		require .SingleFrameNest <= 2
		.SingleFrameNest++

		SpdProcess.CheckAllAlive
		
		|| occured = .PreFrame
		.ClearAlerts
		.HandleAppResize
		.PostLoads
		occured |= .FrameAndEvents
		
		.FrameUpdatesAndResize
		ifn Occured is Exiting
			.PutDialogsInFront
			if occured is activity
				.DrawFrame
			if !sdl_event.HasAny
				app.sleep(.NapTime)
			
			.SingleFrameNest--
			rz = true


	function CheckQuarantine
		if platform.osx and app.isQuarantined
			sdlapp.alert("Please drag this app to the /Applications/ folder to stop MacOSX quarantining it.", "Quarantine Warning" )


	helper Rescale (|float| NewScale, |bool|)
		.init
		if (NewScale >= 1.0 and NewScale <= 8.0)
			.scale = NewScale
			.NewSize = .SizeOrig
		  else
			beep
		return true
	
	
	helper HandleAppResize
		if !.NewSize
			if .ResizedCounter
				.ResizedCounter--
			return
		.ResizedCounter = 4
		
		|| r = .raw
			r.dispose
		|| v = (.NewSize.vec2/.Scale).ivec2
		.newsize = nil										// phew
		.Screen.size = v
		.Raw = SDL_CreateTexture(.renderer!, ChosenPixelFormat, SDL_TEXTUREACCESS_STREAMING, v.x, v.y)

		window.all.FullScreen = v
		.StartResize


	function StartFrame (|&SDL_Texture| T)
		opt norefcounts
		|| stride = 0
		|| bytes = (&(.Screen._ptr))|&&nil|
		t.lockThatZerosEventhoughItShouldnt(nil, bytes, &stride)
		

	
	function EndFrame (|&SDL_Texture| T)
		t.UnlockThatCopiesEvenThoughItShouldnt // seems like we are double doing this?
		|SDL_Rect| dstrect
		SDL_GL_GetDrawableSize( .screenwindow!,  &dstrect.w,  &dstrect.h )
		SDL_RenderCopy(.renderer!, T, nil, &dstrect)
		SDL_RenderPresent(.renderer!)


//	function Delay (|int| ms)
//		cpp_Wrapper SDL_Delay


	function Quit
		.saveprefs // in case we crash during quit
		for w in __modules__.window.all
			w.close(SDLCloseReason.terminating)
		.saveprefs
		.sdl_quit

	
	function FileDroppedIntoApp (|window| w, |string| path, |SDLEventType| t, |int|)
		|| p = (copypasteflags.pastefile, copypasteflags.pastetext)(t is dropfile)
		|| c = w[.MouseXY] ?? w
		c.DoPaste(path, p)
		w.DroppingInto = nil
		return SDLActivity.activity


	function AllowKeyDown (|&SDL_KeySym| Ev, |bool|)
		opt norefcounts
		if !.TextInputEnabledAtStart or ev.command
			return true
		|| k = ev.sym
		if k is tab or escape
			return true
		if k.iscmm
			return true
		if k.isscan or k.isaccept or k.isdelete
			return true


	function AllowTyping (|bool|)
		opt norefcounts
		return .TextInputEnabledAtStart and !keys.cmd
	
	
	function Window.MenuAt (|ivec2| xy, |bool| Test, |guicontrol|)
		opt norefcounts
		|| F = .openedmenu$
		if (!test) or (f.lastrect.horz contains xy.x)
			return f
	
	
	function Window.ControlOrMenuAt (|ivec2| xy, |bool| Test = true, |guicontrol|)
		opt norefcounts
		return .MenuAt(xy, test) ?? self[xy]


	function StandardKeys (|keys| key, |bool|)
		opt norefcounts
		|| w = __modules__.window.front						#require
		|| rf = w.realfocus
			if keys.cmd
				if (key is g)
					if !rf.__Search__(sdlapp.lastsearch, false)
						beep
					return true
				if (key is f) and !(keys.shift or keys.option)
					return rf.OpenSearch
			  elseif (key is find or AC_SEARCH)
				return rf.OpenSearch

		|| gw = rf ?? w
		|| cmd = keys.cmd
			if (key is PLUS or EQUALS)
				return .Rescale(.scale+0.5)
			if (key is minus)
				return .Rescale(.scale-0.5)
			if (key is f) and keys.option
				|| n = .nativewindow
					.JB_SDL_FullScreen(n, false)
				return true
			if (key is l) and keys.shift
				JB_Mem_ClassLeakCounter()
			if (key is squote)
				return w.DoListViewNext(true)
			if (key is semicolon)
				return w.DoListViewNext(false)
		  
		if (key is copy) or (cmd and key is c)
			|| wf = w.focusattempt
			if !rf and wf isa textview
				return wf.docopy
			return gw.DoCopy
		if (key is paste) or (cmd and key is v)
			return gw.DoPaste(clipboard.text)


	function TryQuit (|SDLCloseReason| reason = SDLCloseReason.Terminating, |SDLActivity|)
		opt norefcounts
		if (reason is QuitEvent)
			|| w = .LastWindowClose.ago
			if (w and w < 0.1s)
				return 0 // just block it. SDL is sending duplicate events
			(reason is windowclose     )
		  elseif reason is windowclose
			.LastWindowClose = date.now
			require sdlapp.AskQuit
		
		.IsQuitting = true
		if .DidCloseDialogs and .DidCloseDocuments(reason)
			rz = SDLActivity.Exiting
		  else
			.WantsQuit = false
		.IsQuitting = false


	helper DidCloseDialogs (|bool|)
		opt norefcounts
		for w in window.all
			if w.isdialog
				w.cancelreturn
				require w.CanJustCloseForQuit
				w.remove
		return true
	
	
	helper DidCloseDocuments (|sdlclosereason| reason, |bool|)
		opt norefcounts // dialogs gone by now
		for w in window.all
			require w.close(reason)
		return true


	function TryCloseFrontWindow (|int|)
		.LastWindowClose = date.now
		if !.DialogIsOpen
			|| w = window.front
			if w and w.close
				rz = SDLActivity.activity


	function AppIsBackground (|bool| v, |int|)
		beep.OK = 1
		.keymod = 0
		require .IsBackground != v // what? why is tihs happenin 
		.IsBackground = v
		.FlashSelector = false
		if !.DialogIsOpen
			for w in __modules__.window.all
				w.__AppForegroundChanged__(!v)
		.touch
		return SDLActivity.activity
		

	function CheckEvent (|&SDL_Event| ev, |int|)
		visible
		.keymod = .SDL_GetModState
		|| KeyW = .LastWasKeyWindowClose
		.LastWasKeyWindowClose = false
		|| t = ev.type
		 
		if (t is APP_DIDENTERFOREGROUND or APP_DIDENTERBACKGROUND)
			return .AppIsBackground(t is APP_DIDENTERBACKGROUND)
		
		if  t is APP_TERMINATING
			return .tryquit
		
		if (t is QUIT)
			return .tryquit(sdlclosereason.QuitEvent)
		
		if  t is WINDOWEVENT
			return .windowevent(ev, keyw)

		|| w = .FrontDialog ?? __modules__.window.front
		if (!w)
			return SDLActivity.nothing

		if T is dropbegin
			w.DroppingInto = w[.MouseXY] ?? w
			return SDLActivity.activity
		
		if t is DROPFILE or Droptext
			return .FileDroppedIntoApp(w, ev.drop.filestr, t)
		
		if (t is dropcomplete)
			w.DroppingInto = nil
			return SDLActivity.activity

		if t is TEXTINPUT
			return .TextInputevent(ev, w)

		if t is KEYDOWN
			return .keydownevent(ev, w)
					
		if t is KEYUP
			return .keyupevent(ev)

		if t is MOUSEWHEEL
			return .MousewheelEvent(ev,	w)

		|| b = ev.button
		|| xy = b.xy
		
		if t is MOUSEDOWN
			return .mousedownevent(w, b)

		if t is MOUSEMOVE
			return .MouseMoveEvent(w, xy)

		if t is MOUSEUP
			return .MouseUpEvent(w, xy, b.button)
		
		return SDLActivity.nothing
	
	
	helper WindowEvent (|&SDL_Event| ev, |bool| keyw,  |int|)
		|| wev = ev.window
		|| wt = wev.event
		if (wt is CLOSE)
			if keyw
				return .TryCloseFrontWindow
			  else
				return .TryQuit(sdlclosereason.WindowClose)
		if (wt is RESIZED)
			.NewSize = (wev.data1, wev.data2)
			return SDLActivity.activity
		if (wt is FOCUS_GAINED)
			return .appisbackground(false)
		if (wt is FOCUS_LOST)
			return .appisbackground(true)
		return SDLActivity.nothing


	helper MouseUpEvent (|window| w, |ivec2| XY, |int| Button,  |int|)
		SDL_CaptureMouse(false)
		//.AppResizingXY = nil
		|| dd   = .drawdrag 
		|| di	= w.DroppingInto
			for c in dd
				di.dopaste(c, copypasteflags.pastefile)
		
		|| dow	= w.CurrMouseDown
		|| F	= w.openedmenu
		w.ClearDrops
		if !(di and dd) and (button == 3) and (dow != F) and f and (f contains xy)
			f.__rightclick__(xy)
		  elseif dow and (button!=3) and !ScrollInfo.JustClicked_
			dow.__MouseUp__(xy)
		ScrollInfo.JustClicked_ = false
		return SDLActivity.activity


	helper MouseMoveEvent (|window| w, |ivec2| XY, |int|)
		|| mxy = .MouseXY
		require .CurrMouseXY != mxy
		|| MB = .MouseButtons
		require MB or !.IsBackground
	
		.CurrMouseXY = mxy
		.ShowCursor
		if (.CanDrag >= mousedragging.Within) and (MB is left)
			w.DoMouseDrag(xy, .CanDrag >= mousedragging.Beyond)
			return SDLActivity.activity
		
		|guicontrol--| PrevOver = w.MouseIsOver
		|guicontrol--| Over  = w.ControlOrMenuAt(Xy, false)

		w.MouseIsOver = Over
		|| Down = w.CurrMouseDown
		if !Down and over
			.SetToolTips(Over)

		if (PrevOver != nil and Over) and (Down == nil or PrevOver)
			PrevOver.__MouseMove__(xy, SDL_WindowEventID.leave)

				
		if Over and (down == Over or nil)
			|| state = (SDL_WindowEventID.moved, SDL_WindowEventID.enter)(PrevOver == Over)
			if Over.__MouseMove__(xy, state)
				return SDLActivity.activity


	helper TextInputEvent (|&SDL_Event| ev, |window| w, |int|)
		#!textinput!
		if .AllowTyping and .CanDrag <= mousedragging.Handled
			|| e = ev.Editing.str
			if w.__TypedString__(e)
				return SDLActivity.activity
		return SDLActivity.nothing


	helper KeyUpEvent (|&SDL_Event| ev, |int|)
		return keys.Removekey(ev.key.key)


	helper MouseWheelEvent (|&SDL_Event| ev, |window| w,  |int|)
		|| weel = ev.wheel
		w.__mousewheel__(weel.xy, weel.direction)
		return SDLActivity.activity
	
	
	helper KeyDownEvent (|&SDL_Event| ev, |window| w, |int|)
		#!keydown!
		|| kev = ev.keysym
		|| key = kev.sym
		if key.IsCancelled or .CanDrag >= mousedragging.Within
			return SDLActivity.nothing
		beep.OK = 1
		
		if (key is w) // checking for keys.cmd actually doesnt always work!!
			.LastWasKeyWindowClose = true

		keys.AddKey(key)
		if !w.openedmenu and !.AllowKeyDown(kev)
			return SDLActivity.nothing
		
		ifn (key is escape) and keys.escape.heldkey
			if .StandardKeys(key)
				return SDLActivity.activity
			if w.__keydown__(key)
				return SDLActivity.activity
		
		if (.singleframenest < 2) and keys.escape.HeldKey(0.75s)
			keys.escape.eat
			return  AppMenu.show
		

	helper Window.GetUserAttention
		if !.Focus
			.flashself = true
		.alphafader = date()
		beep
	
	
	helper MouseDownEvent (|window| w,  |&SDL_MouseButtonEvent| B,   |int|)
		|| xy = b.xy
		beep.OK = 1
		if notifications.Clickedat(xy)
			return SDLActivity.activity
			
		|| InsideWin = w.lastrect.soothe contains xy
		if !InsideWin and (w == .FrontDialog)
			if w.ClickOutsideCloses
				w.CloseDialog
				return SDLActivity.activity
			w.GetUserAttention
			return SDLActivity.nothing
		
		.candrag = nil
//		if .inResizeCorner(xy)
//			return SDLActivity.activity

		|| foc = w.realFocus
		if foc and foc.ClickOutsideCloses and !(foc contains xy)
			w.setfocus(nil)
			return SDLActivity.activity

		if w.MouseDownEvent(b.button, b.clicks, xy)
			.Touch
			return SDLActivity.activity
		return SDLActivity.nothing
		
	
	helper Window.MouseDownEvent (|int| bn,  |int| clicks,  |ivec2| xy, |bool|)
		target osx
			|| Ctrl = keys.control // only osx needs this
		  else
			|| ctrl = false
		|guicontrol--| clicked 
		if (bn == 3) or (Ctrl and bn == 1) 
			clicked = .ControlOrMenuAt(xy)
			clicked.__rightclick__(xy)?
		  else
			mousebutton.DownAt = date()
			clicked = .DoMouseDown(xy, clicks)
		.currmousedown = clicked
		return clicked


/*	 // too buggy
	|ivec2| 					AppResizingXY
	
	function FindResizeCorner (|ivec2| p, |ivec2|)
		opt norefcounts
		|| R = window.all.lastrect.x2
		return (r-p.x, p.y)


	function DoAppResize (|ivec2| M, |int|)
		|| P = .FindResizeCorner(M)
		|| D = .AppResizingXY - p
		|| L = window.all.size
		|| R = ((0, 0, l + D).vec4*.scale).ivec4
		.ScreenOrig = R
		return SDLActivity.activity
	

	function InResizeCorner (|ivec2| P, |bool|)
		p = .FindResizeCorner(p)
		|| m = 30
		if p.x < m  and  p.y < m
			.AppResizingXY = p
			SDL_CaptureMouse(True)
			return true
*/

	function GuiControl.ContainedName (|string|)
		|| fs = FastString()
		fs <~ "Name: "
		while
			fs <~ .ID
			self = self.Parent
			loop self and !(self isa window)
			fs <~ " <~ "
		return fs
	
	
	function SetToolTips (|guicontrol?| r)
		opt norefcounts
		|| w = r.window
		require w and r and w.ToolTipControl != r
		w.ToolTipControl = r
		if  r.id  and  keys.Control  and  keys.LALT  and  keys.LGUI
			Notifications <~ r.ContainedName
		
		|| info = ""
		ifn r isa textview  and  r is editable
			info = r.Description
		|| delay = (0.0s, 1.5s)(!sdlapp.FastToolTipDate.fired)
		|| now = date()
		|| t = .tooltip
		if t.name and t.when.fired
			sdlapp.FastToolTipDate = now + 1s
		t.when = now + delay
		t.name = info


function guicontrol.Description (|string|)
	return .tooltip // :)
	
	
	
datatype SDLActivity (uint)
	flags
		|SDLActivity|
		Nothing = 0
		Activity
		Exiting
	syntax is (|SDLActivity| act, |bool|)
		return self & act
		
	
datatype SDLCloseReason (uint)
	flags
		|SDLCloseReason|
		WindowClose
		TryQuit
		QuitEvent
		Terminating
	syntax is (|SDLCloseReason| act, |bool|)
		return self|int| & act|int|
	syntax is (|SDLCloseReason| act, assigns:|bool| value, |SDLCloseReason|)
		cpp_Part Set
		if value
			return (self|int| ||| act|int|)|SDLCloseReason|
		  else
			return (self|int| &~ act|int|)|SDLCloseReason|
			



