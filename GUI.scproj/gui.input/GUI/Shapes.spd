

class VObject
	|vec2| Pos
	|vec2| Radius
	|vec2| Velocity
	|byte| Nudge
	|vec4| Color
	|float|	Health
	
	constructor (|vec4| Pos_Rad, |vec4| Color=colors.white)
		.pos = Pos_Rad.xy
		.Radius = Pos_Rad.zw
		.Color = color
		.Health = 1
		
	function Towards (|vec2| P, |float| Speed)
		.Velocity = (P - .pos).Normal * Speed
	
	syntax Access (|vec4| V, |vec4|)
		|| P = .pos
		V += (P, P) // add nudge later
		return V
		
	function y (|float|)
		return .pos.y

	function x (|float|)
		return .pos.x

	function left (|float|)
		return .pos.x - .Radius.x

	function right (|float|)
		return .pos.x + .Radius.x
		
	function Box (|vec4|)
		|| rad = .Radius
		return (.pos-rad, .pos+rad)

	function Step
		.pos += .Velocity

	function OverlapsBlock (|VObject| B, |vec4|)
		return .OverlapsRect(b.box)
	
	function OverlapsRect (|vec4| B, |vec4|)
		return B.Clip(.box)
	
	function Screen (|ivec4|)
		return .box.Screen
	
	syntax cast (|ivec4|)
		return .Screen
	
	module
		|float| DrawScale
		|vec2|  DrawMovement
// later: handle other ratios, for example (Add borders)
// could we try to get closer to phi? say trim a few pixels off the top? to 865 pixels


function vec4.Scr0 (|vec4|)
	return (self + VObject.Drawmovement.xyxy) * VObject.DrawScale

function ivec4.Screen (|ivec4|)
	return .Vec4.Screen

function vec4.Screen (|ivec4|)
	|| p = .scr0
	|| w = p.Width
	|| h = p.Height
	p = p.floor
	p.x2 = p.x + w
	p.y2 = p.y + h
	return p.iVec4


extend Window
	helper _ReadjustBlock2D
		description "Readjusts current draw-matrix. This converts object coords to screen coords." 
		|| BS = .VastuScale$
		|| J = 0.0
		if bs > 0 
			J = .LastRect.width|float|
		  else
			bs = -bs
			J = .LastRect.height|float|
		
		j/=bs
		VObject.DrawScale = j
		|| M = .VastuMove
		if .VastuMode&1
			M.x = (j-m.x)*0.5
		if .VastuMode&2
			M.y = (j-m.y)*0.5
		VObject.DrawMovement = M

	function BlockX (|bool| Middle=false,assigns:|float|)
		.VastuMove.x = Value
		.VastuMode = (.VastuMode &~1) ||| Middle
	
	function BlockY (|bool| Middle=false,assigns:|float|)
		.VastuMove.y = Value
		.VastuMode = (.VastuMode &~2) ||| (Middle<<1)
		
	setter BlockHeight (|float|)
		.Vastuscale = -Value.abs
	
	setter BlockWidth (|float|)
		.Vastuscale = Value.abs

