
class ListView (GUIControl) {
	linkage
		cpp_part LV
	|int|							RowHeight
	|bool|							MultiLineDraw
	|bool|							UseColorScheme
	|bool|							DontDrawSeparators

	|bool|							IsSpreadSheetLike
	|bool|							IsMainListView
	|bool|							IsTree

	|int|							ScrollShift
	|byte|							ColumnCount
	|bool|							RightClickSelects
	|bool|							MultiSelection
	|bool|							OneAlwaysSelected
	|bool|							AlwaysSendChangeEvent
	|byte|							CommandSelectRow

	|bool|							_IsEditing
	|byte|							_ScrollRealigner
	|bool|							_DuringSelect
	|bool| 							_BlockedDuringUse
	|bool|							_SelJustChanged

	|ListViewRow?|					_DraggingRowTo
	|ListViewrow?|					_ScrollStart
	
	|ListViewCell?|					EditDelayed
	|ListViewCell?|					MouseDownCell
	|ListViewrow?|					DraggingRowFrom
	|ListViewrow|					ListSource
	|listviewrow?| 					Headers
	|string|						ColumnInfo
	|menu?|							CMM
	|DrawStyle|						gRowSel 
	
	|GUIListEditEvent?|				TryEditCell
	|GUIListEvent?|					OnChange
	|GUIListEvent?|					OnSelected
	|GUIKeyEvent?|					OnKeyDown
	|GUIListEvent?|					OnReturn
	|GUIListEvent?|					CalculateColumnWidths
	|GUIListEvent?|					OnRightClick
	|GUIListDragEvent?|				OnDragRow
	|ListViewRowDraw?|				OnDrawRow
	|GUIListEditEvent?| 			OnTypingFilter
	|textview?| 					TypingFilter


	constructor (|guicontrol| p,  |GUIListEvent| sel,  |GUIListEvent| ret)
		super.constructor(p)
		.RowHeight		= 13
		.ColumnCount	= 1
		.takesfocus 	= takesfocusflag.On
		.onreturn		= ret
		.onselected 	= sel
		.oncopy			= (&listview.defaultcopy)|GUICopyEvent|
		is editable
		isnt drawsfocus
		.listsource		= ListViewrow()
		.gRowSel		= .gnormal.copy
		.RightClickSelects = true
		.init
	
	
	helper Init
		.gRowSel.Bg			<~ ((0.5, 0.5, 0.5, 0.65))
		.gnormal.solid		=  ((0,   0,   0,   0.65))
		.gnormal.seps		<~ ((0.5, 0.5, 0.5, 0.65))
			
	
	helper TryRenameCellTo (|listviewcell!| cell, |string| value, |bool|)
		opt NoRefCounts
		require .TryRenameCellSub(cell, value)
		|| o = cell.row
		.touch
		.show(o)
		(.OnChange)(self, o)?
		(.onselected)(self, o)?
		return true


	helper TryRenameCellSub (|listviewcell!| cell, |string| value, |bool|)
		opt norefcounts
		|| tec = .TryEditCell
			return (tec)(self, cell, value)

		|| orig = cell.row
		orig.__textset__(cell.column, value)
		return true


	helper ColorRow (|int| Depth, |int| Column, |int| y, |imagepixel|)
		(depth), (column) // one day i'll use it?
		if y
			return (0.5, 0.5, 0.5, 1.0).px
		if .UseColorScheme
			return ¥.SyxCol[1]
		return imagepixel.clear


	helper DistBetween (|ListViewRow| c, |ListViewRow| b, |int|)
		opt norefcounts
		|| a = c
		while a
			if a == b
				return rz
			a = .Hiernext(a)
			++rz
		a = c
		rz = 0
		while a
			if a == b
				return rz
			a = .Hierprev(a)
			--rz


	function OpenMenu (|ListViewRow| r, |int| y)
		opt norefcounts
		|| m = .cmm	
			m.EditingRow = r
			y = (y+2) - .LastBottom
			y |= !y
			m.ShowCmm(self, -1, y)


	helper DesiredHeight (|int|)
		opt norefcounts
		rz = 2 + ((.ListSource.SlowCount max 2) * .RowHeight)
		|| t = .typingfilter
			rz += t.height - 1 


	helper ColBounds (|int| col,  |ivec4| z2, |ListViewrow| sc, |intrange|)
		(sc) // one day I'll use it, I guess
		|| x1	= .columns(col)
		|| col1 = col+1
		|| x2	= .columns(col1) - 1
		rz		= (x1, x2) + z2.x
		rz.x2	= rz.x2 min z2.x2

		
	helper Columns (|int| i, |int|)
		require i > 0

		|| w = .width
		if .DrawCols == 3
			if i == 1
				return (w/2)
			if i == 2
				return (w/2)+(w/4)
		return w
	
	
	helper DrawCols (|int|)
		if .width < 150
			return 1
		return .ColumnCount

	
	helper TextWidth (|int|)
		opt norefcounts
		for row.all in self
			rz = rz max row.__DisplayName__.bounds.x
		rz *= font.width
	

	function NewCmm (|menu|)
		rz = .cmm
		if rz
			rz.ListSource.Clear
			return rz
		rz = menu(self, nil, nil)
		rz.clipmode = ControlClipMode.SlideBackInWindow
		.cmm = rz
		rz.id = "cmm"
	
	
	behaviour RightClick
		.EditDelayed = nil
		listview.delayEditTill_ = nil
		.MouseDownCell = .FindClickedCell(P)
		|| mdc = .MouseDownCell$
		|| r = mdc.row
		require (r isnt UnSelectable)
		|| y = .DrawYRow(r)
		
		if (.RightClickSelects or .OnRightClick)
			if r isnt selected
				.Select(r)
			|| c = .onrightclick 
			if c and (c)(self, r)
				.openmenu(r, y - .lastbottom)
	

	helper MenuForSelRow
		|| sels = .FirstSelected$
		.showfast(sels)
		|| y = .DrawYRow(sels)
		|| fp = .onrightclick
		if fp and (fp)(self, sels)
			.openmenu(sels, y - .lastbottom)
		
	
	function PressReturnKey (|bool|)
		|| r = .OnReturn$
		(r)(self, .FirstSelected)
		return true
		
	
	function Clear
		.listsource.clear
		._ScrollStart	= nil
		._DraggingRowTo	= nil
		.DraggingRowFrom= nil
		.touch
	

	helper InlineCell (|bool| always = false, |&InlineEditor|)
		opt norefcounts
		|| w = .window$
		|| t = w.inline
		if always or (self == T.editingfor)
			return t

			
	helper ClearEdits
		opt norefcounts
		.InlineCell.FinishAndClear(false)?
	
	
	helper Replaced (|ListViewrow| gone, |ListViewrow?| orig, |ListViewrow|)
		opt norefcounts
		if orig == gone
			return .sib(orig)
		return orig
	
	
	helper Sib (|ListViewrow| r, |ListViewrow|)
		return .HierNext(r) ?? .hierPrev(r)
	
	
	helper CheckRemoved (|ListViewrow| r)
		.ScrollStart	= .replaced(r, ._Scrollstart)
		._DraggingRowTo	= .replaced(r, ._DraggingRowTo)
		.DraggingRowFrom= .replaced(r, .DraggingRowFrom)
	

	helper ChangeSel (|listviewrow| Row, |bool| NewState, |int|)
		if NewState == (row is selected)
			return 0
		if NewState and (row is UnSelectable)
			return 0
		if !gui.TouchCount and .RowIsViewable(row)
			gui.touch
		(row is selected) = newstate
		return 1


	helper RowIsViewable (|listviewrow| Row, |bool|)
		opt NoRefCounts
		// .isviewable checks the entire draw tree.
		// can we avoid this? sigh...
		require .IsViewable
		for s.Visible in self
			if s == row
				return true

	
	function RemoveRow (|ListViewrow| r, |bool| update=true)
		opt norefcounts
		.CheckRemoved(r)
		
		|ListViewrow| willsel
		if r is selected
			willsel = r.prev ?? r.next
		r|list|.remove
		
		if willsel
			.ChangeSel(willsel, true)
			.SelectedEvent( willsel )

		GUI.touch(self)
		if Update
			.touch				// setmodified
	
	
	behaviour Search
		require s
		.OpenSearch // whatever
		if JustUpdate
			return .SearchItems(s)
		|| f = .nextsearch(.lastselected)$
		
		.Show(f)
		if !.DraggingRowFrom
			.Select(f)
		return true
		
		
	function Rename (|bool|)
		cpp_part RenameCurr
		return .startRenameCell(.LastSelected.cell)
	
	
	function Rename (|ListViewRow| r, |bool|) 
		cpp_part RenameRow
		return .startRenameCell(r.cell)
	
	
	function RenameCol (|ListViewRow| r, |int| col, |bool|)
		return .StartRenameCell( r.cell(col) )
	
		
	helper StartRenameCell (|ListViewcell!| rc, |bool|)  //  .editcell
		opt norefcounts
		|| r = rc.row
		.SelectAdd(r)
		.Show(r)
		|| inl = .InlineCell(true)
			expect inl.edit(rc, self)			(beep)
			if .istree
				inl.Editor!.selectall
		return true
	
	
	helper RefilterKeyDown (GUIKeyEvent)
		opt norefcounts
		|| p = .parent as ListView
			if !key.iscancel
				return false
			return p.StopFilter

	helper textview.RefilterTextChange (GUITextEvent)
		opt norefcounts
		|| p = .parent as ListView
			p.UpdateFilter


	helper NeedFilter (|textview|)
		opt norefcounts
		rz = .TypingFilter
		if rz
			return rz
		rz = TextView(Self)
		.Typingfilter = rz
		rz.id = "filter"
		rz.gnormal.bg = vec4(0.1, 1.0).px
		rz.gnormal.edge = .gnormal.edge
		(rz is editable)
		rz.OnKeyDown = RefilterKeyDown|guikeyevent|
		rz.OnTextChanged = textview.Refiltertextchange
		rz.SingleLine = textview.SingleLine
		rz.size = the~width by (15)~down
	
	
	behaviour TypedString
		opt norefcounts
		if .OnTypingFilter
			|| t = .NeedFilter
			|| w = .window
				t.TypedString(input)
				w.SetInnerFocus(t)
				return .UpdateFilter


	helper UpdateFilter (|bool|)
		opt norefcounts
		|| t = .OnTypingFilter
		|| tf = .TypingFilter
		require t and tf
		(t)(self, nil, tf.text)
		.touch
		if self isa menu
			.Sizer.g(1, self) = .DesiredHeight~long
			.sizer.g(2, self) = .CmmWidth(nil)~long
		if .SelectableCount == 1
			.SelectFirst
		
		return true
	
	
	behaviour KeyDown
		opt norefcounts
		if .KeyDownTest(key) or (.OnKeydown)(self, key)?
			.editdelayed = nil
			return true
			
			
	helper KeyDownTest (|keys| key, |bool|)
		if (key is INSERT or Enter)
			.rename
			return true

		if key.IsAccept
			return .PressReturnKey
		
		if key.iscancel
			if .DraggingRowFrom
				.DraggingRowFrom = nil
				._DraggingRowTo = nil
				return true
			if .ClearSearch
				return true
			if keys.shift
				return .rename
			
		if .OnRightClick and (key.IsCmm)
			.menuforselrow
			return true
			
		if (key iscmd keys.A)
			return .SelectAll
		if (key is PAGEUP)
			.ScrollByRows(-.VisibleRows+1)
			return true
		if (key is PAGEDOWN)
			.ScrollByRows(.VisibleRows-1)
			return true
		if (key is HOME)
			.slidescroll = .start
			return true
		if (key is END)
			.slidescroll = .end
			return true
			
		if key is up or down
			|| d = int.max
			if keys.Cmd
				.SelectMove( (-d, d)(Key is up) )
			  elseif !.FirstSelected
				.SelectMove( (-d, d)(Key is down) )
			  else
				|| i = 1
				if keys.option: i = .VisibleRows/2
				if key is up
					i = -i
				.SelectMove(i)
			return true
		
		if .IsTree and (key is left or right)
			return .openorclose(key)

	
	function DefaultCopy (GUICopyEvent)
		|| fs = faststring()
		for s.listsel in self
			|| it = s.__copyid__
			it ?= s.DisplayName
			it ?= s.name
			if fs.length
				fs <~ '\n'
			fs <~ it
		return fs


	helper CopyList (|array of string|)
		opt norefcounts
		.EditDelayed = nil
		.clearinternaldrops
		for s.listsel in self
			|| it = s.__copyid__
				rz <~ it

	
	function IsEmpty (|bool|) // similar: hasany, rowcount
		return !.listsource.hasany
	
	
	function JumpTo (|string| s, ||)
		opt norefcounts
		|| it = .partialfind(S)
			.select(it)
		
		
	helper PartialFind (|string| s, |listviewrow|)
		opt norefcounts
		for r.toplist in self
			if listview.pf(r.name)[0, s]
				return r


	function VisibleRows (|int|)
		|| h = .height + .scrollshift + (.RowHeight - 1)
		|| r = .RowHeight
		return h / r
	

	function FullyVisibleRows (|int|)
		return .height / .RowHeight


	helper VisibleRowsGap (|int|)
		|| m = .FullyVisibleRows
		if m == 5
			return 4
		if m > 5
			return m - 2
		return m
		

	function EnableDrag
		.OnDragRow = (nil, &BasicRowDrag)(.istree)
	

	helper CursorForRow (|listviewCell?| c, |systemcursor|)
		opt norefcounts
		rz = SystemCursor.Arrow
		if c and self isa menu
			rz = SystemCursor.Hand
			|| r = c.row
			if (r is disabled)
				if rz == SystemCursor.hand
					return SystemCursor.arrow // degrade to arrow, rather than all the way down.
				return SystemCursor.no
			  elseif (r is editable)
				if r is Selected
					// should only be if this is CURRENLTY editing!
					return SystemCursor.IBeam
				return SystemCursor.hand
		
	
	behaviour MouseMove
		|| c = .FindClickedCell(P)
		.cursorforrow(c).use
			
		if ._CanSeeAllLines
			.NoScroll
			return .HasFocus

		return .MouseMoveInScrollArea(P)
	
	
	behaviour LostFocus
		.InlineCell.clear(false)?
		.clearinternaldrops
		super.lostfocus
	
	
	function SlideScroll (assigns:|listviewrow|r)
		.ScrollSet(r) // we wanted this to slide smoothly... sigh. do it later.
					  // in terms of the nodes and mousewheel.


	helper ScrollSet (|ListViewrow?| s)
		s ?= .start
		.scrollshift = 0
		._ScrollRealigner = 0
		debug_require s != .listsource
		.scrollstart = s
		.ExpandParents(s)
//		.checkdrawupdate
		if ._isediting and (s and !.InView(s))
			.ClearEdits


//	function VisContains (|ListViewRow| s, |bool|)
//		opt norefcounts
//		require s
//		for r.visible in self
//			if r == s
//				return true


	function InView (|ListViewRow!| s, |bool|)
		opt norefcounts
		for r.fullyvisible in self
			if r == s
				return true


	helper ScrollProtect
		opt norefcounts
		|| s	= .scrollstart
		ifn .listsource contains s
			s = .start
			.scrollstart = s
		require s
		|| last = .end
		for .VisibleRowsGap
			if last == s
				s = nil
			last = .hierstep(last, false) #loop
		if !s
			.scrollset(last)
	
	
	function ExtractSelected (|ListViewrow| c, |ListViewrow|)
		rz = ListViewrow()
		while c
			|| p = c.nextselected
			rz <~ c
			c = p
	
	
	helper FoundSep (|ListViewrow| m, |ListViewrow| before, |int|dir, |bool|)
		opt norefcounts
		|| answer = false
		while
			m = .hierstep(m, dir>0)
			if m == before
				return answer
			require m
			if m is Separator
				answer = true
			

	helper CanBasicRowDragOne (|ListViewrow| s, |ListViewrow| before, |bool|)
		opt norefcounts
		if before
			require before isnt UnSelectable
			require !.FoundSep(s, before, -1)
		return .FoundSep(s, before, 1)
		

	helper HasSeps (|bool|)
		opt norefcounts
		|| l = .start
		while l
			if l is Separator
				return true
			l++


	helper CanBasicRowDrag (|ListViewrow| s, |ListViewrow|before, |bool|)
		opt norefcounts
		if !.hasSeps
			return true
			
		while s
			require .CanBasicRowDragOne(s, before) 
			s = s.nextselected
		return true


	helper BasicRowDrag (GuiListDragEvent)
		|| s = .FirstSelected
		if !s or (.Hiernext(s) == before) and !s.nextselected
			return true // no need
		
		if (before is selected)
			if before != s 
				beep
			return true
		
		if !.CanBasicRowDrag(s, before)
			beep
			return true
		
		for p in .ExtractSelected(s)
			if into	
				into <~ p
			  elseif before
				before.prev = p
		
		return .bumpscroll
	
	
	helper ClearInternalDrops
		._DraggingRowTo = nil
		.CommandSelectRow = nil
		._SelJustChanged = false
		.MouseDownCell = nil
		

	behaviour MouseUp
		ScrollInfo.endDrag

		|| f = .DraggingRowFrom
		|| t = ._DraggingRowTo
		|| sl = ._SelJustChanged
		.DraggingRowFrom = nil
		.ClearInternalDrops

		if f and t
			|| dr = .OnDragRow 	#debug_require
			if t == .listsource
				(dr)(self, nil, t) 
			  else
				(dr)(self, t, nil) 
			
		  elseif !sl
			|| LC = .FindClickedCell(P)
				|| L = LC.row
				require (L isnt UnSelectable)
				if (L is selected) and (.SelCount > 1)
					.Select(l)
				  elseif !keys.Cmd and (.window!._SelBeforeMouseDown == self)
					.DelayedRename(Lc)
			

	function SelCount (|int|)
		opt norefcounts
		for f.listsel in self 
			RZ++


	function SelectableCount (|int|)
		opt norefcounts
		for f.all in self 
			rz += f isnt UnSelectable


	helper DelayedRename (|ListViewCell| i)
		.EditDelayed = i
		delayedittill_ = (gui.FrameStart + 0.5s, nil)(i)


	helper TryDelayedEdit
		|| i = .EditDelayed$
		.EditDelayed = nil
		|| p = .mouseXY
		if self contains p
			|| R = .FindClickedCell(p)
			if r and (R == i)
				.startRenameCell(r)
	
	
	helper CanDrawDrag (|ListViewrow| wantsbefore, |bool|)
		if ._DraggingRowTo or (.FirstSelected != .lastselected)
			return true
		require .draggingrowfrom != WantsBefore
		require .Hiernext(.draggingrowfrom) != wantsbefore
		return true


	function FixScroll (|bool|)
		if !.scrollstart
			.scrollset(.start)
			return .scrollstart
		return true


	behaviour MouseDown
		.EditDelayed = nil
		if clicks >= 0
			._SelJustChanged = clicks > 1
		if (!keys.Cmd) and (.CommandSelectRow)
			return MouseDragging.handled
		
		|| AdjustScroll = (clicks == -1) and (.draggingrowFrom)
		if (clicks == -1) and (.window!.droppinginto == self) // I guess the filter is already done...
			|| i = .FindClicked(p)
				.Select(i)
			AdjustScroll = true

		if AdjustScroll
			if !.fixscroll
				.ClearInternalDrops
				debugger
				return MouseDragging.handled // why MouseDragging.DragWhatFixThis
			|| p2 = (0, .rowheight / 2)
			|| i  = .FindClicked(P - P2)
			|ListViewrow--| WantsTo
			
			if i
				WantsTo = i
			  else
				WantsTo = .listsource
				|| top = .lasttop
				if (p.y+p2.y > .lasttop)
					|| s = .scrollstart
						WantsTo = .bmove(s, -1)
					.ScrollByRows(-1)
					
				  elseif (p.y-p2.y < .LastBottom)
					.ScrollByRows(1)
			
			if .draggingrowFrom and (.CanDrawDrag(Wantsto))
				._draggingrowto = wantsto
			return MouseDragging.EvenIfNoMovement
		
		if .ClickedScroll(P, clicks)
			return MouseDragging.Within
		
		|| FirstSel = .FirstSelected
		|| LC = .FindClickedCell(P)
		if (Clicks > 1) and (firstsel) and !(keys.Cmd)
			if lc and lc.row == firstsel
				(.OnReturn)(self, firstsel)?
			return MouseDragging.handled
		
		.MouseDownCell = LC
		if !lc
			if !Keys.Cmd
				.selectnone
			return MouseDragging.handled
		|| l = lc.row 
		if L is Separator
			return MouseDragging.handled
			
		if keys.Cmd
			.CommandSelectRow ?= 1 + (L is selected)
			|| State = .CommandSelectRow
			.selectadd(l, State == 1, true)
			return MouseDragging.Within
		
		if (L isnt selected) or .AlwaysSendChangeEvent
			if keys.shift
				.selectShift(firstsel, L)
			  else
				.Select(L)

		if (!.istree and .OnDragRow and l is editable)
			.DraggingRowFrom = L
			._draggingrowto = nil
			return MouseDragging.Within
		return MouseDragging.handled
	
	
	function LastSelected (|ListViewrow|)
		opt NoRefCounts
		|| l = .end
		while l
			if l is selected
				return l
			l = .hierPrev(l)
		return .OneAlways
			
		
	helper End (|ListViewrow|)
		opt norefcounts
		|| li = .listsource 
		return (li.hierend(li), li.last)(.IsTree)
	
	helper Edge (|int|i, |bool| Selectable=false, |ListViewrow|)
		opt norefcounts
		rz = (.end, .start)(i > 0)
		if selectable
			while rz and rz is UnSelectable
				rz = rz.step(i <= 0)
		
	autogen Start (|ListViewrow|)
		return .listsource.first
	
	helper SearchStep (|ListViewrow| l, |bool| fwd, |ListViewrow|)
		opt norefcounts
		|| root = .listsource
		if !fwd
			return l.hierprev(root) ?? .end
		if l != root // compare to .list? instead?
			(l is expanded) // might as well
		return l.hiernext(.listsource) ?? .start
	helper HierNext (|ListViewrow| l, |ListViewRow|)
		if l
			return (l.hiernext(.listsource), l.next)(.istree)
	helper HierPrev (|ListViewrow| l, |ListViewrow|)
		if l
			return (l.hierprev(.listsource), l.prev)(.istree)
	helper HierStep (|ListViewrow| l, |bool|fwd, |ListViewrow|)
		if fwd
			return .Hiernext(l)
		return .hierprev(l)
	
	helper BMove (|ListViewrow| l, |int| Req, |ListViewrow|)
		opt norefcounts
		rz = l
		|| n = req.abs
		while n > 0 
			l = .hierstep(l, req > 0) #loop
			if l isnt UnSelectable // skip as if non-existing
				rz = l
				n--


	function NextSearch (|ListViewRow| curr, |ListViewRow|)
		opt norefcounts
		|| dir = !keys.shift				// can do this backwards... if we want.
		
		curr ?= .start
		|| Start = curr
		
		while curr {
			curr = .searchstep(curr, dir)
			if curr == start
				return (start, nil)(curr is found)
		} (curr isnt found)
		return curr
	
	
	helper ClearSearch (|bool|)
		opt norefcounts
		for s.all in self
			if s is found
				(s is Found) = false
				rz = true
	

	function SearchItems (|string| s, |bool|)
		|| c = .columncount
		opt norefcounts
		for m.all in self
			|| n = m.searchcolumns(s, c)
			(m is found) = (n > 0)
			rz = true


	helper OpenOrClose (|keys| K, |bool|)
		opt norefcounts
		|| sel		= .LastSelected$
		|| Expand	= (k is right)
		|| Force	= keys.option 
		if (!Expand and (!sel.hasany or sel isnt expanded))
			sel = sel.parent!
			.Select(sel)
			Force = true
		if sel != .listsource
			if (!Expand and sel contains .scrollstart)
				.showfast(sel)
		if !force and !expand and (sel.AllExpandState & 2)
			for r in sel
				(r is expanded)=false
			return true
		if Force or (Expand == (sel is expanded))
			for r.flat in sel
				(r is expanded) = Expand
		(sel is expanded) = Expand
		return true


	function SelectAtLeastFirst	
		if !.firstselected
			.select(.start)

	function Selected (|ListViewRow|)
		disabled "Do you mean .FirstSelected or .LastSelected?"
	
	
	function FirstSelected (|ListViewRow|)
		opt NoRefCounts
		|| l = .start 		
		while l
			if l is selected
				return l
			l = .HierNext(l)
		return .OneAlways


	helper OneAlways (|ListViewRow|)
		opt norefcounts
		if .onealwaysselected
			|| it = .start
				(it is selected)
				.SelectedEvent( it )
				return it


	function SelectAll (|bool|)
		opt norefcounts
		return .selectshift(.start, .end)


	function SelectFirst (|listviewselectmode| mode = listviewselectmode.normal, |bool|)
		opt norefcounts
		
		.select(.edge(-1, true), mode)
		return true


	function SelectShift (|ListViewrow| l, |ListViewrow| m, |bool|)
		opt norefcounts
		expect (l and m) (Beep)
		expect (.multiselection) (Beep)
		expect (l.parent == m.parent) (beep)
		
		|| modified = 0
		|| all		= (l == .start) and (m == .end) 
		|| i		= .DistBetween(l,m)
		|| n		= i.abs + 1
		for n
			modified += .ChangeSel(l, (l isnt UnSelectable))
			 l = .hierstep(l, i > 0)#loop

		if modified
			|| mode = listviewselectmode.normal
			if all
				mode = listviewselectmode.noshow
			.SelectedEvent( (m, nil)(i), mode )
		return true


	function SelectMove (|int| i, |bool| Multi=keys.shift)
		opt norefcounts
		require i
		|| l = (.LastSelected, .FirstSelected)(i > 0)
		if !l
			// what if .start or .end is disabled?
			|| f = .edge(i, true)
			return .Select(f)
		
		|| m = .bmove(l, i)
		ifn .multiselection and multi
			return .Select(m)
		
		if multi
			.SelectShift(l, m)
		  else
			.SelectAdd(m)


	function SelectAdd (|ListViewrow| m, |bool| State=true, |bool| Multi=keys.shift)
		opt norefcounts
		if .OneAlwaysSelected
			return .Select(m)
		
		if .MultiSelection and multi
			if .ChangeSel(m, state)
				.SelectedEvent(m)
		  else
			.Select(m)


	function SelectNone
		opt NoRefCounts
		|| one = .OneAlwaysSelected
		|| s = (.FirstSelected ?? .start, nil)(one)
		return .Select(s)
	
	
	function SelectNoShow (|ListViewRow| s)
		.select(s, ListViewSelectMode.Noshow)
	
	
	function SelectFast (|ListViewRow| s)
		.select(s, ListViewSelectMode.Fast)
		
		
	function Select (|ListViewRow?| s, |ListViewSelectMode| Mode = ListViewSelectMode.Normal)
		opt norefcounts
		require !._DuringSelect
		require s and s != .ListSource
		require s isnt UnSelectable
		require .owns(s)
		
		.ExpandParents(s)
		|| Modified = 0
		for r.all in self
			modified += .ChangeSel(r, r == s)
		
		if modified or .AlwaysSendChangeEvent or mode is SendEvent
			.SelectedEvent(s, mode)
		if mode isnt noshow
			.show(s)

	
	function FocusOn (|ListViewrow| r)
		require .listsource contains r
		.ScrollSet(r)
		(r is expanded)
		.Select(r)

	
	helper SelectedEvent (|ListViewRow| s, |ListViewSelectMode| Mode=ListViewSelectMode.normal)
		opt norefcounts
		require !._DuringSelect
		.ClearEdits
		._SelJustChanged = true
		if s and (Mode isnt noshow)
			.Show(s)

		if (mode isnt noevent) and !._BlockedDuringUse
			._DuringSelect = true
			(.onselected)(self, s, mode)?
			._DuringSelect = false


	function ScrollByRows (|int| Rows)
		|| s = .scrollstart
			.scrollset( .bmove(s, rows) )
			.bumpscroll

	
	function ScrollMoveTo (|int| v)
		|| s = .start
			.scrollset(.bmove(s, v))
	
	
	helper ShowFast (|ListViewrow| i)
		.show(i)

	
	function Owns (|ListViewrow| i, |bool|)
		return .listsource contains i
	
	
	function ExpandParents (|ListViewrow| i)
		opt norefcounts
		require i
		while i != .listsource
			i = i.parent		#require
			(i is expanded)

	
	function Show (|ListViewrow| i)
		require i
		debug_Require .owns(i)
		
		.ExpandParents(i)
		require !.InView(i)
		|| n = .visiblerows|float|*0.34
		.scrollset(.bmove(i, -n|int|))
	
		
//	function Contains (|int| Y,  |ivec2| Search,  |bool|)
//		|| Srch = (.absleft,  y - .RowHeight,  .right,  y)
//		return Srch.contains(Search)
	
	
	function Use (|ListViewrow!| PL, |bool| SendClickEvent=false)
		opt norefcounts
		require pl != .listsource
		._BlockedDuringUse = !SendClickEvent
		.listsource = pl

		.Scrollset( pl.first )
		if .OneAlwaysSelected
			.SelectFirst
		  else
			.SelectNone
		.bumpscroll
		._BlockedDuringUse = false

	
	helper ClickedScroll (|ivec2| P, |int| Clicks, |bool|)
		return .ClickedScrollbar(P, Clicks, .slh)


	helper FindClicked (|ivec2| Search, |ListViewRow|)
		|| c = .FindClickedCell((0, search.y))
			return c.row
	
	
	helper FindClickedMenu (|int| y, |ListViewRow|)
		opt norefcounts
		if y <= .lastBottom
			return .bmove(.LastVisible$, 1)
		if y > .lastTop
			return .bmove(.scrollstart$, -1)
		|| c = .FindClickedCell((0,y))
			return c.row
	
	
	function StopFilter (|bool|)
		|| t = .typingfilter
			t.remove
			.typingfilter = nil
		return true
		

//	function FilterText (|string|)
//		opt norefcounts
//		|| t = .TypingFilter
//			rz = t.text


	helper ActualRect (|ivec4|)
		opt norefcounts
		rz = .lastrect
		|| t = .TypingFilter
			rz.y2 -= t.height-1
		if .Headers
			rz.y2 -= .HeaderHeight
			
	helper HeaderHeight (|int|)
		return 19
	
	
	helper FindClickedCell (|ivec2| Search, |ListViewCell|)
		opt NoRefCounts
		|| r = .ActualRect
		|| x = search.x - r.x
		
		for sc.visible in self
			r.y = r.y2 - .RowHeight
			if r.vert contains search.y
				return .DrawCell( sc, x )
			r.y2 = r.y


	helper DrawYRow (|ListViewrow| search, |int|)
		opt NoRefCounts
		|| r = .lastrect

		for row.visible in self
			r.y = r.y2 - .rowheight
			if row == search
				return r.y2
			r.y2 = r.y

	
	helper DefaultColumnWidths (GUIListEvent)
		return nil
		/*
		|| cw = .ColumnWidths$
		|| Weights = cw.length$
		|| sect = .width / Weights
		|| x = sect*2
		cw[0] = 0
		cw[1] = x
		
		for i in (2 to weights)
			x+=sect
			cw[i] = x
		cw[weights-1] = .width
		*/
	
	
	function LastVisible (|ListViewrow|)
		opt norefcounts
		for r.visible in self
			rz = r


	helper RowBorderDraw (|ListViewrow| curr, |ListViewrow| find, |int| add, |image| where, |drawstyle| g)
		|| r = .RowBounds(curr, find, add)
			where.DrawRectStyled(r, g)
	
	
	helper ListViewRow.CalculateSkip (|ListViewRow| prev, |int| n, |int|)
		opt norefcounts
		require prev
		for i in 1 to n
			|| t =     .__DisplayName__(i)
			|| p = prev.__DisplayName__(i)
			if (t == p) // and t and p 
				rz |= 1<<i
	
	
	helper CalculateSkips
		opt norefcounts
		|| n = .ColumnCount
		|ListViewRow| prev
		for sk.visible in self
			|| ind = (0, sk.CalculateSkip(prev, n))(.IsTree)
			sk.indent = ind
			prev = sk
	
	
	helper RowBounds (|ListViewrow| curr, |ListViewrow| find, |int| add, |ivec4|)
		opt norefcounts
		require find
		
		|| s	= .ActualRect
		|| low	= s.y1
		|| top	= s.y2 + .scrollshift - .rowheight
		while curr != find
			top -= .rowheight
			require (curr) and (top >= low)
			curr = .hiernext(curr)
		return (s.x, top, s.x2, top + add)


	helper SetInlineSize
		opt norefcounts
		|| inl = .InlineCell								#require
		|| cell = inl.cell									#require
		|| cb = .CellBounds(cell)
			if (cb.y2 <= .height+1) and (cb.y1 >= -1)
				inl.editor!.size = cb.layout
				return
		inl.FinishAndClear


	helper CellBounds (|ListViewCell| cell,  |ivec4|)
		opt norefcounts
		|| sc = cell.row!
		|| B = .RowBounds(.scrollstart, sc, .rowheight)			#require
		B.horz = .ColBounds(cell.column, B, sc)
		B.x += sc.depth(self) * font.width*2
		return (B down .lastbottom) left .lastleft 


	helper DrawBordersSub (|image| Where,  |ivec4| Rect)
		require !.decluttered
		|| z2 = rect
		z2.y2 += .scrollshift
		z2.y  = z2.y2 - .rowheight
		
		if !.DontDrawSeparators and .gnormal.seps.a
			for r.visible in self
				|| n = .drawcols
				for i in n
					z2.horz = .ColBounds(i, rect, r)
					r.DrawDividerH(z2, where, i)
					if i < n-1
						r.DrawDividerV(z2, where)
				z2 = z2 down .rowheight

		z2 = rect
		z2.y2 += .scrollshift
		z2.y = z2.y2 - .rowheight
		for r.visible in self
			if r is Selected
				Where.mark(z2, (2, 3)(.hasfocus))
			z2 = z2 down .rowheight


	function DrawMarks (|image| Where,  |ivec4| Rect)
		where.clearmarks(.lastrect)
		.DrawBordersSub(where, rect)
		|| bg		= .gnormal.bg
		|| focus	= .gRowSel.bg
		|| focuscol	= focus.color
		|| bgcol	= bg.color
		focuscol	= focuscol.a.mix(bgcol, focuscol)
		
		|| unfoc	= vec4(focuscol.lum)
		unfoc.a		= focuscol.a
		unfoc		= 0.667.mix(focuscol, unfoc)
		unfoc.a += 0.2
		where.fillmarks(.lastrect ± 1, bgcol.px, .gnormal.seps, focuscol.px, unfoc.px)

			
	behaviour Gesture
		if !._CanSeeAllLines
			.scrollPixels(wheel.y*6)
		return true


	helper RealignScrollshift (|int| shl, |int|)
		if shl >= (.rowheight/2)
			return shl + 1
		return shl-1


	helper ScrollSlide (|int|)
		|| shl	= .scrollshift
		if !shl or .ScrollPixelsSub(0)
			return 0
		
		|| real = ._ScrollRealigner
			real--
			if real < 32
				shl = .realignscrollshift(shl).clamp(0, .rowheight)
				.scrollshift = shl
				if !shl
					real = 0
			._ScrollRealigner = real
			GUI.touch(Self)
		return shl


	function ScrollPixels (|int| rem)
		if rem
			.ScrollPixelsSub(rem)
			.bumpscroll
			._ScrollRealigner = (48, 0)(.scrollshift)
			GUI.touch(self)


	function ScrollEnd (|listviewrow|)
		opt norefcounts
		rz = .end
		for .VisibleRowsGap
			rz = .hierstep(rz, false) #loop
	
	
	helper ScrollPixelsSub (|int| rem, |bool|)
		rem += .scrollShift
		|| cmp = (.start, .scrollend)(rem < 0) 
		if cmp == .scrollstart
			rem = 0
		
		while (rem < 0)
			.ScrollByRows -1
			rem += .rowheight
		while (rem >= .rowheight)
			.ScrollByRows(1)
			rem -= .rowheight
		.scrollshift = rem
		return !rem


	helper DrawHeaders (|image| where, |ivec4| rect)
		opt NoRefCounts
		|| l = .headers$

		rect.y = rect.y2
		rect.y2 = .lastrect.y2
		l.ListRowDraw(Where, rect, self, 0)
		
		require l.Name
		
		rect.y2 = rect.y +1
		|| v = .GNormal.Seps.color
		v = 0.2.mix(v, colors.white)
		v.a = 0.5
		where.DrawRect(Rect, v) 


	function ScrollStart (|listviewrow|)
		return ._ScrollStart

	
	function ScrollStart (assigns:|listviewrow|)
		if ._ScrollStart!=value
			._ScrollStart = value
			.touch

	behaviour Draw
		opt NoRefCounts
		|| c = (.CalculateColumnWidths) ?? (DefaultColumnWidths)
		(c)(self, nil) // allow the thing to calculate the column widths before draw
		.ScrollProtect
		
		|| clip = where.ControlClip
		|| rect = .ActualRect
		|| L = .ListSource
		
		.CalculateSkips
		.DrawMarks(where, rect)
		.drawheaders(where, rect)
		
		rect.y2 += .ScrollSlide
		|| End = .End
		._CanSeeAllLines = false
		
		
		for r.visible in self
			rect.y = rect.y2 - .RowHeight
			|| D = (r.depth(self), 0)(.IsTree)
			r.ListRowDraw(Where, rect, self, D)
			rect.y2 = rect.y
			if (r == end) and .start == .scrollstart
				._CanSeeAllLines = (rect.y >= .lastrect.y)

		where.controlclip = clip
		where.horz = .lastrect.h // redundant?
		|| cmm = .cmm
			.RowBorderDraw(.scrollstart, cmm.EditingRow, .RowHeight, where, ¥.border)

		|| re = .RowBounds(.scrollstart, ._DraggingRowTo, 1)
			re = re up .rowheight
			if !._draggingrowto.prev
				re = re down 2
			where.DrawRectStyled(re, ¥.border)
		
		.SetInlineSize
		
		where.DrawOutlineOnly(.lastrect, .gnormal.edge, .gnormal.bg, ~.FindAttachedEdge)

		if ._CanSeeAllLines
			.NoScroll
		  else
			.DrawScroll(.slh, where)


	helper textview.MoveListLine (|int| i, |bool|)
		opt norefcounts
		|| inl = .isinline$
		|| c = inl.cell$ // wat
		|| c2 = c.step(i)
			inl.Edit(c2, nil)
			if i < 0
				.selrange = 0
		return true


//	helper LastNamed (|string| s, |ListViewrow|)
//		opt norefcounts
//		rz = .listsource.last
//		while rz
//			if rz.name ~= s
//				return rz
	
	
	syntax Append (|string| name, |GUIMenuEvent| ev, |object| obj=nil)
		cpp_part AppendMenuItem
		self <~ menuitem(name, ev, obj)

	
	Syntax append (|ListViewRow| R)
		|| needsel = .OneAlwaysSelected and !.ListSource.HasAny
		.listsource <~ r
		if needsel
			.select(r)
	
	
	helper AddSep
		self <~ menuitem("", nil)


	iterator ListSel
		|| f = .FirstSelected
		while f
			yield f
			f = f.nextselected


	iterator Visible
		.fixscroll
		|| F = ._scrollstart
		|| n = .VisibleRows
		while f and n >= 1
			yield f
			f = .Hiernext(f)
			--n


	iterator FullyVisible
		.fixscroll
		|| F = ._scrollstart
		|| n = .FullyVisibleRows
		while f and n >= 1
			yield f
			f = .Hiernext(f)
			--n


	iterator All
		|| F = .start
		while f
			yield f
			f = .Hiernext(f)

	iterator TopList
		|| F = .start
		while f
			yield f
			f = f.next

	module
		|date|				DelayEditTill_
		.init

		function Init
	
	
		helper pf (|string| s, |string|)
			for (c in s) (i)
				if c.IsAlphaNum
					return s[i...].lowercase
			
}



function ListViewrow.Depth (|listview| me, |int|)
	opt norefcounts
	|| c = self
	rz = -1
	|| root = me.listsource
	while c and (root != c)
		rz++
		c = c.parent

