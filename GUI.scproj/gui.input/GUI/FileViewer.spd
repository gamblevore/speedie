

class FileViewer (ListView)
	linkage
		cpp_part FV
    |dictionary?|					Excludes
    |dictionary?|					NoFind
    |TextView|						QuickOpen
    |ListView|						ListOpen
    |textView|						Editme
    |filerow?| 						Extra
    |filerow?| 						SearchWithin
    |[string]|						SizeCheckers
    |GUIListEvent?|					OnSearchWithin
    
	constructor (|window| p, |GUIListEvent| sel, |GUIListEvent| ret, |TextView| EditMe)
		super.constructor(p, sel, ret)
		.MultiSelection 	= true
		.editme				= editme
		.OnReturn			= ReturnFileList|GUIListEvent| // c++ work around
		.OnRightClick		= RightClickFileList|GUIListEvent|
		.quickopen			= textview(nil,		nil,		nil)
		.Listopen			= ListView(nil,		nil, 		ClickedOpenList)
		.ondrawrow			= InsideFileViewRowDraw
		.tryeditcell		= FileRowRenamer
		.listsource			= filerow("", "root")
		
		.quickopen.tag = self
		.listopen.tag = self
	
	
	destructor
		.quickopen.ClearTree
		.listopen.ClearTree
		.editme.ClearTree


	function Init
		.IsTree	= true
		.DontDrawSeparators	= true
		.EnableDrag
		with .quickopen
			is enabled
			is editable
			isnt visible
			.clipmode		= ControlClipMode.SlideBackInWindow
			.ID				= "quickopen"
			.size 			= 400~wide by 15~high
			.OnKeyDown		= QuickOpenKey
			.OnTextChanged	= QuickOpenModified
			.onfocus		= QuickOpenFocus
			.SingleLine		= TextView.SingleLine
			.gnormal.solid	= (0.0, 0.1, 0.0, 0.90)

		|| l = .listsource as filerow
			l.AsDir = true
			
		with .Listopen
			is enabled
			is unsearchable
			isnt editable
			isnt visible
			.clipmode		= ControlClipMode.SlideBackInWindow
			.KeepOneSelected = true
			.ID				= "listopen"
			.gnormal.solid	= (0.0, 0.1, 0.0, 0.90)
			.takesfocus 	= 0
	
	
	behaviour MouseDown
		rz = super.MouseDown(p, clicks)
		if clicks == 1 or 0
			.TryReloadPath


	function TryReloadPath
		opt norefcounts
		for s.up in .FirstSelected
			if s isa filerow
				s.TryReload(self)
	
	
	behaviour AppForeGroundChange
		.TryReloadPath
	
	
	behaviour KeyDown
		if key.isdelete
			|| r = .FirstSelected
			if r isa filerow
				r.AskDelete(self)
				return true
		rz = super.keydown(key)
		.TryReloadPath


	function filerow.DirSize (|[string]|sc, |ivec2|)
		opt norefcounts
		require .asdir
		for f in self
			if f isa filerow
				for ext in sc
					if f isa ext
						rz += (f.lastsize|int|, 1)
						exit
				if f.asdir
					rz += f.dirsize(Sc)
		.lastsize = rz[0]
		.lastfilecount = rz[1]
	
	
	function CheckSize
		opt norefcounts
		|| f = .listsource
		if f isa filerow
			f.DirSize(.SizeCheckers)
	
		
	function ShowQuickOpen (|bool|)
		opt norefcounts
		|| w = .window
		|| q = .quickopen
		q.text ?= .editme.seltext.before("\n").trim
		w <~ q
		w <~ .listopen
		if q is visible
			.HideOpenList
			return true
		(q is visible)
		q.SetFocus
		q.SelectAll
		.OpenQuickRelist(q.text)
		return true

			
	function AddRoot (|file| fol, |bool| IsMainProj = false, |filerow|)
		|| C = 3000
		rz = .AddRootSub(fol, IsMainProj, C)
		(rz isnt editable)
		self <~ rz


	libinternal AddRootSub (|file| fol, |bool| IsMainProj, |$int| C, |filerow|)
		|| p = fol.path
		if !fol.isdir
			return FileRow(p, p.name)
		|| F = FileRow(p, p.TrimExtAndPath, .folfile)
		f.InMainProj = IsMainProj
		(f is expanded) = IsMainProj
		f.FillFilesSub(fol, .excludes, .folfile, c)
		return f

	
	function FolFile (|string|)
		return .listsource.name
	
	
	function FolFile (assigns:|string|)
		.listsource.name = value


	function ErrorAt (|error| e)
		|| f = self[e.path]
		while f and f != .ListSource
			f.HasError = true
			f = f.Parent

			
	function ClearErrors
		opt norefcounts
		for s.fast in self
			if s isa filerow
				s.HasError = false


	iterator
		|| _curr = .listsource.first
		while _curr
			|| N_ = _curr.FlatNext
			yield _curr
			_curr = N_
		(self)


	function SaveAll (|int| Purpose, |bool|)
		cpp_part SaveAllArr
		rz = true		
		|| m = .Modified // what if like 100 are invalid? THEN? ask for all?
		for f in m
			if !.Save(f, purpose)
				rz = false


	function Save (|filerow| f, |int| Purpose=documentstate.saving, |bool|)
		opt norefcounts
		require .CanSave(purpose, f)
		
		|| d = f._Document
			|| tSize = D.save(purpose)
				f.lastsize = tsize
		return true


	libinternal CanSave (|int| Purpose, |filerow| f,   |bool|)
		|| d = f._Document		#require
		if !d.ShouldntSave
			return true

		// some kinda error. We'll save anyhow. but alert them.
		.editme.LoadDocument(d, false)
		d.ShowParseErrorInViewers
		if Purpose == DocumentState.ErrorChecking
			return false 
		if (purpose == DocumentState.Saving)
			Notifications <~ "Invalid jeebox saved to: " + f.location
		return true


	function Modified (|[FileRow]|)
		opt norefcounts
		for f in self
			if f isa filerow
				if f.ismodified
					rz <~ f


	function AskSaveAll (|bool|)
		|| m = .modified 
		if !m
			return true
		.focuson(m.first!)

		|| fs = faststring()
		|| n = m.Length
		|| fstr = "file".pluralize(n)
		fs <~ "Save $fstr in project "
		fs <~ .start!.name
		fs <~ "?"
		fs <~ "\n"
		for m0 in m
			fs <~ "\n"
			fs <~ m0.readname
		
		|| answer = fs.render.AskSaveOrCancel
		if answer >= 1  // save
			return .saveall(documentstate.saving)
		return answer == 0	// don't


	syntax access (|int| i, |filerow|)
		return .listsource[i] as filerow


	syntax access (|string| path, |filerow|)
		cpp_part AccessPath
		if path.file.IsDir
			path = path.PathDir // standardise
		for f in self
			if f isa filerow
				if path ~= f.location or f.ReadFrom
					return f


	libinternal string.name (|bool| YeahReally, |string|)
		cpp_part NameMaybe
		if YeahReally
			return .name
		return self


	libinternal string.FileViewMatch (|string| ToFind, |int|)
		if ToFind[0] == '/'
			require (self ~= ToFind)
			return 10

		if Tofind globs self
			return 8
		if (ToFind[0] == '.' and ToFind[1] != '/') and .endswith(ToFind, true)
			return 1

		if ToFind.length < 2
			return 0
			
		|| HasSlash = ToFind contains '/'
		|| which = (self, .name)(HasSlash)
		if which contains ToFind
			rz = 2
			if .endswith(ToFind) and self[(self-ToFind)-1] == '.'
				rz = 1 // sigh

	
	function FindAPath (|string| path,  |filerow| within=nil, |bool| Copy=false, |filerow|)
		return .FindPaths(path, within, copy)[0]


	function FindPaths (|string| path,  |listviewrow| within=nil, |bool| Copy=false, |[filerow]|)
		require path
		|| NameOnly = path[0, "./"] 
			path = path[2...]

		within ?= .listsource
		for f.flat in within
			if f isa filerow
				|| it = f.match(path, NameOnly)
					f.rangelength = it
					if Copy
						f = FileRow(f)
						if f.Location.file.IsDir
							f.name = f.name.PathDir
					rz <~ f
		
		if rz
			rz.sort(FileRowScoreSorter)
			|dictionary of filerow| D = dictionary.new
			for f in rz
				f.samenameasprev = false
				|| old = D[f.name]
					old.SameNameAsPrev = true
					f.SameNameAsPrev = true
				d[f.name] = f
		
		for (f.backwards in rz) (i)
			if f.SameNameAsPrev
				if copy
					|| name = f.name + "  --  " + f.location.neatenpath
					f = filerow(f)
					f.name = name
					rz[i] = f
				  else
					rz.remove(i)
		
	
	function Listviewrow.FileSort
		.sort(&fileviewer.FileRowNameSorter)
	

		
	libinternal CanFind (|filerow| f, |uint| id, |bool|)
		if id
			require f.findid != id
		f.findID = ID
		
		|| s = .SearchWithin
			|| Restrict = s.location
			if !f.location[0,Restrict]
				return false
		
		if !.NoFind or !.nofind.valuelower(f.name)
			return true
	
	
	function BatchFind (|string| str,  |bool| ForReplace,  |message| FoundList,  |uint| FindID=0)
		opt norefcounts
		require str
		
		|| Remain = 6K
		for f in self
			if (f isa filerow) // in case someone added bad stuff.
				if .CanFind(f, FindID)
					f.FindInFile(str, FoundList, Remain, ForReplace)
			  else
/				f.remove
		FoundList.sort(&filerow.BatchFindSorter)
	

	function HideOpenList (|bool|)
		.ListOpen.clear
		.QuickOpen.hide
		.ListOpen.hide
		return true


	function OpenQuickRelist (|string| path, |bool| Improve=false)
		|| b = .OpenQuickRelistsub(path, improve)
			path = b
		.QuickOpen.text = b
		
	libinternal OpenQuickRelistSub (|string| path,  |bool| Improve,  |string|)
		rz = path
		if path < 2
			.listopen.clear
			return rz
		
		ifn path[0, "/"] or path[0, "~/"]
			|| r = .FindPaths(path, nil, true)
			.ListOpen.QuickOpenFill(r)
			return rz
		
		|| f = path.before(":").file
		|| filter = ""
		if !f.Exists
			f = f.Parent
			if !f.exists
				Notifications <~ f + " doesn't exist"
				beep
				return rz
			filter = path.name
		  elseif Improve
			rz = path.PathDir
		.OpenQuickRelistDir(f, filter)


	libinternal ListView.QuickOpenFill (|[filerow]| Items)
		|| HasSel = .FirstSelected != nil
		.clear
		for p in items
			|| m = message(@str, p.name)
			|| dir = p.Location.file.isdir
			m.name = ("\10 ", "\11 ")(dir) + m.name
			
			if HasSel and p isnt UnSelectable
				(p is selected)
				HasSel = false
			m.tag = p.tag
			self <~ m
			m.obj = p
		(self is visible) = items.length > 0
		|| d = .DesiredHeight
		|| d2 = d.clamp(100, 250)
		.size = the~same by 0~before back d2


	libinternal OpenQuickRelistDir (|file| path, |string| filter)
		|| invis = filter starts "."
		|[filerow]| items
		for ch in path
			if invis or ch.visible
				|| s = path[ch]!
				if s.IsDir
					ch += "/"
				|| ffr = filerow(s, ch)
				if filter and !(ch.name starts filter)
					(ffr is Disabled)
				items <~ ffr
		items.sort(FileRowScoreSorter)
		.listopen.QuickOpenFill(items)
	
	
	function OpenQuick (|string| s)
		|| LineByte = (0, 0)
		|| Path = s.ExtractPathAndLine(LineByte)				
			|| r = .FindAPath(path) ?? .AddExtra(path)
				.Select(nil) // do we need this?
				.SelectFast(r)
		  else
			.editme.SetFocus
		
		if LineByte.x1
			.editme.ViewLineAndSel(LineByte.x1)
		if LineByte.x2
			.editme.ShowAndSelect(LineByte.x2, true)


	function AddExtra (|string| path, |filerow|)
		require path[0] == '/' or '~'
		|| f = path.file
		require f.mustexist
		|| x = .extra
		if !x
			x = filerow("", "(Extra)", ".")
			.extra = x
			self <~ x
		
		|| C = 3000
		rz = .AddRootSub(path.file, false, C) 
		x <~ rz
		.SelectFast(rz)

	module
		function FileRowRenamer (guilisteditevent)
			opt norefcounts
			|| o = cell!.row as filerow
				return o.moveto(value)
		
		
		function FileRowScoreSorter (SorterComparer of FileRow)
			opt norefcounts
			if b.rangelength == .rangelength
				|| aprj = .InMainProj
				|| bprj = b.InMainProj
				if aprj == bprj
					return FileRowNameSorter(self, b)
				return bprj - aprj
				
			return b.rangelength - .rangelength

		function FileRowNameSorter (SorterComparerFileRow)
			opt norefcounts
			|| c = b.asdir - a.asdir
				return c
			return A.name.syntaxcompare(b.name, true)

		function QuickOpenModified (GUITextEvent)
			|| fv = .tag as fileviewer
				fv.OpenQuickRelist(.text)

		function QuickOpenKey (guikeyevent)
			|| fv = .tag as fileviewer				#require
			|| l = fv.listopen
			if key.IsAccept
				|| row = l.FirstSelected
					ClickedOpenList(l, row)
				  else
					fv.OpenQuick(.text)
					fv.HideOpenList
				rz = true
			  elseif (key is escape)
				rz = fv.HideOpenList
			  elseif key is up or down
				rz = l.KeyDown(key)


		function QuickOpenFocus (guiaction)
			require state == 0
			|| fv = .tag as FileViewer
				rz = fv.hideopenlist


		function ClickedOpenList (GuiListevent)
			|| V = .tag as FileViewer
			|| r = row!.obj as filerow
			require r and v

			|| loc = r.location
			if loc.file.isdir
				V.OpenQuickRelist(loc, true)
				return true
			v.OpenQuick(loc)
			V.HideOpenList
			return true
			

		function InsideFileViewRowDraw (ListViewRowDraw)
			opt norefcounts
			if self isa filerow
				|| z	= parent.lastrect
				|| sh	= font.width*2
				|| v	= (6 + .depth(parent)*sh, texty)
				|| mod	= ._Document.ismodified?
				|| ddd	= (Colors.black(0.5), Colors.clear)(mod)
				ddd.a *= strength // sigh...
				font.small!.drawicon(.Modifier, where, V, ddd)

				if .HasError and (!.hasany or self isnt expanded)
					v.x = z.x2-sh
					font.small!.drawicon('\17', where, V, ddd) // not sure???
				return (sh, 0)


libinternal string.PlatformVisible (|bool| AtRoot, |bool|)
	require self[0] != '.'
	if !atroot
		return true
	if platform.osx
		return (.length > 3) and !.islower
	return true


function FileRow.Modifier (|byte|)
	if .DoesntExist
		return '?'
	return FileIcon(.location, "", self is expanded, !.asdir)


extend_module filerow
	function FileIcon (|string| self, |string| types, |bool| Expanded = false, |bool| NotDir = false, |CodePoint|)
		|| n = .name
		|| ext = n.ext
		if (.last == '/') and !notdir
			if types and types.TypeContains(ext + "/")
				rz = '\19'
			  elseif expanded
				rz = '\1A'
			  else
				rz = '\10'
		  elseif (ext == "app" or "scproj" or "uwuz")
			rz = '\19'
		  else
			rz = '\11'



function fileviewer.FindSearchWithin (|filerow|)
	opt norefcounts
	|| s = .FirstSelected as filerow
		while (s isa filerow) and !s.AsDir
			s = s.parent
		return s


function fileviewer.ReturnFileList (GUIListEvent)
	if self isa FileViewer
		.Editme.SetFocus
		return true


function fileviewer.RightClickFileList (GUIListEvent)
	opt NoRefCounts
	if self isa fileviewer
		|| m = .NewCmm
		m <~ ("Show In Desktop",	FileViewerCMMShow)
		m <~ ("Copy File Path",		FileViewerCopyPath)
		m <~ ("Rename File",		FileViewerRenameFile)
		m.AddSep
		|| within = .FindSearchWithin
			m <~ ("Search Within: " + within.name, FileViewerSearchWithin, within)
		
		|| rem = .FirstSelected as filerow
			if (.extra contains rem) and rem != .extra
				m <~ ("Remove From Project",	FileViewerRemove)

		m.AddSep
		m <~ ("Create New File",	FileViewerCreateFile)
		m <~ ("Delete File",		FileViewerTrash)
	return true


function FileViewerCMMShow (GuiMenuEvent)
	|| row = clicked!.row
	if row isa FileRow
		row.location.Reveal


function FileViewerRenameFile (GuiMenuEvent)
	|| row = clicked!.row as filerow
	if self isa FileViewer
		.rename(row)
	

function FileViewerCreateFile (GuiMenuEvent)
	|| row = clicked!.row as filerow		#require
	if !row.asdir
		row = row.parent!
	|| l = row.location
	|| f = l.file.UniqueChild("untitled")
	if f and f.CreateEmpty and self isa FileViewer
		|| created = filerow(f.path, f.name) 
		row <~ created
		row.filesort
		.rename(created)
		.checksize
		.SomethingChanged(row)


function FileViewer.SomethingChanged (|filerow?| row)
	.TryReloadPath
	(.OnChange)(self, row)?


function File.CreateEmpty (|bool|)
	expect (!.exists) (self, "File already exists, can't create empty file here.")
	return .writeall("")


function FileViewerCopyPath (GuiMenuEvent)
	opt norefcounts
	if Clicked
		|| row = clicked.row as FileRow
			clipboard.NotifiedText = row.location


function FileViewerTrash (GuiMenuEvent)
	|| row = clicked!.row
	if  row isa FileRow  and  self isa fileviewer
		row.AskDelete(self)
		.SomethingChanged(nil)


function FileViewerRemove (GuiMenuEvent) // just removes knowledge of the item
	|| row = clicked!.row
	if row isa FileRow and self isa FileViewer
		row.remove
		.SomethingChanged(nil)


function FileViewerSearchWithin (GuiMenuEvent)
	opt norefcounts
	|| row = menu.obj as filerow
	if self isa FileViewer
		if row == .listsource
			row = nil
		if row != .Searchwithin
			.SearchWithin = row
			(.OnSearchWithin)(self, row)?
		
	

prototype SorterComparerFileRow (|filerow| a, |filerow| b, |int|)
