#!/usr/local/bin/spd

https://github.com/gamblevore/speedie/blob/main/Documentation/Message.md // docs here
file:///usr/local/speedie/Documentation/Messages.md						 // locally

main 
	|| path = app.args[0]				#expect ("Pass a file-path")
	|| B = path.ExistingFile			#require
	|| jb = B.Parse						#require
	if b isa "xml"
		jb.XMLToJeebox
		|| BoxFile = path.setext("box")
		if BoxFile <~ jb.render		// write to disk
			"Converted XML to Jeebox: $boxfile"

	|| Found = BookSearch(jb, app.Switches)
		""
		for bk in found
			"$bk"
	  else
		"Can't find any books by: ${app.Switches}"
		
		
function BookSearch (|message| BookFile, |[string]| Queries, |[message]|)
	for book in BookFile[@tmp, "catalog"][@arg]
		if book.Expect(@tmp, "book") 
			if book.TestBook(Queries)
				rz <~ book


function message.TestBook (|[string]| queries, |bool|)
	for row in self[@arg,-1]
		for Q in queries
			if row ~= q.ArgName		// ~= is like == but case-insensitive
				require row.first.name contains q.ArgValue
				rz = true
